
uses events/car/walkdecks/carfind1.df

cyoa{ road{ 

: usedlot-carget ($ Thegroup .. "  ottiene una nuova auto usata!" $) cyoa-text! goodfx ;
cyoa-choice: usedlot-SUV image-suv usedlot-carget carinfo-SUV CAR_SUV get-car ;
cyoa-choice: usedlot-muscle image-sports-car usedlot-carget carinfo-muscle CAR_MUSCLE2 get-car ;
cyoa-choice: usedlot-hybrid image-hybrid-battery usedlot-carget carinfo-hybrid CAR_HYBRID2 get-car ;

cyoa-choice: usedlot-van image-white-van usedlot-carget carinfo-van CAR_VAN get-car ;
cyoa-choice: usedlot-grandpa image-car-lot usedlot-carget carinfo-grandpa CAR_GRANDPA get-car ;
cyoa-choice: usedlot-little image-car-lot usedlot-carget carinfo-little CAR_BUG get-car ;

cyoa: car1-usedlot
	"'Prezzi cosi bassi da essere gratuiti" cyoa-title!
	($ Thegroup .. "  trova un lotto di auto usate che sembra intatto dal tempo, o almeno non toccato dai saccheggiatori." .. gThey .. "  trova" .. g1s .. "  una scorta di benzina e alcune auto funzionanti!" $) cyoa-text!
		50 80 rnd trunk-gas+ image-car-lot
		// Choices in events/car/walkdecks/carfind1.df
		70 %chance if 		
			choice( "Passa a SUV" )choice: usedlot-SUV
		else
			choice( "Passa a Muscle Car" )choice: usedlot-muscle
		then
		70 %chance if 	
			choice( "Passa a Tiny Car" )choice: usedlot-little
		else
			choice( "Passa a Classic Car" )choice: usedlot-grandpa
		then
		30 %chance if 
			choice( "Passa a Electric Hybrid" )choice: usedlot-hybrid
		else
			choice( "Passa a Big Van" )choice: usedlot-van
		then
		choice( "Tieni la macchina, vai via" )choice
;	

: mechanic-armor?
	mechanic-silentrepair
	mechanic-diff 1 > if
		3 car-armor+ ($ "SUPER ARMOR UP!" fbold $)
	else
		mechanic-diff -1 > if
			2 car-armor+ ($ "ARMOR UP!" rbold $)
		else
			1 car-armor+ ($ "ARMATURA sorta SU!" rbold $)
		then
	then
	+cr +text
;
: mech-diff-1-morale mechanic-diff -1 > if morale2+all else morale++all then goodfx ;
cyoa-choice: car1-garage-armor 
	($ cname .. " usa i pezzi di metallo sparsi per creare una corazza improvvisata per la macchina. Ci vogliono un paio di giorni." $) cyoa-text!
	mechanic-diff 1 > if
		($ cHe .. "  fa un lavoro incredibile! Lauto ora e praticamente un carro armato!" $)
	else
		mechanic-diff -1 > if
		($ cHe .. "  lavora in modo efficiente. Lauto sembra formidabile!" $)
		else
		"Sembra che larmatura stia per cadere dallauto in qualsiasi momento."
		then
	then 
	+cr +text mechanic-armor? team-count -4 * trunk-food+ mechanicREV mechanic++ 2 calendar-day+
	mech-diff-1-morale heal-event sleep
;

: nocar-garage
	30 50 rnd trunk-gas+ goodfx
	"'Non c'e molto da fare qui senza un'auto!" +crtext
;

cyoa: car1-garage
	image-car-garage
	"Garage blindato" cyoa-title!
	($ Thegroup .. " ' trova un'officina meccanica professionale. Il cartello dice CAR PROZ." .. gThey .. "  trova" .. g1s .. "  un po di benzina e un sacco di rottami." $) cyoa-text!
		vehicle.which@ 0 = if nocar-garage then;
		 "  Serve solo tempo per armeggiare." +text
		30 50 rnd trunk-gas+
		// garage-choices are in carlogic.df, and do the standard ones
		garage-choices
		// Every garage can have a different special choice
		trunk-food team-count 4 * 1 - > if
			choice( "Ripara e blinda lauto per 2 giorni" )choice: car1-garage-armor 
		then
;	

: mechanic-megatune?
	mechanic-silentrepair
	mechanic-diff 1 > if
		10 car-megatune+ ($ "ULTRA MESSA A PUNTO!" fbold $)
		car-mpg 0.6 * to car-mpg
	else
		mechanic-diff -1 > if
			6 car-megatune+ ($ "SUPER MESSA A PUNTO!" rbold $)
			car-mpg 0.8 * to car-mpg
		else
			2 car-megatune+ ($ "MESSA A PUNTO?" rbold $)
		then
	then
	+cr +text
;

cyoa-choice: car1-garage-megatune 
	($ cname .. "  trascorre qualche giorno in garage " .. cHe .. "  spera di migliorare lauto al massimo." $) cyoa-text!
	mechanic-diff 1 > if
		($ cHe .. " lavoro fantastico! La macchina funziona MEGLIO che nuova!" $)
	else
		mechanic-diff -1 > if
		($ cHe .. " 'lavora in modo efficiente. La vita dell'auto si allunga, con un chilometraggio migliore!" $)
		else
		"'E difficile dire se l'auto e cambiata. Forse e meglio?"
		then
	then 
	+cr +text mechanic-megatune? team-count -6 * trunk-food+ mechanicREV mechanic++ 3 calendar-day+
	mech-diff-1-morale heal-event sleep
;

cyoa: car1-tunegarage
	image-car-garage
	"Garage solitario" cyoa-title!
	($ Thegroup .. " 'trova un garage isolato. Il cartello dice MASTER TUNERZ.	" .. gThey .. "  trova" .. g1s .. " della benzina e un sacco di pezzi di ricambio in buone condizioni." $) cyoa-text!
		vehicle.which@ 0 = if nocar-garage then;
		30 50 rnd trunk-gas+
		garage-choices
		trunk-food team-count 6 * 1 - > if
			choice( "Ripara e ottimizza lauto per 3 giorni" )choice: car1-garage-megatune
		then
;	

: mechanic-megaspeed?
	mechanic-silentrepair
		mechanic-diff -1 > if
			1 ' car-speed +! ($ "VELOCITA AUMENTATA!" rbold $) +crtext
		then
;

cyoa-choice: car1-garage-megaspeed
	($ cname .. "  trascorre qualche giorno in garage " .. cHe .. " cerca di capire le parti strane." $) cyoa-text!
		mechanic-diff -1 > if
		($ cHe .. " li installa. Era un RADDOPPIATORE DI VELOCITA!" $)
		else
		"Le parti rimangono un mistero e vengono lasciate indietro. Lauto riceve alcune riparazioni."
		then
	+cr +text mechanic-megaspeed? team-count -6 * trunk-food+ mechanicREV mechanic++ 3 calendar-day+
	mechanic-diff 1 > if morale3+all goodfx else mech-diff-1-morale then heal-event sleep
;

cyoa: car1-speedgarage
	image-car-garage
	"Garage scintillante" cyoa-title!
	($ Thegroup .. " 'trova un garage ancora scintillante. Il cartello dice CARLZ CUSTOMZ.	" .. gThey .. "  trova" .. g1s .. " della benzina e alcune misteriose parti di auto." $) cyoa-text!
		vehicle.which@ 0 = if nocar-garage then;
		30 50 rnd trunk-gas+
		garage-choices
		trunk-food team-count 6 * 1 - > if
			choice( "3 days of repair and 3 days of modding" )choice: car1-garage-megaspeed
		then
;	

cyoa-choice: car1-floodroad-plow
	($ Thegroup .. " decide di rischiare e attraversare la strada allagata con la macchina." $) cyoa-text! car-plowthrough	
;
cyoa-choice: car1-floodroad-jump
	($ Thegroup .. " individua una rampa improvvisata e ha lidea spettacolarmente pericolosa di provare a saltare la parte allagata della strada." $) cyoa-text! car-jump
;
cyoa: car1-floodroad
	image-water-driving
	"Strada allagata" cyoa-title!
	vehicle.which@ 0 = if 
		($ "La strada piu avanti e allagata. Senza macchina, " .. thegroup .. " deve fare una lunga deviazione a piedi." $) text! 1 calendar-day+ +cr fullcamp
	then;
	($ "La strada piu avanti e stata allagata, ma sembra che lauto potrebbe comunque riuscire a superarla." .. Thegroup .. " decide di:" $) cyoa-text!
		// carlogic.df again
		choice( "Fare una deviazione" )choice: car-detour
		choice( "Attraversare" )choice: car1-floodroad-plow
		angriest choosechar chumany? if
			cchar charchoice( "': SALTIAMOLO" )choice: car1-floodroad-jump
		then
;	

cyoa-choice: car1-fallentree-plow
	($ Thegroup .. " 'pensa che sia solo un albero e che l'auto sia fatta di metallo. Quanto puo essere difficile investirlo?" $) cyoa-text! car-plowthrough	
;
cyoa-choice: car1-fallentree-shoot
	($ Thegroup .. " decide di tirare fuori le armi e sparare allalbero morto." $) cyoa-text!
	// car-shoot-check if
	// Retooling this a bit later
		"Il piano in qualche modo funziona! Lalbero viene fatto a pezzi." +cr +text goodfx
	(* else
		($ "'Non ha funzionato affatto. " .. Thegroup .. " poi prova a speronare lauto attraverso lalbero crivellato di proiettili." $) +cr +text car-plowthrough
	then *)
	car-shoot
;
: car1-fallentree-pop dup .morale-- 50 %chance if dup .strength-- else dup team-attacked then ;
cyoa-choice: car1-fallentree-lift
	choosehuman
	($ Thegroup .. " decide di provare a raccogliere lalbero." $) cyoa-text!
	strength-total 10 > if
		($ Thegroup .. "  toglie lalbero dalla strada con la pura forza!" $) +cr +text goodfx
	else
		strength-total 5 > if
			($ Thegroup .. " moves the tree out of the road through extreme effort. 
			" .. cname .. "  ha qualcosa che gli scoppia nel corpo!" $) +cr +text 
				cchar team-attacked morale-- strength-- badfx
		else
			($ Thegroup .. "  non riesce a spostare lalbero, ma riesce a ferirsi " .. ghim .. " abbastanza gravemente. " .. gThey .. "  decidono" .. g1s .. "  di provare a speronare lalbero con la macchina."  $) +cr +text
			car-plowthrough ' car1-fallentree-pop foreach-team-char
		then
	then
;
cyoa: car1-fallentree
	image-tree-in-road
	" Albero sulla strada" cyoa-title!
	" La strada davanti a noi ha un grande albero sdraiato di traverso. Sembra che qualcuno lo abbia tagliato per bloccare la strada. Ci vorrebbe un gruppo di persone forti per spostarlo in sicurezza!" text!
	vehicle.which@ 0 = if 
		($ " Senza macchina, " .. thegroup .. "  scavalca semplicemente lalbero abbattuto." $) +crtext morale++all goodfx
	then;
	($ Thegroup .. " decide di:" $) +crtext
		// carlogic.df again
		choice( "Fare una deviazione" )choice: car-detour
		choice( "Attraversare" )choice: car1-fallentree-plow
		choice( " Solleva lalbero" )choice: car1-fallentree-lift
		// Add an event for having a chainsaw?
		car-shoot-check if
			choice( " SPARAGLI" )choice: car1-fallentree-shoot
		then
;	

cyoa-choice: car1-dashcode-mech
	($ cname .. "  controlla la macchina e trova il problema. Era solo un tappo della benzina allentato. Facile da sistemare!" $) cyoa-text!
		cmechanic- if mechanic++ then
		mechanicREV goodfx 2 4 rnd car-tune+
;
cyoa-choice: car1-dashcode-punch
	cpet? if
		($ cname .. "  morde a morte la macchina, alla grande." $)
	else
		($ cname .. "  sfascia la macchina con " .. chis .. "  suono." $)
	then cyoa-text!	
	"Scarica un po di stress e la spia di errore si spegne!" +crtext
		cchar .strength 4 < if strength++ then
		morale++ strengthREV goodfx -1 car-chassis+
;
cyoa-choice: car1-dashcode-kick
	($ cname .. "  esce dalla macchina e la colpisce con " .. chis .. "  piedi. Che allenamento! La spia di errore rimane accesa, pero." $) cyoa-text!
		cchar .fitness 4 < if fitness++ then
		morale++ fitnessREV goodfx -1 car-chassis+
;

: car1-dashcode-dtext cpet? if "morde" else "prende a pugni" then ;

cyoa: car1-dashcode
	vehicle.which@ 0 = if 
		image-zombie-hoard-1
		"Escursione in Canada" cyoa-title!
		($ Thegroup .. "  non ha la protezione di unauto quando si avvicina al Canada. " .. gTheyare .. "  attaccato da una grande orda allaperto. Non ce posto dove nascondersi e non ce tempo per combattere." $) text! ouch--all bitefx
	then;
	image-check-engine
	"CONTROLLA MOTORE" cyoa-title!
	($ "Lauto funziona bene, ma sul cruscotto compare un codice di errore. " .. Thegroup .. " ' non ha idea di cosa ci sia che non va, quindi " .. gthey .. "  accosta" .. g1s .. "  per vedere cosa succede." $) cyoa-text!
		choice( "Ignorala" )choice
		mechanicest charchoice( "  la ispeziona con attenzione" )choice: car1-dashcode-mech
		strongest choosechar cchar charchoice( " " .. car1-dashcode-dtext .. "  la colpisce molto forte" )choice: car1-dashcode-punch
		fittest charchoice( "  le tira un sacco di calci" )choice: car1-dashcode-kick
;

} }

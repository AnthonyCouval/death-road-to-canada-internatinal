
uses events/car/walkdecks/carfind1.df

cyoa{ road{ 

: usedlot-carget ($ Thegroup .. "  obtiene un auto usado nuevo!" $) cyoa-text! goodfx ;
cyoa-choice: usedlot-SUV image-suv usedlot-carget carinfo-SUV CAR_SUV get-car ;
cyoa-choice: usedlot-muscle image-sports-car usedlot-carget carinfo-muscle CAR_MUSCLE2 get-car ;
cyoa-choice: usedlot-hybrid image-hybrid-battery usedlot-carget carinfo-hybrid CAR_HYBRID2 get-car ;

cyoa-choice: usedlot-van image-white-van usedlot-carget carinfo-van CAR_VAN get-car ;
cyoa-choice: usedlot-grandpa image-car-lot usedlot-carget carinfo-grandpa CAR_GRANDPA get-car ;
cyoa-choice: usedlot-little image-car-lot usedlot-carget carinfo-little CAR_BUG get-car ;

cyoa: car1-usedlot
	"'Precios Tan Bajos Que Son Gratis" cyoa-title!
	($ Thegroup .. "  encuentra un lote de autos usados que parece intacto por el tiempo, o al menos no tocado por saqueadores." .. gThey .. "  encontrar" .. g1s .. "  un suministro de gasolina y algunos autos que funcionan!" $) cyoa-text!
		50 80 rnd trunk-gas+ image-car-lot
		// Choices in events/car/walkdecks/carfind1.df
		70 %chance if 		
			choice( "Cambiar a SUV" )choice: usedlot-SUV
		else
			choice( "Cambiar a Muscle Car" )choice: usedlot-muscle
		then
		70 %chance if 	
			choice( "Cambiar a Tiny Car" )choice: usedlot-little
		else
			choice( "Cambiar a Classic Car" )choice: usedlot-grandpa
		then
		30 %chance if 
			choice( "Cambiar a Electric Hybrid" )choice: usedlot-hybrid
		else
			choice( "Cambiar a Big Van" )choice: usedlot-van
		then
		choice( "Conservar el auto, alejarse conduciendo" )choice
;	

: mechanic-armor?
	mechanic-silentrepair
	mechanic-diff 1 > if
		3 car-armor+ ($ "SUPERARMADURA ARRIBA!" fbold $)
	else
		mechanic-diff -1 > if
			2 car-armor+ ($ "ARMADURA ARRIBA!" rbold $)
		else
			1 car-armor+ ($ "ARMADURA sort of ARRIBA!" rbold $)
		then
	then
	+cr +text
;
: mech-diff-1-morale mechanic-diff -1 > if morale2+all else morale++all then goodfx ;
cyoa-choice: car1-garage-armor 
	($ cname .. " usa los restos de metal que hay por ahi para crear una armadura improvisada en el coche. Tarda un par de dias." $) cyoa-text!
	mechanic-diff 1 > if
		($ cHe .. "  hace un trabajo increible! El auto es practicamente un tanque ahora!" $)
	else
		mechanic-diff -1 > if
		($ cHe .. "  funciona eficientemente. El auto luce formidable!" $)
		else
		"Parece que la armadura se va a caer del auto en cualquier momento."
		then
	then 
	+cr +text mechanic-armor? team-count -4 * trunk-food+ mechanicREV mechanic++ 2 calendar-day+
	mech-diff-1-morale heal-event sleep
;

: nocar-garage
	30 50 rnd trunk-gas+ goodfx
	"'No hay mucho que hacer aqui sin un auto!" +crtext
;

cyoa: car1-garage
	image-car-garage
	"Garaje Blindado" cyoa-title!
	($ Thegroup .. " ' encuentra un taller de autos profesional. El letrero dice 'CAR PROZ'.	" .. gThey .. "  encontrar" .. g1s .. "  algo de gasolina y mucho metal de desecho." $) cyoa-text!
		vehicle.which@ 0 = if nocar-garage then;
		 "  Solo necesito tiempo para jugar." +text
		30 50 rnd trunk-gas+
		// garage-choices are in carlogic.df, and do the standard ones
		garage-choices
		// Every garage can have a different special choice
		trunk-food team-count 4 * 1 - > if
			choice( "Repara y blinda el auto durante 2 dias" )choice: car1-garage-armor 
		then
;	

: mechanic-megatune?
	mechanic-silentrepair
	mechanic-diff 1 > if
		10 car-megatune+ ($ "ULTRA PUESTA A PUNTO!" fbold $)
		car-mpg 0.6 * to car-mpg
	else
		mechanic-diff -1 > if
			6 car-megatune+ ($ "SUPER PUESTA A PUNTO!" rbold $)
			car-mpg 0.8 * to car-mpg
		else
			2 car-megatune+ ($ "PUESTO A PUNTO?" rbold $)
		then
	then
	+cr +text
;

cyoa-choice: car1-garage-megatune 
	($ cname .. "  pasa unos dias en el taller. " .. cHe .. "  espera mejorar el auto hasta sus limites." $) cyoa-text!
	mechanic-diff 1 > if
		($ cHe .. "  hace un trabajo increible! El auto esta funcionando MEJOR que nuevo!" $)
	else
		mechanic-diff -1 > if
		($ cHe .. " ' trabaja eficientemente. La vida util del auto se extiende, con mejor kilometraje!" $)
		else
		"'Es dificil decir si el auto cambio en algo. Quizas esta mejor?"
		then
	then 
	+cr +text mechanic-megatune? team-count -6 * trunk-food+ mechanicREV mechanic++ 3 calendar-day+
	mech-diff-1-morale heal-event sleep
;

cyoa: car1-tunegarage
	image-car-garage
	"Garaje solitario" cyoa-title!
	($ Thegroup .. " ' encuentra un garaje de autos aislado. El letrero dice 'MASTER TUNERZ'.	" .. gThey .. "  encontrar" .. g1s .. "  algo de gasolina y muchas piezas de repuesto en buen estado." $) cyoa-text!
		vehicle.which@ 0 = if nocar-garage then;
		30 50 rnd trunk-gas+
		garage-choices
		trunk-food team-count 6 * 1 - > if
			choice( "Reparar y afinar el auto por 3 dias" )choice: car1-garage-megatune
		then
;	

: mechanic-megaspeed?
	mechanic-silentrepair
		mechanic-diff -1 > if
			1 ' car-speed +! ($ "VELOCIDAD AUMENTADA!" rbold $) +crtext
		then
;

cyoa-choice: car1-garage-megaspeed
	($ cname .. "  pasa unos dias en el taller. " .. cHe .. "  intenta descifrar las piezas raras." $) cyoa-text!
		mechanic-diff -1 > if
		($ cHe .. "  las instala. Era un PROPULSOR DE COHETES!" $)
		else
		"Las piezas siguen siendo un misterio y se dejan atras. El auto recibe algunas reparaciones."
		then
	+cr +text mechanic-megaspeed? team-count -6 * trunk-food+ mechanicREV mechanic++ 3 calendar-day+
	mechanic-diff 1 > if morale3+all goodfx else mech-diff-1-morale then heal-event sleep
;

cyoa: car1-speedgarage
	image-car-garage
	"Garaje brillante" cyoa-title!
	($ Thegroup .. " ' encuentra un garaje de autos aun brillante. El letrero dice 'CARLZ CUSTOMZ'.	" .. gThey .. "  encontrar" .. g1s .. "  algo de gasolina y algunas piezas de auto misteriosas." $) cyoa-text!
		vehicle.which@ 0 = if nocar-garage then;
		30 50 rnd trunk-gas+
		garage-choices
		trunk-food team-count 6 * 1 - > if
			choice( "Reparar e intentar mod durante 3 dias" )choice: car1-garage-megaspeed
		then
;	

cyoa-choice: car1-floodroad-plow
	($ Thegroup .. "  decide arriesgarse y atravesar el auto por la carretera inundada." $) cyoa-text! car-plowthrough	
;
cyoa-choice: car1-floodroad-jump
	($ Thegroup .. "  ve una rampa improvisada y tiene la idea espectacularmente peligrosa de intentar saltar la parte inundada de la carretera." $) cyoa-text! car-jump
;
cyoa: car1-floodroad
	image-water-driving
	"Carretera arrasada" cyoa-title!
	vehicle.which@ 0 = if 
		($ "El camino por delante esta inundado. Sin coche, " .. thegroup .. "  tiene que tomar un largo desvio a pie." $) text! 1 calendar-day+ +cr fullcamp
	then;
	($ "El camino por delante se ha inundado, pero parece que el coche aun podria atravesarlo." .. Thegroup .. "  decide:" $) cyoa-text!
		// carlogic.df again
		choice( "Tomar un desvio" )choice: car-detour
		choice( "Atravesar" )choice: car1-floodroad-plow
		angriest choosechar chumany? if
			cchar charchoice( "': VAMOS A SALTARLO" )choice: car1-floodroad-jump
		then
;	

cyoa-choice: car1-fallentree-plow
	($ Thegroup .. " ' piensa que es solo un arbol, y el auto esta hecho de metal. Que tan dificil podria ser atropellarlo?" $) cyoa-text! car-plowthrough	
;
cyoa-choice: car1-fallentree-shoot
	($ Thegroup .. "  decide sacar las armas y disparar al arbol muerto." $) cyoa-text!
	// car-shoot-check if
	// Retooling this a bit later
		"El plan de alguna manera funciona! El arbol esta destrozado." +cr +text goodfx
	(* else
		($ "'Eso no funciono en absoluto " .. Thegroup .. "  luego intenta embestir el auto contra el arbol acribillado a balazos." $) +cr +text car-plowthrough
	then *)
	car-shoot
;
: car1-fallentree-pop dup .morale-- 50 %chance if dup .strength-- else dup team-attacked then ;
cyoa-choice: car1-fallentree-lift
	choosehuman
	($ Thegroup .. "  decide intentar levantar el arbol." $) cyoa-text!
	strength-total 10 > if
		($ Thegroup .. "  mueve el arbol fuera del camino con pura fuerza!" $) +cr +text goodfx
	else
		strength-total 5 > if
			($ Thegroup .. " moves the tree out of the road through extreme effort. 
			" .. cname .. "  tiene algo que explota en su cuerpo!" $) +cr +text 
				cchar team-attacked morale-- strength-- badfx
		else
			($ Thegroup .. "  no logra mover el arbol en absoluto, pero logra lesionar " .. ghim .. "bastante mal. " .. gThey .. "  deciden" .. g1s .. "  intentar embestir el arbol con el auto."  $) +cr +text
			car-plowthrough ' car1-fallentree-pop foreach-team-char
		then
	then
;
cyoa: car1-fallentree
	image-tree-in-road
	"Arbol en el camino" cyoa-title!
	"El camino por delante tiene un gran arbol atravesado. Parece que alguien lo corto para bloquear el camino. Se necesitaria un grupo de personas fuertes para moverlo de manera segura!" text!
	vehicle.which@ 0 = if 
		($ "Sin coche, " .. thegroup .. "  simplemente trepa por el arbol caido." $) +crtext morale++all goodfx
	then;
	($ Thegroup .. "  decide:" $) +crtext
		// carlogic.df again
		choice( "Tomar un desvio" )choice: car-detour
		choice( "Atravesar" )choice: car1-fallentree-plow
		choice( "Levantar el arbol" )choice: car1-fallentree-lift
		// Add an event for having a chainsaw?
		car-shoot-check if
			choice( "DISPARARLE" )choice: car1-fallentree-shoot
		then
;	

cyoa-choice: car1-dashcode-mech
	($ cname .. "  revisa el coche y encuentra el problema. Era solo un tapon de gasolina suelto. Facil de arreglar!" $) cyoa-text!
		cmechanic- if mechanic++ then
		mechanicREV goodfx 2 4 rnd car-tune+
;
cyoa-choice: car1-dashcode-punch
	cpet? if
		($ cname .. "  muerde el coche a lo grande." $)
	else
		($ cname .. "  estrella el coche con " .. chis .. " ruido." $)
	then cyoa-text!	
	"Alivia algo de estres y la luz de error se apaga!" +crtext
		cchar .strength 4 < if strength++ then
		morale++ strengthREV goodfx -1 car-chassis+
;
cyoa-choice: car1-dashcode-kick
	($ cname .. "  sale del coche y le da con " .. chis .. "  pies. Que entrenamiento! Sin embargo, la luz de error permanece encendida." $) cyoa-text!
		cchar .fitness 4 < if fitness++ then
		morale++ fitnessREV goodfx -1 car-chassis+
;

: car1-dashcode-dtext cpet? if "muerde" else "golpea" then ;

cyoa: car1-dashcode
	vehicle.which@ 0 = if 
		image-zombie-hoard-1
		"Caminata a Canada" cyoa-title!
		($ Thegroup .. "  carece de la proteccion de un coche al acercarse a Canada. " .. gTheyare .. "  atacado por una gran horda al aire libre. No hay donde esconderse ni tiempo para luchar." $) text! ouch--all bitefx
	then;
	image-check-engine
	"SERVICIO PRONTO MOTOR" cyoa-title!
	($ "El coche funciona bien, pero aparece un codigo de error en el salpicadero. " .. Thegroup .. " ' no tiene ni idea de que pasa, asi que " .. gthey .. " tira" .. g1s .. " se acerca para ver que esta pasando." $) cyoa-text!
		choice( "Ignoralo" )choice
		mechanicest charchoice( " inspecciona cuidadosamente" )choice: car1-dashcode-mech
		strongest choosechar cchar charchoice( " " .. car1-dashcode-dtext .. " lo trata de abrir muy fuerte" )choice: car1-dashcode-punch
		fittest charchoice( " lo patala mucho" )choice: car1-dashcode-kick
;

} }

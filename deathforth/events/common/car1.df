
uses events/car/walkdecks/carfind1.df

cyoa{ road{ 

: usedlot-carget ($ Thegroup .. " obtient un nouveau vehicule d'occasion !" $) cyoa-text! goodfx ;
cyoa-choice: usedlot-SUV image-suv usedlot-carget carinfo-SUV CAR_SUV get-car ;
cyoa-choice: usedlot-muscle image-sports-car usedlot-carget carinfo-muscle CAR_MUSCLE2 get-car ;
cyoa-choice: usedlot-hybrid image-hybrid-battery usedlot-carget carinfo-hybrid CAR_HYBRID2 get-car ;

cyoa-choice: usedlot-van image-white-van usedlot-carget carinfo-van CAR_VAN get-car ;
cyoa-choice: usedlot-grandpa image-car-lot usedlot-carget carinfo-grandpa CAR_GRANDPA get-car ;
cyoa-choice: usedlot-little image-car-lot usedlot-carget carinfo-little CAR_BUG get-car ;

cyoa: car1-usedlot
	"Des prix si bas qu'ils sont gratuits." cyoa-title!
	($ Thegroup .. " recherche un terrain de voitures d'occasion qui semble impacte par le temps, ou du moins pas touche par les vandales... ." .. gThey .. " trouver" .. g1s .. " une fourniture de gazole, et quelques voitures fonctionnelles !" $) cyoa-text!
		50 80 rnd trunk-gas+ image-car-lot
		// Choices in events/car/walkdecks/carfind1.df
		70 %chance if 		
			choice( "Echangeons par une suv." )choice: usedlot-SUV
		else
			choice( "Changer pour voiture muscle" )choice: usedlot-muscle
		then
		70 %chance if 	
			choice( "Echangez pour une voiturette compacte" )choice: usedlot-little
		else
			choice( "Echangez pour une voiture de collection." )choice: usedlot-grandpa
		then
		30 %chance if 
			choice( "Passage a la propulsion hybride electrique." )choice: usedlot-hybrid
		else
			choice( "Echangeons pour le camion grand" )choice: usedlot-van
		then
		choice( "Garde la voiture et conduis-en loin." )choice
;	

: mechanic-armor?
	mechanic-silentrepair
	mechanic-diff 1 > if
		3 car-armor+ ($ "SUPER ARMURE ACTIVEE !" fbold $)
	else
		mechanic-diff -1 > if
			2 car-armor+ ($ "MUNIS DE L'ARMEUR, S.V.P." rbold $)
		else
			1 car-armor+ ($ "L'armure va vers le haut !" rbold $)
		then
	then
	+cr +text
;
: mech-diff-1-morale mechanic-diff -1 > if morale2+all else morale++all then goodfx ;
cyoa-choice: car1-garage-armor 
	($ cname .. " utilisez les restes de metal a la disposition pour creer une armure rudimentaire sur la voiture. cela prend quelques jours." $) cyoa-text!
	mechanic-diff 1 > if
		($ cHe .. " le vehicule fonctionne remarquablement ! l'automobile est presque une panthere blindee maintenant !" $)
	else
		mechanic-diff -1 > if
		($ cHe .. " le vehicule fonctionne efficacement. l'auto a l'apparence impressionnante !" $)
		else
		"Il semble que l'armure va retomber du vehicule a tout moment."
		then
	then 
	+cr +text mechanic-armor? team-count -4 * trunk-food+ mechanicREV mechanic++ 2 calendar-day+
	mech-diff-1-morale heal-event sleep
;

: nocar-garage
	30 50 rnd trunk-gas+ goodfx
	"Je preparais deja des plans pour emmenager au canada !" +crtext
;

cyoa: car1-garage
	image-car-garage
	"Garage blinde" cyoa-title!
	($ Thegroup .. " cherche une garage automobile professionnel. l'indication dit 'car proz'." .. gThey .. " trouver" .. g1s .. " des gaz et beaucoup de metal ferraille." $) cyoa-text!
		vehicle.which@ 0 = if nocar-garage then;
		 " Il me faut du temps pour travailler dessus." +text
		30 50 rnd trunk-gas+
		// garage-choices are in carlogic.df, and do the standard ones
		garage-choices
		// Every garage can have a different special choice
		trunk-food team-count 4 * 1 - > if
			choice( "Reparer et equiper la voiture pendant deux jours." )choice: car1-garage-armor 
		then
;	

: mechanic-megatune?
	mechanic-silentrepair
	mechanic-diff 1 > if
		10 car-megatune+ ($ "ULTRA REGLAGE A FOND !" fbold $)
		car-mpg 0.6 * to car-mpg
	else
		mechanic-diff -1 > if
			6 car-megatune+ ($ "SUPER REGLAGE INTENSIF !" rbold $)
			car-mpg 0.8 * to car-mpg
		else
			2 car-megatune+ ($ "REGULE" rbold $)
		then
	then
	+cr +text
;

cyoa-choice: car1-garage-megatune 
	($ cname .. " passe quelques jours dans la garde-manger. " .. cHe .. " espere ameliorer la voiture a ses extremites." $) cyoa-text!
	mechanic-diff 1 > if
		($ cHe .. " la voiture fonctionne exceptionnellement ! elle fonctionne meilleur qu'a neuf !" $)
	else
		mechanic-diff -1 > if
		($ cHe .. " la voiture fonctionne efficacement. la vie de la voiture est etendue, avec une meilleure autonomie!" $)
		else
		"Il est difficile de dire si la voiture a change du tout. peut-etre est-ce bien meilleure ?"
		then
	then 
	+cr +text mechanic-megatune? team-count -6 * trunk-food+ mechanicREV mechanic++ 3 calendar-day+
	mech-diff-1-morale heal-event sleep
;

cyoa: car1-tunegarage
	image-car-garage
	"+2 de force" cyoa-title!
	($ Thegroup .. " un garage isole pour voitures, le signe indique 'master tunerz'." .. gThey .. " trouver" .. g1s .. " des gaz et beaucoup de pieces detachees d'occasion en bon etat." $) cyoa-text!
		vehicle.which@ 0 = if nocar-garage then;
		30 50 rnd trunk-gas+
		garage-choices
		trunk-food team-count 6 * 1 - > if
			choice( "Reparer et ajusteur la voiture pendant trois jours." )choice: car1-garage-megatune
		then
;	

: mechanic-megaspeed?
	mechanic-silentrepair
		mechanic-diff -1 > if
			1 ' car-speed +! ($ "ACCELERATION DE VITESSE !" rbold $) +crtext
		then
;

cyoa-choice: car1-garage-megaspeed
	($ cname .. " passe quelques jours dans la garde-manger. " .. cHe .. " essayez de determiner les parties etranges." $) cyoa-text!
		mechanic-diff -1 > if
		($ cHe .. " installez-les. c'etait un booster roquette !" $)
		else
		"Les parties restent un mystere et sont laissees derriere. la voiture subit quelques reparations."
		then
	+cr +text mechanic-megaspeed? team-count -6 * trunk-food+ mechanicREV mechanic++ 3 calendar-day+
	mechanic-diff 1 > if morale3+all goodfx else mech-diff-1-morale then heal-event sleep
;

cyoa: car1-speedgarage
	image-car-garage
	"Garage shiny" cyoa-title!
	($ Thegroup .. " une usine de carrosserie appelee 'carlz customz' affiche des voitures neuves et brillantes encore." .. gThey .. " trouver" .. g1s .. " des gaz et quelques pieces d'auto mysterieuses." $) cyoa-text!
		vehicle.which@ 0 = if nocar-garage then;
		30 50 rnd trunk-gas+
		garage-choices
		trunk-food team-count 6 * 1 - > if
			choice( "Reparer et tenter une modification durant trois jours." )choice: car1-garage-megaspeed
		then
;	

cyoa-choice: car1-floodroad-plow
	($ Thegroup .. " se decide a prendre un risque en conduisant la voiture a travers la route inondee." $) cyoa-text! car-plowthrough	
;
cyoa-choice: car1-floodroad-jump
	($ Thegroup .. " sa voit un palissage fait maison et a l'idee spectaculairement dangereuse de sauter la partie inondee de la route." $) cyoa-text! car-jump
;
cyoa: car1-floodroad
	image-water-driving
	"Route lavee" cyoa-title!
	vehicle.which@ 0 = if 
		($ "La route a l'avant est inondee. sans voiture, " .. thegroup .. " il doit faire une detour importante a pied." $) text! 1 calendar-day+ +cr fullcamp
	then;
	($ "La route devant nous est inondee, mais il semble que la voiture puisse peut-etre encore passer dessus... ." .. Thegroup .. " decide de :" $) cyoa-text!
		// carlogic.df again
		choice( "Prendre un detour" )choice: car-detour
		choice( "Cultiver sans relache, trainer a travers" )choice: car1-floodroad-plow
		angriest choosechar chumany? if
			cchar charchoice( "SAUTEZ-LE !" )choice: car1-floodroad-jump
		then
;	

cyoa-choice: car1-fallentree-plow
	($ Thegroup .. " pense qu'il s'agit qu'd'un arbre et que la voiture est faite de metal. comment pourrait-il etre si dur a ecraser ?" $) cyoa-text! car-plowthrough	
;
cyoa-choice: car1-fallentree-shoot
	($ Thegroup .. " il decide de sortir ses armes et tirer sur l'arbre mort." $) cyoa-text!
	// car-shoot-check if
	// Retooling this a bit later
		"La strategie fonctionne a peu pres ! l'arbre est dechire en morceaux." +cr +text goodfx
	(* else
		($ "Ceci n'a fonctionne du tout au fait. " .. Thegroup .. " essaye de percuter la voiture contre l'arbre pique de balles." $) +cr +text car-plowthrough
	then *)
	car-shoot
;
: car1-fallentree-pop dup .morale-- 50 %chance if dup .strength-- else dup team-attacked then ;
cyoa-choice: car1-fallentree-lift
	choosehuman
	($ Thegroup .. " essaye de prendre l'arbre." $) cyoa-text!
	strength-total 10 > if
		($ Thegroup .. " fait sortir l'arbre de la route par simple force !" $) +cr +text goodfx
	else
		strength-total 5 > if
			($ Thegroup .. " moves the tree out of the road through extreme effort. 
			" .. cname .. " quelque chose se trouve dans leur corps, pousse-pousse !" $) +cr +text 
				cchar team-attacked morale-- strength-- badfx
		else
			($ Thegroup .. " ne parvient pas a deplacer l'arbre du tout, mais lui cause des blessures. " .. ghim .. "moi, c'est plutot mauvais. " .. gThey .. " decidez." .. g1s .. " essayer de percuter l'arbre avec la voiture."  $) +cr +text
			car-plowthrough ' car1-fallentree-pop foreach-team-char
		then
	then
;
cyoa: car1-fallentree
	image-tree-in-road
	"Arbre dans la route." cyoa-title!
	"Il y a une grande arbre qui barre le chemin a l'avant. il semble que quelqu'un l'ait coupe pour bloquer la route. il faudrait une petite troupe de personnes fortes pour le faire bouger sans danger !" text!
	vehicle.which@ 0 = if 
		($ "Sans voiture, " .. thegroup .. " montrez simplement votre chemin sur l'arbre abattu." $) +crtext morale++all goodfx
	then;
	($ Thegroup .. " decide de :" $) +crtext
		// carlogic.df again
		choice( "Prendre un detour" )choice: car-detour
		choice( "Cultiver sans relache, trainer a travers" )choice: car1-fallentree-plow
		choice( "Remplez le arbre du sol." )choice: car1-fallentree-lift
		// Add an event for having a chainsaw?
		car-shoot-check if
			choice( "FLATULETTES." )choice: car1-fallentree-shoot
		then
;	

cyoa-choice: car1-dashcode-mech
	($ cname .. " c'etait juste une gachette de gaz qui etait mal serree. facile a reparer !" $) cyoa-text!
		cmechanic- if mechanic++ then
		mechanicREV goodfx 2 4 rnd car-tune+
;
cyoa-choice: car1-dashcode-punch
	cpet? if
		($ cname .. " mord vraiment fort a la voiture." $)
	else
		($ cname .. " laisser le moteur allume " .. chis .. " poings fermes." $)
	then cyoa-text!	
	"Cela soulage un peu de la tension, et la lampe d'erreur s'eteint !" +crtext
		cchar .strength 4 < if strength++ then
		morale++ strengthREV goodfx -1 car-chassis+
;
cyoa-choice: car1-dashcode-kick
	($ cname .. " se deleste de son vehicule en se jetant a l'exterieur et en le frappant violemment. " .. chis .. " les pieds ... tres interessant ! mais la lumiere d'erreur reste allumee malgre cela." $) cyoa-text!
		cchar .fitness 4 < if fitness++ then
		morale++ fitnessREV goodfx -1 car-chassis+
;

: car1-dashcode-dtext cpet? if "methode d'analy" else "coups de poing" then ;

cyoa: car1-dashcode
	vehicle.which@ 0 = if 
		image-zombie-hoard-1
		"Marche a pied vers le canada" cyoa-title!
		($ Thegroup .. " manquent la protection d'une voiture quand ils approchent du canada. " .. gTheyare .. " s'etre fait attaquer par une grande horde en plein air. il n'y a pas de refuge et pas le temps pour se battre." $) text! ouch--all bitefx
	then;
	image-check-engine
	"SERVICE ENGINE SOON" cyoa-title!
	($ "L'automobile fonctionne bien, mais un code d'erreur s'affiche sur le tableau de bord. " .. Thegroup .. " aucune idee de quoi il s'agit, donc rien a faire. " .. gthey .. " tirez." .. g1s .. " aller voir ce qu'il en est." $) cyoa-text!
		choice( "Ignorer cela" )choice
		mechanicest charchoice( " examine-le-dans-de-la-gaucheterie" )choice: car1-dashcode-mech
		strongest choosechar cchar charchoice( " " .. car1-dashcode-dtext .. " je te le dis" )choice: car1-dashcode-punch
		fittest charchoice( " il la frappe beaucoup" )choice: car1-dashcode-kick
;

} }

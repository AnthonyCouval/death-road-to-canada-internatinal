
(*

- ouija board event "Weegee"
	- Pick a question to ask
	- Different if solo
		- AM I PRETTY? [yes] Morale Boost
		- WHERE'S THE LOOT? Shows hidden supplies nearby
		- 

	
*)

cyoa{ road{ 

// Not a Ouija Board

: weegeetm .. "Tabuleiro Weegee [tm]" pbold .. ;
: weegeecurse args( char ) 13 <- char .seed! ;

: weegee-end
	($ Thegroup .. "  agora se sente incrivelmente azarado. Cuidado com a " .. "Maldicao de Weegee!" rbold $) +crtext
	"SORTE MASSIVAMENTE DIMINUIDA!" fbold +crtext
	"Em algum lugar distante, um goblin ri." pbold +crtext goblin-laugh
	
;

cyoa-choice: common4-weegee-pretty
	choosehuman cicon
	($ "O " weegeetm "  soletra que " .. cname .. "  e " .. "	MUITO BONITO" pbold $) text!
	($ "Legal! " .. cname .. " ' Nao se sente amaldicoado por este adoravel Weegee." $) +crtext
	morale2+
	"Somewhere in the distance, a goblin laughs, but in a nice way. As if to say 'hey.... what's up. have a nice day.'" pbold +crtext goblin-laugh
	choice( "legal" )choice
;

cyoa-choice: common4-weegee-loot
	($ "O " weegeetm "  soletra a localizacao de um esconderijo de saque na casa!" $) text!
	homesearch1
	weegee-end
;

cyoa-choice: common4-weegee-check
	weaselest choosechar cicon
	($ "Quando perguntado quem vai trair o grupo, o " weegeetm "  soletra " $) text!
	weaselest-pass if
		($ cname .. "'nome!" $) +text
		loyaltyREV
		weegee-end
	else
		"Y O U R B U T T" pbold +crtext
		($ "O que isso pode significar? O grupo fica inquieto e a duvida aumenta. Poderia ser a " .. "Maldicao de Weegee?" rbold $) +crtext
		morale--all goblin-laugh
	then
;

cyoa-choice: common4-weegee-canada
	($ "O " weegeetm "  aponta para a palavra " .. "NAO" pbold .. "  com velocidade incrivel. Entao aponta para " .. "NAO" fbold .. "  novamente, enquanto de alguma forma pega fogo." $) text!
	($ "Tanto faz! Essas coisas estao sempre erradas, de qualquer maneira! " .. cname .. "  joga o " weegeetm "  em chamas do outro lado da sala. Isso faz " .. chim .. " tambem pega fogo." $) +crtext
	cchar isfireproof? if 
		($ cname .. " adora ser a prova de fogo!" $) +crtext
		morale++ goodfx
	else
		ouch-- alive? if bitefx then
	then
	alive? if
		"Foi um bom exercicio, pelo menos." +crtext fitness++
	else
		($ "'Nao mexa com o " .. "Maldicao de Weegee!" rbold $) +crtext goblin-laugh
	then
	
;

cyoa: common4-weegee
	"Weegee Board" cyoa-title! choosehuman cicon
	($ Thegroup .. " esta vasculhando uma casa abandonada e encontra um " weegeetm " 'na mesa da cozinha. E um jogo comum usado para se comunicar com " .. "FANTASMAS" fbold $) text!
		choice( "Deixe esse ABSURDO de lado" )choice
		($ "Dizem que essas coisas sao " .. "INCRIVELMENTE AMALDICOADAS COM MA SORTE" rbold .. ", so beware! Still, it may be worth asking it a question..." $) +crtext
		solohuman if
			choice( "EU SOU BONITA?" )choice: common4-weegee-pretty
		else
			choice( "QUEM VAI TRAIR O TIME?" )choice: common4-weegee-check
		then
			choice( "VONTADE" .. I-or-WE .. "FAZER ISSO NO CANADA?" )choice: common4-weegee-canada
			choice( "'ONDE ESTA O BOTIM?" )choice: common4-weegee-loot
;

// Dog Poop

: common4-dogpoo-trueend 16 trunk-food+ goblin-laugh ;
: common4-dogpoo-reward 16 trunk-food+ badfx ;

cyoa-choice: common4-dogpoo-ignore
	($ Thegroup .. "  ignora a caixa de suprimentos e toda a comida dentro dela." .. gThey .. "  encontrar" .. g1s .. "  algumas latas de comida enquanto vasculha o resto da casa, mas deve se perguntar o que " .. gthey .. "  perdeu." $) text!
	2 trunk-food+ goodfx
;

cyoa-choice: common4-dogpoo-pick

	cdog? if
		($ "O coco do cachorro desaparece misteriosamente antes de " .. cname .. "  cutuca a caixa para abri la!" .. cname .. "  parece realmente culpado." $) text!
		morale--
		common4-dogpoo-reward
	then;
	ccat? if
		($ cname .. "  parece especialmente irritado por ter que afastar o coco do cachorro para abrir a caixa. " .. cHe .. "  realmente sente vontade de machucar alguma coisa, mais do que o normal." $) text!
		morale4-
		common4-dogpoo-reward
	then;
	cpet? if
		($ cname .. "  chuta a caixa de suprimentos para abri la. Como um animal, " .. che .. " ' realmente nao se importa com coco de cachorro, entao nao foi um grande obstaculo." $) text!
		16 trunk-food+ goodfx
	then;
	dogpal? if
		($ cname .. "  adora cachorros e nao e estranho pegar coco de cachorro e coloca lo em um saco plastico. Na verdade, isso lembra " .. cname .. "  da civilizacao. Sentindo aquele calor na sua mao, sentindo se vivo!" $) text!
		morale2+ 16 trunk-food+ goodfx
	then;
	cchar oblivious? if
	($ cname .. "  abre a caixa de suprimentos. O que era tao especial?		" .. cHe .. "  nunca percebeu o fezes do cao." $) text!
		solo if
			goodfx
		else
			($ Everyone .. "  se envergonha!" $) +crtext cchar morale2-all-except badfx
		then 16 trunk-food+
	then;
	($ cname .. "  tenta empurrar o fezes fora da caixa com uma peca de madeira, mas isso vai TERRIBILMENTE errado. " .. cHe .. "  se envergonha muito" $) text!
		cattitude+ if
			badfx
			($ cHe .. "  tentar manter um atitude positiva. Um dia, " .. cname .. "  podera olhar para isso e rir. Mas hoje nao." $)
		else
			($ "Isso e, sem duvida, a pior coisa que aconteceu com " .. cname .. "  no apocalipse zombie. " .. cHe .. "  nunca sera o mesmo." $) +crtext
			($ cname .. ":" pbold .. "  PENALIDADE MORALE PERMANENTE!" fbold $)
			-1 STAT_MORALE cchar .bonus+ sfx{ creepy }
		then +crtext morale2- 16 trunk-food+
	
;

cyoa-bridge: common4-dogpoo-stick
	"VOCE GANHOU" cyoa-title! end-music
	"'Toda crianca sabe que o unico jeito infalivel de mover um pedaco de coco de cachorro e usar um galho de arvore. E o removedor de coco perfeito da natureza. Voce canalizou seu senso de fantasia e admiracao infantil para triunfar sobre essa situacao perigosa." text!
	"Parabens!" rainbold +crtext
	"'VOCE ENCONTROU O VERDADEIRO FINAL PARA DEATH ROAD TO CANADA!" fbold +crtext
	choice( "Eu consegui!" )choice: common4-dogpoo-trueend
;

cyoa-bridge: common4-dogpoo-open
	solo if
		soloer choosechar common4-dogpoo-pick
	then;
	($ "Quem deve abrir a caixa?" .. "		AVISO: ISSO E REALMENTE NOJENTO		HA UM COCO DE CACHORRO NA CAIXA" rbold .. "		ISSO PODE SER UM GRANDE PROBLEMA" fbold $) text!
	' common4-dogpoo-pick cyoa-team-choice
;

cyoa: common4-dogpoo
	"VERDADEIRO DESESPERO" cyoa-title! picon!
	($ "Como " .. thegroup .. "  procura uma casa abandonada, um milagre acontece. " .. gThey .. "  encontrar" .. g1s .. "  uma caixa de racao do governo, fechada." $) text!
	($ "Para " .. ghis .. "  HORROR" rbold .. ", acontece que ha um " .. "coco de cachorro" fbold .. "  sentado " .. " BEM NA MACANETA" rbold .. "  da caixa de suprimentos." .. "  Nao tem como abrir sem encostar no coco." rbold $) +crtext
	WEAPON_TREEBRANCH roadtrip-has-weapon? not if
		" Este pode ser o maior desafio ate agora!" +crtext
	then
		choice( " De jeito nenhum, nojento, ignora" )choice: common4-dogpoo-ignore
		solo if
			soloer charchoice( "  abre" )choice: common4-dogpoo-open
		else
			choice( " Pede para alguem abrir" )choice: common4-dogpoo-open
		then
		WEAPON_TREEBRANCH roadtrip-has-weapon? if
			choice( " Usa galho de arvore para abrir" )choice: common4-dogpoo-stick
		then
;

// Losing a sneeze

cyoa-choice: common4-sneeze-try
	($ cname .. " ' nao consegue espirrar quando quer. " .. cHe .. " ' perde o espirro! E muito chato!" $) text!
	morale--
	cattitude- if
		($ " Esta e a " .. " pior coisa que pode acontecer com ALGUEM" rbold .. "  no zumbocalipse!" $) +crtext morale-- attitudeREV
	then
;

cyoa-choice: common4-sneeze-smash
	bitefx
	cchar .trait@ "BERSERK!" $= if
		($ cname .. "  grita " .. "BERSERKEERRR!" fbold .. "  e entao soca " .. chim .. "no nariz, o mais forte possivel. Nenhum instinto de autopreservacao." $) text!
		ouch--
	then;
	($ cname .. "  esmaga " .. chis .. "  nariz proprio em uma parede." $) text!
	sting--
	($ "'E um truque antigo! A dor cegante faz " .. cname .. "  esquecer do espirro perdido." $) +crtext
;

cyoa-choice: common4-sneeze-animal
	cchar to victim
	somepet if choosechar then
	($ vname .. "  realmente enfia " .. vhis .. "  nariz em " .. cname .. "  e bufa.	Isso provoca um bom espirro!" $) text! goodfx
;

cyoa-choice: common4-sneeze-spicy
	($ cname .. "  abre um saco de batatas fritas super picantes. " .. cHe .. "  da uma cheirada forte." $) text!
	($ cname .. "  espirra acidentalmente no saco! 	" .. cHe .. "  tambem pode comer o saco inteiro de batatas fritas agora." $) +crtext
	-2 trunk-food+
	morale++
;

cyoa: common4-sneeze
	"O ESPIRRO PERDIDO" cyoa-title! choosehuman cicon
	($ cname .. "  realmente sente que " .. che .. " ' tem que espirrar. Esse sentimento se arrasta, mas nao ha espirro. Isso pode se transformar em uma daquelas vezes em que um espirro e perdido! Combinado com as pressoes do apocalipse zumbi, isso pode quebrar " .. cname .. "''s moral!" $) text!
		choice( "Tente espirrar" )choice: common4-sneeze-try
		choice( "Esmague se no nariz" )choice: common4-sneeze-smash
		numpets 0 > if choice( "Farejar pelos de animais" )choice: common4-sneeze-animal then
		trunk-food 1 > if choice( "Farejar comida picante   Custa 2 alimentos" )choice: common4-sneeze-spicy then
;

// Game Bronus Entertainment System

: common4-gamebro-ender
	($ "Este jogo e muito dificil! " .. "AAAAAAAAAARGH!" fbold $) +crtext
	($ cname .. "  joga o GameBronus com forca, destruindo o." $) +crtext
	cattitude- if morale3- attitudeREV else morale2- then badfx
;

cyoa-choice: common4-gamebro-shoot
	($ "Esquadrao Drobnus" bbold .. "  e um jogo sobre Drobnus explodindo alienigenas com uma metralhadora. A maioria das mortes acontece devido a controles de salto ruins misturados com pocos de morte instantanea." $) text!
	shooting++
	common4-gamebro-ender
;

cyoa-choice: common4-gamebro-med
	($ "Dr. Drobnus Deluxe" bbold .. "  e um simulador de cirurgia excessivamente detalhado, renderizado em graficos de 8 bits. A maioria dos jogadores perde por nao conseguir usar as luvas cirurgicas esterilizadas corretamente." $) text!
	medical++
	common4-gamebro-ender
;

cyoa-choice: common4-gamebro-mech
	($ "Drobnus Kart" bbold .. " ' e um jogo de corrida estrelado pelo popular personagem Drobnus e todos os seus amigos. Mesmo se voce estiver em primeiro lugar, voce pode ser atingido por um Vaso Sanitario Teleguiado e acabar em ultimo lugar." $) text!
	mechanic++
	common4-gamebro-ender
;

cyoa-bridge: common4-gamebro-med-pick
	solohuman if
		choosehuman common4-gamebro-med
	then;
	($ "Quem deve jogar " .. "Dr. Drobnus Deluxe" bbold .. "?" $) cyoa-text!
	"'AVISO: Vai ser duro para o moral!" rbold +crtext
	' common4-gamebro-med cyoa-human-choice
;

cyoa-bridge: common4-gamebro-mech-pick
	solohuman if
		choosehuman common4-gamebro-mech
	then;
	($ "Quem deve jogar " .. "Drobnus Kart" bbold .. "?" $) cyoa-text!
	"'AVISO: Vai ser duro para o moral!" rbold +crtext
	' common4-gamebro-mech cyoa-human-choice
;

cyoa-bridge: common4-gamebro-shoot-pick
	solohuman if
		choosehuman common4-gamebro-shoot
	then;
	($ "Quem deve jogar " .. "Esquadrao Drobnus" bbold .. "?" $) cyoa-text!
	"'AVISO: Vai ser duro para o moral!" rbold +crtext
	' common4-gamebro-shoot cyoa-human-choice
;

cyoa: common4-gamebro
	"GameBronus Entertainment System" cyoa-title! someicon
	($ Thegroup .. "  encontra um velho " .. "GameBronus [TM]" bbold .. "' E um videogame portatil que sobreviveu por tanto tempo porque e construido como um tijolo. Houve alguns jogos com ele, todos conhecidos por sua " .. "dificuldade extremamente frustrante:" rbold $) text!
		choice( "Deixe o GameBronus para tras" )choice
		choice( "Dr. Drobnus Deluxe" )choice: common4-gamebro-med-pick
		choice( "Drobnus Kart" )choice: common4-gamebro-mech-pick
		choice( "Esquadrao Drobnus" )choice: common4-gamebro-shoot-pick
;

// Baked Beans on Garbage Mountain

: common4-beans-ender
	sting--
	($ "Lixo se espalha por todo " .. cname .. "! Valeu a pena! Foi tudo pelos feijoes!" $) +crtext
	morale2- 3 trunk-food+ badfx
	
;

cyoa-choice: common4-beans-eat
	($ cname .. " " .. chim .. ".	Que solucao deliciosa!" $) text!
	morale2+ 3 trunk-food+ goodfx
;

cyoa-choice: common4-beans-smash
	($ cname .. " smashes the garbage pile! It collapses right onto " .. chim .. "." $) text!
	strength++ common4-beans-ender
;

cyoa-choice: common4-beans-climb
	($ cname .. " " .. chim .. "  no assunto." $) text!
	fitness++ common4-beans-ender
;

cyoa-bridge: common4-beans-smash-pick
	solohuman if
		choosehuman common4-beans-smash
	then;
	($ "Quem deve lutar contra a pilha de lixo?" $) cyoa-text!
	' common4-beans-smash cyoa-human-choice
;

cyoa-bridge: common4-beans-climb-pick
	solo if
		soloer choosechar common4-beans-climb
	then;
	($ "Quem deve escalar a pilha de lixo?" $) cyoa-text!
	' common4-beans-climb cyoa-team-choice
;

cyoa-bridge: common4-beans-eat-pick
	solopet if
		choosepet common4-beans-eat
	then;
	($ "Quem deve comer a pilha de lixo?" $) cyoa-text!
	' common4-beans-eat cyoa-pet-choice
;

cyoa: common4-beans
	"A Batalha da Montanha de Lixo" cyoa-title! someicon
	($ Thegroup .. " ' avista uma pilha de lixo extra alta perto de alguns predios. Tem talvez mais de 12 pes de altura. Jogar lixo ficou muito ruim perto do colapso da civilizacao. Um " .. "'Lata Extra Grande de Feijao Assado" rbold .. "  esta preso perto do topo da pilha. Brilha como um farol de esperanca." $) text!
		choice( "Deixe o sozinho" )choice
		choice( "Esmagar pilha de lixo" )choice: common4-beans-smash-pick
		choice( "Escalar pilha de lixo" )choice: common4-beans-climb-pick
		numpets 0 > if choice( "Comer pilha de lixo" )choice: common4-beans-eat-pick then
;

// Old VHS tape

: common4-vhs-end
	morale++all
	($ "A TV da um curto apos o filme e e deixada para tras." +crtext
;

: sumo-vhs args( char )
	<- char ispet? if 
		<- char dog? if
			($ <- char .name@ .. "  aprende sumo, apesar de ser um cachorro." $) +crtext
			WEAPON_SUMO <- char .weapon_default!
		then
	then;	
	<- char .weapon_default@ WEAPON_SUMO = if
		($ <- char .name@ .. "  aprende tecnicas superiores de sumo!" $) +crtext
		WEAPON_SUPERSUMO <- char .weapon_default!
	else WEAPON_SUMO <- char .weapon_default! then
;

cyoa-blank: common4-vhs-sumo
	"Sumo Wrestling estava no top 5 dos esportes na America antes do zombocalipse. Isso levou a muitos filmes de sumo, dramas de TV de sumo, desenhos animados de sumo, e assim por diante." text!
	($ Thegroup .. " pega uns golpes de sumo assistindo ao filme!" $) +crtext
	' sumo-vhs foreach-team-char common4-vhs-end
;

cyoa-blank: common4-vhs-shoot
	($ "O filme e uma adaptacao do jogo de fliperama " .. "DESTRUICAO EXPLOSIVA!" fbold .. " Antes um filme ruim do que nenhum filme." $) text!
	
	' .shooting++ foreach-team-char common4-vhs-end
;

cyoa-blank: common4-vhs-fit
	($ Thegroup .. " segue o instrutor de ginastica no video. Todos estao usando faixas de suor neon." $) text!
	
	' .fitness++ foreach-team-char common4-vhs-end
;

cyoa-blank: common4-vhs-morale
	($ "O filme de comedia acaba sendo pessimo para a maioria dos padroes, mas " .. thegroup .. " ' e completamente absorvido por ele. E muito raro ver um filme hoje em dia." $) text!	morale3+all
	"A TV desliga no meio da segunda exibicao e nunca mais volta a ligar." +crtext
;

cyoa: common4-vhs-rare
	"Video Cassette Player" cyoa-title!
	($ Thegroup .. " ' encontra o esconderijo abandonado de alguem em uma casa fechada.	Nao sobrou muita coisa aqui, exceto uma pequena TV movida a bateria conectada a um antigo videocassete.	Ha apenas um filme no videocassete:" $) text!
		choice( "Filme de acao" )choice:  common4-vhs-shoot
		choice( "Fita de ginastica" )choice: common4-vhs-fit
		choice( "Filme de comedia" )choice: common4-vhs-morale
		choice( "Filme de sumo" )choice: common4-vhs-sumo
;

// Spider attack based on a true story where I ate a spider

cyoa-choice: common4-spider-run
	($ Thegroup .. "  foge daquela casa!	A aranha continua caminhando lentamente pelas paredes de uma maneira assustadora e nojenta." $) text!
	morale--all badfx
;

cyoa-choice: common4-spider-smoosh
	cchar .trait@ "BERSERK!" $= if
		($ cname .. "  grita " .. "BERSERKEERRR!" fbold .. "  e entao " .. "OBLITERA" fbold .. "  a aranha. Resta apenas uma fumaca dela." $) text!
		morale2+ goodfx
	else
		($ cname .. " ' esmaga a aranha. Ha um barulho particularmente nojento de trituracao." $) text!
		cattitude+ if morale-- else morale2- then badfx
	then
		($ Thegroup .. "  procura pela casa e encontra comida!" $) +crtext 2 5 rnd trunk-food+
;

cyoa-bridge: common4-spider-smoosh-pick
	solohuman if
		choosehuman common4-spider-smoosh
	then;
	($ "Quem deveria esmagar a aranha?" $) cyoa-text!
	' common4-spider-smoosh cyoa-human-choice
;

// Gourmand logic below, also used in a bandit event but not enough to move and untangle dependencies
: isgourmand? .trait@ "Gourmand" $= ;
: numgourmandfilt args( chara ) <- chara isgourmand? ;
: numgourmands team-stack ' numgourmandfilt stack-filter nip ;

: gourmandcheck
	checkchar isgourmand?
;

: gourmandfilt ' isgourmand? stack-filter drop ;
: cyoa-gourmand-choice team-stack gourmandfilt cyoa-char-stack-choice ;

cyoa-choice: common4-spider-eat
	($ cname .. " ' olha em volta para se certificar de que ninguem esta olhando, entao rapidamente come a aranha.	E crocante." $) text! goodfx
	morale++
	cchar isgourmand? if
		($ "I was hungry..." pbold .. "
		-- " .. cname $) +crtext
	then
;

cyoa-bridge: common4-spider-eat-pick
	solopet numgourmands 1 < and if
		choosepet common4-spider-eat
	then;
	($ "Quem deveria comer a aranha?" $) cyoa-text!
	' common4-spider-eat cyoa-pet-choice
	' common4-spider-eat cyoa-gourmand-choice
;

cyoa: common4-spider
	"Ataque de Aranha Gigante" cyoa-title! someicon
	($ Thegroup .. "  tenta descansar em uma casa mas e emboscado por uma " .. "aranha gigante!" fbold $) text!
	($ "'Bem talvez seja uma aranha normal mas e de aparencia nojenta. A aranha esta drenando rapidamente " .. thegroup .. "''s moral!" $) +crtext
		choice( "Corra de nojo" )choice: common4-spider-run
		choice( "Esmagar aranha" )choice: common4-spider-smoosh-pick
		numpets 0 > numgourmands 0 > or if choice( "Comer Aranha" )choice: common4-spider-eat-pick then
		// choosepet cchar charchoice( ": Comer Aranha"
;

// Strange Vending Machine

0 value common4-vending-num

cyoa-choice: common4-vending-go
	"Demora uma eternidade para alimentar as notas de dolar. A maquina de venda automatica continua cuspindo os." text!
	cattitude+ if morale-- attitudeREV else morale2- then goodfx
	common4-vending-num case 
	1 of 
		"Uma espingarda totalmente carregada sai da maquina de venda automatica!" +crtext
		1 WEAPON_SHOTGUN trunk.weapon+ 6 trunk-shotgun-shell+
	endof
	2 of 
		"Um rifle de cauboi carregado sai da maquina de venda automatica!" +crtext
		1 WEAPON_RIFLE trunk.weapon+ 10 trunk-rifle-shell+
	endof
	3 of 
		"Um revolver Dolt carregado sai da maquina de venda automatica!" +crtext
		1 WEAPON_DOLT trunk.weapon+ 6 trunk-bullet+
	endof
	4 of 
		"'Um saco de batatas fritas cai! Nao e o tamanho usual de maquina de venda automatica, e o tamanho enorme da familia." +crtext 4 trunk-food+
	endof
	5 of 
		"Uma lata de gasolina cai da maquina de venda automatica!" +crtext 30 trunk-gas+
	endof
	6 of 
		"Uma grande caixa de bandagens medicinais sai da maquina de venda automatica!" +crtext 3 trunk-medical+
	endof 
	7 of 
		($ "The vending machine's shape changes! It transforms into an electric car!
			" .. Thegroup .. "  entra." $) +crtext CAR_HYBRID2 get-car 30 trunk-gas+
	endof
	8 of 
		"'Um graveto cai da maquina de venda automatica! Nao tenho certeza do que voce estava esperando, e um graveto." +crtext
		1 WEAPON_TREEBRANCH trunk.weapon+
	endof
	9 of 
		($ "Um chapeu sai da maquina de venda automatica!" .. cname .. "  coloca o." $) +crtext
		1 27 rnd cchar .hattype! morale++
	endof 
	endcase
;

cyoa-bridge: common4-vending-pick
	solohuman if
		choosehuman cicon
		($ cname .. "  vai ter que colocar as notas rasgadas e amassadas na maquina de venda automatica. Isso sera irritante." $) text!
		cchar charchoice( ": Insira notas" )choice: common4-vending-go
		choice( "Deixe o sozinho" )choice
	then;
	($ "Quem deve colocar as notas de um dolar quase podres na maquina?" $) cyoa-text!
	"AVISO: Este sera um processo muito irritante" rbold +crtext
	' common4-vending-go cyoa-human-choice
	choice( "Deixe a sozinha depois de tudo" )choice
;

: common4-vending-1 1 to common4-vending-num common4-vending-pick ;
: common4-vending-2 2 to common4-vending-num common4-vending-pick ;
: common4-vending-3 3 to common4-vending-num common4-vending-pick ;
: common4-vending-4 4 to common4-vending-num common4-vending-pick ;
: common4-vending-5 5 to common4-vending-num common4-vending-pick ;
: common4-vending-6 6 to common4-vending-num common4-vending-pick ;
: common4-vending-7 7 to common4-vending-num common4-vending-pick ;
: common4-vending-8 8 to common4-vending-num common4-vending-pick ;
: common4-vending-9 9 to common4-vending-num common4-vending-pick ;

cyoa: common4-vending
	"Maquina de Venda Estranha" cyoa-title! someicon
	($ Thegroup .. " corre para uma maquina de vending muito fora de lugar. Ela tem o aspecto de ter sido feita de pedacos de ferro de lixo. Suas luzes ainda estao acesas.	Tudo que esta no maquina custa $20, e " .. thegroup .. " tem aproximadamente isso em bilhetes amassados e desgastados." $) text!
		choice( "Deixe o sozinho" )choice
		
		33 %chance if
			choice( "PAQUETE DE DIVERSAO DE FUSILLAO" )choice: common4-vending-1
		else
			50 %chance if
				choice( "FUSIL RACIO COM TOOTHPIECE" )choice: common4-vending-2
			else
				choice( "CONJUNTO DE REVOLVER REAL" )choice: common4-vending-3
			then
		then
		
		33 %chance if
			choice( "SACO DE BATATA DOCES" )choice: common4-vending-4
		else
			50 %chance if
				choice( "CANETA DE GASOLINA" )choice: common4-vending-5
			else
				choice( "'CAIXA DE FAIXINHAS" )choice: common4-vending-6
			then
		then
		
		10 %chance if
			choice( "UM CARRO NOVO!" )choice: common4-vending-7
		else
			50 %chance if
				choice( "UMA PALITA" )choice: common4-vending-8
			else
				choice( "UM NOVO SOMBRERO" )choice: common4-vending-9
			then
		then		
;

} }

// Blank
(*
cyoa-choice: common4--

;

cyoa: common4-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)
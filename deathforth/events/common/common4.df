
(*

- ouija board event "Weegee"
	- Pick a question to ask
	- Different if solo
		- AM I PRETTY? [yes] Morale Boost
		- WHERE'S THE LOOT? Shows hidden supplies nearby
		- 

	
*)

cyoa{ road{ 

// Not a Ouija Board

: weegeetm .. "Weegee Board [tm]" pbold .. ;
: weegeecurse args( char ) 13 <- char .seed! ;

: weegee-end
	($ Thegroup .. "  fuhlt sich jetzt unglaublich unglucklich. Hute dich vor dem " .. "Fluch von Weegee!" rbold $) +crtext
	"GLUCK MASSIV GESENKT!" fbold +crtext
	"Irgendwo in der Ferne lacht ein Goblin." pbold +crtext goblin-laugh
	
;

cyoa-choice: common4-weegee-pretty
	choosehuman cicon
	($ "Der " weegeetm "  buchstabiert das " .. cname .. "  ist " .. "	V E R Y P R E T T Y" pbold $) text!
	($ "Schon! " .. cname .. " ' Fuhlt sich uberhaupt nicht verflucht von diesem entzuckenden Weegee." $) +crtext
	morale2+
	"Somewhere in the distance, a goblin laughs, but in a nice way. As if to say 'hey.... what's up. have a nice day.'" pbold +crtext goblin-laugh
	choice( "nett" )choice
;

cyoa-choice: common4-weegee-loot
	($ "Der " weegeetm "  buchstabiert den Ort eines versteckten Beutevorrats im Haus!" $) text!
	homesearch1
	weegee-end
;

cyoa-choice: common4-weegee-check
	weaselest choosechar cicon
	($ "Als gefragt wird, wer die Gruppe verraten wird, " weegeetm "  buchstabiert " $) text!
	weaselest-pass if
		($ cname .. "'s Name!" $) +text
		loyaltyREV
		weegee-end
	else
		"Y O U R B U T T" pbold +crtext
		($ "Was konnte das bedeuten? Die Gruppe wird unruhig und Zweifel keimen. Konnte dies der " .. "Fluch von Weegee?" rbold $) +crtext
		morale--all goblin-laugh
	then
;

cyoa-choice: common4-weegee-canada
	($ "Der " weegeetm "  zeigt auf das Wort " .. "NEIN" pbold .. "  mit erstaunlicher Geschwindigkeit. Dann zeigt es auf " .. "NEIN" fbold .. "  wieder, wahrend es irgendwie Feuer fangt." $) text!
	($ "Was auch immer! Diese Dinge sind sowieso immer falsch! " .. cname .. "  wirft das brennende " weegeetm "  durch den Raum. Das macht " .. chim .. "  fangt auch Feuer." $) +crtext
	cchar isfireproof? if 
		($ cname .. "  liebt es feuerfest zu sein!" $) +crtext
		morale++ goodfx
	else
		ouch-- alive? if bitefx then
	then
	alive? if
		"Es war zumindest eine gute Ubung." +crtext fitness++
	else
		($ "'Leg dich nicht mit dem " .. "Fluch von Weegee!" rbold $) +crtext goblin-laugh
	then
	
;

cyoa: common4-weegee
	"Weegee Board" cyoa-title! choosehuman cicon
	($ Thegroup .. "  stobert in einem verlassenen Haus herum und findet ein " weegeetm " ' auf dem Kuchentisch. Es ist ein gangiges Spiel das verwendet wird um mit ' zu kommunizieren " .. "GEISTE" fbold $) text!
		choice( "LASS DAS NONSENSE AUFSEHEN" )choice
		($ "Diese Dinge sind gerugt um " .. "AUF UNSCHABig WUNSCHKRANK MIT SCHLECHTER GLUCK" rbold .. ", so beware! Still, it may be worth asking it a question..." $) +crtext
		solohuman if
			choice( "BIN ICH SCHON?" )choice: common4-weegee-pretty
		else
			choice( "WER VERRATET DIE MANNESCHAFT?" )choice: common4-weegee-check
		then
			choice( "WILL" .. I-or-WE .. "KOMMT ES NACH KANADA?" )choice: common4-weegee-canada
			choice( "'WO IST DAS BEUTEL?" )choice: common4-weegee-loot
;

// Dog Poop

: common4-dogpoo-trueend 16 trunk-food+ goblin-laugh ;
: common4-dogpoo-reward 16 trunk-food+ badfx ;

cyoa-choice: common4-dogpoo-ignore
	($ Thegroup .. " verneigt sich vor dem Vorratskasten und alles Essen darin hinein.	" .. gThey .. " findet" .. g1s .. " ein paar Dosen Nahrung wahrend der Suche im Rest des Hauses, aber muss fragen was " .. gthey .. " verpasst hat." $) text!
	2 trunk-food+ goodfx
;

cyoa-choice: common4-dogpoo-pick

	cdog? if
		($ "Der Hundehaufen verschwindet auf mysteriose Weise bevor " .. cname .. "  die Kiste aufstot!		" .. cname .. "  sieht wirklich schuldig aus." $) text!
		morale--
		common4-dogpoo-reward
	then;
	ccat? if
		($ cname .. "  scheint besonders wutend daruber zu sein dass es den Hundehaufen aus dem Weg schlagen musste um die Kiste zu offnen. " .. cHe .. "  hat wirklich Lust, etwas zu verprugeln, mehr als sonst." $) text!
		morale4-
		common4-dogpoo-reward
	then;
	cpet? if
		($ cname .. "  tritt die Versorgungsbox auf. Als Tier, " .. che .. " ' kummert sich wirklich nicht um Hundehaufen, daher war es kein groes Hindernis." $) text!
		16 trunk-food+ goodfx
	then;
	dogpal? if
		($ cname .. "  liebt Hunde und ist es gewohnt, Hundehaufen aufzuheben und in eine Plastiktute zu stecken.		Tatsachlich erinnert " .. cname .. "  an die Zivilisation. Diese Warme in deiner Hand zu spuren, sich lebendig zu fuhlen!" $) text!
		morale2+ 16 trunk-food+ goodfx
	then;
	cchar oblivious? if
	($ cname .. "  offnet die Versorgungsbox. Was war die groe Sache?		" .. cHe .. "  hat den Hundehaufen nie bemerkt." $) text!
		solo if
			goodfx
		else
			($ Everyone .. "  ist angewidert!" $) +crtext cchar morale2-all-except badfx
		then 16 trunk-food+
	then;
	($ cname .. "  versucht, den Kot mit einem Stuck Holz von der Schachtel zu stoen, aber es geht SCHRECKLICH schief. " .. cHe .. "  ekelt sich ziemlich schlimm!" $) text!
		cattitude+ if
			badfx
			($ cHe .. " " .. cname .. " heute nicht." $)
		else
			($ "In deutsch Dies ist bei weitem das Schlimmste, was passieren kann " .. cname .. " in der Zombie Apokalypse.  " .. cHe .. " wird nie, nie, nie wieder das gleiche sein." $) +crtext
			($ cname .. ":" pbold .. " PERMANENTER MORALABZUG!" fbold $)
			-1 STAT_MORALE cchar .bonus+ sfx{ creepy }
		then +crtext morale2- 16 trunk-food+
	
;

cyoa-bridge: common4-dogpoo-stick
	"DU HAST GEWONNEN" cyoa-title! end-music
	"'Jedes Kind wei dass der einzig sichere Weg ein Stuck Hundekacke zu bewegen darin besteht einen Stock von einem Ast zu verwenden. Es ist der perfekte Kackeentferner der Natur. Du hast deinen Sinn fur Laune und kindliche Verwunderung kanalisiert um diese gefahrliche Situation zu meistern." text!
	"Herzlichen Gluckwunsch!" rainbold +crtext
	"'DU HAST DAS WAHRE ENDE VON DEATH ROAD TO CANADA GEFUNDEN!" fbold +crtext
	choice( "Ich habe es geschafft!" )choice: common4-dogpoo-trueend
;

cyoa-bridge: common4-dogpoo-open
	solo if
		soloer choosechar common4-dogpoo-pick
	then;
	($ "Wer soll die Kiste offnen?" .. "		WARNUNG: DAS IST WIRKLICH EKLIG		ES GIBT EINE HUNDEKACKE AUF DER KISTE" rbold .. "		DAS KONNTE EINE GROSSE SACHE SEIN" fbold $) text!
	' common4-dogpoo-pick cyoa-team-choice
;

cyoa: common4-dogpoo
	"ECHTE VERZWEIFLUNG" cyoa-title! picon!
	($ "Als " .. thegroup .. "  durchsucht ein verlassenes Haus, ein Wunder geschieht. " .. gThey .. " findet" .. g1s .. "  eine staatliche Essensration, ungeoffnet." $) text!
	($ "Zu " .. ghis .. "  HORROR" rbold .. ", es stellt sich heraus, dass es einen " .. "hundekacke" fbold .. " sitzend " .. "RECHTS AM GRIFF" rbold .. "  der Versorgungsbox." .. "  Es gibt keine Moglichkeit sie zu offnen ohne die Kacke zu beruhren." rbold $) +crtext
	WEAPON_TREEBRANCH roadtrip-has-weapon? not if
		"Dies konnte die grote Herausforderung sein, uberhaupt!" +crtext
	then
		choice( "Auf keinen Fall, ekelhaft, ignoriere es" )choice: common4-dogpoo-ignore
		solo if
			soloer charchoice( "  offnet sie" )choice: common4-dogpoo-open
		else
			choice( "Lass sie von jemandem offnen" )choice: common4-dogpoo-open
		then
		WEAPON_TREEBRANCH roadtrip-has-weapon? if
			choice( "Verwende einen Ast um sie zu offnen" )choice: common4-dogpoo-stick
		then
;

// Losing a sneeze

cyoa-choice: common4-sneeze-try
	($ cname .. " ' kann nicht auf Kommando niesen " .. cHe .. " ' verliert das Niesen! Es ist sehr argerlich!" $) text!
	morale--
	cattitude- if
		($ "Das ist der " .. "schlimmste was JEDEM passieren kann" rbold .. "  in der Zomboka Lyopse!" $) +crtext morale-- attitudeREV
	then
;

cyoa-choice: common4-sneeze-smash
	bitefx
	cchar .trait@ "BERSERKER!" $= if
		($ cname .. "  schreit " .. "BERSERKEERRR!" fbold .. "  und schlagt sich dann " .. chim .. " selbst so hart wie moglich auf die Nase. Kein Selbsterhaltungstrieb uberhaupt." $) text!
		ouch--
	then;
	($ cname .. "  zerschmettert " .. chis .. "  eigene Nase an einer Wand." $) text!
	sting--
	($ "'Es ist ein alter Trick! Der blendende Schmerz lasst " .. cname .. "  das verlorene Niesen vergessen." $) +crtext
;

cyoa-choice: common4-sneeze-animal
	cchar to victim
	somepet if choosechar then
	($ vname .. "  stopft sich richtig " .. vhis .. "  nase an " .. cname .. "  und schnaubt. Das lockt ein gutes Niesen hervor!" $) text! goodfx
;

cyoa-choice: common4-sneeze-spicy
	($ cname .. "  offnet eine Tute super scharfer Chips. " .. cHe .. "  nimmt einen groen Schnupfen." $) text!
	($ cname .. "  niest versehentlich in die Tute! 	" .. cHe .. "  kann jetzt auch gleich die ganze Tute Chips essen." $) +crtext
	-2 trunk-food+
	morale++
;

cyoa: common4-sneeze
	"DAS VERLORENE NIESEN" cyoa-title! choosehuman cicon
	($ cname .. " fuhlt sich wirklich so dass " .. che .. " " .. cname .. "''s Moral!" $) text!
		choice( "Versuche zu niesen" )choice: common4-sneeze-try
		choice( "Schlage dir selbst auf die Nase" )choice: common4-sneeze-smash
		numpets 0 > if choice( "Tierhaare schnupfen" )choice: common4-sneeze-animal then
		trunk-food 1 > if choice( "Scharfes Essen schnupfen   Kostet 2 Essen" )choice: common4-sneeze-spicy then
;

// Game Bronus Entertainment System

: common4-gamebro-ender
	($ "Dieses Spiel ist viel zu schwierig " .. "AAAAAAAAAARGH!" fbold $) +crtext
	($ cname .. "  wirft das GameBronus mit voller Wucht zu Boden und zerstort es." $) +crtext
	cattitude- if morale3- attitudeREV else morale2- then badfx
;

cyoa-choice: common4-gamebro-shoot
	($ "Drobnus Squadron" bbold .. "  ist ein Spiel in dem Drobnus mit einem Maschinengewehr auf Aliens schiet. Die meisten Tode werden durch schlechte Sprungsteuerung in Kombination mit todlichen Abgrunden verursacht." $) text!
	shooting++
	common4-gamebro-ender
;

cyoa-choice: common4-gamebro-med
	($ "Dr. Drobnus Deluxe" bbold .. "  ist eine ubermaig detaillierte Chirurgie Simulation, die in 8 Bit Grafik gerendert wird. Die meisten Spieler scheitern, weil sie die richtige sterile Handschuhtechnik nicht beherrschen." $) text!
	medical++
	common4-gamebro-ender
;

cyoa-choice: common4-gamebro-mech
	($ "Drobnus Kart" bbold .. " ' ist ein Rennspiel mit dem beliebten Charakter Drobnus und all seinen Freunden. Selbst wenn du auf dem ersten Platz liegst, kannst du von einer zielsuchenden Toilette getroffen werden und auf dem letzten Platz landen." $) text!
	mechanic++
	common4-gamebro-ender
;

cyoa-bridge: common4-gamebro-med-pick
	solohuman if
		choosehuman common4-gamebro-med
	then;
	($ "Wer sollte " .. "Dr. Drobnus Deluxe" bbold .. "?" $) cyoa-text!
	"'WARNUNG: Das wird dem Moral schaden!" rbold +crtext
	' common4-gamebro-med cyoa-human-choice
;

cyoa-bridge: common4-gamebro-mech-pick
	solohuman if
		choosehuman common4-gamebro-mech
	then;
	($ "Wer sollte " .. "Drobnus Kart" bbold .. "?" $) cyoa-text!
	"'WARNUNG: Das wird dem Moral schaden!" rbold +crtext
	' common4-gamebro-mech cyoa-human-choice
;

cyoa-bridge: common4-gamebro-shoot-pick
	solohuman if
		choosehuman common4-gamebro-shoot
	then;
	($ "Wer sollte " .. "Drobnus Squadron" bbold .. "?" $) cyoa-text!
	"'WARNUNG: Das wird dem Moral schaden!" rbold +crtext
	' common4-gamebro-shoot cyoa-human-choice
;

cyoa: common4-gamebro
	"GameBronus Entertainment System" cyoa-title! someicon
	($ Thegroup .. "  findet einen alten " .. "GameBronus [TM]" bbold .. "'	Das ist ein tragbares Videospielsystem, das so lange uberlebt hat, weil es so robust wie ein Ziegelstein ist.	Es gab ein paar Spiele dafur, die alle fur ihren " .. "extrem frustrierenden Schwierigkeitsgrad bekannt sind:" rbold $) text!
		choice( "GameBronus zurucklassen" )choice
		choice( "Dr. Drobnus Deluxe" )choice: common4-gamebro-med-pick
		choice( "Drobnus Kart" )choice: common4-gamebro-mech-pick
		choice( "Drobnus Squadron" )choice: common4-gamebro-shoot-pick
;

// Baked Beans on Garbage Mountain

: common4-beans-ender
	sting--
	($ "Mull landet uberall auf " .. cname .. "! Es war es wert! Das alles war fur die Bohnen!" $) +crtext
	morale2- 3 trunk-food+ badfx
	
;

cyoa-choice: common4-beans-eat
	($ cname .. "  isst einen Teil des Mullhaufens! Dadurch wird die Bohnendose oben gelockert, ohne dass der ganze Haufen auf " .. chim .. " fallt.	Was fur eine kostliche Losung!" $) text!
	morale2+ 3 trunk-food+ goodfx
;

cyoa-choice: common4-beans-smash
	($ cname .. "  zerschlagt den Mullhaufen! Er sturzt direkt auf " .. chim .. "." $) text!
	strength++ common4-beans-ender
;

cyoa-choice: common4-beans-climb
	($ cname .. "  klettert auf den Mullhaufen! Er sturzt mit " .. chim .. "  darauf ein." $) text!
	fitness++ common4-beans-ender
;

cyoa-bridge: common4-beans-smash-pick
	solohuman if
		choosehuman common4-beans-smash
	then;
	($ "Wer soll den Mullhaufen bekampfen?" $) cyoa-text!
	' common4-beans-smash cyoa-human-choice
;

cyoa-bridge: common4-beans-climb-pick
	solo if
		soloer choosechar common4-beans-climb
	then;
	($ "Wer soll den Mullhaufen besteigen?" $) cyoa-text!
	' common4-beans-climb cyoa-team-choice
;

cyoa-bridge: common4-beans-eat-pick
	solopet if
		choosepet common4-beans-eat
	then;
	($ "Wer soll den Mullhaufen essen?" $) cyoa-text!
	' common4-beans-eat cyoa-pet-choice
;

cyoa: common4-beans
	"Die Schlacht auf dem Mullberg" cyoa-title! someicon
	($ Thegroup .. " 'entdeckt nahe bei einigen Gebauden eine auergewohnlich hohe Mullkippe. Sie konnte uber 12 Fuss hoch sein. Die Verschmutzung hat sich wahrend des Zusammenbruchs der Zivilisation verstarkt. Ein " .. "'Extra Groes Dosen Baked Beans" rbold .. " ist an der Spitze des Haufens steckengeschlagen. Es glanzt wie ein Zeichen der Hoffnung." $) text!
		choice( "Lass es alleine" )choice
		choice( "Mullhaufen zerschmettern" )choice: common4-beans-smash-pick
		choice( "Auf den Mullhaufen klettern" )choice: common4-beans-climb-pick
		numpets 0 > if choice( "Den Mullhaufen essen" )choice: common4-beans-eat-pick then
;

// Old VHS tape

: common4-vhs-end
	morale++all
	($ "Der Fernseher schaltet sich nach dem Film ab und wird zuruckgelassen." +crtext
;

: sumo-vhs args( char )
	<- char ispet? if 
		<- char dog? if
			($ <- char .name@ .. " lernte Sumo, trotzdem ein Hund war." $) +crtext
			WEAPON_SUMO <- char .weapon_default!
		then
	then;	
	<- char .weapon_default@ WEAPON_SUMO = if
		($ <- char .name@ .. "  lernt uberlegene Sumo Techniken!" $) +crtext
		WEAPON_SUPERSUMO <- char .weapon_default!
	else WEAPON_SUMO <- char .weapon_default! then
;

cyoa-blank: common4-vhs-sumo
	"Sumo Ringen war vor der Zombieapokalypse eine der funf beliebtesten Sportarten in Amerika. Dies fuhrte zu vielen Sumo Filmen, Sumo Fernsehdramen, Sumo Cartoons usw." text!
	($ Thegroup .. "  nimmt einige Sumo Zuge auf indem er sich den Film ansieht!" $) +crtext
	' sumo-vhs foreach-team-char common4-vhs-end
;

cyoa-blank: common4-vhs-shoot
	($ "Das Videoband ist eine Filmadaption des Arcade Spiels " .. "TODESEXPLOSION!" fbold .. " ' Es ist besser als gar kein Film." $) text!
	
	' .shooting++ foreach-team-char common4-vhs-end
;

cyoa-blank: common4-vhs-fit
	($ Thegroup .. " ' folgt dem Fitnesstrainer auf dem Videoband. Alle tragen Neon Schweibander." $) text!
	
	' .fitness++ foreach-team-char common4-vhs-end
;

cyoa-blank: common4-vhs-morale
	($ "Der Comedy Film erweist sich nach den meisten Mastaben als schrecklich, aber " .. thegroup .. " ' ist davon vollig begeistert. Es ist sehr selten heutzutage einen Film zu sehen." $) text!	morale3+all
	"Der Fernseher schaltet sich wahrend der zweiten Wiedergabe ab und geht nie wieder an." +crtext
;

cyoa: common4-vhs-rare
	"Videokassettenrekorder" cyoa-title!
	($ Thegroup .. " ' findet in einem vernagelten Haus das verlassene Versteck von jemandem. Hier ist nicht mehr viel ubrig, auer einem kleinen batteriebetriebenen Fernseher, der an einen alten VHS Player angeschlossen ist. Im Videorecorder befindet sich nur ein Film:" $) text!
		choice( "Actionfilm" )choice:  common4-vhs-shoot
		choice( "Fitnessband" )choice: common4-vhs-fit
		choice( "Komodienfilm" )choice: common4-vhs-morale
		choice( "Sumofilm" )choice: common4-vhs-sumo
;

// Spider attack based on a true story where I ate a spider

cyoa-choice: common4-spider-run
	($ Thegroup .. "  rennt aus dem Haus! Die Spinne kriecht weiter langsam und ekelhaft an den Wanden herum." $) text!
	morale--all badfx
;

cyoa-choice: common4-spider-smoosh
	cchar .trait@ "BERSERKER!" $= if
		($ cname .. "  schreit " .. "BERSERKEERRR!" fbold .. "  und dann " .. "VERNICHTET" fbold .. "  die Spinne. Es ist nur noch ein Hauch Rauch von ihr ubrig." $) text!
		morale2+ goodfx
	else
		($ cname .. " ' zerquetscht die Spinne. Es gibt ein besonders ekliges Knirschen." $) text!
		cattitude+ if morale-- else morale2- then badfx
	then
		($ Thegroup .. "  durchsucht das Haus und findet etwas zu essen!" $) +crtext 2 5 rnd trunk-food+
;

cyoa-bridge: common4-spider-smoosh-pick
	solohuman if
		choosehuman common4-spider-smoosh
	then;
	($ "Wer soll die Spinne zerquetschen?" $) cyoa-text!
	' common4-spider-smoosh cyoa-human-choice
;

// Gourmand logic below, also used in a bandit event but not enough to move and untangle dependencies
: isgourmand? .trait@ "Gourmand" $= ;
: numgourmandfilt args( chara ) <- chara isgourmand? ;
: numgourmands team-stack ' numgourmandfilt stack-filter nip ;

: gourmandcheck
	checkchar isgourmand?
;

: gourmandfilt ' isgourmand? stack-filter drop ;
: cyoa-gourmand-choice team-stack gourmandfilt cyoa-char-stack-choice ;

cyoa-choice: common4-spider-eat
	($ cname .. " ' schaut sich um, um sicherzustellen, dass niemand zusieht, und frisst dann schnell die Spinne. Sie ist knusprig." $) text! goodfx
	morale++
	cchar isgourmand? if
		($ "I was hungry..." pbold .. "
		-- " .. cname $) +crtext
	then
;

cyoa-bridge: common4-spider-eat-pick
	solopet numgourmands 1 < and if
		choosepet common4-spider-eat
	then;
	($ "Wer soll die Spinne essen?" $) cyoa-text!
	' common4-spider-eat cyoa-pet-choice
	' common4-spider-eat cyoa-gourmand-choice
;

cyoa: common4-spider
	"Angriff der Riesenspinne" cyoa-title! someicon
	($ Thegroup .. "  versucht sich in einem Haus auszuruhen wird aber von einer " .. "riesige Spinne!" fbold $) text!
	($ "'Naja vielleicht ist es eine normale Spinne aber sie sieht eklig aus. Die Spinne leert sich schnell " .. thegroup .. "''s Moral!" $) +crtext
		choice( "Lauf angewidert" )choice: common4-spider-run
		choice( "Zerschmettere Spinne" )choice: common4-spider-smoosh-pick
		numpets 0 > numgourmands 0 > or if choice( "Iss Spinne" )choice: common4-spider-eat-pick then
		// choosepet cchar charchoice( ": Iss Spinne"
;

// Strange Vending Machine

0 value common4-vending-num

cyoa-choice: common4-vending-go
	"Es dauert ewig die Dollarscheine einzuzahlen. Der Verkaufsautomat spuckt sie immer wieder aus." text!
	cattitude+ if morale-- attitudeREV else morale2- then goodfx
	common4-vending-num case 
	1 of 
		"Eine voll geladene Schrotflinte kommt aus dem Verkaufsautomaten!" +crtext
		1 WEAPON_SHOTGUN trunk.weapon+ 6 trunk-shotgun-shell+
	endof
	2 of 
		"Ein geladenes Cowboy Gewehr kommt aus dem Verkaufsautomaten!" +crtext
		1 WEAPON_RIFLE trunk.weapon+ 10 trunk-rifle-shell+
	endof
	3 of 
		"Ein geladener Dolt Revolver kommt aus dem Verkaufsautomaten!" +crtext
		1 WEAPON_DOLT trunk.weapon+ 6 trunk-bullet+
	endof
	4 of 
		"'Eine Tute Chips fallt heraus! Nicht in der ublichen Groe eines Verkaufsautomaten, sondern in der riesigen Familiengroe." +crtext 4 trunk-food+
	endof
	5 of 
		"Ein Kanister Benzin fallt aus dem Verkaufsautomaten!" +crtext 30 trunk-gas+
	endof
	6 of 
		"Eine groe Schachtel mit medizinischen Bandagen kommt aus dem Verkaufsautomaten!" +crtext 3 trunk-medical+
	endof 
	7 of 
		($ "The vending machine's shape changes! It transforms into an electric car!
			" .. Thegroup .. "  steigt ein." $) +crtext CAR_HYBRID2 get-car 30 trunk-gas+
	endof
	8 of 
		"'Ein Stock fallt aus dem Automaten! Ich bin nicht sicher, was du erwartet hast, es ist ein Stock." +crtext
		1 WEAPON_TREEBRANCH trunk.weapon+
	endof
	9 of 
		($ "Ein Hut springt aus dem Automaten!		" .. cname .. "  setzt ihn auf." $) +crtext
		1 27 rnd cchar .hattype! morale++
	endof 
	endcase
;

cyoa-bridge: common4-vending-pick
	solohuman if
		choosehuman cicon
		($ cname .. "  muss die zerrissenen und zerknitterten Scheine in den Automaten eingeben. Das wird nervig sein." $) text!
		cchar charchoice( ": Geldscheine eingeben" )choice: common4-vending-go
		choice( "Lass es alleine" )choice
	then;
	($ "Wer soll die grotenteils verrotteten Dollarscheine in den Automaten eingeben?" $) cyoa-text!
	"WARNUNG: Dies wird ein sehr nerviger Prozess sein" rbold +crtext
	' common4-vending-go cyoa-human-choice
	choice( "Lass es doch einfach" )choice
;

: common4-vending-1 1 to common4-vending-num common4-vending-pick ;
: common4-vending-2 2 to common4-vending-num common4-vending-pick ;
: common4-vending-3 3 to common4-vending-num common4-vending-pick ;
: common4-vending-4 4 to common4-vending-num common4-vending-pick ;
: common4-vending-5 5 to common4-vending-num common4-vending-pick ;
: common4-vending-6 6 to common4-vending-num common4-vending-pick ;
: common4-vending-7 7 to common4-vending-num common4-vending-pick ;
: common4-vending-8 8 to common4-vending-num common4-vending-pick ;
: common4-vending-9 9 to common4-vending-num common4-vending-pick ;

cyoa: common4-vending
	"Seltsamer Automat" cyoa-title! someicon
	($ Thegroup .. "  stot auf einen sehr fehl am Platz wirkenden Automaten. Er sieht aus, als hatte ihn jemand aus Schrottstucken gebaut. Seine Lichter leuchten noch. Alles in dem Automaten kostet 20 Dollar, und " .. thegroup .. "  hat ungefahr so viel in zerknitterten und ausgefransten Scheinen." $) text!
		choice( "Lass es alleine" )choice
		
		33 %chance if
			choice( "SHOTGUN FUN PACK" )choice: common4-vending-1
		else
			50 %chance if
				choice( "ROOTEM TOOTEM RIFLE" )choice: common4-vending-2
			else
				choice( "REAL REVOLVER SET" )choice: common4-vending-3
			then
		then
		
		33 %chance if
			choice( "Tute Chips" )choice: common4-vending-4
		else
			50 %chance if
				choice( "Benzinkanister" )choice: common4-vending-5
			else
				choice( "'Verbandskasten" )choice: common4-vending-6
			then
		then
		
		10 %chance if
			choice( "Ein brandneues Auto!" )choice: common4-vending-7
		else
			50 %chance if
				choice( "Ein Stock" )choice: common4-vending-8
			else
				choice( "Ein neuer Hut" )choice: common4-vending-9
			then
		then		
;

} }

// Blank
(*
cyoa-choice: common4--

;

cyoa: common4-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)
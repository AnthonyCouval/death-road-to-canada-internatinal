
(*

- ouija board event "Ouija"
	- Pick a question to ask
	- Different if solo
		- AM I PRETTY? [yes] Morale Boost
		- WHERE'S THE LOOT? Shows hidden supplies nearby
		- 

	
*)

cyoa{ road{ 

// Not a Ouija Board

: weegeetm .. "Tablero de Weegee [tm]" pbold .. ;
: weegeecurse args( char ) 13 <- char .seed! ;

: weegee-end
	($ Thegroup .. "  sign. " .. "Maldicion de Weegee!" rbold $) +crtext
	"SUERTE MASIVAMENTE DISMINUIDA!" fbold +crtext
	"En algun lugar a lo lejos, un goblin rie." pbold +crtext goblin-laugh
	
;

cyoa-choice: common4-weegee-pretty
	choosehuman cicon
	($ "El " weegeetm " ' escribe lo que " .. cname .. " ' es " .. "	ES VERY PRETTY" pbold $) text!
	($ "Que bueno! " .. cname .. " 'No se siente maldito en absoluto por este encantador Weegee." $) +crtext
	morale2+
	"Somewhere in the distance, a goblin laughs, but in a nice way. As if to say 'hey.... what's up. have a nice day.'" pbold +crtext goblin-laugh
	choice( "bueno" )choice
;

cyoa-choice: common4-weegee-loot
	($ "El " weegeetm " 'escribe el lugar de una cachaza oculta en la casa!" $) text!
	homesearch1
	weegee-end
;

cyoa-choice: common4-weegee-check
	weaselest choosechar cicon
	($ "Cuando se le pregunta quien traicionara al grupo, el " weegeetm " 'escribe " $) text!
	weaselest-pass if
		($ cname .. "nombre!" $) +text
		loyaltyREV
		weegee-end
	else
		"TU BOTTE" pbold +crtext
		($ "Que podria significar esto? La grupo se vuelve inquieto, y la duda brota. Podria ser el " .. "Maldicion de Weegee?" rbold $) +crtext
		morale--all goblin-laugh
	then
;

cyoa-choice: common4-weegee-canada
	($ "El " weegeetm " 'apunta hacia la palabra " .. "NO" pbold .. " 'con una velocidad asombrosa. Despues, apunta hacia " .. "NO" fbold .. " 'de nuevo, mientras inexplicablemente se prende en fire." $) text!
	($ "Que mas da! Estas cosas siempre estan equivocadas, de todos modos! " .. cname .. " 'lanza el objeto en llamas por la habitacion. Esto hace que " weegeetm " 'tambien se incendie. " .. chim .. " 'a el le encanta ser resistente al fuego!" $) +crtext
	cchar isfireproof? if 
		($ cname .. " 'Fue un buen ejercicio, al menos." $) +crtext
		morale++ goodfx
	else
		ouch-- alive? if bitefx then
	then
	alive? if
		"Al menos fue un buen ejercicio." +crtext fitness++
	else
		($ "No se meta con el " .. "Maldicion de Weegee!" rbold $) +crtext goblin-laugh
	then
	
;

cyoa: common4-weegee
	"Tablero de Weegee" cyoa-title! choosehuman cicon
	($ Thegroup .. " 'esta revisando una casa abandonada y encuentra un " weegeetm " 'en la mesa de la cocina. Es un juego comun utilizado para comunicarse con " .. "FANTASMAS" fbold $) text!
		choice( "Deja esta tonteria en paz" )choice
		($ "Estas cosas se rumorea que son " .. "INCREIBLEMENTE MALDITO CON MALA SUERTE" rbold .. ", so beware! Still, it may be worth asking it a question..." $) +crtext
		solohuman if
			choice( "SOY BONITA?" )choice: common4-weegee-pretty
		else
			choice( "QUIEN TRAICIONARA AL EQUIPO?" )choice: common4-weegee-check
		then
			choice( "VOLUNTAD" .. I-or-WE .. "HACERLO HASTA CANADA?" )choice: common4-weegee-canada
			choice( "DONDE ESTA EL Botin?" )choice: common4-weegee-loot
;

// Dog Poop

: common4-dogpoo-trueend 16 trunk-food+ goblin-laugh ;
: common4-dogpoo-reward 16 trunk-food+ badfx ;

cyoa-choice: common4-dogpoo-ignore
	($ Thegroup .. " 'ignora la caja de suministros y todo lo que hay dentro de ella.	" .. gThey .. " 'encontrar" .. g1s .. " 'tomaba unos pocos lotes de comida mientras buscaba el resto de la casa, pero debio preguntarse que " .. gthey .. " 'se habia ido." $) text!
	2 trunk-food+ goodfx
;

cyoa-choice: common4-dogpoo-pick

	cdog? if
		($ "El cadejo fecales misteriosamente desaparecen antes de " .. cname .. " 'abra la caja!		" .. cname .. " 'parecia muy culpable." $) text!
		morale--
		common4-dogpoo-reward
	then;
	ccat? if
		($ cname .. " 'parece especialmente enojado que tuvo que empujar el cadejo fecales fuera del camino para abrir la caja. " .. cHe .. " 'realmente se siente como si quisiera destrozar algo, mas que lo normal." $) text!
		morale4-
		common4-dogpoo-reward
	then;
	cpet? if
		($ cname .. " 'abre la caja de suministros con un pie. Como animal, " .. che .. " 'realmente no importa en nada los cadejos fecales, asi que no fue mucho obstaculo." $) text!
		16 trunk-food+ goodfx
	then;
	dogpal? if
		($ cname .. "  loves dogs, and is no stranger to picking up dog poop and putting it in a plastic bag.		In fact, doing so reminds " .. cname .. "  de la civilizacion. Sentir ese calor en tu mano, sentirse vivo!" $) text!
		morale2+ 16 trunk-food+ goodfx
	then;
	cchar oblivious? if
	($ cname .. " 0x0000000000000000," .. cHe .. "  nunca note el caca de perro." $) text!
		solo if
			goodfx
		else
			($ Everyone .. " esta asqueado!" $) +crtext cchar morale2-all-except badfx
		then 16 trunk-food+
	then;
	($ cname .. "  intenta empujar la caca fuera de la caja con un pedazo de madera, pero sale TERRIBLEMENTE mal. " .. cHe .. " 'se horroriza bastante!" $) text!
		cattitude+ if
			badfx
			($ cHe .. " 'intenta mantener una actitud positiva. Un dia, " .. cname .. " 'podra mirar esto y reirse. Pero no hoy." $)
		else
			($ "Esto, sin duda, es lo peor que le ha pasado a " .. cname .. " 'en el apocalipsis zombie. " .. cHe .. " 'nunca, nunca volvera a ser el mismo." $) +crtext
			($ cname .. ":" pbold .. " 'PENALIZACION PERMANENTE DE MORAL!" fbold $)
			-1 STAT_MORALE cchar .bonus+ sfx{ creepy }
		then +crtext morale2- 16 trunk-food+
	
;

cyoa-bridge: common4-dogpoo-stick
	"GANASTE" cyoa-title! end-music
	"Todo nino sabe que la unica forma segura de mover una caca de perro es utilizar un palo de una rama de arbol. Es la herramienta perfecta para quitar la caca. Has convocado tu sentido de capricho y maravilla infantil para superar esta situacion peligrosa." text!
	"Felicidades!" rainbold +crtext
	"HAS ENCONTRADO EL VERDADERO FINAL DE DEATH ROAD TO CANADA!" fbold +crtext
	choice( "Lo hice!" )choice: common4-dogpoo-trueend
;

cyoa-bridge: common4-dogpoo-open
	solo if
		soloer choosechar common4-dogpoo-pick
	then;
	($ "Quien deberia abrir la caja?" .. "	ADVERTENCIA: ESTO ES REALMENTE AFILOSO		HAY UN CUCHILLO DE PERRO EN LA CESTA" rbold .. "	PUEDE SER UN GRAN PROBLEMA" fbold $) text!
	' common4-dogpoo-pick cyoa-team-choice
;

cyoa: common4-dogpoo
	"VERDADERO DESesperanza" cyoa-title! picon!
	($ "Como " .. thegroup .. " ' busca una casa abandonada, sucede un milagro. " .. gThey .. " 'encontrar" .. g1s .. " ' un cajon de raciones del gobierno, inedito." $) text!
	($ "A " .. ghis .. " ' HORROR" rbold .. ", resulta que hay un " .. "cachorro" fbold .. " ' sentado " .. "EN EL MANOJO" rbold .. " ' del cajon de suministros." .. " No hay manera de abrirlo sin tocar la caca." rbold $) +crtext
	WEAPON_TREEBRANCH roadtrip-has-weapon? not if
		"Esto podria ser el desafio mas grande, hasta ahora!" +crtext
	then
		choice( "No hay forma, asqueroso, ignoralo" )choice: common4-dogpoo-ignore
		solo if
			soloer charchoice( " 'abre eso" )choice: common4-dogpoo-open
		else
			choice( "Haz que alguien abra eso" )choice: common4-dogpoo-open
		then
		WEAPON_TREEBRANCH roadtrip-has-weapon? if
			choice( "Usa una rama de arbol para abrirlo" )choice: common4-dogpoo-stick
		then
;

// Losing a sneeze

cyoa-choice: common4-sneeze-try
	($ cname .. " ' no puede estornudar a voluntad. " .. cHe .. " 'perdio el estornudo, es muy molesto!" $) text!
	morale--
	cattitude- if
		($ "Esto es lo " .. "peor cosa que le haya pasado a ALGUIEN" rbold .. " ' durante el apocalipsis zombie!" $) +crtext morale-- attitudeREV
	then
;

cyoa-choice: common4-sneeze-smash
	bitefx
	cchar .trait@ "ENLOQUECER!" $= if
		($ cname .. " 'grita " .. "BERSERKEERRR!" fbold .. " 'y luego golpea " .. chim .. "a si mismo en la nariz, lo mas fuerte posible. No tiene instinto de preservacion!" $) text!
		ouch--
	then;
	($ cname .. "  aplasta " .. chis .. " to be stubborn" $) text!
	sting--
	($ "Es un viejo truco! El dolor cegador hace " .. cname .. " olvidate del estornudo perdido." $) +crtext
;

cyoa-choice: common4-sneeze-animal
	cchar to victim
	somepet if choosechar then
	($ vname .. "  rellena de verdad " .. vhis .. "  nariz en " .. cname .. " ' y afiliona una buena toser.	Esto desencadena una tos fuerte!" $) text! goodfx
;

cyoa-choice: common4-sneeze-spicy
	($ cname .. " ' abre una bolsa de papas a las que les han agregado mucha pimienta. " .. cHe .. " ' toma un gran sorbo." $) text!
	($ cname .. " ' suelta un tos accidentalmente en la bolsa! 	" .. cHe .. " ' tal vez deberia comer todo el saco de papas ahora." $) +crtext
	-2 trunk-food+
	morale++
;

cyoa: common4-sneeze
	"EL SORPRESADO TOSE PERDIDO" cyoa-title! choosehuman cicon
	($ cname .. " ' realmente se siente que " .. che .. " ' tiene que toser. Este sentimiento persiste, pero no hay tos.	Este podria convertirse en uno de esos momentos en que un tos se pierde! Unido a las presiones del apocalipsis zombi, esto podria romper " .. cname .. "la moral de" $) text!
		choice( "Intente toser" )choice: common4-sneeze-try
		choice( "Chocar el hueso del naso a si mismo" )choice: common4-sneeze-smash
		numpets 0 > if choice( "Soplar polen animal" )choice: common4-sneeze-animal then
		trunk-food 1 > if choice( "1 happiness" )choice: common4-sneeze-spicy then
;

// Game Bronus Entertainment System

: common4-gamebro-ender
	($ "Este juego es demasiado dificil! " .. "AAAAAAAAAARGH!" fbold $) +crtext
	($ cname .. " lanza el GameBronus con fuerza, destruyendolo." $) +crtext
	cattitude- if morale3- attitudeREV else morale2- then badfx
;

cyoa-choice: common4-gamebro-shoot
	($ "Escuadron Drobnus" bbold .. " 1/5" $) text!
	shooting++
	common4-gamebro-ender
;

cyoa-choice: common4-gamebro-med
	($ "Dr. Drobnus de Lujo" bbold .. " 'Surgeon Simulator 2013' is a darkly humorous over-the-top operation sim game where players become Nigel Burke, a would-be surgeon taking life into his own shaky hands, performing life-saving surgical maneuvers on passive patients." $) text!
	medical++
	common4-gamebro-ender
;

cyoa-choice: common4-gamebro-mech
	($ "Drobnus Carta" bbold .. " 'es un juego de carreras que presenta al popular personaje Drobnus y a todos sus amigos. Aunque estes en la primera posicion, puedes ser golpeado por un Homing Bano y terminar en ultima posicion." $) text!
	mechanic++
	common4-gamebro-ender
;

cyoa-bridge: common4-gamebro-med-pick
	solohuman if
		choosehuman common4-gamebro-med
	then;
	($ "Quien deberia jugar " .. "Dr. Drobnus de Lujo" bbold .. "?" $) cyoa-text!
	"ADVERTENCIA: Va a ser duro para la moral!" rbold +crtext
	' common4-gamebro-med cyoa-human-choice
;

cyoa-bridge: common4-gamebro-mech-pick
	solohuman if
		choosehuman common4-gamebro-mech
	then;
	($ "Quien deberia jugar " .. "Drobnus Carta" bbold .. "?" $) cyoa-text!
	"ADVERTENCIA: Va a ser duro para la moral!" rbold +crtext
	' common4-gamebro-mech cyoa-human-choice
;

cyoa-bridge: common4-gamebro-shoot-pick
	solohuman if
		choosehuman common4-gamebro-shoot
	then;
	($ "Quien deberia jugar " .. "Escuadron Drobnus" bbold .. "?" $) cyoa-text!
	"ADVERTENCIA: Va a ser duro para la moral!" rbold +crtext
	' common4-gamebro-shoot cyoa-human-choice
;

cyoa: common4-gamebro
	"Sistema de Entretenimiento GameBronus [TM]" cyoa-title! someicon
	($ Thegroup .. " 'encontra una antigua " .. "GameBronus [TM]" bbold .. "	Es un sistema portatil de videojuegos que sobrevivio hasta ahora porque esta construido como una piedra.	Habia unos juegos con el, todos conocidos por su " .. "dificultad extremadamente frustrante:" rbold $) text!
		choice( "Dejese atras el GameBronus" )choice
		choice( "Dr. Drobnus de Lujo" )choice: common4-gamebro-med-pick
		choice( "Drobnus Carta" )choice: common4-gamebro-mech-pick
		choice( "Escuadron Drobnus" )choice: common4-gamebro-shoot-pick
;

// Baked Beans on Garbage Mountain

: common4-beans-ender
	sting--
	($ "Se le ensuna la basura " .. cname .. "! Valio la pena! Valio todo por las judias!" $) +crtext
	morale2- 3 trunk-food+ badfx
	
;

cyoa-choice: common4-beans-eat
	($ cname .. " 'come algo del monton de basura! Esto hace que el lata de frijoles en la parte superior se suelte, sin que todo el monton caiga sobre " .. chim .. ". Que solucion deliciosa!" $) text!
	morale2+ 3 trunk-food+ goodfx
;

cyoa-choice: common4-beans-smash
	($ cname .. " 'ahora mismo el monton de basura! Se derrumba justo encima de " .. chim .. "." $) text!
	strength++ common4-beans-ender
;

cyoa-choice: common4-beans-climb
	($ cname .. " 'sube por el monton de basura! Se derrumba con " .. chim .. " 'encima." $) text!
	fitness++ common4-beans-ender
;

cyoa-bridge: common4-beans-smash-pick
	solohuman if
		choosehuman common4-beans-smash
	then;
	($ "Quien deberia luchar contra el monton de basura?" $) cyoa-text!
	' common4-beans-smash cyoa-human-choice
;

cyoa-bridge: common4-beans-climb-pick
	solo if
		soloer choosechar common4-beans-climb
	then;
	($ "Quien deberia subir por el monton de basura?" $) cyoa-text!
	' common4-beans-climb cyoa-team-choice
;

cyoa-bridge: common4-beans-eat-pick
	solopet if
		choosepet common4-beans-eat
	then;
	($ "Quien deberia comerse el monton de basura?" $) cyoa-text!
	' common4-beans-eat cyoa-pet-choice
;

cyoa: common4-beans
	"La Batalla de la Montana de Basura" cyoa-title! someicon
	($ Thegroup .. " 've un monton de basura extra alto cerca de algunos edificios. Mide mas de 12 pies de alto. La basura se acumulo mucho cerca del colapso de la civilizacion.	Un " .. "Lata Extra Grande de Frijoles Horneados" rbold .. " 'esta atascado cerca de la cima del monton. Brilla como una senal de esperanza." $) text!
		choice( "Dejemoslo solo." )choice
		choice( "Romper monton de basura" )choice: common4-beans-smash-pick
		choice( "Subir monton de basura" )choice: common4-beans-climb-pick
		numpets 0 > if choice( "Comer basura" )choice: common4-beans-eat-pick then
;

// Old VHS tape

: common4-vhs-end
	morale++all
	($ "La television se descompone despues de la pelicula, y se queda atras." +crtext
;

: sumo-vhs args( char )
	<- char ispet? if 
		<- char dog? if
			($ <- char .name@ .. " aprende sumo, a pesar de ser un perro." $) +crtext
			WEAPON_SUMO <- char .weapon_default!
		then
	then;	
	<- char .weapon_default@ WEAPON_SUMO = if
		($ <- char .name@ .. " aprende tecnicas superiores de sumo!" $) +crtext
		WEAPON_SUPERSUMO <- char .weapon_default!
	else WEAPON_SUMO <- char .weapon_default! then
;

cyoa-blank: common4-vhs-sumo
	"Sumo Wrestling estaba en el top 5 de deportes en America antes del zombocalipsis. Esto llevo a muchas peliculas de sumo, dramas de sumo en la television, dibujos animados de sumo, y asi sucesivamente." text!
	($ Thegroup .. " 'Aprende algunos movimientos de sumo viendo la pelicula!" $) +crtext
	' sumo-vhs foreach-team-char common4-vhs-end
;

cyoa-blank: common4-vhs-shoot
	($ "El videocasete es una adaptacion cinematografica del juego arcade " .. "DEATHSPLOSIONAL!" fbold .. " 'es mejor que no haya pelicula alguna." $) text!
	
	' .shooting++ foreach-team-char common4-vhs-end
;

cyoa-blank: common4-vhs-fit
	($ Thegroup .. " 'se sigue al instructor de fitness en el videocasete. Todos usan rebozos neon." $) text!
	
	' .fitness++ foreach-team-char common4-vhs-end
;

cyoa-blank: common4-vhs-morale
	($ "La comedia cinematografica resulta ser terrible segun la mayoria de las normas, pero " .. thegroup .. " 'esta completamente absorbido por ella. Es muy raro ver una pelicula mas alguna." $) text!	morale3+all
	"La television se apaga durante el medio de la segunda visualizacion, y nunca vuelve a encender." +crtext
;

cyoa: common4-vhs-rare
	"Reproductor de Videocasete" cyoa-title!
	($ Thegroup .. " 'encontra un refugio abandonado en una casa tabada. Hay poco que quede aqui, excepto un pequeno televisor a pila electrica conectado a un reproductor de videocasetes antiguo.	Hay solo una pelicula en el reproductor de videocasete:" $) text!
		choice( "Pelicula de Accion" )choice:  common4-vhs-shoot
		choice( "Grabacion Fitness" )choice: common4-vhs-fit
		choice( "Pelicula de Comedia" )choice: common4-vhs-morale
		choice( "Pelicula de Sumo" )choice: common4-vhs-sumo
;

// Spider attack based on a true story where I ate a spider

cyoa-choice: common4-spider-run
	($ Thegroup .. " 'salio corriendo de esa casa!	La arana continua caminando lentamente por las paredes de manera espeluznante y asquerosa." $) text!
	morale--all badfx
;

cyoa-choice: common4-spider-smoosh
	cchar .trait@ "ENLOQUECER!" $= if
		($ cname .. " 'grita " .. "BERSERKEERRR!" fbold .. "  y luego " .. "ANIQUILA" fbold .. " 'la arana. Solamente queda un rastro de humo de ella." $) text!
		morale2+ goodfx
	else
		($ cname .. " 'aplasta la arana. Se oye un crujido particularmente asqueroso." $) text!
		cattitude+ if morale-- else morale2- then badfx
	then
		($ Thegroup .. " 'busca en la casa y encuentra algo de comida!" $) +crtext 2 5 rnd trunk-food+
;

cyoa-bridge: common4-spider-smoosh-pick
	solohuman if
		choosehuman common4-spider-smoosh
	then;
	($ "Quien deberia aplastar la arana?" $) cyoa-text!
	' common4-spider-smoosh cyoa-human-choice
;

// Gourmand logic below, also used in a bandit event but not enough to move and untangle dependencies
: isgourmand? .trait@ "Gourmet" $= ;
: numgourmandfilt args( chara ) <- chara isgourmand? ;
: numgourmands team-stack ' numgourmandfilt stack-filter nip ;

: gourmandcheck
	checkchar isgourmand?
;

: gourmandfilt ' isgourmand? stack-filter drop ;
: cyoa-gourmand-choice team-stack gourmandfilt cyoa-char-stack-choice ;

cyoa-choice: common4-spider-eat
	($ cname .. " 'mira a su alrededor para asegurarse de que no hay nadie mirando, luego come rapidamente la arana.	Esta crujiente." $) text! goodfx
	morale++
	cchar isgourmand? if
		($ "I was hungry..." pbold .. "
		-- " .. cname $) +crtext
	then
;

cyoa-bridge: common4-spider-eat-pick
	solopet numgourmands 1 < and if
		choosepet common4-spider-eat
	then;
	($ "Quien deberia comer la arana?" $) cyoa-text!
	' common4-spider-eat cyoa-pet-choice
	' common4-spider-eat cyoa-gourmand-choice
;

cyoa: common4-spider
	"Ataque de arana gigante" cyoa-title! someicon
	($ Thegroup .. " 'trata de descansar en una casa pero es atacado por una " .. "arana gigante!" fbold $) text!
	($ "Bueno, tal vez sea una arana normal pero se ve asquerosa. La arana esta drenando rapidamente " .. thegroup .. "la moral de" $) +crtext
		choice( "Correr en asco" )choice: common4-spider-run
		choice( "Aplastar arana" )choice: common4-spider-smoosh-pick
		numpets 0 > numgourmands 0 > or if choice( "Comer Arana" )choice: common4-spider-eat-pick then
		// choosepet cchar charchoice( "comer Arana"
;

// Strange Vending Machine

0 value common4-vending-num

cyoa-choice: common4-vending-go
	"Se tarda una eternidad en meter los billetes de dolar. La maquina expendedora no deja de escupirlos." text!
	cattitude+ if morale-- attitudeREV else morale2- then goodfx
	common4-vending-num case 
	1 of 
		"Una escopeta completamente cargada sale de la maquina expendedora!" +crtext
		1 WEAPON_SHOTGUN trunk.weapon+ 6 trunk-shotgun-shell+
	endof
	2 of 
		"Una carabina de vaquero cargada sale de la maquina expendedora!" +crtext
		1 WEAPON_RIFLE trunk.weapon+ 10 trunk-rifle-shell+
	endof
	3 of 
		"Un revolver cargado de Dolt sale del dispensador!" +crtext
		1 WEAPON_DOLT trunk.weapon+ 6 trunk-bullet+
	endof
	4 of 
		"Cae una bolsa de papas! No es el tamano habitual, es la familia extrana." +crtext 4 trunk-food+
	endof
	5 of 
		"Salta un jerron de gasolina del dispensador!" +crtext 30 trunk-gas+
	endof
	6 of 
		"Sale una caja grande de vendas medicadas del dispensador!" +crtext 3 trunk-medical+
	endof 
	7 of 
		($ "The vending machine's shape changes! It transforms into an electric car!
			" .. Thegroup .. " 'se mete." $) +crtext CAR_HYBRID2 get-car 30 trunk-gas+
	endof
	8 of 
		"Salta un palo del dispensador! No estoy seguro de lo que estaba esperando, es un palo." +crtext
		1 WEAPON_TREEBRANCH trunk.weapon+
	endof
	9 of 
		($ "Un sombrero sale del dispensador!" .. cname .. " 'ponelo." $) +crtext
		1 27 rnd cchar .hattype! morale++
	endof 
	endcase
;

cyoa-bridge: common4-vending-pick
	solohuman if
		choosehuman cicon
		($ cname .. " 'Tendra que meter las monedas rotas y gastadas en la maquina. Esto sera molesto." $) text!
		cchar charchoice( ": Introduce monedas" )choice: common4-vending-go
		choice( "Dejemoslo solo." )choice
	then;
	($ "Quien debe introducir las monedas casi roidas en la maquina?" $) cyoa-text!
	"ADVERTENCIA: Este sera un proceso molesto" rbold +crtext
	' common4-vending-go cyoa-human-choice
	choice( "Dejalo asi al fin y al cabo" )choice
;

: common4-vending-1 1 to common4-vending-num common4-vending-pick ;
: common4-vending-2 2 to common4-vending-num common4-vending-pick ;
: common4-vending-3 3 to common4-vending-num common4-vending-pick ;
: common4-vending-4 4 to common4-vending-num common4-vending-pick ;
: common4-vending-5 5 to common4-vending-num common4-vending-pick ;
: common4-vending-6 6 to common4-vending-num common4-vending-pick ;
: common4-vending-7 7 to common4-vending-num common4-vending-pick ;
: common4-vending-8 8 to common4-vending-num common4-vending-pick ;
: common4-vending-9 9 to common4-vending-num common4-vending-pick ;

cyoa: common4-vending
	"Maquina Expendedora Rara" cyoa-title! someicon
	($ Thegroup .. " 'se topa con una muy fuera de lugar maquina expendedora. Parece que alguien la fabrico con trozos de hierro derretido. Sus luces aun estan encendidas. Todo en la maquina cuesta $20, y " .. thegroup .. " 'tiene alrededor de esa cantidad en billetes arrugados y deshilachados." $) text!
		choice( "Dejemoslo solo." )choice
		
		33 %chance if
			choice( "PAQUETE DE PISTOLA DE CALIBRE 12" )choice: common4-vending-1
		else
			50 %chance if
				choice( "ROOTEM TOOTEM RIFLE" )choice: common4-vending-2
			else
				choice( "SET DE REVOLVER REAL" )choice: common4-vending-3
			then
		then
		
		33 %chance if
			choice( "BOLSA DE PAPAS" )choice: common4-vending-4
		else
			50 %chance if
				choice( "LATA DE GASOLINA" )choice: common4-vending-5
			else
				choice( "CAJA DE VENDAJES" )choice: common4-vending-6
			then
		then
		
		10 %chance if
			choice( "UN COCHE NUEVO!" )choice: common4-vending-7
		else
			50 %chance if
				choice( "UN PALO" )choice: common4-vending-8
			else
				choice( "UN NUEVO SOMBRERO" )choice: common4-vending-9
			then
		then		
;

} }

// Blank
(*
cyoa-choice: common4--

;

cyoa: common4-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)
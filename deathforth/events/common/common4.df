
(*

- ouija board event "Luigi Malleo  Mario"
	- Pick a question to ask
	- Different if solo
		- AM I PRETTY? [yes] Morale Boost
		- WHERE'S THE LOOT? Shows hidden supplies nearby
		- 

	
*)

cyoa{ road{ 

// Not a Ouija Board

: weegeetm .. "Weegee Board [tm]" pbold .. ;
: weegeecurse args( char ) 13 <- char .seed! ;

: weegee-end
	($ Thegroup .. " sign. " .. "Maledizione di Weegee!" rbold $) +crtext
	"FORTUNA DIMINUITA MASSIVAMENTE!" fbold +crtext
	"Da qualche parte in lontananza, un goblin ride." pbold +crtext goblin-laugh
	
;

cyoa-choice: common4-weegee-pretty
	choosehuman cicon
	($ "Il " weegeetm "  scrive che " .. cname .. "  e " .. "	M O L T O B E L L O" pbold $) text!
	($ "Bello! " .. cname .. " 'Non si sente per niente maledetto da questo delizioso Weegee." $) +crtext
	morale2+
	"Somewhere in the distance, a goblin laughs, but in a nice way. As if to say 'hey.... what's up. have a nice day.'" pbold +crtext goblin-laugh
	choice( "bello" )choice
;

cyoa-choice: common4-weegee-loot
	($ "Il " weegeetm "  scrive la posizione di un nascondiglio di bottino nascosto in casa!" $) text!
	homesearch1
	weegee-end
;

cyoa-choice: common4-weegee-check
	weaselest choosechar cicon
	($ "Quando gli e stato chiesto chi tradira il gruppo, il " weegeetm "  scrive " $) text!
	weaselest-pass if
		($ cname .. "'nome!" $) +text
		loyaltyREV
		weegee-end
	else
		"I L T U O S E D E R E" pbold +crtext
		($ "Cosa potrebbe significare? Il gruppo diventa irrequieto e il dubbio si fa strada. Potrebbe essere il " .. "Maledizione di Weegee?" rbold $) +crtext
		morale--all goblin-laugh
	then
;

cyoa-choice: common4-weegee-canada
	($ "Il " weegeetm " punta alla parola " .. "NO" pbold .. " con una velocita meravigliosa. Poi punta a " .. "NO" fbold .. " ancora, riuscendo a prendere fuoco." $) text!
	($ "Qualunque cosa! Questi oggetti sono sempre sbagliati, in qualsiasi maniera " .. cname .. " getta il fuoco " weegeetm " attraverso la stanza. Questo fa accendere anche " .. chim .. " anche questo" $) +crtext
	cchar isfireproof? if 
		($ cname .. " ama essere fuoriuscito al fuoco!" $) +crtext
		morale++ goodfx
	else
		ouch-- alive? if bitefx then
	then
	alive? if
		"Fu un buon esercizio, almeno." +crtext fitness++
	else
		($ "Non farti il capriccio con il " .. "Maledizione di Weegee!" rbold $) +crtext goblin-laugh
	then
	
;

cyoa: common4-weegee
	"Tavolo Weegee" cyoa-title! choosehuman cicon
	($ Thegroup .. "  sta curiosando in una casa abbandonata e trova un " weegeetm " ' sul tavolo della cucina. E un gioco comune usato per comunicare con i " .. " FANTASMI" fbold $) text!
		choice( " Lascia stare questa STRONZATA" )choice
		($ " Si dice che queste cose siano " .. " INCREDIBILMENTE MALEDETTE E PORTINO SFORTUNA" rbold .. ", so beware! Still, it may be worth asking it a question..." $) +crtext
		solohuman if
			choice( " SONO BELLA?" )choice: common4-weegee-pretty
		else
			choice( " CHI TRADIRA LA SQUADRA?" )choice: common4-weegee-check
		then
			choice( " VOLONTA" .. I-or-WE .. " CE LA FARA IN CANADA?" )choice: common4-weegee-canada
			choice( "' DOVE SONO I SOLDI?" )choice: common4-weegee-loot
;

// Dog Poop

: common4-dogpoo-trueend 16 trunk-food+ goblin-laugh ;
: common4-dogpoo-reward 16 trunk-food+ badfx ;

cyoa-choice: common4-dogpoo-ignore
	($ Thegroup .. "  ignora la cassa di rifornimenti e tutto il cibo al suo interno.	" .. gThey .. "  trova" .. g1s .. "  un paio di lattine di cibo mentre perlustra il resto della casa, ma deve chiedersi cosa " .. gthey .. "  si sia perso." $) text!
	2 trunk-food+ goodfx
;

cyoa-choice: common4-dogpoo-pick

	cdog? if
		($ "Il cacca del cane scompare misteriosamente prima " .. cname .. " spinge la scatola per aprirla!" .. cname .. "  sembra davvero colpevole." $) text!
		morale--
		common4-dogpoo-reward
	then;
	ccat? if
		($ cname .. "  sembra particolarmente arrabbiato per aver dovuto colpire la cacca di cane per aprire la scatola. " .. cHe .. "  ha davvero voglia di malmenare qualcosa, piu del solito." $) text!
		morale4-
		common4-dogpoo-reward
	then;
	cpet? if
		($ cname .. "  prende a calci la scatola di rifornimenti per aprirla. Come animale, " .. che .. " ' non gli importa davvero della cacca di cane, quindi non e stato un grande ostacolo." $) text!
		16 trunk-food+ goodfx
	then;
	dogpal? if
		($ cname .. "  ama i cani e non e estraneo alla raccolta di cacca di cane e al metterla in un sacchetto di plastica.		In effetti, farlo ricorda " .. cname .. "  della civilta. Sentendo quel calore nella tua mano, sentendoti vivo!" $) text!
		morale2+ 16 trunk-food+ goodfx
	then;
	cchar oblivious? if
	($ cname .. "  apre la scatola di rifornimenti. Qual era il problema?		" .. cHe .. "  non ha mai notato la cacca di cane." $) text!
		solo if
			goodfx
		else
			($ Everyone .. "  e disgustato!" $) +crtext cchar morale2-all-except badfx
		then 16 trunk-food+
	then;
	($ cname .. "  cerca di spingere la cacca fuori dalla scatola con un pezzo di legno, ma va ORRIBILEMENTE male. " .. cHe .. "  si disgusta parecchio!" $) text!
		cattitude+ if
			badfx
			($ cHe .. "  tenta di mantenere un atteggiamento positivo. Un giorno, " .. cname .. "  sara in grado di guardare questo e ridere. Ma non oggi." $)
		else
			($ "Questo e, di gran lunga, la cosa peggiore che possa accadere a " .. cname .. " nellapocalisse zombie " .. cHe .. " mai, mai piu sara lo stesso." $) +crtext
			($ cname .. ":" pbold .. " PENALITA DI MORALE PERMANENTE!" fbold $)
			-1 STAT_MORALE cchar .bonus+ sfx{ creepy }
		then +crtext morale2- 16 trunk-food+
	
;

cyoa-bridge: common4-dogpoo-stick
	"HAI VINTO" cyoa-title! end-music
	"'Ogni bambino sa che l'unico modo sicuro per spostare un pezzo di cacca di cane e usare un bastone di un ramo d'albero. E il perfetto rimuovi cacca della natura.	Hai incanalato il tuo senso di stravaganza e meraviglia infantile per trionfare su questa pericolosa situazione." text!
	"Congratulazioni!" rainbold +crtext
	"'HAI TROVATO IL VERO FINALE PER DEATH ROAD TO CANADA!" fbold +crtext
	choice( "Ce lho fatta!" )choice: common4-dogpoo-trueend
;

cyoa-bridge: common4-dogpoo-open
	solo if
		soloer choosechar common4-dogpoo-pick
	then;
	($ "Chi dovrebbe aprire la cassa?" .. "		ATTENZIONE: QUESTO E VERAMENTE SCHIFOSO		CE UNA CACCA DI CANE SULLA CASSA" rbold .. "		QUESTA POTREBBE ESSERE UNA COSA SERIA" fbold $) text!
	' common4-dogpoo-pick cyoa-team-choice
;

cyoa: common4-dogpoo
	"VERA DISPERAZIONE" cyoa-title! picon!
	($ "Mentre " .. thegroup .. "  cerca in una casa abbandonata, accade un miracolo. " .. gThey .. "  trova" .. g1s .. "  una scatola di razioni governative, non aperta." $) text!
	($ "Per " .. ghis .. "  ORRORE" rbold .. ", si scopre che ce una " .. "cacca di cane" fbold .. "  seduta " .. "PROPRIO SULLA MANIGLIA" rbold .. "  della scatola delle forniture." .. "  Non ce modo di aprirlo senza toccare la cacca." rbold $) +crtext
	WEAPON_TREEBRANCH roadtrip-has-weapon? not if
		"Questa potrebbe essere la sfida piu grande di tutte!" +crtext
	then
		choice( "Non farlo, fa schifo, ignoralo" )choice: common4-dogpoo-ignore
		solo if
			soloer charchoice( "  lo apre" )choice: common4-dogpoo-open
		else
			choice( "Fallo aprire a qualcun altro" )choice: common4-dogpoo-open
		then
		WEAPON_TREEBRANCH roadtrip-has-weapon? if
			choice( "Usa un ramo dalbero per aprirlo" )choice: common4-dogpoo-stick
		then
;

// Losing a sneeze

cyoa-choice: common4-sneeze-try
	($ cname .. " ' non riesce a starnutire a comando " .. cHe .. " ' perde lo starnuto! E molto fastidioso!" $) text!
	morale--
	cattitude- if
		($ "Questa e la " .. "cosa peggiore che possa capitare a CHIUNQUE" rbold .. "  nello zombocaylpse!" $) +crtext morale-- attitudeREV
	then
;

cyoa-choice: common4-sneeze-smash
	bitefx
	cchar .trait@ "BERSERK!" $= if
		($ cname .. "  urla " .. "BERSERKEERRR!" fbold .. "  e poi si colpisce " .. chim .. " me stesso nel naso, il piu forte possibile. Nessun istinto di autoconservazione." $) text!
		ouch--
	then;
	($ cname .. "  schiaccia " .. chis .. "  il mio naso contro un muro." $) text!
	sting--
	($ "'E un vecchio trucco! Il dolore accecante fa " .. cname .. "  dimenticare lo starnuto perduto." $) +crtext
;

cyoa-choice: common4-sneeze-animal
	cchar to victim
	somepet if choosechar then
	($ vname .. "  si tappa davvero " .. vhis .. "  il naso su " .. cname .. "  e sbuffa. Questo provoca un buon starnuto!" $) text! goodfx
;

cyoa-choice: common4-sneeze-spicy
	($ cname .. "  apre una busta di patatine super piccanti. " .. cHe .. "  fa una grande sniffata." $) text!
	($ cname .. "  starnutisce accidentalmente nella busta!" .. cHe .. "  puo anche mangiare lintera busta di patatine adesso." $) +crtext
	-2 trunk-food+
	morale++
;

cyoa: common4-sneeze
	"LO STARNUTO PERDUTO" cyoa-title! choosehuman cicon
	($ cname .. "  si sente veramente che " .. che .. " ' deve starnutire. Questa sensazione si trascina, ma non c'e starnuto. Questo potrebbe trasformarsi in uno di quei momenti in cui si perde uno starnuto! Combinato con le pressioni dello zombocalypse, questo potrebbe rompere " .. cname .. "''s morale!" $) text!
		choice( "Prova a starnutire" )choice: common4-sneeze-try
		choice( "Schiacciati il naso" )choice: common4-sneeze-smash
		numpets 0 > if choice( "Annusa forfora animale" )choice: common4-sneeze-animal then
		trunk-food 1 > if choice( "Annusa cibo piccante   Costa 2 cibi" )choice: common4-sneeze-spicy then
;

// Game Bronus Entertainment System

: common4-gamebro-ender
	($ "Questo gioco e troppo difficile! " .. "AAAAAAAAAARGH!" fbold $) +crtext
	($ cname .. "  lancia con forza il GameBronus, distruggendolo." $) +crtext
	cattitude- if morale3- attitudeREV else morale2- then badfx
;

cyoa-choice: common4-gamebro-shoot
	($ "Squadrone Drobnus" bbold .. "  e un gioco in cui Drobnus spara agli alieni con una mitragliatrice. La maggior parte delle morti avviene a causa di cattivi controlli di salto mescolati a pozzi a morte istantanea." $) text!
	shooting++
	common4-gamebro-ender
;

cyoa-choice: common4-gamebro-med
	($ "Dr. Drobnus Deluxe" bbold .. "  e un simulatore di chirurgia eccessivamente dettagliato, reso in grafica a 8 bit. La maggior parte dei giocatori perde non riuscendo a seguire la corretta procedura per i guanti chirurgici sterili." $) text!
	medical++
	common4-gamebro-ender
;

cyoa-choice: common4-gamebro-mech
	($ "Drobnus Kart" bbold .. " " $) text!
	mechanic++
	common4-gamebro-ender
;

cyoa-bridge: common4-gamebro-med-pick
	solohuman if
		choosehuman common4-gamebro-med
	then;
	($ "Chi dovrebbe giocare " .. "Dr. Drobnus Deluxe" bbold .. "?" $) cyoa-text!
	"Attenzione: sara dura per il morale!" rbold +crtext
	' common4-gamebro-med cyoa-human-choice
;

cyoa-bridge: common4-gamebro-mech-pick
	solohuman if
		choosehuman common4-gamebro-mech
	then;
	($ "Chi dovrebbe giocare " .. "Drobnus Kart" bbold .. "?" $) cyoa-text!
	"Attenzione: sara dura per il morale!" rbold +crtext
	' common4-gamebro-mech cyoa-human-choice
;

cyoa-bridge: common4-gamebro-shoot-pick
	solohuman if
		choosehuman common4-gamebro-shoot
	then;
	($ "Chi dovrebbe giocare " .. "Squadrone Drobnus" bbold .. "?" $) cyoa-text!
	"Attenzione: sara dura per il morale!" rbold +crtext
	' common4-gamebro-shoot cyoa-human-choice
;

cyoa: common4-gamebro
	"Sistema di Intrattenimento GameBronus" cyoa-title! someicon
	($ Thegroup .. "  trova un vecchio " .. "GameBronus [TM]" bbold .. "'	E un sistema di videogiochi portatile che e sopravvissuto cosi a lungo perche e costruito come un mattone.	C'erano alcuni giochi con esso, tutti noti per la loro " .. "estremamente frustrante difficolta:" rbold $) text!
		choice( "Lascia indietro GameBronus" )choice
		choice( "Dr. Drobnus Deluxe" )choice: common4-gamebro-med-pick
		choice( "Drobnus Kart" )choice: common4-gamebro-mech-pick
		choice( "Squadrone Drobnus" )choice: common4-gamebro-shoot-pick
;

// Baked Beans on Garbage Mountain

: common4-beans-ender
	sting--
	($ "La spazzatura si sparge su " .. cname .. "! Ne e valsa la pena! Era tutto per i fagioli!" $) +crtext
	morale2- 3 trunk-food+ badfx
	
;

cyoa-choice: common4-beans-eat
	($ cname .. "  mangia un po del cumulo di spazzatura! Cio fa cadere la lattina di fagioli in cima, senza che lintero mucchio cada su " .. chim .. ".	Che deliziosa soluzione!" $) text!
	morale2+ 3 trunk-food+ goodfx
;

cyoa-choice: common4-beans-smash
	($ cname .. "  distrugge il cumulo di spazzatura! Crolla proprio su " .. chim .. "." $) text!
	strength++ common4-beans-ender
;

cyoa-choice: common4-beans-climb
	($ cname .. "  scala il cumulo di spazzatura! Crolla con " .. chim .. "  su di esso." $) text!
	fitness++ common4-beans-ender
;

cyoa-bridge: common4-beans-smash-pick
	solohuman if
		choosehuman common4-beans-smash
	then;
	($ "Chi dovrebbe combattere il cumulo di spazzatura?" $) cyoa-text!
	' common4-beans-smash cyoa-human-choice
;

cyoa-bridge: common4-beans-climb-pick
	solo if
		soloer choosechar common4-beans-climb
	then;
	($ "Chi dovrebbe arrampicarsi sul cumulo di spazzatura?" $) cyoa-text!
	' common4-beans-climb cyoa-team-choice
;

cyoa-bridge: common4-beans-eat-pick
	solopet if
		choosepet common4-beans-eat
	then;
	($ "Chi dovrebbe mangiare il cumulo di spazzatura?" $) cyoa-text!
	' common4-beans-eat cyoa-pet-choice
;

cyoa: common4-beans
	"La Battaglia della Montagna di Spazzatura" cyoa-title! someicon
	($ Thegroup .. " ' vede un cumulo di spazzatura estremamente alto vicino a qualche edificio. Potrebbe essere alto oltre i 12 piedi. La sversata era diventata molto grave intorno alla caduta della civilta.	Un " .. "'Cassetta da Conserva O' Fagioli Giganti" rbold .. "  e intrappolato in cima al cumulo. Brilla come una segnalazione di speranza." $) text!
		choice( "Lascialo stare" )choice
		choice( "Frantumare il cumulo di spazzatura" )choice: common4-beans-smash-pick
		choice( "Arrampicarsi sul cumulo di spazzatura" )choice: common4-beans-climb-pick
		numpets 0 > if choice( "Mangiare il cumulo di spazzatura" )choice: common4-beans-eat-pick then
;

// Old VHS tape

: common4-vhs-end
	morale++all
	($ "La TV si spegne dopo il film, e viene abbandonata." +crtext
;

: sumo-vhs args( char )
	<- char ispet? if 
		<- char dog? if
			($ <- char .name@ .. "  impara il sumo, nonostante fosse un cane." $) +crtext
			WEAPON_SUMO <- char .weapon_default!
		then
	then;	
	<- char .weapon_default@ WEAPON_SUMO = if
		($ <- char .name@ .. "  impara tecniche di sumo superiori!" $) +crtext
		WEAPON_SUPERSUMO <- char .weapon_default!
	else WEAPON_SUMO <- char .weapon_default! then
;

cyoa-blank: common4-vhs-sumo
	"Sumo Wrestling era tra i primi 5 sport in America prima dello zombocalypse. Questo ha portato a molti film di sumo, drammi televisivi di sumo, cartoni animati di sumo, e cosi via." text!
	($ Thegroup .. " impara alcune mosse di sumo guardando il film!" $) +crtext
	' sumo-vhs foreach-team-char common4-vhs-end
;

cyoa-blank: common4-vhs-shoot
	($ "La videocassetta e un adattamento cinematografico del videogioco " .. "ESPLOSIONE MORTALE!" fbold .. " E' meglio di niente." $) text!
	
	' .shooting++ foreach-team-char common4-vhs-end
;

cyoa-blank: common4-vhs-fit
	($ Thegroup .. " segue l'istruttore di fitness sul nastro video. Tutti indossano fasce da sudore neon." $) text!
	
	' .fitness++ foreach-team-char common4-vhs-end
;

cyoa-blank: common4-vhs-morale
	($ "La commedia risulta essere terribile per la maggior parte degli standard, ma " .. thegroup .. " ' ne e completamente assorbito. E molto raro vedere ancora un film." $) text!	morale3+all
	"La TV si spegne durante la meta della seconda visione e non si riaccende piu." +crtext
;

cyoa: common4-vhs-rare
	"Videoregistratore" cyoa-title!
	($ Thegroup .. " ' trova il nascondiglio abbandonato di qualcuno in una casa chiusa con assi. Non e rimasto molto qui, tranne una piccola TV a batteria collegata a un vecchio lettore VHS. C'e solo un film nel lettore del videoregistratore:" $) text!
		choice( "Film dazione" )choice:  common4-vhs-shoot
		choice( "Cassetta di fitness" )choice: common4-vhs-fit
		choice( "Film commedia" )choice: common4-vhs-morale
		choice( "Film di sumo" )choice: common4-vhs-sumo
;

// Spider attack based on a true story where I ate a spider

cyoa-choice: common4-spider-run
	($ Thegroup .. "  scappa da quella casa! Il ragno continua a camminare lentamente sui muri in modo inquietante e disgustoso." $) text!
	morale--all badfx
;

cyoa-choice: common4-spider-smoosh
	cchar .trait@ "BERSERK!" $= if
		($ cname .. "  urla " .. "BERSERKEERRR!" fbold .. "  e poi " .. "ANNIENTA" fbold .. "  il ragno. Ne rimane solo un filo di fumo." $) text!
		morale2+ goodfx
	else
		($ cname .. " ' schiaccia il ragno. Si sente un rumore scricchiolante particolarmente disgustoso." $) text!
		cattitude+ if morale-- else morale2- then badfx
	then
		($ Thegroup .. "  cerca in casa e trova del cibo!" $) +crtext 2 5 rnd trunk-food+
;

cyoa-bridge: common4-spider-smoosh-pick
	solohuman if
		choosehuman common4-spider-smoosh
	then;
	($ "Chi dovrebbe schiacciare il ragno?" $) cyoa-text!
	' common4-spider-smoosh cyoa-human-choice
;

// Gourmand logic below, also used in a bandit event but not enough to move and untangle dependencies
: isgourmand? .trait@ "Gourmand" $= ;
: numgourmandfilt args( chara ) <- chara isgourmand? ;
: numgourmands team-stack ' numgourmandfilt stack-filter nip ;

: gourmandcheck
	checkchar isgourmand?
;

: gourmandfilt ' isgourmand? stack-filter drop ;
: cyoa-gourmand-choice team-stack gourmandfilt cyoa-char-stack-choice ;

cyoa-choice: common4-spider-eat
	($ cname .. " ' si guarda intorno per assicurarsi che nessuno stia guardando, poi mangia rapidamente il ragno. E croccante." $) text! goodfx
	morale++
	cchar isgourmand? if
		($ "I was hungry..." pbold .. "
		-- " .. cname $) +crtext
	then
;

cyoa-bridge: common4-spider-eat-pick
	solopet numgourmands 1 < and if
		choosepet common4-spider-eat
	then;
	($ "Chi dovrebbe mangiare il ragno?" $) cyoa-text!
	' common4-spider-eat cyoa-pet-choice
	' common4-spider-eat cyoa-gourmand-choice
;

cyoa: common4-spider
	"Attacco del ragno gigante" cyoa-title! someicon
	($ Thegroup .. "  cerca di riposare in una casa ma viene aggredito da un " .. "ragno gigante!" fbold $) text!
	($ "'Beh forse e un ragno normale ma e brutto. Il ragno sta rapidamente prosciugando " .. thegroup .. "''s morale!" $) +crtext
		choice( "Corri disgustato" )choice: common4-spider-run
		choice( "Schiaccia il ragno" )choice: common4-spider-smoosh-pick
		numpets 0 > numgourmands 0 > or if choice( "Mangia ragno" )choice: common4-spider-eat-pick then
		// choosepet cchar charchoice( ": Mangia ragno"
;

// Strange Vending Machine

0 value common4-vending-num

cyoa-choice: common4-vending-go
	"Ci vuole uneternita per inserire le banconote. Il distributore automatico continua a sputarle fuori." text!
	cattitude+ if morale-- attitudeREV else morale2- then goodfx
	common4-vending-num case 
	1 of 
		"Un fucile da caccia completamente carico esce dal distributore automatico!" +crtext
		1 WEAPON_SHOTGUN trunk.weapon+ 6 trunk-shotgun-shell+
	endof
	2 of 
		"Un fucile da cowboy carico esce dal distributore automatico!" +crtext
		1 WEAPON_RIFLE trunk.weapon+ 10 trunk-rifle-shell+
	endof
	3 of 
		"Un revolver Dolt carico esce dalla macchina distributrice!" +crtext
		1 WEAPON_DOLT trunk.weapon+ 6 trunk-bullet+
	endof
	4 of 
		"Una busta di patatine cade! Non la solita dimensione da distributore automatico, ma la gigantesca confezione familiare." +crtext 4 trunk-food+
	endof
	5 of 
		"Una tanica di benzina cade dalla macchinetta!" +crtext 30 trunk-gas+
	endof
	6 of 
		"Una grande scatola di bende medicinali esce dalla macchinetta!" +crtext 3 trunk-medical+
	endof 
	7 of 
		($ "The vending machine's shape changes! It transforms into an electric car!
			" .. Thegroup .. "  entra." $) +crtext CAR_HYBRID2 get-car 30 trunk-gas+
	endof
	8 of 
		"'Un bastone cade dalla macchinetta! Non sono sicuro di cosa ti aspettassi, e un bastone." +crtext
		1 WEAPON_TREEBRANCH trunk.weapon+
	endof
	9 of 
		($ "Un cappello esce dalla macchinetta!" .. cname .. "  lo indossa." $) +crtext
		1 27 rnd cchar .hattype! morale++
	endof 
	endcase
;

cyoa-bridge: common4-vending-pick
	solohuman if
		choosehuman cicon
		($ cname .. "  dovra inserire le banconote strappate e spiegazzate nella macchinetta. Sara fastidioso." $) text!
		cchar charchoice( ": Inserisci le banconote" )choice: common4-vending-go
		choice( "Lascialo stare" )choice
	then;
	($ "Chi dovrebbe inserire le banconote per lo piu marce nella macchina?" $) cyoa-text!
	"ATTENZIONE: questo sara un processo molto fastidioso" rbold +crtext
	' common4-vending-go cyoa-human-choice
	choice( "Lasciala stare dopo tutto" )choice
;

: common4-vending-1 1 to common4-vending-num common4-vending-pick ;
: common4-vending-2 2 to common4-vending-num common4-vending-pick ;
: common4-vending-3 3 to common4-vending-num common4-vending-pick ;
: common4-vending-4 4 to common4-vending-num common4-vending-pick ;
: common4-vending-5 5 to common4-vending-num common4-vending-pick ;
: common4-vending-6 6 to common4-vending-num common4-vending-pick ;
: common4-vending-7 7 to common4-vending-num common4-vending-pick ;
: common4-vending-8 8 to common4-vending-num common4-vending-pick ;
: common4-vending-9 9 to common4-vending-num common4-vending-pick ;

cyoa: common4-vending
	"Strana macchinetta" cyoa-title! someicon
	($ Thegroup .. "  si imbatte in una macchinetta molto fuori luogo. Sembra che qualcuno labbia realizzata con pezzi di rottami di ferro. Le sue luci sono ancora accese. Tutto nella macchina costa $ 20 e " .. thegroup .. "  ha circa quella cifra in banconote spiegazzate e logore." $) text!
		choice( "Lascialo stare" )choice
		
		33 %chance if
			choice( "PACCHETTO DIVERTIMENTO SHOTGUN" )choice: common4-vending-1
		else
			50 %chance if
				choice( "FUCILE ROOTEM TOOTEM" )choice: common4-vending-2
			else
				choice( "SET RIVOLVERE VERO" )choice: common4-vending-3
			then
		then
		
		33 %chance if
			choice( "SACCO DI PATATINE" )choice: common4-vending-4
		else
			50 %chance if
				choice( "TANICA DI BENZINA" )choice: common4-vending-5
			else
				choice( "'SCATOLA DI BENDA" )choice: common4-vending-6
			then
		then
		
		10 %chance if
			choice( "UNAUTO NUOVA DI ZECCA!" )choice: common4-vending-7
		else
			50 %chance if
				choice( "UN BASTONE" )choice: common4-vending-8
			else
				choice( "UN CAPPELLO NUOVO" )choice: common4-vending-9
			then
		then		
;

} }

// Blank
(*
cyoa-choice: common4--

;

cyoa: common4-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)
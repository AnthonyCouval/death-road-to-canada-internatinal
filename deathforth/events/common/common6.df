
// ILEUM Update events

cyoa{ road{ 

// Fast Food

: common6-fastfood-burged args( chara ) 
	<- chara .morale2+
	<- chara .trait@ "Gourmand" $= if
		($ <- chara .name@ .. "'s fine tastes allow true appreciation of these burgers! VITALITY up, SPEED down!" $) rainbold +crtext
		<- chara .vitality++ -1 STAT_DEXTERITY <- chara .bonus+
		// 1 <- chara .scale.body.y!
		1.6 <- chara .scale.body.x!
	else
		<- chara .sting--
	then
;
: common6-fastfood-megaburged args( chara ) 
	<- chara .morale3+
	2 <- chara .scale.body.x!
	<- chara .trait@ "Gourmand" $= if
		($ <- chara .name@ .. "'s fine tastes allow true appreciation of these burgers! VITALITY up, SPEED down!" $) rainbold +crtext
		<- chara .vitality++ -1 STAT_DEXTERITY <- chara .bonus+
	else
		<- chara .sting2-
	then
;


cyoa-choice: common6-fastfood-burgin
	($ cname .. "  trova una scatola di hamburger Big McDowell. Potrebbero passare 100 anni e nemmeno insetti o muffa si avvicinerebbero a quelle cose. " .. gHe .. " si mette a scavare!" $) text!
	($ cname .. " ' esagera e mangia TROPPO di questo 'cibo'." $) +crtext
		cchar common6-fastfood-megaburged
		
	notsolo if
		($ Everyone .. " ' adora quegli hamburger, ma non sono un granche per i vecchi organi interni." $) +crtext
		cchar ' common6-fastfood-burged foreach-team-char-except
	then goodfx
;

cyoa-choice: common6-fastfood-toys
	($ Thegroup .. " ' trova un giocattolo del menu per bambini. E un pezzo pregiato!" $) text!
	60 %chance if
		1 WEAPON_PUKEYBALL trunk.weapon+
		" Trovata la POKEBALL!"
	else
		50 %chance if
			1 WEAPON_GRENADE trunk.weapon+
			"' trovata una GRANATA PER BAMBINI"
		else
			1 WEAPON_BLUEROBOT trunk.weapon+
			" Trovato il ROBOT BLU!"
		then
	then
	pbold +crtext goodfx 
;

cyoa: common6-fastfood
	" Il Big McDowell" cyoa-title! choosehuman cicon
	($ Thegroup .. " ' saccheggia un McDonald's, che era il ristorante piu popolare negli Stati Uniti prima della caduta della civilta. Nonostante tutto questo tempo, il cibo che si trova qui e ancora conservato grazie a tutte le sostanze chimiche." $) text! "' ATTENZIONE: mangiare hamburger non fa bene alla salute ma non ti uccidera." rbold +crtext
		choice( " Trova e mangia hamburger, paga il prezzo degli hamburger" )choice: common6-fastfood-burgin
		choice( "' Cerca invece i giocattoli del menu per bambini" )choice: common6-fastfood-toys
;


// Cloud Watching

: common6-cloud-go
	($ "  BENE!" rainbold .. cname .. "  e sollevato e ispirato da questo!" $) +text morale++ goodfx
;

cyoa-choice: common6-cloud-bicep
	" Le nuvole sembrano unenorme quantita di bicipiti attaccati luno allaltro, che si flettono in pompa magna." text!
	common6-cloud-go strength++
;

cyoa-choice: common6-cloud-canada
	"Le nuvole sembrano esattamente il paese del Canada!" text!
	($ "BELLO! " rainbold .. Thegroup .. " e ispirato da questo inequivocabile presagio!" $) +crtext morale++ morale++all
;

cyoa-choice: common6-cloud-bazooka
	"La nuvola sembra un enorme bazooka, pronta a far saltare in aria unOrda MegaHorde di zombi al formaggio massimo." text!
	common6-cloud-go shooting++ 
;

cyoa-choice: common6-cloud-car
	"The clouds look like a car that's exploding..." text!
	common6-cloud-go mechanic++
	"Ci penso un po di piu, questo e davvero un po deprimente." +crtext morale-- badfx
;

cyoa-choice: common6-cloud-fit
	"La nuvola sembra una grande fascia antisudore, come se il cielo stesse facendo cardio, forse facendo jazzercising." text!
	common6-cloud-go fitness++
;

cyoa-choice: common6-cloud-butt
	($ "Quella nuvola " .. "CAPITA PROPRIO" rbold .. " di sembrare un sedere. Davvero maturo." $) text!
	morale++ goodfx
	"DAVVERO maturo." rbold +crtext
;

cyoa-choice: common6-cloud-doom
	"Le nuvole sembrano esattamente il tuo imminente fallimento sulla Strada della Morte. Che delusione." text!
	morale-- badfx
;

cyoa-choice: common6-cloud-dad
	($ "Quella nuvola sembra esattamente " .. cname .. "''s papa!" .. "BELLO! " rainbold $) text!
	morale++ goodfx
	($ "BELLO!" rbold $) +crtext ($ "BELLO!" bbold $) +crtext
	morale++ goodfx
	($ "BELLO!" fbold $) +crtext morale++
	"'Sembra che sia il tuo giorno fortunato!" +crtext
;

cyoa-choice: common6-cloud-cloud
	"'La nuvola sembra una nuvola. Sono deluso che tu abbia scelto questo, a proposito." text!
	morale-- badfx
;

cyoa-choice: common6-cloud-bees
	"La nuvola sembra un enorme sciame di api.	E, in effetti, un enorme sciame di api." text!
	($ "AAAAAAAAAAAAAAAAH!" fbold $) +crtext sting--all morale--all bitefx
;

cyoa-choice: common6-cloud-rain
	"'La nuvola sembra che stia per piovere.	E poi piove." text!
	morale-- badfx 0.4 1 frnd weather! -1 1 frnd wind_force!
;

cyoa-choice: common6-cloud-zombie
	"Le nuvole sembrano un enorme sciame di zombi!	Che cattivo presagio!" text! morale-- badfx
	($ "Il pensiero di cio rende " .. cname .. " coraggioso " .. chim .. "se stesso per il futuro." $) +crtext
	cchar damage 0 > if ($ cname .. " riacquista un po di salute!" $) +crtext health++ then
	cmorale- if morale++ then
;

0 var 4-clouds-stack
0 var clouds-pick-stack

: cyoa-4-clouds
	4-clouds-stack sizes for 4-clouds-stack drawcard next drop
;

deck-new
	"Canada" choice-card: common6-cloud-canada
	"Un Bazooka Gigante" choice-card: common6-cloud-bazooka
	"Diversi Bicipiti Grandi Flessi" choice-card: common6-cloud-bicep
	"UnAuto Esplosiva" choice-card: common6-cloud-car
	"Una Fascia Sudatoria" choice-card: common6-cloud-fit
	"Un Sedere" choice-card: common6-cloud-butt
	"La Tua Imminente Rovina" choice-card: common6-cloud-doom
	"Tuo Padre" choice-card: common6-cloud-dad
	"Un Brulicare di Api" choice-card: common6-cloud-bees
	"Una Nuvola" choice-card: common6-cloud-cloud
	"Pioggia" choice-card: common6-cloud-rain
	"Unorda di zombie" choice-card: common6-cloud-zombie
		value clouds-pick-stack-base
		clouds-pick-stack-base shuffles to clouds-pick-stack

: draw-4-clouds 3 stack sizes for // was originally 4, hence the name
		clouds-pick-stack depths 0= if clouds-pick-stack-base shuffles to clouds-pick-stack then
		pops nip shove
	next
	to 4-clouds-stack
;

: pick-4-clouds
	draw-4-clouds
	cyoa-4-clouds
;

cyoa: common6-cloud

	"Osservazione delle nuvole" cyoa-title! someicon
	($ "Mentre viaggia su un tratto di strada tranquillo ma noioso, " .. cname .. "  vede una strana nuvola! La nuvola sembra proprio:" $) text!
		pick-4-clouds

;

// Working Computer

cyoa-choice: common6-computer-internet
	($ "Il computer emette strani e acuti rumori e cinguettii distorti. Cosa diavolo sta succedendo?" $) text!
	mechanicest choosechar cicon
	cmechanic+ if
		($ cname .. "  riesce a far funzionare il computer. E stata la cosa piu difficile che " .. che .. "  abbia mai fatto." $) +crtext mechanicREV mechanic++ mechanicREV morale++
		"The internet seems to be down, though. How come???" +crtext cchar morale--all-except goodfx
	else
		($ cname .. "  armeggia con lantico computer. E un completo e assoluto mistero. " .. cname .. "  inizia a fare progressi nella comprensione dei computer molto vecchi e, in qualche modo, nella riparazione delle auto." $) +crtext mechanic++
		"Poi la corrente si spegne." +crtext morale--all
	then badfx
;

cyoa-choice: common6-computer-qbasic
	($ Thegroup .. "  gioca a un gioco sui gorilla che si lanciano banane a vicenda. E incredibile. Alla fine la corrente finisce, ma fino ad allora lesperienza e magica." $) text! morale++all goodfx
;

cyoa-choice: common6-computer-gas
	($ Thegroup .. " ' sifona la benzina dal generatore di casa. Ne e rimasto un bel po'. La corrente si spegne, insieme alla possibilita di andare su Internet." $) text! 25 40 rnd trunk-gas+ badfx morale--all
;

cyoa: common6-computer
	"Italiano" cyoa-title! someicon
	($ "In una casa con un generatore attaccato, " .. thegroup .. " ' inciampa su un computer funzionante. E uno dei modelli piu vecchi, quando i monitor erano costruiti come un enorme mattone con il vetro davanti.	" .. Thegroup .. "  sbava. Finalmente, " .. "FINALMENTE" rbold .. ", unoccasione per guardare i video di Youtube!" $) text!
		choice( "Prova ad accedere a Internet" )choice: common6-computer-internet
		choice( "Gioca invece ai giochi QBasic" )choice: common6-computer-qbasic
		choice( "Sifonare il generatore" )choice: common6-computer-gas
;

// Other party's car

0 stack
	"Perche DIAVOLO degli scheletri guidano una macchina??" shove
	"Sembra che questi nerd siano rimasti intrappolati nella ZONA OSSEA!" shove
	"Hoisted... by their own peatarts...." shove
	"Sembra che qualcuno sia stato PROPRIETARIO." shove
	"Comunque! Meglio loro che me!" shove
	"'Sembra che stiamo FINALMENTE ottenendo una risposta alle GRANDI DOMANDE!" shove
	"It's the circle of life and death... on the road of death....." shove
	"'Se solo questi idioti avessero una bella fibbia come me, non si sarebbero trasformati in scheletri!" shove
	"'Probabilmente erano degli stronzi o qualcosa del genere, non lo so." shove
	"Mi chiedo se le cose sarebbero migliori o peggiori se invece ci fosse unapocalisse di scheletri." shove
	"'Mi chiedo perche non proviamo semplicemente a mangiare le ossa? Aiuterebbe con le scorte di cibo." shove
	"When you think about it, skeletons really are a 'social construct'..." shove
	"ORA le cose stanno iniziando a diventare piccanti!" shove
value common6-othercar-line-base
common6-othercar-line-base shuffles var common6-othercar-line

: common6-othercar-say
	common6-othercar-line depths 0= if common6-othercar-line-base shuffles to common6-othercar-line then pops nip
;

cyoa-choice: common6-othercar-lwit
	($ common6-othercar-say bbold $) text!
	solo if 
		($ "Qualcuno doveva dirlo, e quel qualcuno ero IO" bbold .. ", dice " .. cname .. "  a nessuno in particolare, a meno che " .. che .. "  non stesse parlando agli scheletri." $) +crtext
	else 
		($ "Tutti contemplano in silenzio " .. cname .. "''s intuizione filosofica." $) +crtext
	then morale++ goodfx
;

: common6-othercar-witspeech args( @ )
	<- @ .cmorale+ if
		<- @ .morale--
	else
		<- @ .cmorale- if
			<- @ .morale++
		else
			($ <- @ .colourname .. "  ha sentimenti contrastanti riguardo al discorso." $) +crtext 
		then
	then
;

cyoa-choice: common6-othercar-hwit
	($ cname .. "  fa un discorso ispiratore e contemplativo su come tutti abbiamo degli scheletri dentro di noi, e su come ignorare questi " .. "scheletri interiori" bbold .. "  e cio che rende preziosa la vita." $) text!
	solo if
		($ "Amico, ho davvero inchiodato questo discorso" bbold .. ", dice " .. cname .. " a un pubblico di assolutamente nessuno." $) +crtext morale++ goodfx
	else
		"This is uplifting and confusing/depressing at the same time." +crtext ' common6-othercar-witspeech foreach-team-char	
	then
	
	
	// inspirational speech that gives morale if low, but reduces it if high
;

cyoa-choice: common6-othercar-repair
	($ cname .. "  recupera parti e rottami da utilizzare sullauto. " $) text!
	cchar .mechanical low if
		($ cHe .. "  e terribile in questo, e riesce a rompere " .. chis .. "  mani!" $) +text morale-- strength-- bitefx
	then 
	mechanic-weldup +crtext
	car-chassis car-max-chassis = if
		"La macchina sembra intatta!" 
	else
		car-chassis 3 < if
			"La macchina sembra ancora tutta distrutta."
		else
			"La macchina sembra tutta malconcia, ma ancora robusta."
		then
	then
	car-tuneup
	+text mechanicREV mechanic-diff -2 > if mechanic++ then +cr mechanic-day?
;

cyoa-choice: common6-othercar-loot
	($ Thegroup .. "  prende le forniture " .. gthey .. "  puo trovare dallauto." $) text! ammosearch1 homesearch1
	"'Il bagagliaio e COMPLETAMENTE imbottito con rotoli di carta igienica! Sfortunatamente, e tutto marcio e pieno di insetti. Che delusione." +crtext morale2-all badfx
;

cyoa: common6-othercar
	"Death Car a Right Here" cyoa-title! someicon
	($ Thegroup .. "  si imbatte in unaltra macchina, molto simile a " .. ghis .. " ' macchina. E in putrefazione, quasi distrutta, ed e piena di " .. "SCHELETRI" rbold .. "  e probabilmente un mucchio di insetti e altre cose disgustose. Ancora un altro segno che molti tentano la Death Road to Canada, e non molti ce la fanno." $) text!
		dumbest-pass if dumbest charchoice( "italiano" )choice: common6-othercar-lwit then
		smartest-pass if smartest charchoice( "italiano" )choice: common6-othercar-hwit then
		mechanicest charchoice( ": Smonta lauto per i pezzi" )choice: common6-othercar-repair
		choice( "Cerca bottino nellauto" )choice: common6-othercar-loot
		choice( "Lascia stare lauto" )choice
;

// Special Burger (Rare)

0 value burgtype
0 3 rnd to burgtype

cyoa-choice: common6-burg-pick
	($ cname .. "  divora quel burg! " .. "IT WAS A CRITICAL HIT!!!" fbold $) text! goodfx
	"IL " bbold +crtext burgtype case
		0 of "XTRA SPICY WONKOR" endof
		1 of "WONKOR MAXIMUM" endof
		2 of "GREASED WONKOR SLIDER" endof
		3 of "WONKOR MIRACOLOSO" endof
	endcase bbold +text "  CONFERISCE LA SUA BENEDIZIONE" bbold +text
	burgtype case
		0 of cchar isfireproof? if 
			"ABBastanza PICCANTE PER TE? SCARICA DI ENDORFINE PICCANTI!" fbold +crtext
			strength++ vitality++ fitness++		
		else 
			cchar statmod{ .fireproof }
			sting2-
			"ORA LE COSE STANNO INIZIANDO A DIVENTARE PICCANTI!" fbold +crtext
			"IMMUNITA AL CALORE CONCESSA!" rbold +crtext
			0.4 cchar .tint.skin.g!
			0.4 cchar .tint.skin.b!
		then endof
		1 of strength++ vitality++ 1.2 cchar .scale.body.y! 1.3 cchar .scale.body.x! endof
		2 of fitness++ dexterity++ 2 cchar .tint.skin.r! endof
		3 of vitality++ vitality++ cchar .fullheal 2 cchar .tint.skin.b! endof
	endcase morale4+
	"Potrebbe essere un dono degli Dei dei Burgs?" +crtext
;

// XTRA SPICY WONKOR - fireproof sting-- morale2+

cyoa: common6-burg-rare
	"Extra Deluxe Ultima Wonkor Burger" cyoa-title! someicon
	($ Thegroup .. " " .. "*" rbold .. " " .. ghis .. " " $) text!
	burgtype case
		0 of "It is the XTRA SPICY WONKOR, world famous as a burg of pain, but also... hope?" endof
		1 of "E noto come WONKOR MAXIMUM, un gigantesco burg di potenza in edizione limitata." endof
		2 of "E il GREASED WONKOR SLIDER, un burg ingannevole che potrebbe scivolarti dalle mani." endof
		3 of "E il MIRACLE WONKOR, progettato come il burg perfetto sia per la salute che per il gusto." endof
	endcase bbold +text 
	solo not if "  Chi mangera questo burg?" +text then
	
	($ "'*Nota del traduttore: Burg significa hamburger." rbold $) +crtext
		solo if
			cchar charchoice( ": Mangia quel Burg" )choice: common6-burg-pick
			choice( "Abbandona stoltamente Burg" )choice: badfx
		else ' common6-burg-pick cyoa-team-choice then
;

// Coal Walking corporate retreat picnic (Rare)

cyoa-choice: common6-coal-pick
	cchar isfireproof? if
		($ cname .. "  non sente nulla dalle braci ardenti, se non un leggero senso di superiorita." $) text! goodfx morale2+
		notsolo if ($ Everyone .. "  si sente vagamente deluso da questo, in qualche modo." $) +crtext then
	then;
	($ cname .. "  inciampa quasi allistante e colpisce le braci ardenti con la faccia in avanti." $) text! bitefx sting--
	($ "Questa esperienza tempra " .. chis .. "  la pelle! E anche umilia " .. chim .. "!" $) +crtext vitality++ morale--
	notsolo if ($ Everyone .. "  fa una bella risata." $) +crtext cchar morale++all-except 
	else
		"Beh, almeno nessuno lha visto." +crtext morale++
	then goodfx
;

cyoa-choice: common6-coal-pick4
	"Chi puo camminare sui carboni?" text!
	' common6-coal-pick cyoa-team-choice
;

cyoa: common6-coal-rare
	"Esercizio di costruzione della fiducia" cyoa-title!
	($ Thegroup .. " ' trova un'area picnic con tavoli apparecchiati. C'e un sacco di cibo in giro, una parte del quale e ancora buono." $) text! 7 10 rnd trunk-food+
	"Questo era un ritiro aziendale, con carboni allestiti per le persone su cui camminare per costruire morale e fiducia tra loro o qualche altra assurdita. Potrebbero ancora essere accesi." +crtext
		choice( "Lascia semplicemente questo posto" )choice
		solo if
			soloer charchoice( ": Cammina sui carboni ardenti" )choice: common6-coal-pick
		then;
		"  Chi dovrebbe camminare sui carboni ardenti?" +text
	team-count 3 > if
		' common6-coal-pick 2 #cyoa-team-choice
		choice( "Scegli qualcun altro" )choice: common6-coal-pick4
	else
		' common6-coal-pick 3 #cyoa-team-choice
	then		
;

} }

// Blank
(*
cyoa-choice: common6--

;

cyoa: common6-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)

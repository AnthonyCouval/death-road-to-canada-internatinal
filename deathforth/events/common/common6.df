
// ILEUM Update events

cyoa{ road{ 

// Fast Food

: common6-fastfood-burged args( chara ) 
	<- chara .morale2+
	<- chara .trait@ "Gourmand" $= if
		($ <- chara .name@ .. "''s trouve ces burgers fins et apprecie vraiment! VITALITE en hausse, VITESSE en baisse!'" $) rainbold +crtext
		<- chara .vitality++ -1 STAT_DEXTERITY <- chara .bonus+
		// 1 <- chara .scale.body.y!
		1.6 <- chara .scale.body.x!
	else
		<- chara .sting--
	then
;
: common6-fastfood-megaburged args( chara ) 
	<- chara .morale3+
	2 <- chara .scale.body.x!
	<- chara .trait@ "Gourmand" $= if
		($ <- chara .name@ .. "''s trouve ces burgers fins et apprecie vraiment! VITALITE en hausse, VITESSE en baisse!'" $) rainbold +crtext
		<- chara .vitality++ -1 STAT_DEXTERITY <- chara .bonus+
	else
		<- chara .sting2-
	then
;


cyoa-choice: common6-fastfood-burgin
	($ cname .. "  trouve une boite de Big McDowell. Meme dans 100 ans, pas meme les insectes ou la moisissure ne sapprocheraient de ces choses " .. gHe .. "  se regale !" $) text!
	($ cname .. " ' mange beaucoup trop de cette ''nourriture''." $) +crtext
		cchar common6-fastfood-megaburged
		
	notsolo if
		($ Everyone .. " ' apprecie vraiment ces burgers, mais ils ne sont pas bons pour les organes internes." $) +crtext
		cchar ' common6-fastfood-burged foreach-team-char-except
	then goodfx
;

cyoa-choice: common6-fastfood-toys
	($ Thegroup .. " ' trouve un jouet de menu enfant. C'est un bon !'" $) text!
	60 %chance if
		1 WEAPON_PUKEYBALL trunk.weapon+
		"PUKEYBALL trouvee !"
	else
		50 %chance if
			1 WEAPON_GRENADE trunk.weapon+
			"''GRENADE POUR ENFANTS"
		else
			1 WEAPON_BLUEROBOT trunk.weapon+
			"ROBOT BLEU trouve !"
		then
	then
	pbold +crtext goodfx 
;

cyoa: common6-fastfood
	"Le Big McDowell" cyoa-title! choosehuman cicon
	($ Thegroup .. " ' pille un McDowell's, qui etait le restaurant le plus populaire aux Etats Unis avant la chute de la civilisation. Meme apres tout ce temps, la nourriture trouvee ici est toujours conservee grace a tous les produits chimiques." $) text! "'ATTENTION : Burging n'est pas bon pour la sante mais ne vous tuera pas." rbold +crtext
		choice( "Trouvez et mangez des hamburgers, payez le prix du hamburger" )choice: common6-fastfood-burgin
		choice( "Rechercher des jouets de repas pour les enfants a la place" )choice: common6-fastfood-toys
;


// Cloud Watching

: common6-cloud-go
	($ " NICE!	" rainbold .. cname .. " est soulage et inspire par cela" $) +text morale++ goodfx
;

cyoa-choice: common6-cloud-bicep
	"Les nuages ressemblent a une gigantesque quantite de biceps se reliant les uns aux autres, qui setirent en pompitude." text!
	common6-cloud-go strength++
;

cyoa-choice: common6-cloud-canada
	"Les nuages ressemblent exactement au pays du Canada !" text!
	($ "NICE! " rainbold .. Thegroup .. " est inspire par ce symbole incontestablement!" $) +crtext morale++ morale++all
;

cyoa-choice: common6-cloud-bazooka
	"Le nuage ressemble a une grosse bazooka, prete a faire exploser une Horde Maximum de Fromages MegaZombies." text!
	common6-cloud-go shooting++ 
;

cyoa-choice: common6-cloud-car
	"The clouds look like a car that's exploding..." text!
	common6-cloud-go mechanic++
	"En pensant un peu plus a cela, cest vraiment un peu triste." +crtext morale-- badfx
;

cyoa-choice: common6-cloud-fit
	"Le nuage ressemble a une grosse bandelette de sueur, comme si le ciel faisait du sport, peut etre du jazzercise." text!
	common6-cloud-go fitness++
;

cyoa-choice: common6-cloud-butt
	($ "Ce nuage " .. "ARRIVE PAR COINCIDENCE" rbold .. " " $) text!
	morale++ goodfx
	"Vraiment mature." rbold +crtext
;

cyoa-choice: common6-cloud-doom
	"Les nuages ressemblent exactement a votre echec imminent sur la route de la mort. Quel dommage." text!
	morale-- badfx
;

cyoa-choice: common6-cloud-dad
	($ "Cette nuage ressemble exactement a " .. cname .. "'s dad!" .. "NICE! " rainbold $) text!
	morale++ goodfx
	($ "BIEN!" rbold $) +crtext ($ "BIEN!" bbold $) +crtext
	morale++ goodfx
	($ "BIEN!" fbold $) +crtext morale++
	"On dirait que c'est ton jour de chance!" +crtext
;

cyoa-choice: common6-cloud-cloud
	"P" text!
	morale-- badfx
;

cyoa-choice: common6-cloud-bees
	"	Le nuage ressemble a un enorme essaim dabeilles. 	Il sagit en fait dun enorme essaim dabeilles." text!
	($ "AAAAAAAAAAAAAAAAH!" fbold $) +crtext sting--all morale--all bitefx
;

cyoa-choice: common6-cloud-rain
	"Le nuage semble pret a pleurer.	And then it does.	CK	" text!
	morale-- badfx 0.4 1 frnd weather! -1 1 frnd wind_force!
;

cyoa-choice: common6-cloud-zombie
	"Les nuages ressemblent a un enorme essaim de zombies! Quel mauvais presage!" text! morale-- badfx
	($ "La pensee de cela rend " .. cname .. " accolade " .. chim .. "self pour le futur." $) +crtext
	cchar damage 0 > if ($ cname .. " recupere de la sante !" $) +crtext health++ then
	cmorale- if morale++ then
;

0 var 4-clouds-stack
0 var clouds-pick-stack

: cyoa-4-clouds
	4-clouds-stack sizes for 4-clouds-stack drawcard next drop
;

deck-new
	"Canada" choice-card: common6-cloud-canada
	"Un Bazooka Geant" choice-card: common6-cloud-bazooka
	"Plusieurs Gros Biceps Flexibles" choice-card: common6-cloud-bicep
	"Une Voiture qui Explose" choice-card: common6-cloud-car
	"Un Bandeau Anti Transpiration" choice-card: common6-cloud-fit
	"Un Cul" choice-card: common6-cloud-butt
	"Votre Mort Imminente" choice-card: common6-cloud-doom
	"Votre Pere" choice-card: common6-cloud-dad
	"Un Essaim dAbeilles Bourdonnantes" choice-card: common6-cloud-bees
	"Un Nuage" choice-card: common6-cloud-cloud
	"Pluie" choice-card: common6-cloud-rain
	"Une Horde de Zombies" choice-card: common6-cloud-zombie
		value clouds-pick-stack-base
		clouds-pick-stack-base shuffles to clouds-pick-stack

: draw-4-clouds 3 stack sizes for // was originally 4, hence the name
		clouds-pick-stack depths 0= if clouds-pick-stack-base shuffles to clouds-pick-stack then
		pops nip shove
	next
	to 4-clouds-stack
;

: pick-4-clouds
	draw-4-clouds
	cyoa-4-clouds
;

cyoa: common6-cloud

	"Observation des Nuages" cyoa-title! someicon
	($ "En descendant une route paisible mais ennuyeuse " .. cname .. " voit un nuage bizarre!	Le nuage ressemble a:" $) text!
		pick-4-clouds

;

// Working Computer

cyoa-choice: common6-computer-internet
	($ "Lordinateur emet des bruits etranges, des sifflements deformes et des gazouillis. Que se passe t il?" $) text!
	mechanicest choosechar cicon
	cmechanic+ if
		($ cname .. "  obtient de maniere experte que lordinateur fonctionne. Cetait la chose la plus difficile " .. che .. "  ait jamais fait." $) +crtext mechanicREV mechanic++ mechanicREV morale++
		"The internet seems to be down, though. How come???" +crtext cchar morale--all-except goodfx
	else
		($ cname .. "  tripote lancien ordinateur. Cest un mystere complet et absolu. " .. cname .. "  commence a progresser dans la comprehension des tres vieux ordinateurs et, dune maniere ou dune autre, dans la reparation de voitures." $) +crtext mechanic++
		"Puis le courant sarrete." +crtext morale--all
	then badfx
;

cyoa-choice: common6-computer-qbasic
	($ Thegroup .. "  joue a un jeu sur des gorilles se lancant des bananes. Cest incroyable. Le pouvoir finit par sepuiser, mais jusque la, lexperience est magique." $) text! morale++all goodfx
;

cyoa-choice: common6-computer-gas
	($ Thegroup .. " ' siphonner l'essence du generateur de la maison. Il en reste une bonne quantite.	L'electricite s'arrete, ainsi que la possibilite d'aller sur Internet." $) text! 25 40 rnd trunk-gas+ badfx morale--all
;

cyoa: common6-computer
	"Dernier ordinateur, derniere chance pour Internet" cyoa-title! someicon
	($ "Dans une maison avec un generateur attache, " .. thegroup .. " ' tombe sur un ordinateur qui fonctionne. C'est l'un des plus anciens, lorsque les moniteurs etaient construits comme une enorme brique avec du verre a l'avant.	" .. Thegroup .. "  salive. Enfin, " .. "ENFIN" rbold .. ", une chance de regarder des videos Youtube !" $) text!
		choice( "Essayer daller sur Internet" )choice: common6-computer-internet
		choice( "Jouer a des jeux QBasic a la place" )choice: common6-computer-qbasic
		choice( "Siphonner le generateur" )choice: common6-computer-gas
;

// Other party's car

0 stack
	"Pourquoi les squelettes conduisent ils une voiture ??" shove
	"Ces nerds semblent etre bloques dans la ZONE OSSEUSE!" shove
	"Hoisted... by their own peatarts...." shove
	"Celui ci a ete ecrase" shove
	"Quels que soient eux je prefere moi !" shove
	"'Nous allons enfin avoir une reponse aux GRANDES QUESTIONS !" shove
	"It's the circle of life and death... on the road of death....." shove
	"'Si seuls ces gaffes avaient des ceintures magnifiques comme moi, ils n'auraient pas transformes en squelettes !" shove
	"'Peut etre qu'ils etaient des cons ou quelque chose de ce genre, je ne le sais pas." shove
	"Je me demande si les choses seraient meilleures ou pires si cetait une apocalypse squelettique a la place." shove
	"'Je me demandais simplement pourquoi nous ne tentons pas de manger les os ? Cela aiderait avec les approvisionnements en nourriture." shove
	"When you think about it, skeletons really are a 'social construct'..." shove
	"Maintenant que ca commence a se chauffer !" shove
value common6-othercar-line-base
common6-othercar-line-base shuffles var common6-othercar-line

: common6-othercar-say
	common6-othercar-line depths 0= if common6-othercar-line-base shuffles to common6-othercar-line then pops nip
;

cyoa-choice: common6-othercar-lwit
	($ common6-othercar-say bbold $) text!
	solo if 
		($ "Quelquun devait dire cela, et ce fut moi" bbold .. "dit elle " .. cname .. " a personne en particulier, sauf " .. che .. " parlait aux squelettes." $) +crtext
	else 
		($ "Tout le monde contemple tranquillement " .. cname .. "'s insight into philosophy." $) +crtext
	then morale++ goodfx
;

: common6-othercar-witspeech args( @ )
	<- @ .cmorale+ if
		<- @ .morale--
	else
		<- @ .cmorale- if
			<- @ .morale++
		else
			($ <- @ .colourname .. " a des sentiments mitiges a propos du discours." $) +crtext 
		then
	then
;

cyoa-choice: common6-othercar-hwit
	($ cname .. " bad " .. "squelettes internes" bbold .. " ce qui rend la vie precieuse." $) text!
	solo if
		($ "Jai vraiment cloue ce discours" bbold .. "dit elle " .. cname .. "  a un public de absolument personne." $) +crtext morale++ goodfx
	else
		"This is uplifting and confusing/depressing at the same time." +crtext ' common6-othercar-witspeech foreach-team-char	
	then
	
	
	// inspirational speech that gives morale if low, but reduces it if high
;

cyoa-choice: common6-othercar-repair
	($ cname .. "  recupere des pieces et de la ferraille a utiliser sur la voiture. " $) text!
	cchar .mechanical low if
		($ cHe .. "  est terrible a ca, et parvient a casser " .. chis .. "  mains!" $) +text morale-- strength-- bitefx
	then 
	mechanic-weldup +crtext
	car-chassis car-max-chassis = if
		"La voiture semble intacte !" 
	else
		car-chassis 3 < if
			"La voiture semble toujours aussi defoncee."
		else
			"La voiture a lair toute cabossee, mais elle est toujours robuste."
		then
	then
	car-tuneup
	+text mechanicREV mechanic-diff -2 > if mechanic++ then +cr mechanic-day?
;

cyoa-choice: common6-othercar-loot
	($ Thegroup .. "  prend les fournitures " .. gthey .. "  peut trouver de la voiture." $) text! ammosearch1 homesearch1
	"'Le coffre est COMPLETEMENT bourre de rouleaux de papier toilette ! Malheureusement, tout est pourri et plein d'insectes. Quelle deception." +crtext morale2-all badfx
;

cyoa: common6-othercar
	"Voiture de la mort a juste ici" cyoa-title! someicon
	($ Thegroup .. "  percute une autre voiture, tout comme " .. ghis .. " ' voiture. Elle pourrit, est presque totalement detruite et elle est pleine de " .. "SQUELETTES" rbold .. "  et probablement un tas dinsectes et dautres choses degoutantes. Encore un signe que beaucoup tentent la route de la mort vers le Canada, et que tres peu y parviennent." $) text!
		dumbest-pass if dumbest charchoice( ": Soyez philosophique" )choice: common6-othercar-lwit then
		smartest-pass if smartest charchoice( ": Soyez philosophique" )choice: common6-othercar-hwit then
		mechanicest charchoice( ": Demanteler la voiture pour les pieces" )choice: common6-othercar-repair
		choice( "Chercher des objets dans la voiture" )choice: common6-othercar-loot
		choice( "Laisser la voiture seule" )choice
;

// Special Burger (Rare)

0 value burgtype
0 3 rnd to burgtype

cyoa-choice: common6-burg-pick
	($ cname .. " devore cette ville! " .. "IT WAS A CRITICAL HIT!!!" fbold $) text! goodfx
	"Letters " bbold +crtext burgtype case
		0 of "XTRA EPICE WONKOR" endof
		1 of "WONKOR MAXIMUM" endof
		2 of "SLIDER DE WONKOR GRAISSE" endof
		3 of "MIRACLE WORKER" endof
	endcase bbold +text " CONFERE SA BENEDICTION" bbold +text
	burgtype case
		0 of cchar isfireproof? if 
			"Cest a dire assez chaud pour toi ? Rush dendorphines a lepice !" fbold +crtext
			strength++ vitality++ fitness++		
		else 
			cchar statmod{ .fireproof }
			sting2-
			"Maintenant que les choses commencent a sechauffer !" fbold +crtext
			"Immunite au feu accordee !" rbold +crtext
			0.4 cchar .tint.skin.g!
			0.4 cchar .tint.skin.b!
		then endof
		1 of strength++ vitality++ 1.2 cchar .scale.body.y! 1.3 cchar .scale.body.x! endof
		2 of fitness++ dexterity++ 2 cchar .tint.skin.r! endof
		3 of vitality++ vitality++ cchar .fullheal 2 cchar .tint.skin.b! endof
	endcase morale4+
	"Peut etre quil sagit dun cadeau des Dieux des Burgers ?" +crtext
;

// XTRA SPICY WONKOR - fireproof sting-- morale2+

cyoa: common6-burg-rare
	"Extra Deluxe Ultima Burger Wonkor" cyoa-title! someicon
	($ Thegroup .. " prend refuge dans un Wonkor Burger, qui etait une chaine de restaurants avant lapocalypse Malheureusement, ils servaient de la nourriture reelle, donc tout devrait etre spoile par maintenantPour" .. "*" rbold .. " grand surprise, il y a un Wonkor Burger inedit et encore chaud sur une table. " .. ghis .. " 'C'est le WONKOR MAXIMUM, une grosse edition limitee de burger de puissance " $) text!
	burgtype case
		0 of "It is the XTRA SPICY WONKOR, world famous as a burg of pain, but also... hope?" endof
		1 of "Cest le SLIDER WONKOR GRASSE, un burger trompeur qui peut glisser entre vos mains." endof
		2 of "Cest le MIRACLE WONKOR, concu comme le parfait burger pour la sante et le gout." endof
		3 of "Qui mangera ce burger" endof
	endcase bbold +text 
	solo not if " Qui mangera ce burg?" +text then
	
	($ "'Burg' is a slang term for 'burglar'." rbold $) +crtext
		solo if
			cchar charchoice( "mange ce burger" )choice: common6-burg-pick
			choice( "Abandonner Foolishly Burg" )choice: badfx
		else ' common6-burg-pick cyoa-team-choice then
;

// Coal Walking corporate retreat picnic (Rare)

cyoa-choice: common6-coal-pick
	cchar isfireproof? if
		($ cname .. "  ne ressent rien des charbons ardents, a part un leger sentiment de superiorite." $) text! goodfx morale2+
		notsolo if ($ Everyone .. "  se sent vaguement decu par cela, dune certaine maniere." $) +crtext then
	then;
	($ cname .. "  trebuche presque instantanement et heurte les charbons ardents face contre terre." $) text! bitefx sting--
	($ "Cette experience endurcit " .. chis .. "  la peau ! Et humilie aussi " .. chim .. "!" $) +crtext vitality++ morale--
	notsolo if ($ Everyone .. "  a bien ri." $) +crtext cchar morale++all-except 
	else
		"Bon, au moins personne ne la vu." +crtext morale++
	then goodfx
;

cyoa-choice: common6-coal-pick4
	"Qui peut marcher sur les charbons ?" text!
	' common6-coal-pick cyoa-team-choice
;

cyoa: common6-coal-rare
	"Exercice de Renforcement de la Confiance" cyoa-title!
	($ Thegroup .. " ' trouve une aire de pique nique avec des tables dressees. Il y a beaucoup de nourriture qui traine, dont une partie est encore bonne." $) text! 7 10 rnd trunk-food+
	"Cetait une retraite dentreprise, avec des charbons installes pour que les gens puissent marcher dessus afin de renforcer le moral et la confiance entre eux ou quelque chose comme ca. Ils pourraient encore etre allumes." +crtext
		choice( "Il suffit de quitter cet endroit" )choice
		solo if
			soloer charchoice( "marcher sur des braises ardentes" )choice: common6-coal-pick
		then;
		" Qui doit marcher sur des charbons ardents?" +text
	team-count 3 > if
		' common6-coal-pick 2 #cyoa-team-choice
		choice( "Choisissez quelquun dautre" )choice: common6-coal-pick4
	else
		' common6-coal-pick 3 #cyoa-team-choice
	then		
;

} }

// Blank
(*
cyoa-choice: common6--

;

cyoa: common6-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)

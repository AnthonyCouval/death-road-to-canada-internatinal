
// ILEUM Update events

cyoa{ road{ 

// Fast Food

: common6-fastfood-burged args( chara ) 
	<- chara .morale2+
	<- chara .trait@ "Gourmand" $= if
		($ <- chara .name@ .. "'s fine tastes allow true appreciation of these burgers! VITALITY up, SPEED down!" $) rainbold +crtext
		<- chara .vitality++ -1 STAT_DEXTERITY <- chara .bonus+
		// 1 <- chara .scale.body.y!
		1.6 <- chara .scale.body.x!
	else
		<- chara .sting--
	then
;
: common6-fastfood-megaburged args( chara ) 
	<- chara .morale3+
	2 <- chara .scale.body.x!
	<- chara .trait@ "Gourmand" $= if
		($ <- chara .name@ .. "'s fine tastes allow true appreciation of these burgers! VITALITY up, SPEED down!" $) rainbold +crtext
		<- chara .vitality++ -1 STAT_DEXTERITY <- chara .bonus+
	else
		<- chara .sting2-
	then
;


cyoa-choice: common6-fastfood-burgin
	($ cname .. " 100 anos poderiam passar, e nem mesmo " .. gHe .. " desenterra!" $) text!
	($ cname .. " exagera e come MUITO desse 'alimento'." $) +crtext
		cchar common6-fastfood-megaburged
		
	notsolo if
		($ Everyone .. " realmente gosta daqueles hamburgueres, mas eles nao sao otimos para os velhos orgaos internos." $) +crtext
		cchar ' common6-fastfood-burged foreach-team-char-except
	then goodfx
;

cyoa-choice: common6-fastfood-toys
	($ Thegroup .. " ' encontra um brinquedo de refeicao infantil restante. E um bom!" $) text!
	60 %chance if
		1 WEAPON_PUKEYBALL trunk.weapon+
		"PUKEYBALL encontrado!"
	else
		50 %chance if
			1 WEAPON_GRENADE trunk.weapon+
			"'GRENADA INFANTIL"
		else
			1 WEAPON_BLUEROBOT trunk.weapon+
			"ROBO AZUL encontrado!"
		then
	then
	pbold +crtext goodfx 
;

cyoa: common6-fastfood
	"O Big McDowell" cyoa-title! choosehuman cicon
	($ Thegroup .. " ' saquea um McDowell's, que era o restaurante mais popular dos Estados Unidos antes da civilizacao cair. Mesmo depois de todo esse tempo, a comida encontrada aqui ainda esta preservada devido a todos os produtos quimicos." $) text! "'AVISO: Burging nao e otimo para sua saude mas nao o matara." rbold +crtext
		choice( "Encontre e coma hamburgueres, pague o preco do hamburguer" )choice: common6-fastfood-burgin
		choice( "'Procure brinquedos de refeicao infantil em vez disso" )choice: common6-fastfood-toys
;


// Cloud Watching

: common6-cloud-go
	($ "  AGRADAVEL!	" rainbold .. cname .. "  e elevado e inspirado por isso!" $) +text morale++ goodfx
;

cyoa-choice: common6-cloud-bicep
	"As nuvens parecem uma quantidade gigantesca de biceps presos uns aos outros, flexionando em bombeamento." text!
	common6-cloud-go strength++
;

cyoa-choice: common6-cloud-canada
	"As nuvens se parecem exatamente com o pais do Canada!" text!
	($ "AGRAVEL " rainbold .. Thegroup .. "  e inspirado por este pressagio inconfundivel!" $) +crtext morale++ morale++all
;

cyoa-choice: common6-cloud-bazooka
	"A nuvem parece uma bazuca enorme, pronta para explodir uma MegaHorda de zumbis com Queijo Maximo." text!
	common6-cloud-go shooting++ 
;

cyoa-choice: common6-cloud-car
	"The clouds look like a car that's exploding..." text!
	common6-cloud-go mechanic++
	"Pensando um pouco mais, isso e realmente meio deprimente." +crtext morale-- badfx
;

cyoa-choice: common6-cloud-fit
	"A nuvem parece uma grande faixa de suor, como se o ceu estivesse fazendo exercicios aerobicos, possivelmente jazzercise." text!
	common6-cloud-go fitness++
;

cyoa-choice: common6-cloud-butt
	($ "Aquela nuvem " .. "ACONTECE" rbold .. "  parecer uma bunda. Muito maduro." $) text!
	morale++ goodfx
	"REALMENTE maduro." rbold +crtext
;

cyoa-choice: common6-cloud-doom
	"As nuvens parecem exatamente com seu fracasso iminente na Estrada da Morte. Que chatice." text!
	morale-- badfx
;

cyoa-choice: common6-cloud-dad
	($ "Aquela nuvem se parece exatamente com " .. cname .. "' o pai de!" .. "AGRAVEL " rainbold $) text!
	morale++ goodfx
	($ "LEGAL!" rbold $) +crtext ($ "LEGAL!" bbold $) +crtext
	morale++ goodfx
	($ "LEGAL!" fbold $) +crtext morale++
	"'Parece que e o seu dia de sorte!" +crtext
;

cyoa-choice: common6-cloud-cloud
	"'A nuvem parece uma nuvem. Estou desapontado que voce tenha escolhido isso, a proposito." text!
	morale-- badfx
;

cyoa-choice: common6-cloud-bees
	"A nuvem parece um enorme enxame de abelhas. Na verdade, e um enorme enxame de abelhas." text!
	($ "AAAAAAAAAAAAAAAAH!" fbold $) +crtext sting--all morale--all bitefx
;

cyoa-choice: common6-cloud-rain
	"'A nuvem parece que vai chover. E entao chove." text!
	morale-- badfx 0.4 1 frnd weather! -1 1 frnd wind_force!
;

cyoa-choice: common6-cloud-zombie
	"As nuvens parecem um enorme enxame de zumbis! Que mau pressagio!" text! morale-- badfx
	($ "O pensamento disso faz " .. cname .. "  preparar " .. chim .. " a si mesmo para o futuro." $) +crtext
	cchar damage 0 > if ($ cname .. "  recupera um pouco de saude!" $) +crtext health++ then
	cmorale- if morale++ then
;

0 var 4-clouds-stack
0 var clouds-pick-stack

: cyoa-4-clouds
	4-clouds-stack sizes for 4-clouds-stack drawcard next drop
;

deck-new
	"Canada" choice-card: common6-cloud-canada
	"Uma bazuca gigante" choice-card: common6-cloud-bazooka
	"Varios biceps grandes flexionados" choice-card: common6-cloud-bicep
	"Um carro explodindo" choice-card: common6-cloud-car
	"Uma faixa de suor" choice-card: common6-cloud-fit
	"Uma bunda" choice-card: common6-cloud-butt
	"Sua desgraca iminente" choice-card: common6-cloud-doom
	"Seu Pai" choice-card: common6-cloud-dad
	"Um Enxame de Abelhas" choice-card: common6-cloud-bees
	"Uma Nuvem" choice-card: common6-cloud-cloud
	"Chuva" choice-card: common6-cloud-rain
	"Uma Horda de Zumbis" choice-card: common6-cloud-zombie
		value clouds-pick-stack-base
		clouds-pick-stack-base shuffles to clouds-pick-stack

: draw-4-clouds 3 stack sizes for // was originally 4, hence the name
		clouds-pick-stack depths 0= if clouds-pick-stack-base shuffles to clouds-pick-stack then
		pops nip shove
	next
	to 4-clouds-stack
;

: pick-4-clouds
	draw-4-clouds
	cyoa-4-clouds
;

cyoa: common6-cloud

	"Observando as Nuvens" cyoa-title! someicon
	($ "Quando viaja por um trecho tranquilo, porem chato da estrada " .. cname .. " ve uma nuvem estranha! A nuvem parece com:" $) text!
		pick-4-clouds

;

// Working Computer

cyoa-choice: common6-computer-internet
	($ "O computador faz ruidos estranhos e agudos e emite sons distorcidos. O que diabos esta acontecendo?" $) text!
	mechanicest choosechar cicon
	cmechanic+ if
		($ cname .. " consegue habilmente fazer o computador funcionar. Foi a coisa mais dificil que " .. che .. " ja fez." $) +crtext mechanicREV mechanic++ mechanicREV morale++
		"The internet seems to be down, though. How come???" +crtext cchar morale--all-except goodfx
	else
		($ cname .. " brinca com o computador antigo. E um misterio completo e absoluto " .. cname .. " comeca a progredir na compreensao de computadores muito antigos e, de alguma forma, conserto de carros." $) +crtext mechanic++
		"Entao a energia acaba." +crtext morale--all
	then badfx
;

cyoa-choice: common6-computer-qbasic
	($ Thegroup .. " joga um jogo sobre gorilas atirando bananas uns nos outros. E incrivel. A energia eventualmente acaba, mas ate la, a experiencia e magica." $) text! morale++all goodfx
;

cyoa-choice: common6-computer-gas
	($ Thegroup .. " " $) text! 25 40 rnd trunk-gas+ badfx morale--all
;

cyoa: common6-computer
	"Ultimo Computador, Ultima Chance para Internet" cyoa-title! someicon
	($ "Em uma casa com um gerador conectado, " .. thegroup .. " ' tropeca em um computador funcionando. E um dos modelos antigos, quando os monitores eram construidos como um enorme tijolo com vidro na frente.	" .. Thegroup .. "  saliva. Finalmente, " .. "FINALMENTE" rbold .. ", uma chance de assistir a videos do Youtube!" $) text!
		choice( "Tente entrar na internet" )choice: common6-computer-internet
		choice( "Jogue jogos do QBasic em vez disso" )choice: common6-computer-qbasic
		choice( "Sifao o gerador" )choice: common6-computer-gas
;

// Other party's car

0 stack
	"Por que diabos os esqueletos estao dirigindo um carro??" shove
	"Parece que esses nerds ficaram presos na ZONA OSSEA!" shove
	"Hoisted... by their own peatarts...." shove
	"Parece que alguem foi DONO." shove
	"Tanto faz! Melhor eles do que eu!" shove
	"'Parece que estamos FINALMENTE obtendo uma resposta para as GRANDES PERGUNTAS!" shove
	"It's the circle of life and death... on the road of death....." shove
	"'Se ao menos esses idiotas tivessem uma fivela de cinto legal como eu, eles nao teriam se transformado em esqueletos!" shove
	"'Eles provavelmente eram idiotas ou algo assim, eu nao sei." shove
	"Gostaria de saber se as coisas seriam melhores ou piores se fosse um apocalipse de esqueletos." shove
	"'Gostaria de saber por que nao tentamos simplesmente comer os ossos? Isso ajudaria com os suprimentos de comida." shove
	"When you think about it, skeletons really are a 'social construct'..." shove
	"AGORA as coisas estao comecando a ficar picantes!" shove
value common6-othercar-line-base
common6-othercar-line-base shuffles var common6-othercar-line

: common6-othercar-say
	common6-othercar-line depths 0= if common6-othercar-line-base shuffles to common6-othercar-line then pops nip
;

cyoa-choice: common6-othercar-lwit
	($ common6-othercar-say bbold $) text!
	solo if 
		($ "Alguem tinha que dizer isso, e esse alguem era EU" bbold .. ", diz " .. cname .. " para ninguem em particular, a menos que " .. che .. " estivesse conversando com os esqueletos." $) +crtext
	else 
		($ "Todos contemplam silenciosamente " .. cname .. "'a percepcao filosofica de" $) +crtext
	then morale++ goodfx
;

: common6-othercar-witspeech args( @ )
	<- @ .cmorale+ if
		<- @ .morale--
	else
		<- @ .cmorale- if
			<- @ .morale++
		else
			($ <- @ .colourname .. " tem sentimentos contraditorios sobre o discurso." $) +crtext 
		then
	then
;

cyoa-choice: common6-othercar-hwit
	($ cname .. " faz um discurso inspirador e contemplativo sobre como todos nos temos esqueletos dentro de nos, e como ignorar esses " .. "esqueletos internos" bbold .. " e o que torna a vida preciosa." $) text!
	solo if
		($ "Portugues" bbold .. ", diz " .. cname .. "  para um publico de ninguem absolutamente." $) +crtext morale++ goodfx
	else
		"This is uplifting and confusing/depressing at the same time." +crtext ' common6-othercar-witspeech foreach-team-char	
	then
	
	
	// inspirational speech that gives morale if low, but reduces it if high
;

cyoa-choice: common6-othercar-repair
	($ cname .. "  recupera pecas e latacoes para usar no carro. " $) text!
	cchar .mechanical low if
		($ cHe .. "  e terrivel nisso, e consegue quebrar " .. chis .. "  maos!" $) +text morale-- strength-- bitefx
	then 
	mechanic-weldup +crtext
	car-chassis car-max-chassis = if
		"O carro parece sem danos!" 
	else
		car-chassis 3 < if
			"O carro ainda parece todo destruido."
		else
			"O carro parece todo batido, mas ainda resistente."
		then
	then
	car-tuneup
	+text mechanicREV mechanic-diff -2 > if mechanic++ then +cr mechanic-day?
;

cyoa-choice: common6-othercar-loot
	($ Thegroup .. "  pega o que suprimentos " .. gthey .. "  pode encontrar do carro." $) text! ammosearch1 homesearch1
	"'O portao traseiro esta TOTALMENTE cheio de rolos de papel higienico! Porem, eles estao todos podres e infestados de insectos. Que decepcionante." +crtext morale2-all badfx
;

cyoa: common6-othercar
	"Carro da Morte Aqui a Direita" cyoa-title! someicon
	($ Thegroup .. " colide com outro carro, muito como " .. ghis .. " 'carro. Ele esta roto, quase totalmente, e esta cheio de " .. "ESQUELETOS" rbold .. "  e provavelmente um monte de insetos e outras coisas nojentas. Mais um sinal de que muitos tentam a Estrada da Morte para o Canada, e nao muitos conseguem." $) text!
		dumbest-pass if dumbest charchoice( ": Seja filosofico" )choice: common6-othercar-lwit then
		smartest-pass if smartest charchoice( ": Seja filosofico" )choice: common6-othercar-hwit then
		mechanicest charchoice( ": Desmonte o carro para pecas" )choice: common6-othercar-repair
		choice( "Procure itens no carro" )choice: common6-othercar-loot
		choice( "Deixe o carro em paz" )choice
;

// Special Burger (Rare)

0 value burgtype
0 3 rnd to burgtype

cyoa-choice: common6-burg-pick
	($ cname .. "  devora aquele burg! " .. "IT WAS A CRITICAL HIT!!!" fbold $) text! goodfx
	"O " bbold +crtext burgtype case
		0 of "XTRA APIMENTADO WONKOR" endof
		1 of "WONKOR MAXIMO" endof
		2 of "CONTROLE DESLIZANTE WONKOR ENGRAIXADO" endof
		3 of "WONKOR MILAGROSO" endof
	endcase bbold +text "  CONFERE SUA BENCAO" bbold +text
	burgtype case
		0 of cchar isfireproof? if 
			"QUENTE O SUFICIENTE PARA VOCE? CORRIDA DE ENDORFINA DE ESPECIARIAS!" fbold +crtext
			strength++ vitality++ fitness++		
		else 
			cchar statmod{ .fireproof }
			sting2-
			"AGORA AS COISAS ESTAO COMECANDO A FICAR APIMENTADAS!" fbold +crtext
			"IMUNIDADE A CALOR CONCEDIDA!" rbold +crtext
			0.4 cchar .tint.skin.g!
			0.4 cchar .tint.skin.b!
		then endof
		1 of strength++ vitality++ 1.2 cchar .scale.body.y! 1.3 cchar .scale.body.x! endof
		2 of fitness++ dexterity++ 2 cchar .tint.skin.r! endof
		3 of vitality++ vitality++ cchar .fullheal 2 cchar .tint.skin.b! endof
	endcase morale4+
	"Sera que isso pode ser um presente dos Deuses de Burgs?" +crtext
;

// XTRA SPICY WONKOR - fireproof sting-- morale2+

cyoa: common6-burg-rare
	"Extra Deluxe Ultima Wonkor Burger" cyoa-title! someicon
	($ Thegroup .. "  se abriga em um Wonkor Burger, que era uma cadeia de burg" .. "*" rbold .. "  restaurantes antes do apocalipse. Infelizmente, eles serviam comida de verdade, entao agora tudo deve estar estragado.	Para " .. ghis .. " ' grande surpresa, ha um Wonkor Burger intocado e ainda quente em uma mesa. " $) text!
	burgtype case
		0 of "It is the XTRA SPICY WONKOR, world famous as a burg of pain, but also... hope?" endof
		1 of "E conhecido como WONKOR MAXIMUM, um gigantesco burg de poder de edicao limitada." endof
		2 of "E o GREASED WONKOR SLIDER, um burg complicado que pode escorregar de suas maos." endof
		3 of "E o MIRACLE WONKOR, projetado como o burg perfeito para saude e sabor." endof
	endcase bbold +text 
	solo not if "  Quem vai comer este burg?" +text then
	
	($ "'*Nota do tradutor: 'Burg' significa 'hamburguer'." rbold $) +crtext
		solo if
			cchar charchoice( ": Coma aquele Burg" )choice: common6-burg-pick
			choice( "Abandone Tolamente o Burg" )choice: badfx
		else ' common6-burg-pick cyoa-team-choice then
;

// Coal Walking corporate retreat picnic (Rare)

cyoa-choice: common6-coal-pick
	cchar isfireproof? if
		($ cname .. "  nao sente nada das brasas em chamas, exceto por um leve sentimento de superioridade." $) text! goodfx morale2+
		notsolo if ($ Everyone .. "  sente se vagamente desapontado com isso, de alguma forma." $) +crtext then
	then;
	($ cname .. "  quase instantaneamente tropeca e atinge as brasas em chamas com o rosto primeiro." $) text! bitefx sting--
	($ "Esta experiencia fortalece " .. chis .. "  a pele! E tambem humilha " .. chim .. "!" $) +crtext vitality++ morale--
	notsolo if ($ Everyone .. "  tem uma boa risada." $) +crtext cchar morale++all-except 
	else
		"Bem, pelo menos ninguem viu." +crtext morale++
	then goodfx
;

cyoa-choice: common6-coal-pick4
	"Quem vai andar sobre as brasas?" text!
	' common6-coal-pick cyoa-team-choice
;

cyoa: common6-coal-rare
	"Exercicio de construcao de confianca" cyoa-title!
	($ Thegroup .. " ' encontra um local para piquenique com mesas dispostas. Ha muita comida espalhada, uma parte da qual ainda esta boa." $) text! 7 10 rnd trunk-food+
	"Este foi um retiro da empresa, com brasas preparadas para as pessoas andarem para construir moral e confianca umas com as outras ou alguma bobagem dessas. Eles ainda poderiam ser acesos." +crtext
		choice( "Apenas deixe este lugar" )choice
		solo if
			soloer charchoice( ": Ande sobre brasas em chamas" )choice: common6-coal-pick
		then;
		"  Quem deveria andar sobre brasas em chamas?" +text
	team-count 3 > if
		' common6-coal-pick 2 #cyoa-team-choice
		choice( "Escolha outra pessoa" )choice: common6-coal-pick4
	else
		' common6-coal-pick 3 #cyoa-team-choice
	then		
;

} }

// Blank
(*
cyoa-choice: common6--

;

cyoa: common6-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)


// ILEUM Update events

cyoa{ road{ 

// Fast Food

: common6-fastfood-burged args( chara ) 
	<- chara .morale2+
	<- chara .trait@ "Gourmand" $= if
		($ <- chara .name@ .. "'Los buenos gustos permiten una verdadera apreciacion de estas hamburguesas! VITALIDAD arriba, VELOCIDAD abajo!" $) rainbold +crtext
		<- chara .vitality++ -1 STAT_DEXTERITY <- chara .bonus+
		// 1 <- chara .scale.body.y!
		1.6 <- chara .scale.body.x!
	else
		<- chara .sting--
	then
;
: common6-fastfood-megaburged args( chara ) 
	<- chara .morale3+
	2 <- chara .scale.body.x!
	<- chara .trait@ "Gourmand" $= if
		($ <- chara .name@ .. "'Los buenos gustos permiten una verdadera apreciacion de estas hamburguesas! VITALIDAD arriba, VELOCIDAD abajo!" $) rainbold +crtext
		<- chara .vitality++ -1 STAT_DEXTERITY <- chara .bonus+
	else
		<- chara .sting2-
	then
;


cyoa-choice: common6-fastfood-burgin
	($ cname .. "  encuentra una caja de hamburguesas Big McDowell. Podrian pasar 100 anos, y ni siquiera los insectos o el moho se acercarian a esas cosas. " .. gHe .. "  cava!" $) text!
	($ cname .. " ' se va por la borda y come DEMASIADO de esta 'comida'." $) +crtext
		cchar common6-fastfood-megaburged
		
	notsolo if
		($ Everyone .. " ' realmente disfruta esas hamburguesas, pero no son geniales para los viejos organos internos." $) +crtext
		cchar ' common6-fastfood-burged foreach-team-char-except
	then goodfx
;

cyoa-choice: common6-fastfood-toys
	($ Thegroup .. " ' encuentra un juguete de comida para ninos restante. Es bueno!" $) text!
	60 %chance if
		1 WEAPON_PUKEYBALL trunk.weapon+
		"PUKEYBALL encontrada!"
	else
		50 %chance if
			1 WEAPON_GRENADE trunk.weapon+
			"'GRANADA PARA NINOS encontrada!"
		else
			1 WEAPON_BLUEROBOT trunk.weapon+
			"ROBOT AZUL encontrado!"
		then
	then
	pbold +crtext goodfx 
;

cyoa: common6-fastfood
	"La Gran McDowell" cyoa-title! choosehuman cicon
	($ Thegroup .. " saquea un McDowell's, que fue el restaurante mas popular de los Estados Unidos antes de que cayera la civilizacion. A pesar de todo este tiempo, la comida que se encuentra aqui todavia esta preservada debido a todos los quimicos." $) text! "'ADVERTENCIA: Beber no es bueno para la salud pero no te matara." rbold +crtext
		choice( "Encuentra y come hamburguesas, paga el precio de las hamburguesas" )choice: common6-fastfood-burgin
		choice( "Juguetes de comida para ninos" )choice: common6-fastfood-toys
;


// Cloud Watching

: common6-cloud-go
	($ " BIEN!" rainbold .. cname .. "  se siente elevado e inspirado por esto!" $) +text morale++ goodfx
;

cyoa-choice: common6-cloud-bicep
	"Las nubes parecen una cantidad gigantesca de biceps unidos entre si, flexionandose con bombismo." text!
	common6-cloud-go strength++
;

cyoa-choice: common6-cloud-canada
	"Las nubes se ven exactamente como el pais de Canada!" text!
	($ "GUAY! " rainbold .. Thegroup .. "  se inspira en este presagio inconfundible!" $) +crtext morale++ morale++all
;

cyoa-choice: common6-cloud-bazooka
	"La nube parece una enorme bazuca, lista para hacer estallar una MegaHorda de zombis con queso maximo." text!
	common6-cloud-go shooting++ 
;

cyoa-choice: common6-cloud-car
	"The clouds look like a car that's exploding..." text!
	common6-cloud-go mechanic++
	"Pensandolo un poco mas, esto es realmente una especie de fastidio." +crtext morale-- badfx
;

cyoa-choice: common6-cloud-fit
	"La nube parece una gran banda para el sudor, como si el cielo estuviera haciendo cardio, posiblemente haciendo aerobic." text!
	common6-cloud-go fitness++
;

cyoa-choice: common6-cloud-butt
	($ "Esa nube " .. "CASUALMENTE" rbold .. "  parece un trasero. Muy maduro." $) text!
	morale++ goodfx
	"MUY maduro." rbold +crtext
;

cyoa-choice: common6-cloud-doom
	"Las nubes se ven exactamente como tu inminente fracaso en Death Road. Que fastidio." text!
	morale-- badfx
;

cyoa-choice: common6-cloud-dad
	($ "Esa nube se ve exactamente como " .. cname .. "el papa de !" .. "GUAY! " rainbold $) text!
	morale++ goodfx
	($ "BIEN!" rbold $) +crtext ($ "BIEN!" bbold $) +crtext
	morale++ goodfx
	($ "BIEN!" fbold $) +crtext morale++
	"'Parece que es tu dia de suerte!" +crtext
;

cyoa-choice: common6-cloud-cloud
	"'La nube parece una nube. Estoy decepcionado de que hayas elegido esto, por cierto." text!
	morale-- badfx
;

cyoa-choice: common6-cloud-bees
	"La nube parece un enorme enjambre de abejas. De hecho, es un enorme enjambre de abejas." text!
	($ "AAAAAAAAAAAAAAAAH!" fbold $) +crtext sting--all morale--all bitefx
;

cyoa-choice: common6-cloud-rain
	"'Parece que la nube va a llover. Y luego lo hace." text!
	morale-- badfx 0.4 1 frnd weather! -1 1 frnd wind_force!
;

cyoa-choice: common6-cloud-zombie
	"Las nubes parecen un enorme enjambre de zombis! Que mal presagio!" text! morale-- badfx
	($ "Pensar en esto hace que " .. cname .. " se prepare " .. chim .. "para el futuro." $) +crtext
	cchar damage 0 > if ($ cname .. " recupera algo de salud!" $) +crtext health++ then
	cmorale- if morale++ then
;

0 var 4-clouds-stack
0 var clouds-pick-stack

: cyoa-4-clouds
	4-clouds-stack sizes for 4-clouds-stack drawcard next drop
;

deck-new
	"Canada" choice-card: common6-cloud-canada
	"Una bazuca gigante" choice-card: common6-cloud-bazooka
	"Varios biceps grandes y flexibles" choice-card: common6-cloud-bicep
	"Un coche que explota" choice-card: common6-cloud-car
	"Una cinta para el sudor" choice-card: common6-cloud-fit
	"Un trasero" choice-card: common6-cloud-butt
	"Tu Muerte Inminente" choice-card: common6-cloud-doom
	"Tu Padre" choice-card: common6-cloud-dad
	"Un Enjambre de Abejas" choice-card: common6-cloud-bees
	"Una Nube" choice-card: common6-cloud-cloud
	"Lluvia" choice-card: common6-cloud-rain
	"Una Horda de Zombies" choice-card: common6-cloud-zombie
		value clouds-pick-stack-base
		clouds-pick-stack-base shuffles to clouds-pick-stack

: draw-4-clouds 3 stack sizes for // was originally 4, hence the name
		clouds-pick-stack depths 0= if clouds-pick-stack-base shuffles to clouds-pick-stack then
		pops nip shove
	next
	to 4-clouds-stack
;

: pick-4-clouds
	draw-4-clouds
	cyoa-4-clouds
;

cyoa: common6-cloud

	"Observando las Nubes" cyoa-title! someicon
	($ "Al viajar por un tramo de carretera tranquilo pero aburrido, " .. cname .. "  ve una nube extrana! La nube se parece a:" $) text!
		pick-4-clouds

;

// Working Computer

cyoa-choice: common6-computer-internet
	($ "La computadora emite ruidos extranos y agudos y chirridos distorsionados. Que diablos esta pasando?" $) text!
	mechanicest choosechar cicon
	cmechanic+ if
		($ cname .. "  consigue que la computadora funcione de forma experta. Fue lo mas dificil que " .. che .. "  haya hecho jamas." $) +crtext mechanicREV mechanic++ mechanicREV morale++
		"The internet seems to be down, though. How come???" +crtext cchar morale--all-except goodfx
	else
		($ cname .. " juguela con una computadora antigua. Es un misterio completo y absoluto. " .. cname .. " comienza a hacer progresos para entender computadoras muy antiguas y, de alguna manera, reparacion de automoviles." $) +crtext mechanic++
		"Luego el poder se apaga." +crtext morale--all
	then badfx
;

cyoa-choice: common6-computer-qbasic
	($ Thegroup .. " juega un juego sobre gorilas que arrojan bananas entre si. Es increible. El poder eventualmente se agota, pero hasta entonces, la experiencia es magica." $) text! morale++all goodfx
;

cyoa-choice: common6-computer-gas
	($ Thegroup .. " 'extrae el gasolina del generador de la casa. Hay una cantidad suficiente.	El poder se apaga, junto con la oportunidad de ir a Internet." $) text! 25 40 rnd trunk-gas+ badfx morale--all
;

cyoa: common6-computer
	"Ultima Computadora, Ultima Oportunidad para Internet" cyoa-title! someicon
	($ "En una casa con un generador adjunto, " .. thegroup .. " 'cayo sobre una computadora funcionando. Es una de las estilos mas viejos, cuando los monitores eran como una enorme ladrillo con vidrio delantero.	" .. Thegroup .. " abre la boca. Finalmente, " .. "FINALMENTE" rbold .. ", una oportunidad de ver videos de Youtube!" $) text!
		choice( "Intenta conectarte a internet" )choice: common6-computer-internet
		choice( "Mejor juega a juegos de QBasic" )choice: common6-computer-qbasic
		choice( "Sifona el generador" )choice: common6-computer-gas
;

// Other party's car

0 stack
	"Por que DIABLOS hay esqueletos conduciendo un coche?" shove
	"Parece que estos empollones han quedado atrapados en la ZONA OSEA!" shove
	"Hoisted... by their own peatarts...." shove
	"Parece que alguien ha sido POSEIDO." shove
	"Lo que sea! Mejor ellos que yo!" shove
	"'Parece que FINALMENTE vamos a obtener una respuesta a las GRANDES PREGUNTAS!" shove
	"It's the circle of life and death... on the road of death....." shove
	"'Si estos idiotas tuvieran una hebilla de cinturon tan chula como la mia, no se habrian convertido en esqueletos!" shove
	"'Probablemente eran unos idiotas o algo asi, no lo se." shove
	"Me pregunto si las cosas serian mejores o peores si fuera un apocalipsis de esqueletos." shove
	"'Me pregunto por que no intentamos simplemente comernos los huesos. Ayudaria con los suministros de alimentos." shove
	"When you think about it, skeletons really are a 'social construct'..." shove
	"AHORA las cosas empiezan a ponerse interesantes!" shove
value common6-othercar-line-base
common6-othercar-line-base shuffles var common6-othercar-line

: common6-othercar-say
	common6-othercar-line depths 0= if common6-othercar-line-base shuffles to common6-othercar-line then pops nip
;

cyoa-choice: common6-othercar-lwit
	($ common6-othercar-say bbold $) text!
	solo if 
		($ "Alguien tenia que decir esto, y ese alguien era YO" bbold .. ", dice " .. cname .. "  a nadie en particular, a menos que " .. che .. "  estuviera hablando con los esqueletos." $) +crtext
	else 
		($ "Todos contemplan silenciosamente " .. cname .. "''s perspicacia filosofica." $) +crtext
	then morale++ goodfx
;

: common6-othercar-witspeech args( @ )
	<- @ .cmorale+ if
		<- @ .morale--
	else
		<- @ .cmorale- if
			<- @ .morale++
		else
			($ <- @ .colourname .. "  tiene sentimientos encontrados sobre el discurso." $) +crtext 
		then
	then
;

cyoa-choice: common6-othercar-hwit
	($ cname .. "  da un discurso inspirador y contemplativo sobre como todos tenemos esqueletos dentro de nosotros, y como ignorar estos " .. "esqueletos internos" bbold .. "  es lo que hace que la vida sea preciosa." $) text!
	solo if
		($ "Hombre, realmente clave este discurso" bbold .. ", dice " .. cname .. "  a una audiencia de absolutamente nadie." $) +crtext morale++ goodfx
	else
		"This is uplifting and confusing/depressing at the same time." +crtext ' common6-othercar-witspeech foreach-team-char	
	then
	
	
	// inspirational speech that gives morale if low, but reduces it if high
;

cyoa-choice: common6-othercar-repair
	($ cname .. "  recupera piezas y chatarra para usar en el auto. " $) text!
	cchar .mechanical low if
		($ cHe .. "  es terrible en esto, y logra romperse " .. chis .. "  manos!" $) +text morale-- strength-- bitefx
	then 
	mechanic-weldup +crtext
	car-chassis car-max-chassis = if
		"El auto luce sin danos!" 
	else
		car-chassis 3 < if
			"El auto aun luce todo abollado."
		else
			"El auto luce todo golpeado, pero aun resistente."
		then
	then
	car-tuneup
	+text mechanicREV mechanic-diff -2 > if mechanic++ then +cr mechanic-day?
;

cyoa-choice: common6-othercar-loot
	($ Thegroup .. " toma lo que suministra " .. gthey .. " puede encontrar en el coche." $) text! ammosearch1 homesearch1
	"El maletero esta LLENO de rollos de papel higienico! Lamentablemente, esta todo podrido y lleno de bichos. Que decepcion." +crtext morale2-all badfx
;

cyoa: common6-othercar
	"Auto de la muerte hasta aqui" cyoa-title! someicon
	($ Thegroup .. "  choca con otro auto, muy parecido a " .. ghis .. " ' carro. Se esta pudriendo, casi destruido, y esta lleno de " .. "ESQUELETOS" rbold .. "  y probablemente un monton de bichos y otras cosas asquerosas. Otra senal de que muchos intentan el Camino de la Muerte a Canada, y no muchos lo logran." $) text!
		dumbest-pass if dumbest charchoice( ": Se filosofico" )choice: common6-othercar-lwit then
		smartest-pass if smartest charchoice( ": Se filosofico" )choice: common6-othercar-hwit then
		mechanicest charchoice( ": Desmontar el coche para piezas" )choice: common6-othercar-repair
		choice( "Buscar botin en el coche" )choice: common6-othercar-loot
		choice( "Dejar el coche en paz" )choice
;

// Special Burger (Rare)

0 value burgtype
0 3 rnd to burgtype

cyoa-choice: common6-burg-pick
	($ cname .. "  devora esa hamburguesa! " .. "IT WAS A CRITICAL HIT!!!" fbold $) text! goodfx
	"EL " bbold +crtext burgtype case
		0 of "WONKOR EXTRA PICANTE" endof
		1 of "WONKOR MAXIMO" endof
		2 of "DESLIZADOR DE WONKOR ENGRASADO" endof
		3 of "MILAGRO WONKOR" endof
	endcase bbold +text "  CONFERS ITS BLESSING" bbold +text
	burgtype case
		0 of cchar isfireproof? if 
			"LO SUFICIENTEMENTE CALIENTE PARA TI? RAFAGA DE ENDORFINAS PICANTES!" fbold +crtext
			strength++ vitality++ fitness++		
		else 
			cchar statmod{ .fireproof }
			sting2-
			"AHORA LAS COSAS ESTAN EMPEZANDO A PICAR!" fbold +crtext
			"SE OTORGA INMUNIDAD AL CALOR!" rbold +crtext
			0.4 cchar .tint.skin.g!
			0.4 cchar .tint.skin.b!
		then endof
		1 of strength++ vitality++ 1.2 cchar .scale.body.y! 1.3 cchar .scale.body.x! endof
		2 of fitness++ dexterity++ 2 cchar .tint.skin.r! endof
		3 of vitality++ vitality++ cchar .fullheal 2 cchar .tint.skin.b! endof
	endcase morale4+
	"Podria ser este un regalo de los Dioses de Burgs?" +crtext
;

// XTRA SPICY WONKOR - fireproof sting-- morale2+

cyoa: common6-burg-rare
	"Extra Deluxe Ultima Wonkor Burger" cyoa-title! someicon
	($ Thegroup .. "  se refugia en un Wonkor Burger, que era una cadena de burg" .. "*" rbold .. "  restaurantes antes del apocalipsis. Desafortunadamente, sirvieron comida real, por lo que todo deberia estar estropeado a estas alturas. Para " .. ghis .. " ' gran sorpresa, hay una Wonkor Burger intacta y aun caliente sobre una mesa. " $) text!
	burgtype case
		0 of "It is the XTRA SPICY WONKOR, world famous as a burg of pain, but also... hope?" endof
		1 of "Es conocido como el WONKOR MAXIMUM, un gigantesco burg de poder de edicion limitada." endof
		2 of "Es el GREASED WONKOR SLIDER, un burg enganoso que podria escaparse de tus manos." endof
		3 of "Es el MIRACLE WONKOR, disenado como el burg perfecto tanto para la salud como para el sabor." endof
	endcase bbold +text 
	solo not if "  Quien se comera este burg?" +text then
	
	($ "'*Nota del traductor: 'Burg' significa 'hamburguesa'." rbold $) +crtext
		solo if
			cchar charchoice( ": Come esa Hamburguesa" )choice: common6-burg-pick
			choice( "Abandonar Tonto la Hamburguesa" )choice: badfx
		else ' common6-burg-pick cyoa-team-choice then
;

// Coal Walking corporate retreat picnic (Rare)

cyoa-choice: common6-coal-pick
	cchar isfireproof? if
		($ cname .. "  se siente nada de las brasas ardientes, excepto por una sensacion debil de superioridad." $) text! goodfx morale2+
		notsolo if ($ Everyone .. "  se siente un poco decepcionado por esto, alguna manera." $) +crtext then
	then;
	($ cname .. "  casi inmediatamente cae y se enfrenta a las brasas calientes cara primera." $) text! bitefx sting--
	($ "Esta experiencia endurece la piel de " .. chis .. "  piel! Y tambien acaba de burlarse de " .. chim .. "!" $) +crtext vitality++ morale--
	notsolo if ($ Everyone .. "  tiene una buena carcajada." $) +crtext cchar morale++all-except 
	else
		"Bueno, al menos nadie lo vio." +crtext morale++
	then goodfx
;

cyoa-choice: common6-coal-pick4
	"Quien va a caminar sobre las brasas?" text!
	' common6-coal-pick cyoa-team-choice
;

cyoa: common6-coal-rare
	"Ejercicio de Fomento de la Confianza" cyoa-title!
	($ Thegroup .. " encuentra un sitio de picnic con mesas desplegadas. Hay mucha comida que esta por alli, una porcion de la cual aun es buena.'," $) text! 7 10 rnd trunk-food+
	"Este era un retiro de empresa, con brasas preparadas para que las personas caminaran sobre ellas para construir moral y confianza entre si o alguna cosa asi. Pueden seguir estar encendidas." +crtext
		choice( "Solo abandonar este lugar" )choice
		solo if
			soloer charchoice( "caminar sobre brasas ardientes" )choice: common6-coal-pick
		then;
		" Quien deberia caminar sobre brasas ardientes?" +text
	team-count 3 > if
		' common6-coal-pick 2 #cyoa-team-choice
		choice( "Escoge a alguien mas" )choice: common6-coal-pick4
	else
		' common6-coal-pick 3 #cyoa-team-choice
	then		
;

} }

// Blank
(*
cyoa-choice: common6--

;

cyoa: common6-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)

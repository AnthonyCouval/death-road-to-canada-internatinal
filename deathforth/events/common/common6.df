
// ILEUM Update events

cyoa{ road{ 

// Fast Food

: common6-fastfood-burged args( chara ) 
	<- chara .morale2+
	<- chara .trait@ "Gourmand" $= if
		($ <- chara .name@ .. "'s fine tastes allow true appreciation of these burgers! VITALITY up, SPEED down!" $) rainbold +crtext
		<- chara .vitality++ -1 STAT_DEXTERITY <- chara .bonus+
		// 1 <- chara .scale.body.y!
		1.6 <- chara .scale.body.x!
	else
		<- chara .sting--
	then
;
: common6-fastfood-megaburged args( chara ) 
	<- chara .morale3+
	2 <- chara .scale.body.x!
	<- chara .trait@ "Gourmand" $= if
		($ <- chara .name@ .. "'s fine tastes allow true appreciation of these burgers! VITALITY up, SPEED down!" $) rainbold +crtext
		<- chara .vitality++ -1 STAT_DEXTERITY <- chara .bonus+
	else
		<- chara .sting2-
	then
;


cyoa-choice: common6-fastfood-burgin
	($ cname .. " encontra uma caixa de hamburgueres do Big McDowell. 100 anos poderiam se passar, e nem mesmo insetos ou mofo chegariam perto dessas coisas. " .. gHe .. " se empolga!" $) text!
	($ cname .. " 'exagera e come MUITO dessa 'comida'." $) +crtext
		cchar common6-fastfood-megaburged
		
	notsolo if
		($ Everyone .. " 'realmente gosta desses hamburgueres, mas eles nao sao otimos para os orgaos internos." $) +crtext
		cchar ' common6-fastfood-burged foreach-team-char-except
	then goodfx
;

cyoa-choice: common6-fastfood-toys
	($ Thegroup .. " 'encontra um brinquedo de refeicao infantil restante. E um bom brinquedo!" $) text!
	60 %chance if
		1 WEAPON_PUKEYBALL trunk.weapon+
		"PUKEYBALL encontrada!"
	else
		50 %chance if
			1 WEAPON_GRENADE trunk.weapon+
			"'GRANADA INFANTIL"
		else
			1 WEAPON_BLUEROBOT trunk.weapon+
			"ROBO AZUL encontrado!"
		then
	then
	pbold +crtext goodfx 
;

cyoa: common6-fastfood
	"O Big McDowell" cyoa-title! choosehuman cicon
	($ Thegroup .. " 'saqueia um McDowell's, que era o restaurante mais popular nos Estados Unidos antes da civilizacao cair. Mesmo depois de todo esse tempo, a comida encontrada aqui ainda esta preservada devido a todos os produtos quimicos." $) text! "'AVISO: comer hamburgueres nao e bom para sua saude mas nao vai te matar." rbold +crtext
		choice( "Encontre e coma hamburgueres, pague o preco do hamburguer" )choice: common6-fastfood-burgin
		choice( "'Procure brinquedos de refeicao infantil em vez disso" )choice: common6-fastfood-toys
;


// Cloud Watching

: common6-cloud-go
	($ "  LEGAL!	" rainbold .. cname .. "  e elevado e inspirado por isso!" $) +text morale++ goodfx
;

cyoa-choice: common6-cloud-bicep
	"As nuvens parecem uma quantidade gigantesca de biceps grudados uns nos outros, flexionando em pumpitude." text!
	common6-cloud-go strength++
;

cyoa-choice: common6-cloud-canada
	"As nuvens se parecem exatamente com o pais do Canada!" text!
	($ "QUALQUEIRA COISA! " rainbold .. Thegroup .. "  ispirado por esta omem inconfundivel!" $) +crtext morale++ morale++all
;

cyoa-choice: common6-cloud-bazooka
	"A nuvem se parece com um bazooka enorme, pronto para destruir uma Maximum Cheesed MegaHorde de zumbis." text!
	common6-cloud-go shooting++ 
;

cyoa-choice: common6-cloud-car
	"The clouds look like a car that's exploding..." text!
	common6-cloud-go mechanic++
	"Pensando nisso um pouco mais, isto e realmente um pouco triste." +crtext morale-- badfx
;

cyoa-choice: common6-cloud-fit
	"A nuvem se parece com uma grande faixa de sudor, como o ceu esta fazendo cardio, talvez jazzercising." text!
	common6-cloud-go fitness++
;

cyoa-choice: common6-cloud-butt
	($ "Essa nuvem " .. "ACABA DE SUceder" rbold .. " parece um tronco. Maturidade real." $) text!
	morale++ goodfx
	"Maturidade real." rbold +crtext
;

cyoa-choice: common6-cloud-doom
	"As nuvens se parecem exatamente com sua iminente falha na Estrada da Morte. Que pena." text!
	morale-- badfx
;

cyoa-choice: common6-cloud-dad
	($ "Essa nuvem se parece exatamente com " .. cname .. "'pai dele!" .. "QUALQUEIRA COISA! " rainbold $) text!
	morale++ goodfx
	($ "Otimo!" rbold $) +crtext ($ "Otimo!" bbold $) +crtext
	morale++ goodfx
	($ "Otimo!" fbold $) +crtext morale++
	"'Parece que voce esta tendo um dia muito sortudo!" +crtext
;

cyoa-choice: common6-cloud-cloud
	"'A nuvem parece uma nuvem.	Por favor, estou desapontado que voce escolheu isso." text!
	morale-- badfx
;

cyoa-choice: common6-cloud-bees
	"A nuvem parece um enorme colmeia. 	Esta e, de fato, uma enorme colmeia." text!
	($ "AAAAAAAAAAAAAAAAH!" fbold $) +crtext sting--all morale--all bitefx
;

cyoa-choice: common6-cloud-rain
	"'A nuvem parece que vai chover.	E entao ela chove." text!
	morale-- badfx 0.4 1 frnd weather! -1 1 frnd wind_force!
;

cyoa-choice: common6-cloud-zombie
	"As nuvens parecem um enorme bando de zumbis!	Que pessima premonicao!" text! morale-- badfx
	($ "A ideia disso faz " .. cname .. " se preparar " .. chim .. "seu proprio futuro." $) +crtext
	cchar damage 0 > if ($ cname .. " recupera alguma saude!" $) +crtext health++ then
	cmorale- if morale++ then
;

0 var 4-clouds-stack
0 var clouds-pick-stack

: cyoa-4-clouds
	4-clouds-stack sizes for 4-clouds-stack drawcard next drop
;

deck-new
	"Canada" choice-card: common6-cloud-canada
	"Uma Bazuca Gigante" choice-card: common6-cloud-bazooka
	"Algumas Grandes Musculos Flexionando" choice-card: common6-cloud-bicep
	"Um Carro Explosivo" choice-card: common6-cloud-car
	"Uma Faixa de Suor" choice-card: common6-cloud-fit
	"Um Rabo" choice-card: common6-cloud-butt
	"Sua Perdicao Iminente" choice-card: common6-cloud-doom
	"Seu Pai" choice-card: common6-cloud-dad
	"Um Enxame de Abelhas Zumbindo" choice-card: common6-cloud-bees
	"Uma Nuvem" choice-card: common6-cloud-cloud
	"Chuva" choice-card: common6-cloud-rain
	"Uma Horda de Zumbis" choice-card: common6-cloud-zombie
		value clouds-pick-stack-base
		clouds-pick-stack-base shuffles to clouds-pick-stack

: draw-4-clouds 3 stack sizes for // was originally 4, hence the name
		clouds-pick-stack depths 0= if clouds-pick-stack-base shuffles to clouds-pick-stack then
		pops nip shove
	next
	to 4-clouds-stack
;

: pick-4-clouds
	draw-4-clouds
	cyoa-4-clouds
;

cyoa: common6-cloud

	"Observando Nuvens" cyoa-title! someicon
	($ "Ao viajar por um trecho tranquilo, mas chato da estrada, " .. cname .. "  ve uma nuvem estranha! A nuvem se parece com:" $) text!
		pick-4-clouds

;

// Working Computer

cyoa-choice: common6-computer-internet
	($ "O computador faz barulhos estranhos e agudos e chiados distorcidos. O que diabos esta acontecendo?" $) text!
	mechanicest choosechar cicon
	cmechanic+ if
		($ cname .. "  consegue habilmente fazer o computador funcionar. Foi a coisa mais dificil " .. che .. " fez isso alguma vez." $) +crtext mechanicREV mechanic++ mechanicREV morale++
		"The internet seems to be down, though. How come???" +crtext cchar morale--all-except goodfx
	else
		($ cname .. " toca com o antigo computador. E uma completa e absoluta misterio. " .. cname .. " comeca a avancar na compreensao de computadores muito velhos e, alguma forma, de reparacao de carros." $) +crtext mechanic++
		"Entao o fornecimento eletrico se apaga." +crtext morale--all
	then badfx
;

cyoa-choice: common6-computer-qbasic
	($ Thegroup .. " joga um jogo sobre macacos que jogam bananas uns nos outros umas as outras. E incrivel. O fornecimento de energia acaba, mas enquanto isso a experiencia e magica." $) text! morale++all goodfx
;

cyoa-choice: common6-computer-gas
	($ Thegroup .. " 'extrai o gasolina do gerador da casa. Ha uma boa quantidade restante.	O fornecimento eletrico se apaga, juntamente com a chance de ir na internet." $) text! 25 40 rnd trunk-gas+ badfx morale--all
;

cyoa: common6-computer
	"Ultimo Computador, Ultima Oportunidade para Internet" cyoa-title! someicon
	($ "Em uma casa com um gerador anexado, " .. thegroup .. " 'acaba por encontrar um computador funcionando. E um dos estilos mais antigos, quando os monitores eram construidos como uma enorme tijola de vidro na frente.'	" .. Thegroup .. " abre a boca. Por fim, " .. "FINALMENTE" rbold .. ", uma chance de assistir videos do Youtube!" $) text!
		choice( "Tente entrar na internet" )choice: common6-computer-internet
		choice( "Jogue jogos QBasic" )choice: common6-computer-qbasic
		choice( "Sifone o gerador" )choice: common6-computer-gas
;

// Other party's car

0 stack
	"Por que DIABOS esqueletos estao dirigindo um carro??" shove
	"Parece que esses nerds ficaram presos na ZONA OSSEA!" shove
	"Hoisted... by their own peatarts...." shove
	"Parece que alguem foi DOMINADO." shove
	"Tanto faz! Melhor eles do que eu!" shove
	"'Parece que finalmente estamos obtendo uma resposta para as GRANDES PERGUNTAS!" shove
	"It's the circle of life and death... on the road of death....." shove
	"'Se ao menos esses idiotas tivessem uma fivela de cinto legal como eu, eles nao teriam se transformado em esqueletos!" shove
	"'Eles provavelmente eram idiotas ou algo assim, nao sei." shove
	"Eu me pergunto se as coisas seriam melhores ou piores se fosse um apocalipse de esqueleto." shove
	"'Eu me pergunto por que nao tentamos simplesmente comer os ossos? Isso ajudaria com os suprimentos de comida." shove
	"When you think about it, skeletons really are a 'social construct'..." shove
	"AGORA as coisas estao comecando a ficar picantes!" shove
value common6-othercar-line-base
common6-othercar-line-base shuffles var common6-othercar-line

: common6-othercar-say
	common6-othercar-line depths 0= if common6-othercar-line-base shuffles to common6-othercar-line then pops nip
;

cyoa-choice: common6-othercar-lwit
	($ common6-othercar-say bbold $) text!
	solo if 
		($ "Alguem tinha que dizer isso, e esse alguem era EU" bbold .. "diz " .. cname .. " a ninguem em particular, a menos que " .. che .. " estava falando com os esqueletos." $) +crtext
	else 
		($ " Todos contemplam silenciosamente " .. cname .. "''filosofia profunda." $) +crtext
	then morale++ goodfx
;

: common6-othercar-witspeech args( @ )
	<- @ .cmorale+ if
		<- @ .morale--
	else
		<- @ .cmorale- if
			<- @ .morale++
		else
			($ <- @ .colourname .. " tem sentimentos mistos sobre o discurso." $) +crtext 
		then
	then
;

cyoa-choice: common6-othercar-hwit
	($ cname .. "  skeletons can lead to our downfall. " .. "esqueletos internos" bbold .. "  e o que torna a vida preciosa." $) text!
	solo if
		($ "Eu realmente acertei nessa palestra" bbold .. "diz " .. cname .. "  para uma audiencia de absolutamente ninguem." $) +crtext morale++ goodfx
	else
		"This is uplifting and confusing/depressing at the same time." +crtext ' common6-othercar-witspeech foreach-team-char	
	then
	
	
	// inspirational speech that gives morale if low, but reduces it if high
;

cyoa-choice: common6-othercar-repair
	($ cname .. "  pega pecas e lata para usar no carro. " $) text!
	cchar .mechanical low if
		($ cHe .. "  e terrivel nisso, e consegue quebrar " .. chis .. "  maos!" $) +text morale-- strength-- bitefx
	then 
	mechanic-weldup +crtext
	car-chassis car-max-chassis = if
		"O carro parece intacto!" 
	else
		car-chassis 3 < if
			"O carro ainda parece todo quebrado."
		else
			"O carro parece todo amassado, mas ainda resistente."
		then
	then
	car-tuneup
	+text mechanicREV mechanic-diff -2 > if mechanic++ then +cr mechanic-day?
;

cyoa-choice: common6-othercar-loot
	($ Thegroup .. "  pega o que suprimentos " .. gthey .. "  pode encontrar do carro." $) text! ammosearch1 homesearch1
	"'A caixa e TOTALMENTE enchida de rolos de papel higienico! Porem, eles estao todos podres e cheios de insectos. Que decepcionante." +crtext morale2-all badfx
;

cyoa: common6-othercar
	"Carro da Morte ate Aqui" cyoa-title! someicon
	($ Thegroup .. "  bate em outro carro, muito semelhante a " .. ghis .. " ' carro. Ele esta podre, quase totalmente destruido, e esta cheio de " .. "ESQUELETOS" rbold .. "  e provavelmente uma grande quantidade de insectos e outras coisas grossas.	Outro sinal de que muitos tentam a Rodovia da Morte para o Canada, mas poucos chegam." $) text!
		dumbest-pass if dumbest charchoice( ": Seja filosofico" )choice: common6-othercar-lwit then
		smartest-pass if smartest charchoice( ": Seja filosofico" )choice: common6-othercar-hwit then
		mechanicest charchoice( ": Desmonte o carro por pecas" )choice: common6-othercar-repair
		choice( "Procure no carro por saque" )choice: common6-othercar-loot
		choice( "Deixe o carro em paz" )choice
;

// Special Burger (Rare)

0 value burgtype
0 3 rnd to burgtype

cyoa-choice: common6-burg-pick
	($ cname .. "  devora aquele burg! " .. "IT WAS A CRITICAL HIT!!!" fbold $) text! goodfx
	"O " bbold +crtext burgtype case
		0 of "XTRA APIMENTADO WONKOR" endof
		1 of "WONKOR MAXIMO" endof
		2 of "SLIDER WONKOR UNGIDO" endof
		3 of "WONKOR MILAGROSO" endof
	endcase bbold +text "  CONFERE SUA BENCaO" bbold +text
	burgtype case
		0 of cchar isfireproof? if 
			"QUENTE O SUFICIENTE PARA VOCE? RUSH DE ENDORFINA APIMENTADA!" fbold +crtext
			strength++ vitality++ fitness++		
		else 
			cchar statmod{ .fireproof }
			sting2-
			"AGORA AS COISAS ESTaO COMECANDO A FICAR APIMENTADAS!" fbold +crtext
			"IMUNIDADE A CALOR CONCEDIDA!" rbold +crtext
			0.4 cchar .tint.skin.g!
			0.4 cchar .tint.skin.b!
		then endof
		1 of strength++ vitality++ 1.2 cchar .scale.body.y! 1.3 cchar .scale.body.x! endof
		2 of fitness++ dexterity++ 2 cchar .tint.skin.r! endof
		3 of vitality++ vitality++ cchar .fullheal 2 cchar .tint.skin.b! endof
	endcase morale4+
	"Sera que isto e um presente dos Deuses das Burgas?" +crtext
;

// XTRA SPICY WONKOR - fireproof sting-- morale2+

cyoa: common6-burg-rare
	"Extra Deluxe Ultimate Wonkor Burger" cyoa-title! someicon
	($ Thegroup .. " burguer, and the name of the chain is a reference to the word" .. "*" rbold .. "  0.0 " .. ghis .. " ' grande surpresa, ha um Wonkor Burger intocado e ainda quente em uma mesa. " $) text!
	burgtype case
		0 of "It is the XTRA SPICY WONKOR, world famous as a burg of pain, but also... hope?" endof
		1 of "E conhecido como o WONKOR MAXIMO, uma grande edicao limitada de hamburguer de poder." endof
		2 of "E o SLIDER WONKOR ENGORDADO, um hamburguer engracado que pode escorregar das maos." endof
		3 of "E o MIRACULOSO WONKOR, projetado para ser a perfeita opcao de hamburguer para saude e sabor." endof
	endcase bbold +text 
	solo not if " Quem vai comer este hamburguer?" +text then
	
	($ "'*Nota do tradutor: 'Hamburguer' significa 'hamburguer'." rbold $) +crtext
		solo if
			cchar charchoice( "come essa refeicao." )choice: common6-burg-pick
			choice( "Abandona a refeicao de hamburguer." )choice: badfx
		else ' common6-burg-pick cyoa-team-choice then
;

// Coal Walking corporate retreat picnic (Rare)

cyoa-choice: common6-coal-pick
	cchar isfireproof? if
		($ cname .. " nada se sente da coala ardente, exceto por uma sensacao fraca de superioridade." $) text! goodfx morale2+
		notsolo if ($ Everyone .. " sentindose levemente desapontado por isso." $) +crtext then
	then;
	($ cname .. " ele se cai quase imediatamente e cai no coalo ardente rosto primeiro." $) text! bitefx sting--
	($ "Essa experiencia o endurece " .. chis .. "  pele! E tambem humilha " .. chim .. "!" $) +crtext vitality++ morale--
	notsolo if ($ Everyone .. "  da uma boa risada." $) +crtext cchar morale++all-except 
	else
		"Bem, pelo menos ninguem viu." +crtext morale++
	then goodfx
;

cyoa-choice: common6-coal-pick4
	"Quem vai andar sobre brasas?" text!
	' common6-coal-pick cyoa-team-choice
;

cyoa: common6-coal-rare
	"Exercicio de construcao de confianca" cyoa-title!
	($ Thegroup .. " ' encontra um local para piquenique com mesas dispostas. Ha muita comida espalhada, uma parte da qual ainda esta boa." $) text! 7 10 rnd trunk-food+
	"Este era um retiro da empresa, com brasas preparadas para as pessoas andarem para construir moral e confianca umas com as outras ou alguma bobagem do tipo. Eles ainda podiam ser acesos." +crtext
		choice( "Simplesmente deixe este lugar" )choice
		solo if
			soloer charchoice( ": Ande sobre brasas ardentes" )choice: common6-coal-pick
		then;
		"  Quem deve andar sobre brasas ardentes?" +text
	team-count 3 > if
		' common6-coal-pick 2 #cyoa-team-choice
		choice( "Escolha alguem outro" )choice: common6-coal-pick4
	else
		' common6-coal-pick 3 #cyoa-team-choice
	then		
;

} }

// Blank
(*
cyoa-choice: common6--

;

cyoa: common6-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)

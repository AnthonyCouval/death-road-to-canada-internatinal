
// EYEBALL Update events

cyoa{ road{ 

// Future Toilet, based on a true story

: common5-toilet-end ($ "Die Toilette der Zukunft gibt ein surrendes Gerausch von sich und hort auf zu funktionieren." $) +crtext goodfx ;

cyoa-choice: common5-toilet-water
	($ "Die Toilette spritzt eine enorme Menge Wasser aus einem versteckten Bidet. Dies bedeckt den Raum mit Wasser, aber das meiste davon trifft " .. cname .. " direkt in die Augen." $) text!
	morale--
	solo not if ($ Theothersarename .. " freut sich daruber " $) +crtext cchar ' .morale2+ foreach-team-char-except then
	cchar .shooting@ 6 < if ($ "Aber warte! Das Bidetwasser war medizinisch und angenehm duftend " .. cname .. "'wird scharfer!" $) +crtext shooting++ then
	most-hurt damage 0 > if ($ Thegroup .. " wird durch das medizinische Bidetwasser vollstandig geheilt!" $) +crtext ' .fullheal foreach-team-char then
	common5-toilet-end
;

cyoa-blank: common5-toilet-note
	"Lied von der Toilette" cyoa-title!
	"Die Toilette spielt schone Musik. Die Musik hat keine Worte, vermittelt aber eine starke und eindringliche Botschaft der Hoffnung." text!
	morale4+all common5-toilet-end
;

cyoa-choice: common5-toilet-wings
	"'Der Toilettendeckel offnet und schliet sich schnell, mit groen WHOOMP Gerauschen, wahrend der Deckel Luftstoe ausstot. Der Deckel offnet und schliet sich etwa 30 Mal hintereinander und stoppt dann." text!
	($ cname .. "  ist verwirrt uber den Zweck dieser Funktion." $) +crtext
	common5-toilet-end gstats{ 30 ' toilets-opened +! }
;

cyoa-choice: common5-toilet-butt
	"Der Toilettensitz fangt an zu summen und leuchtet leicht." text!
	($ cname .. "  setzt sich hin. Der elektronische Toilettensitz gibt " .. chim .. " " .. "*" pbold .. "  massage, wie die Toilette wiederholt in verschiedenen Sprachen singt:" $) +crtext
	"DIE GESAE SIND DER SCHLUSSEL ZUR FITNESS	DIE GESAE SIND DER SCHLUSSEL ZUR STARKE	DIE GESAE SIND DER SCHLUSSEL ZUR MACHT	DIE GESAE SIND DAS ZENTRUM DES LEBENS" bbold +crtext
	morale++ strength++ fitness++ common5-toilet-end
	"'*Wortschatzhinweis: Gluteal bedeutet 'Hintern'" pbold +crtext
;

cyoa: common5-toilet-rare
	"'Toilette der Zukunft der Vergangenheit" cyoa-title! choosehuman cicon
	($ Thegroup .. "  entdeckt etwas Unglaubliches wahrend er ein Haus erkundet: Eine tatsachlich funktionierende Toilette. Jeder hat sein Geschaft im Wald gemacht.	Die Toilette hat ein futuristisches Bedienfeld voller Knopfe. " .. cname .. "  fuhlt sich gezwungen, einen zu drucken, und wahlt den Knopf mit dem Bild von:" $) text!
		choice( "Wasserstrahlen?" )choice: common5-toilet-water
		choice( "Musiknoten" )choice: common5-toilet-note
		choice( "Flugel schlagen" )choice: common5-toilet-wings
		choice( "Ein Hinterteil?" )choice: common5-toilet-butt
;

// Long Line

: common5-line-camp ($ "Mit dem Tag vergeudet, " .. thegroup .. " muss auf Lager legen." $) +crtext +cr fullcamp 1 calendar-day+ ;
: common5-line-shop ($ "'Das Ladenl local ist ein trendiges hochpreisiges Lebensmittelgeschaft. Die teureren Artikel sind inzwischen verrottet, aber es gibt noch Beute.'," $) +crtext
	8 12 rnd trunk-food+ 4 6 rnd trunk-medical+ goodfx ;

cyoa-choice: common5-line-pick
	
	cdog? if
		($ cname .. "  rennt entlang der Reihe und uberholt sie vollstandig. Stunden spater kehrt " .. cname .. "  mit einem Stock in der Mund zuruck " .. chis .. " " $) text!
		1 WEAPON_TREEBRANCH trunk.weapon+
		morale2+ cchar morale--all-except goodfx
		common5-line-camp
	then;
	
	ccat? if
		($ cname .. " sturmt in den Laden, um die Schlange herum. " .. cHe .. "  tritt heraus, tragend ein totem Nagetier, vielleicht ein Hamster?," $) text!
		2 trunk-food+ morale2+ goodfx
	then;
	
	cpet? if
		($ cname .. "  liegt sich hin fur eine kleine Pause nieder. Als Tier " .. che .. " 'versteht es weder den Begriff einer Reihe, noch lasst es diesen vor.'," $) text!
		morale++ goodfx
	then;
	
	cchar oblivious? if
		($ cname .. " uberspringt die Zeile komplett " .. cHe .. " ich habe die Linie nicht bemerkt, oder die Zombies." $) text!
		witsREV attitudeREV common5-line-shop
	then;
	
	ctemper- if
		cloyalty- if
			($ cname .. " " .. cHe .. " spielt nicht nach den Regeln!" $) text!
			morale++ aholeREV common5-line-shop
		then;
		($ cname .. " dauert etwa eine Minute in der Schl " .. chis .. " " $) text!
		cstrength+ if
			"WARTEN IN DER SCHLANGE IST FUR NERDS!" rbold +crtext
			($ cHe .. " explodes. " .. che .. " wird mude und stolpert weg, schwitzend. Was fur ein Workout!" $) +crtext
			rageREV fitness++ fitnessREV morale++ goodfx
		then;
		($ cHe .. "  greift den Zombie vor sich an " .. chim .. ". Die anderen Zombies bemerken es schlielich und greifen an " .. cname .. "  muss fliehen!" $) +crtext
		sting-- bitefx temperREV 
	then;
	
	ctemper+ if
		($ cname .. "  wartet geduldig in der langen Schlange. Sie bewegt sich nur sehr langsam, aber " .. che .. "  wartet ruhig ab." $) text!
		temperREV 
		common5-line-shop common5-line-camp
	then;
	
	($ cname .. "  wartet in der Schlange. Sie bewegt sich nur Zentimeterweise, zu " .. cname .. "''s groem Arger." $) text!
	attitude+roll if 
		($ cname .. "  versucht positiv zu denken " .. cHe .. "  schafft es durchzuhalten und in den Laden zu gelangen!" $) +crtext
		attitudeREV morale-- common5-line-shop
	else
		($ "Nach Stunden greifen die Zombies " .. cname .. "  wegen " .. chis .. "  standigem Gejammer an " .. cHe .. "  muss fliehen!" $) +crtext
		morale2- sting-- bitefx 
	then temperREV common5-line-camp
;

cyoa-bridge: common5-line-pick4
	"Wahle aus wer in der Zombie Schlange warten darf:" text!
	' common5-line-pick cyoa-team-choice 
;

cyoa-blank: common5-line
	"Immer noch in der Warteschlange" cyoa-title!
	($ Thegroup .. " ' trifft auf eine Horde Zombies. Sie sind vollig gefugig und warten nur in einer groen, einreihigen Schlange vor einem Geschaft. Jeder Zombie starrt geradeaus." $) text!
		choice( "Diese Verrucktheit ignorieren" )choice
	solo if
		"'Das Schild des Ladens ist verblasst und nicht mehr lesbar." +crtext
		soloer charchoice( ": In der Schlange warten" )choice: common5-line-pick
	then;
	"Du kannst jemanden schicken, der sich in die Schlange stellt, um dieses Ratsel zu losen. Es sieht so aus, als konnte es eine lange Wartezeit werden." +crtext
	team-count 3 > if
		' common5-line-pick 2 #cyoa-team-choice
		choice( "Wahle jemand anderen" )choice: common5-line-pick4
	else
		' common5-line-pick 3 #cyoa-team-choice
	then	
;

// Rock throwing

cyoa-choice: common5-rock-throw
	($ cname .. "  ist gezwungen einen Stein auf den unglucklichen Zombie zu werfen." $) text!
	cchar oblivious? if
		($ cHe .. "  wirft den Stein immer wieder versehentlich so dass er zu " .. chim .. " zuruckkehrt. Erst warf er ihn geradeaus, dann traf er beim nachsten Mal den Zaun. Das geht mehrere Stunden so weiter.		" .. cname .. "  wirft den Stein schlielich knapp uber den Zaun " .. cHe .. "  ist zu diesem Zeitpunkt fast tot vor Erschopfung." $) +crtext
		morale3- sting-- cchar morale--all-except fitness++ strength++ fitnessREV strengthREV witsattREV
		common5-line-camp
	then;
	strength+roll if
		($ cHe .. "  setzt all " .. chis .. "  kraft in einen machtigen Wurf nach unten. Der Stein verfehlt den Zombie, vernichtet aber einen kleinen Gartenzwerg." $) +crtext
		strengthREV morale++all goodfx
		33 stpick if choosechar ($ cname .. "  erkennt dass dies " .. chim .. "!" $) +crtext morale4- badfx then
	then;
	cfitness+ if
		($ cHe .. "  fuhrt einen Wurf mit perfekter Form aus. Der Stein segelt anmutig durch die Luft. Er verfehlt den Zombie bei weitem, aber was fur ein schoner Wurf!" $) +crtext
		fitnessREV morale2+ cchar morale++all-except goodfx
	then;
	($ cHe .. "  verfehlt nicht nur, sondern fuhrt auch einen wirklich peinlichen Wurf aus. " .. cHe .. "  stolpert und fallt fast uber den Zaun und die Klippe hinunter." $) +crtext badfx strengthREV fitnessREV morale-- cchar morale++all-except
;

cyoa: common5-rock
	"Der perfekte Stein" cyoa-title!
	($ Thegroup .. "  findet einen perfekten Wurffelsen auf dem Boden. Er befindet sich neben einem niedrigen Zaun, der einen groen Sturz bewacht. Unten an diesem Abgrund steht ein einziger Zombie, der dort steht und ins Leere starrt." $) text!
	solohuman if choosehuman
		($ cname ..  "  verspurt ein brennendes Verlangen den Stein auf diesen Zombie zu werfen." $) +crtext
		choice( "Zombie in Ruhe lassen" )choice
		cchar charchoice( ": Stein werfen!" )choice: common5-rock-throw
	else
		"'Das ist zu schon um es sich entgehen zu lassen. Jemand MUSS diesen Stein auf diesen Zombie werfen:" +crtext
		' common5-rock-throw cyoa-human-choice
	then
;

// Drag Race

cyoa-choice: common5-drag-race
	vehicle.which@ 1 < if
		($ Thegroup .. " ' hat gerade kein Auto, also ist es kein sehr knappes Rennen." $) text!
		"ICH GEWINNE, NERDS!" rbold +crtext morale3-all badfx
	then;
	($ "Das Rennen beginnt! Beide Fahrer " .. "BELASTIGE ES!" fbold $) text!
	car-speed 2 < if
		($ Thegroup .. "''s langsames Auto hatte in diesem Rennen uberhaupt keine Chance." $) +crtext badfx morale3-all
	then;
	car-speed 2 = if
		($ Thegroup .. "''s Auto ist zu langsam und bleibt weit zuruck." $) +crtext badfx morale--all
	then;
	car-speed 3 > if
		"Dein Auto gewinnt das Rennen locker und lasst den anderen Fahrer im Staub zuruck." +crtext goodfx morale4+all
	then;
	car-speed 2 > if
		"Dein Auto gewinnt das Rennen nur knapp. Der andere Fahrer gibt einen Daumen hoch." +crtext morale2+all goodfx
	then;
;

cyoa-choice: common5-drag-ignore
	vehicle.which@ 0 > if
		"'DAS KANNST DU NICHT TUN! DU KANNST EINE DRAG RACE HERAUSFORDERUNG NICHT IGNORIEREN!" rbold text!
		car-armor 0 > if
			($ "Der Fahrer stot gegen " .. thegroup .. "'s car, but just bounces right off the armor.
			The driver's car careens off the road and crashes into a ditch." .. Thegroup .. "  ist sehr amusiert daruber." $) +crtext morale3+all goodfx
		then;
		($ "Der Fahrer stot gegen " .. thegroup .. "'s Auto und bringt es ins Schleudern." $) +crtext
		-1 car-chassis+
		car-chassis 0 > if 
			($ "Das Auto nimmt leichten Schaden und " .. thegroup .. "  wird verletzt." $) +crtext sting--all bitefx
		else
			($ "'Das ramponierte Auto konnte dieser Belastung nicht standhalten. Es bleibt stehen." $) +crtext badfx
		then
	else
		"'SCHON, SEI SO! ICH KOMME WIEDER, WENN DU EIN AUTO HAST!" rbold text!
		($ "Der Fahrer " .. "FAHRT LOS!" fbold $) +crtext
	then

;

cyoa-choice: common5-drag-ram
	($ "Bevor das Drag Race beginnen kann, " .. thegroup .. "  pflugt "  .. ghis .. " ' Auto in das Auto des Gegners." $) text!
	car-armor 0 > if
		"'Ihr Panzerwagen vernichtet das schnelle, aber schwache Fahrzeug, die Panzerung wird nicht einmal zerkratzt." +crtext
	else
		-2 car-chassis+ bitefx
		car-chassis 0 > if
			"'Beide Autos erleiden Schaden, aber das Fahrzeug des Gegners erleidet viel mehr. Du gewinnst das Rennen!" +crtext
		else
			"Beide Autos sind so stark beschadigt dass sie nicht mehr fahren konnen. Das macht das Rennen zu einem Unentschieden!" +crtext
		then 
	then
	($ "Der nervige Fahrer rennt weg und schreit " .. "BETRUGER! " rbold .. Thegroup .. "  zapft etwas Gas aus seinem Wrack ab." $) +crtext
	morale++all 30 40 rnd trunk-gas+ goodfx
;

cyoa-choice: common5-drag-rules
	($ cname .. " ' ist sich uber die Regeln des Drag Racing nicht im Klaren." $) text!
	"THE ONLY RULE OF DRAG RACING... IS THAT THERE ARE NO RULES!" rbold +crtext
	"Der Fahrer faselt dann uber eine lange Liste von Drag Racing Regeln. " +crtext
	most-ahole if choosechar
		($ "Wahrenddessen, " .. cname .. " ' geht hin und zapft das gesamte Benzin aus dem Auto des Fahrers ab." $) +text 80 120 rnd trunk-gas+ aholeREV
	else
		($ "Etwa bei Regel 60, " .. thegroup .. "  fahrt langsam weg wahrend der Fahrer weiter redet." $) +text
	then goodfx
;

cyoa: common5-drag
	"Deutsch sein" cyoa-title! someicon
	($ "Ein schneller und schicker Wagen halt vor dir dramatisch an. Der Fahrer ruft aus seinem Fenster:" $) text!
	"AUF DER TODESSTRASSE IST DER EINZIGE WEG ZU UBERLEBEN, SCHNELL ZU SEIN. UND AUCH WUTEND." rbold +crtext
	"Er fordert dich zu einem Autorennen heraus." +crtext
		choice( "Ignoriere den Wahnsinnigen" )choice: common5-drag-ignore
	vehicle.which@ 0 > if
		choice( "Ramme sein Auto" )choice: common5-drag-ram
	then
	choice( "Nimm die Herausforderung an" )choice: common5-drag-race
	most-oblivious if
		charchoice( ": Frage nach den Regeln" )choice: common5-drag-rules
	then
;

// Radio Station

cyoa-choice: common5-radiodj-siphon
	($ Thegroup .. " ' zapft das Benzin aus dem Generator ab. Es war nicht mehr viel ubrig, aber jedes bisschen hilft.	Der Radiosender wird abgeschaltet." $) text! 15 30 rnd trunk-gas+ goodfx
;

0 value DJ-PICK

cyoa-choice: common5-radiodj-pick

	// This should be expanded later for lots of possible stuff

	cdog? if
		($ cname .. "  sagt etwas in der Art von " .. "BELLEN!" rbold .. "  oder vielleicht " .. "BORK?" rbold .. "'		Hoffentlich hat das viele Leute verwirrt die Radio horen." $) text!
		morale++ goodfx
	then;
	
	cpet? if
		($ cname .. "  greift das Mikrofon an!		Das Mikrofon ist zerstort!		" .. cname .. "  ist sehr erfreut diese Bedrohung losgeworden zu sein." $) text! bitefx morale2+ strengthREV fitnessREV
	then;
	
	cchar oblivious? if
		($ cname .. "  schaltet versehentlich das Mikrofon ein und platzt dann viele peinliche personliche Fakten heraus. Dazu gehoren einige nutzliche Informationen fur Banditen, wie z. B. Standort und aktuelle Menge an Vorraten." $) text!
		witsattREV cchar morale2-all-except
	then;
	
	cchar grating? if
		($ cname .. "  schreit wiederholt " .. "COOL IT!" fbold .. "  ins Mikrofon. Nach elf dieser Schreie fallt die Funkausrustung aus und der Generator brennt." $) text! witsattREV morale++ cchar morale2-all-except 11 to cool-it
	then;
	
	cchar paranoid? if
		($ cname .. "  sendet eine lange, fast atemlose Tirade daruber, wie die Zombie Apokalypse von der Regierung geschaffen wurde. Das Projekt trug den Codenamen " .. "PROJEKT: DINGUS" bbold .. "  und das angestrebte Ziel war es als Deckmantel fur die Herstellung von Jorts zu dienen" .. 
		"*" rbold .. "  illegal. ENDLICH wurde die Wahrheit enthullt!" $) text! witsattREV morale3+ cchar morale--all-except
		"'* Hinweis: Jorts bedeutet 'Jeansshorts'." rbold +crtext
	then;
	
	cloyalty- if
		($ cname .. "  sendet eine hilfreiche Nachricht:" $) text!
		cchar ahole? if
			"CIVILIZATION HAS ENDED!
			LIE, CHEAT, STEAL GAS FROM TOILETS!
			HOMEWORK SUCKS!!!" rbold +crtext
			"Die letzte Zeile dieser Tirade wird so laut geschrien dass das Mikrofon Ruckkopplungen aussendet." +crtext
			aholeREV morale2+
			cchar ' .loyalty-- foreach-team-char-except
		else
			"EVERYONE BUT ME
			... IS A NERD!" rbold +crtext
			loyaltyREV morale++
		then cchar morale--all-except
	then;

	cchar .hiskill-stack depths 0= if 0 else shuffles pops then to DJ-PICK drop
	
	DJ-PICK STAT_MEDICAL = if
		cchar .medical 3 > if
			($ cname .. "  wei dass " .. chis .. "  botschaft Leben retten kann, und rasselt verzweifelt so viel praktisches medizinisches Wissen herunter wie " .. che .. " can in a short time.
			Maybe this can help someone." $) text!
			medicalREV morale++
			' .medical++ foreach-team-char
		then;
		cchar .medical 3 < if
			($ cname .. "  stolz so viel medizinisches Wissen ausspuckt wie " .. che .. " can in a short time.
			It's all really bad advice that makes anyone that hears it actually worse off." $) text!
			medicalREV morale2+
			cchar ' .medical-- foreach-team-char-except
		then; medicalREV
	then
	
	DJ-PICK STAT_MECHANICAL = if
		cchar .mechanical 3 > if
			($ cname .. " broadcasts a short impromptu talk show that is a crash course in car repair. It's informative and entertaining.
			Maybe this can help someone." $) text!
			mechanicREV morale++
			' .mechanic++ foreach-team-char
		then;
		cchar .mechanical 3 < if
			($ cname .. " rambles on about repairing sweet cars and 'crafting level 3 lighting-flame-chainsaws' until the power runs out.
			Anyone that hears this actually gets worse at repairing things." $) text!
			mechanicREV morale2+
			cchar ' .mechanic-- foreach-team-char-except
		then; mechanicREV
	then
	
	DJ-PICK STAT_FITNESS = if
		cfitness+ if
			($ cname .. " creates an intense radio calisthenics show. Whoever is listening joins in with the exercises.
			The total combined sweat from this could fill up a bathtub." $) text!
			fitnessREV morale2+
			' .fitness++ foreach-team-char
		then;
	then
	
	DJ-PICK STAT_SHOOTING = if
		cshooting+ if
			($ cname .. " broadcasts some very effective tips and theories on zombie shooting.
			During the show, " .. cHe .. "  kommt mit neuen Trick Schusse auf." $) text!
			shootingREV morale++
			' .shooting++ foreach-team-char		
		then;	
	then

	DJ-PICK STAT_STRENGTH = if
		cstrength+ if
			($ cname .. "  ein Gedicht vorliest das " .. che .. "  daruber schrieb wie sehr " .. che .. " loves being strong.
			The Four Spirits of Swoleness hear this sweet poem, and they confer their blessing!" $) text!
			strengthREV morale++
			' .strength++ foreach-team-char		
		then;	
	then
	
	cchar charming? if
		($ cname .. " ' nichts mehr ausdenken kann, also " .. che .. " ' singt ein Lied.		Es ist eine groartige Leistung, sicherlich ein neuer Post Zombocalypse Hit Single!" $) text! witsattREV morale2+all
	then;
		
	($ cname .. " ' nichts aufzudenken hat und auch dann still bleibt, wenn der Strom aus." $) text! badfx morale--
;

cyoa-bridge: common5-radiodj-pick4
	"Wer wird der Radiosprecher sein?" text!
	' common5-radiodj-pick cyoa-team-choice
;

cyoa: common5-radiodj
	"Todesstrae Radio" cyoa-title!
	($ Thegroup .. " ' findet eine Radio Station. Das Sendeausrustung ist an einen Gasgenerator angeschlossen mit einem kleinen Rest Treibstoff ubrig. Genug Leistung um eine Nachricht an jemanden zu senden der sie hort " $) text!
		choice( "Generator siphonen" )choice: common5-radiodj-siphon
	solo if
		soloer charchoice( ": Nachricht senden" )choice: common5-radiodj-pick
	then;
	"Wer sollte die Nachricht senden?" +crtext
	team-count 3 > if
		' common5-radiodj-pick 2 #cyoa-team-choice
		choice( "Wahle jemand anderen" )choice: common5-radiodj-pick4
	else
		' common5-radiodj-pick 3 #cyoa-team-choice
	then	
;

// Mysterious Radio Signal

: common5-signal-win
	"Die Nachricht warnt, dass die Basis verlassen wurde, aber sie haben einen Vorrat an Munition versteckt, um anderen Uberlebenden zu helfen. Der Vorrat befindet sich in der Nahe!" +crtext morale++all ammosearch2
	solohuman if witsREV else ' .witsREV foreach-team-char // wits-total is for the whole team instead of just humans, oddly... may change this later
		wits-total 17 > if
			"The team decodes an additional ultra" +crtext
			char-gen to recruitee ricon
			($ "Wir haben auch abgeladen " .. recruitee .name@ .. ", " .. rhe .. " was a real nerd.
			You can recruit " .. rhim .. "  wenn du verzweifelt bist." $) bbold bbold +crtext
			recruit-yesno
		then
	then
;

cyoa-blank: common5-signal-decode
	solohuman if
		choosehuman ($ cname .. "  versucht den Code herauszufinden durch " .. chim .. "self..." $) text!
		cwits+ if common5-signal-win then;
	else
		"The team puts their heads together to figure out the code..." text!
		wits-total 10 > if common5-signal-win then;
	then
	"Die Nachricht sagt so etwas wie " +crtext "TRINK MEHR OVALTUMS" bbold +text
	"'Es ist nur Unsinn! Was fur eine Zeitverschwendung!" +crtext morale--all
;

cyoa-blank: common5-signal-drive
	($ Thegroup .. "  versucht zum Signal zu fahren indem er auf die Radiosendung hort wenn sie schwacher oder starker wird. Das braucht ziemlich viel Zeit." $) text!
	car-mpg 0 = if
		"Komischerweise braucht dieses Auto kein Benzin." +crtext
		goodfx
	else		
		trunk-gas 70 car-mpg * >= if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			bandit-gas
		then
	then +cr fullcamp heal-event
	($ gThey .. " findet" .. g1s .. "  ein Prepper Bunker, in dem immer noch die Nachricht abgespielt wird. Alle sind in Eile gegangen und haben einen Vorrat an Vorraten zuruckgelassen.	" $) +crtext homesearch2 ammosearch2
;

cyoa-choice: common5-signal-gov
	"DAS IST EINE OFFENSICHTLICHE FALLE DIE VON DER REGIERUNG AUFGEBAUT WURDE.	DIE GEHEIME ZOMBIE REGIERUNG!" pbold text!
	($ gHe .. "  verschwendet keine Zeit damit " .. "SCHALEN" fbold .. "  von dort!	" .. cname .. "  ist ziemlich erleichtert entkommen zu sein." $) +crtext morale2+ witsattREV
;

cyoa: common5-signal
	"Mysterioses Radiosignal" cyoa-title!
	($ Thegroup .. " ' findet eine seltsame sich wiederholende Nachricht im Radio. Es ist eine Reihe kurzer Musikstucke, Pieptone, gesprochene Zahlen und eindringliche Gleitpfeifengerausche. Da ist auch ein Kazoo drin.	Was konnte das alles bedeuten?" $) text!
		choice( "Ignorieren Sie es" )choice
		choice( "Nachricht entziffern" )choice: common5-signal-decode
		choice( "Zum Signal fahren" )choice: common5-signal-drive
		most-paranoid if
			charchoice( ": DAS IST EINE FALLE!" )choice: common5-signal-gov
		then
;

// Repair Shop repair

cyoa-choice: common5-repair-pick
	($ cname .. " spends a day trying to fix something in the junk pile... " $) text!
	cchar .mechanical@ case
		0 of ($ cHe .. "  bekommt versehentlich viele Schnitte, Kratzer und Prellungen. " .. cHe .. "  lernt auf die harte Tour, wie man repariert." $) +text endof
		1 of ($ cHe .. "  nimmt einen kaputten Tennisschlager und verwandelt ihn in einen " .. "Level 2 Ultra Ergonomischer Tennisschlager des Wals" bbold .. ". Das ist genau dasselbe wie ein normaler Tennisschlager." $) +text 1 WEAPON_TENNIS trunk.weapon+ endof
		2 of ($ cHe .. "  verwandelt eine nutzlose rostige Machete in eine nutzliche oder zumindest brauchbare " .. "Rostige Machete" bbold .. "'. Es ist eine deutliche Verbesserung." $) +text 1 WEAPON_MACHETE1 trunk.weapon+ endof
		3 of ($ cHe .. " verstarkte einen zersplitterten Baseballschlager mit Nageln und Metall um einen " .. "Nagelbesetzter Baseballschlager" bbold .. "'! Er ist groartig bis er kaputt geht!" $) +text 1 WEAPON_SPIKEDBAT trunk.weapon+ endof
		4 of ($ cHe .. " scharft einen stumpfen " .. "Astschere" bbold .. "'! Es ist eine anstandige wenn auch etwas umstandliche Waffe die nicht an dir zerbrechen wird!" $) +text 1 WEAPON_BRANCHTRIM trunk.weapon+ endof // This was a sturdy machete until I chickened out
		5 of ($ cHe .. " repariert einen kaputten " .. "Laubblaser" bbold .. " wieder zum Laufen! Lass Zombies wie Blatter fliegen!" $) +text 1 WEAPON_BLOWER trunk.weapon+ endof
		6 of ($ cHe .. " repariert einen defekten " .. "Rasentrimmer" bbold .. "'! Er ist wie ein etwas schwachlicher Sohn der Kettensage! Um die mangelnde Leistung auszugleichen, ist er viel sparsamer im Gasverbrauch." $) +text 1 WEAPON_WHACKER trunk.weapon+ endof
	endcase mechanicREV mechanic++
	+cr fullcamp heal-event
;

cyoa-bridge: common5-repair-pick4
	"Wer sollte einen Tag damit verbringen Schrott zu reparieren?" text!
	' common5-repair-pick cyoa-human-choice

;

cyoa: common5-repair
	"Reparaturwerkstatt Reparatur" cyoa-title!
	($ Thegroup .. " ' findet eine Reparaturwerkstatt. Es gibt ein bisschen Benzin und eine Menge kaputten Mull. Hier konnte etwas Gutes dabei sein, wenn man wei, wie man es repariert." $) text!
	10 16 rnd trunk-gas+
		choice( "Mull ignorieren" )choice
	solohuman if choosehuman cchar charchoice( ": Mull reparieren" )choice: common5-repair-pick "Du konntest einen Tag damit verbringen hier Dinge zu reparieren." +crtext then;
	"Wahle jemanden aus mit dem du einen Tag lang Mull reparieren kannst:" +crtext
	numhums 3 > if
		' common5-repair-pick 2 #cyoa-human-choice
		choice( "Wahle jemand anderen" )choice: common5-repair-pick4
	else
		' common5-repair-pick cyoa-human-choice
	then
;

// Health Food Store healing

0 stack
	"findet das Essen in Ordnung." shove
	"'macht das schreckliche Essen nichts aus." shove
	"hat eine positive Einstellung zu dieser Pampe!" shove
	"versucht aufgeschlossen zu bleiben." shove
	"'gibt vor dass es sich um Junkfood handelt." shove
	"ignoriert das Essen erfolgreich." shove
	"denkt an Curry." shove
	"konzentriert sich auf die gesundheitlichen Vorteile." shove
	"versucht den Geruch zu blockieren." shove
	"hat moglicherweise schon schlechteres Essen gegessen." shove
	"blockt den Ekel mental aus." shove
	"unterdruckt gerade jedes Gefuhl!" shove
value common5-healthfood-line-base
common5-healthfood-line-base shuffles var common5-healthfood-line

: common5-healthfood-say
	common5-healthfood-line depths 0= if common5-healthfood-line-base shuffles to common5-healthfood-line then pops nip
;

: common5-healthfood-stat args( @ )
	<- @ .cattitude+ if
		($ <- @ .colourname .. " " .. common5-healthfood-say $) +crtext 
	else
		<- @ .cattitude- if
			<- @ .morale2-
		else
			<- @ .morale--
		then
	then <- @ .attitudeREV <- @ .health++
	
;

cyoa-blank: common5-healthfood-go
 ($ Thegroup .. "  plundert die ekligen gesunden Lebensmittel und nimmt, was " .. gthey .. "  damit leben kann. Das ist insgesamt nicht so viel von dem Essen." $) text!
  4 6 rnd trunk-food+
  ' common5-healthfood-stat foreach-team-char
  ($ Thegroup .. "  fuhlt sich schon gesunder wenn er sich nur das eklige Essen ansieht!" $) +crtext
;

cyoa: common5-healthfood
	"Gesundheitskost Chaos" cyoa-title!
	($ Thegroup .. " ' findet ein Reformhaus. Es scheint, als hatte sich niemand die Muhe gemacht, es zu plundern, weil das Essen so ekelhaft war.	Das meiste Essen scheint sowieso zu verrotten. Oder vielleicht auch nicht, es konnte sein, dass das Essen so aussehen und riechen soll." $) text!
		choice( "Aus moralischen Grunden ignorieren" )choice: morale++all
		choice( "Plundere das eklige Essen" )choice: common5-healthfood-go
;

// The Swole-O-Flex

0 value common5-swoloflex@

: common5-swoloflex-go
	common5-swoloflex@ 1 < if
		"The machine starts its patented automated total-body bodybuilding regiment..." text!
		($ cname .. "'s Korper macht horbare Knack  und Knirschgerausche! Wie schrecklich!" $) rbold +crtext
		strength++ ouch-- 1 to common5-swoloflex@ bitefx
	else
		($ "Die Maschine klemmt " .. cname .. " in place for its innovative gymnastics simulator..." $) text!
		($ "'Ein Hochspannungskabel reit sich los und peitscht herum! Es bleibt keine Zeit darauf zu reagieren!" $) rbold +crtext
		fitness++ ouch-- 0 to common5-swoloflex@ bitefx
	then
;

cyoa-choice: common5-swoloflex-again
	($ cname .. "  beendet " .. chis .. " trainings, egal was es kostet!" $) text!
	common5-swoloflex-go
	dead? if
		($ "Bitte warten Sie bis die Maschine gestoppt ist  Please wait until the machine has stopped " .. "Kein Schmerz kein Gewinn!" rbold $) +crtext
	then;
;

cyoa-bridge: common5-swoloflex-pick
	($ cname .. " uses the innovative Swolo-O-Flex personal exercise system..." $) text!
	common5-swoloflex-go
	dead? if
		($ "Bitte warten Sie bis die Maschine gestoppt ist  Please wait until the machine has stopped " .. "Keine Ruckerstattung!" rbold $) +crtext
	then;
		choice( "Vermeiden Sie weitere Verletzungen" )choice
		choice( "Beende das Training!" )choice: common5-swoloflex-again
;

cyoa-bridge: common5-swoloflex-pick4
	"Wer sollte durch den Swole O Flex verstummelt oder getotet werden?" rbold text!
	' common5-swoloflex-pick cyoa-human-choice
;

cyoa: common5-swoloflex
	"Muskelaufbau" cyoa-title!
	0 1 rnd to common5-swoloflex@
	($ Thegroup .. " stolpert uber ein " .. "Swole O Flex Heimstarke  und Fitnessmaschine [tm]" bbold .. "'!		Du siehst diese nicht mehr so oft nach all den Klagen gegen sie und den von der Regierung angeordneten Ruckrufen." $) text!
		// The company that made it went out of business shortly before the zombocalypse hit. - Extra text
		choice( "Bleib weg davon" )choice
	solohuman if choosehuman cchar charchoice( ": Nutze Swole O Flex" )choice: common5-swoloflex-pick "Die Verwendung davon wird wehtun." rbold +crtext then;
	"Wahle jemanden aus der Swole O Flex verwendet und wahrscheinlich getotet wird:" rbold +crtext
	numhums 3 > if
		' common5-swoloflex-pick 2 #cyoa-human-choice
		choice( "Wahle jemand anderen" )choice: common5-swoloflex-pick4
	else
		' common5-swoloflex-pick cyoa-human-choice
	then
;

// Gnome Challenge Conduct Rare

defer common5-gnomerepeat
: common5-gnome-back choice( "ZURUCK" )choice: common5-gnomerepeat ;

cyoa-choice: common5-gnome-no
	"Alle Herausforderungen klingen schrecklich." text! ricon
	($ "Der Gnom kreischt " .. "WAS AUCH IMMER, NERDS!" rbold .. "  und watschelt dann schmollend davon." $) +crtext
	($ Thegroup .. "  ist erleichtert dass das alles vorbei ist." $) +crtext morale++all goodfx
;

0 stack
	"  kichert leise." shove
	" ' fuhlt sich nicht viel anders, nur irgendwie selbstgefallig." shove
	"  zuckt mit den Schultern, verdreht die Augen." shove
	"  versucht nicht zu lachen." shove
value common5-gnome-line-base
common5-gnome-line-base shuffles var common5-gnome-line

: common5-gnome-say	common5-gnome-line depths 0= if common5-gnome-line-base shuffles to common5-gnome-line then pops nip ;

: common5-gnome-morale-stat args( @ )
	<- @ .trait@ "Zivilisiert" $= if
		($ <- @ .colourname .. common5-gnome-say $) +crtext
		<- @ .morale3+
	then
	<- @ .shooting++ <- @ .mechanic++ <- @ .medical++ <- @ .strength++ <- @ .fitness++
	<- @ .vitality++ -3 STAT_MORALE <- @ .bonus!
;

cyoa-blank: common5-gnome-morale-go
	"Kannst du die Traurigkeit uberleben?" rbold text! goodfx
	' common5-gnome-morale-stat foreach-team-char
;

cyoa-bridge: common5-gnome-morale
	"Sei verflucht von ultimativer Emo Traurigkeit!" rbold text! ricon
	($ "Wenn du das auswahlst wird " .. thegroup .. "''s maximale Moral um 3 niedriger als normal sein. Im Gegenzug erhalt " .. gthey .. "  dies:" $) +crtext
	"+ Ein zusatzlicher Punkt zur maximalen Gesundheit! + Ein Punkt in allen Fertigkeiten, bis zum jeweiligen Maximum!" bbold +crtext
	common5-gnome-back
	choice( "Herausforderung annehmen!" )choice: common5-gnome-morale-go
;

: common5-gnome-melee1 "Hab den WUMMSER!" bbold +crtext 1 WEAPON_MEGASPIKEDBAT trunk.weapon+ ;
: common5-gnome-melee2 "Hab ein STARKES SCHWERT!" bbold +crtext 1 WEAPON_SCLEAVER trunk.weapon+ ;
: common5-gnome-melee3 "Hab eine HOLZKEULE!" bbold +crtext 1 WEAPON_WOODCLUB trunk.weapon+ ;
: common5-gnome-melee4 "Hab ein MACHTIGES SCHWERT!" fbold +crtext 1 WEAPON_CLAYMORE trunk.weapon+ ;
: common5-gnome-melee5 "Hab eine HALBARDE!" fbold +crtext 1 WEAPON_HALBERD trunk.weapon+ ;

0 stack
	' common5-gnome-melee1 shove
	' common5-gnome-melee2 shove
	' common5-gnome-melee3 shove
	' common5-gnome-melee4 shove
	' common5-gnome-melee5 shove
value common5-gnome-meleewep-base
common5-gnome-meleewep-base shuffles var common5-gnome-meleewep

: common5-gnome-givemelee common5-gnome-meleewep depths 0= if common5-gnome-meleewep-base shuffles to common5-gnome-meleewep then pops nip execute ;

cyoa-blank: common5-gnome-shoot-go
	"KEINE WAFFEN ODER MOTORSAGEN MEHR! GENIEE DEINE HERAUSFORDERUNG!" rbold text! goodfx
	' .nocanshoot foreach-team-char
	team-count-alive for common5-gnome-givemelee next
;

cyoa-choice: common5-gnome-shoot
	"WAFFEN UND MOTORSAGEN SIND ZU LAUT! ICH HASSE SIE!" rbold text! ricon
	($ "Wenn du das auswahlst wird " .. thegroup .. " nicht mehr in der Lage, Waffen zu schieen oder Kettensagen oder ahnliche Waffen zu benutzen. Dafur erhaltst du " $) +crtext
	"weapons" bbold +text
	common5-gnome-back
	choice( "Herausforderung annehmen!" )choice: common5-gnome-shoot-go
;

: common5-gnome-firearm1 "Hab eine UZI!" bbold +crtext 1 WEAPON_UZI trunk.weapon+ 80 trunk-bullet+ ;
: common5-gnome-firearm2 "Hab ein STURMGEWEHR!" bbold +crtext 1 WEAPON_AK47 trunk.weapon+ 50 trunk-rifle-shell+ ;
: common5-gnome-firearm3 "Hab eine MINIGUN!" fbold +crtext 1 WEAPON_MINIGUN trunk.weapon+ 100 trunk-rifle-shell+ ;
: common5-gnome-firearm4 "Hab eine AUTOMATISCHE SCHROTF Flinte!" bbold +crtext 1 WEAPON_AUTOSHOT trunk.weapon+ 30 trunk-shotgun-shell+ ;
: common5-gnome-firearm5 "Hab eine GEZOGENE LAUF SCHROTF Flinte!" bbold +crtext 1 WEAPON_SLUG trunk.weapon+ 30 trunk-shotgun-shell+ ;

0 stack
	' common5-gnome-firearm1 shove
	' common5-gnome-firearm2 shove
	' common5-gnome-firearm3 shove
	' common5-gnome-firearm4 shove
	' common5-gnome-firearm5 shove
value common5-gnome-firearm-base
common5-gnome-firearm-base shuffles var common5-gnome-firearm

: common5-gnome-givefirearm common5-gnome-firearm depths 0= if common5-gnome-firearm-base shuffles to common5-gnome-firearm then pops nip execute ;

cyoa-blank: common5-gnome-melee-go
	"NAH KAMPF WAFFEN SIND FUR IMMER VERBOTEN! VIEL GLUCK!" rbold text! goodfx
	' .atkspecial foreach-team-char
	team-count-alive for common5-gnome-givefirearm next
;

cyoa-choice: common5-gnome-melee
	"WER BRAUCHT NAHKAMPF WAFFEN WENN MAN FREUNDSCHAFT HAT?" rbold text! ricon
	($ "Wenn du das auswahlst wird " .. thegroup .. "  nicht mehr in der Lage, Nahkampfangriffe auszufuhren. Im Gegenzug erhaltst du " $) +crtext
	"eine zufallige hochwertige Schusswaffe fur jedes Mitglied deiner Gruppe mit Munition! Mogliche Waffen: Uzi, Sturmgewehr, Automatische Schrotflinte, Slug Schrotflinte, Minigun" bbold +text
	common5-gnome-back
	choice( "Herausforderung annehmen!" )choice: common5-gnome-melee-go
;

cyoa: common5-gnome-rare
	"Herausforderungsmodus" cyoa-title! char-gen to recruitee ricon recruitee .gnomify2
	($ "'Wahrend eines friedlichen Moments taucht ein Gnom auf und beginnt zu kreischen.	" .. "'ICH BIN CNOMEY DER HERAUSFORDERUNGS GNOM!" rbold .. "	Es gibt dir die Wahl einer zusatzlichen Herausforderung im Austausch fur eine " .. "Fabelhafte Belohnung!" rainbold $) text!
		choice( "Nein danke" )choice: common5-gnome-no
		choice( "Keine Nahkampfangriffe" )choice: common5-gnome-melee
		choice( "No Shooting/Chainsaw" )choice: common5-gnome-shoot
		choice( "Maximaler Moralitatsmalus" )choice: common5-gnome-morale
;

' common5-gnome-rare to common5-gnomerepeat

// Cursed Ancient Chest

0 value common5-chest@

cyoa-choice: common5-chest-rare-

;

: common5-chest-weapon
	common5-chest@ case
	0 of "Piratensabel" endof
	1 of "Machtiges Claymore" endof
	2 of "Stabiles Hackmesser" endof
	endcase rbold
;

: common5-chest-weaponpick
	common5-chest@ case
		0 of WEAPON_CUTLASS endof
		1 of WEAPON_CLAYMORE endof
		2 of WEAPON_SCLEAVER endof
	endcase cchar
;

cyoa-choice: common5-chest-pick
	($ cname .. "  hebt das auf " .. common5-chest-weapon .. "!	Es verschmilzt mit " .. chis .. "  handen und verhindert so den Gebrauch jeder anderen Waffe!" $) text!
	ctakeweapons 1 cchar .carrying.slot_count!
	common5-chest-weaponpick .weapona! 1 cchar .weapon_nodropa!
	cchar .trait@ "Spezialist" $= if
		($ cname .. "  fuhlt sich mit dieser Waffe irgendwie von Natur aus vertraut und auch ein bisschen selbstgefallig." $) +crtext strength++ fitness++ morale++ goodfx
	then; goblin-laugh
;

cyoa-bridge: common5-chest-pick4
	solohuman if
		choosehuman common5-chest-pick
	then;
	($ "Wer soll den unglaublich verfluchten " .. common5-chest-weapon .. "?" $) text!
	"WARNUNG: Die Waffe wird an den Charakter gebunden und der Charakter wird auf einen Trageslot reduziert!" rbold +crtext
	' common5-chest-pick cyoa-human-choice
;

cyoa-choice: common5-chest-book
	solohuman if
		choosehuman ($ cname .. "  liest den " .. "Deutsch sein" rbold $) text!
	else
		($ "Signs and the person who reads the last    sign has to " .. "Deutsch sein" rbold $) text!	
	then
	($ "Es ist ein boses, altes Buch, bedeckt mit Augen und Mundern. Es macht kleine Bisse und schreit auch " .. "'Streber' oder 'Nerd'" fbold .. " in tausend verschiedenen vergessenen Sprachen." $) +crtext
	morale--all sting--all bitefx
	"Das Buch vermittelt sein VERBOTENES Wissen!" bbold +crtext
	' rewardevent-random foreach-human-char
	choosehuman cchar rewardevent-random
;

0 value common5-lootweapon@

: common5-lootweapon-name
	common5-lootweapon@ case
		0 of "Ritter Schwert" endof
		1 of "Bardiche" endof
		2 of "Mittelalterliche Axt" endof
		3 of "Robustes Messer" endof
		4 of "Rapier" endof
		5 of "Halbschwert" endof
		6 of "Morgenstern" endof
	endcase rbold
;

: common5-lootweapon-pick
	1 common5-lootweapon@ case
		0 of WEAPON_KNIGHT endof
		1 of WEAPON_BARDICHE endof
		2 of WEAPON_MEDIEVAL endof
		3 of WEAPON_MACHETE2 endof
		4 of WEAPON_RAPIER endof
		5 of WEAPON_HALBERD endof
		6 of WEAPON_MORNING endof
	endcase trunk.weapon+
;

cyoa-choice: common5-chest-loot
	0 6 rnd to common5-lootweapon@
	($ Thegroup .. " findet eine Versteckung alten Waffen! Wahrend der Sortierung durch unbrauchbares Trummer, " .. gthey .. "  laufen" .. g1s .. " fallt in viele Probleme mit Splintern und winzigen, beienden Spinnen.," $) text!
	morale--all sting--all bitefx
	($ gThey .. " nimmt einige nutzliche Waffen, einschlielich eines " .. common5-lootweapon-name .. "!" $) +crtext
	common5-lootweapon-pick
	1 30 %chance if WEAPON_WOODCLUB else 50 %chance if WEAPON_GAUNTLET else WEAPON_DAGGER then then trunk.weapon+ 1 WEAPON_DAGGER trunk.weapon+ 
;

cyoa-choice: common5-chest-basket
	($ Thegroup .. " fand eine Goblin Geschenkkorb!" $) text!
	homesearch2 ammosearch2 morale++all
	($ "Das Geschenkkorb ist leicht verflucht! Nun " .. cname .. " ist verpflichtet ein Geschenk fur die ungesehenen und moglicherweise nicht existierenden Goblins zu kaufen!" $) +crtext goblin-laugh
;

: common5-chest-flash
	common5-chest@ case
	0 of "Piratensabel" endof // CUTLASS
	1 of "Machtiges Claymore" endof // CLAYMORE
	2 of "Stabiles Hackmesser" endof // SCLEAVER
	endcase fbold
;

cyoa: common5-chest-rare
	"Verfluchtes Beute" cyoa-title! someicon
	0 2 rnd to common5-chest@
	($ Thegroup .. " sucht nach Vorraten in einer gruseligen Hutte und entdeckt einen verborgenen Keller mit einem uralten Schatz!" $) text!
	"Is a mistake in the translation it should be a : instead" rbold +crtext " blinken  blinking   blinzeln " fbold +text "wahl wird den Charakter dem du die Waffe gibst auf einen gesperrten Slot setzen!" rbold +text
		choice( "Goblin Geschenk Korb" )choice: common5-chest-basket
		choice( "Das Dingusnomikon" rbold )choice: common5-chest-book
		choice( "Lange verlorene Waffen" rbold )choice: common5-chest-loot
		choice( common5-chest-flash )choice: common5-chest-pick4
;

// Hand Replacement

cyoa-choice: common5-hand-pick
	($ nursename .. "  versucht den Raum fur die Operation vorzubereiten." $) text!
	teamnurse .medicalREV
	($ cname .. "  verliert " .. chis .. "  langweilige alte Hand und bekommt eine " .. "Kettensagenhand!" rainbold .. "  Diese modifizierte Kettensage ist effizienter!" $) +crtext
	ctakeweapons WEAPON_CHAINHAND cchar .weapona! 1 cchar .weapon_nodropa!
	cchar .trait@ "Spezialist" $= if
		($ cname .. "  fuhlt sich UNGLAUBLICH selbstgefallig!" rainbold $) +crtext
		2 cchar .carrying.slot_count! morale4+
	then
	teamnurse .cmedical+ teamnurse .medical statroll or if
		"Die Operation ist gut verlaufen!" +crtext goodfx
	else
		"Die Operation war sehr ungeschickt." +crtext strength-- sting-- badfx
	then
	1 WEAPON_DSHOTGUN trunk.weapon+ // Putting this here just so that it can be given last in -loot
	choice( "Groovy" )choice
;

cyoa-bridge: common5-hand-pick4
	"Wem willst du eine Kettensagenhand geben?" text!
	' common5-hand-pick cyoa-human-choice
;

cyoa-choice: common5-hand-loot
	($ "Anstatt die Handersetzungsoperation durchzufuhren, " .. thegroup .. " ' plundert alle medizinischen Vorrate im Raum. Es gibt sogar ein Heilspray!" $) text!
		9 trunk-medical+ 3 WEAPON_HEALSPRAY trunk.weapon+ goodfx
	mechanicest choosechar cmechanic+ if
		($ cname .. " verwandelt die Kettensagenhand in eine normale schwingbare Kettensage!" $) +crtext
		mechanicREV 1 WEAPON_CHAINSAW trunk.weapon+ mechanicREV
	else
		"Die Kettensagenhand wurde nur fur die Verwendung am Arm entwickelt, deshalb wird sie zuruckgelassen." +crtext
	then 1 WEAPON_DSHOTGUN trunk.weapon+
;

cyoa: common5-hand-rare
	"Hand Upgrade" cyoa-title!
	medicalest to teamnurse
	($ Thegroup .. "  findet einen behelfsmaigen OP Saal. In einer Ecke befindet sich ein Skelett mit einer doppellaufigen Schrotflinte und einer prothetischen Kettensagenhand, beide leer. Kettensagenhand? Das scheint eine gute Idee zu sein!" $) text!
		choice( "Stattdessen Vorrate plundern" )choice: common5-hand-loot
	solohuman if
		choosehuman cchar charchoice( ": Hand ersetzen" )choice: common5-hand-pick 
		"Kettensagen sind besser als Hande." +crtext then;
	"'Ersetze jemandes Hand durch eine Kettensage:" +crtext
	numhums 3 > if
		' common5-hand-pick 2 #cyoa-human-choice
		choice( "Wahle jemand anderen" )choice: common5-hand-pick4
	else
		' common5-hand-pick cyoa-human-choice
	then
;

} }

// Blank
(*
cyoa-choice: common5--

;

cyoa: common5-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)

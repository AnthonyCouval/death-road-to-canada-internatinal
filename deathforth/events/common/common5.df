
// EYEBALL Update events

cyoa{ road{ 

// Future Toilet, based on a true story

: common5-toilet-end ($ "O banheiro do futuro emite um zumbido e para de funcionar." $) +crtext goodfx ;

cyoa-choice: common5-toilet-water
	($ "O banheiro pulveriza uma enorme quantidade de agua de um bide oculto. Isso cobre a sala com agua, mas a maior parte acerta " .. cname .. "  bem nos olhos." $) text!
	morale--
	solo not if ($ Theothersarename .. "  encantado com isso! " $) +crtext cchar ' .morale2+ foreach-team-char-except then
	cchar .shooting@ 6 < if ($ "Mas espere! A agua do bide era medicada e agradavelmente perfumada! " .. cname .. "'a visao fica mais nitida!" $) +crtext shooting++ then
	most-hurt damage 0 > if ($ Thegroup .. "  e totalmente curado pela agua medicada do bide!" $) +crtext ' .fullheal foreach-team-char then
	common5-toilet-end
;

cyoa-blank: common5-toilet-note
	"Musica do Banheiro" cyoa-title!
	"O banheiro toca musica linda. A musica nao tem palavras, mas transmite uma forte e triste mensagem de esperanca." text!
	morale4+all common5-toilet-end
;

cyoa-choice: common5-toilet-wings
	"'A tampa do banheiro abre e fecha rapidamente, com grandes soms WHOOMP enquanto a tampa arremessa rajadas de ar. A tampa abre e fecha aproximadamente 30 vezes em fila, depois para silencio." text!
	($ cname .. " esta confuso com a finalidade desta funcao." $) +crtext
	common5-toilet-end gstats{ 30 ' toilets-opened +! }
;

cyoa-choice: common5-toilet-butt
	"A toalha do banheiro comeca a vibrar e brilhar levemente." text!
	($ cname .. " se senta. A toalha eletrica do banheiro da " .. chim .. " " .. "*" pbold .. " massagem, pois o banheiro repetidamente canta em diferentes idiomas:" $) +crtext
	"OS GLUTEOS SaO A CHAVE DA FISCALIZA	OS GLUTEOS SaO A CHAVE DA FORCA	OS GLUTEOS SaO A CHAVE DO PODER	OS GLUTEOS SaO O CENTRO DA VIDA" bbold +crtext
	morale++ strength++ fitness++ common5-toilet-end
	"'*Nota de vocabulario: Gluteal significa 'cara'" pbold +crtext
;

cyoa: common5-toilet-rare
	"'Banheiro do Passado e Futuro" cyoa-title! choosehuman cicon
	($ Thegroup .. " descobre algo incrivel enquanto explora uma casa: Um banheiro realmente funcional. Todos estavam fazendo seus negocios na floresta.	O banheiro possui um painel de controle futuristico cheio de botoes. " .. cname .. " se sente obrigado a pressionar uma, e escolhe o botao marcado com uma imagem de:" $) text!
		choice( "Jatos de Agua?" )choice: common5-toilet-water
		choice( "Notas Musicais" )choice: common5-toilet-note
		choice( "Bater de Asas" )choice: common5-toilet-wings
		choice( "Uma bunda?" )choice: common5-toilet-butt
;

// Long Line

: common5-line-camp ($ "Com o dia perdido, " .. thegroup .. " tem que montar acampamento." $) +crtext +cr fullcamp 1 calendar-day+ ;
: common5-line-shop ($ "A loja e uma mercearia chique e moderna. A maioria dos itens mais chiques apodreceu, mas ainda ha alguns saques." $) +crtext
	8 12 rnd trunk-food+ 4 6 rnd trunk-medical+ goodfx ;

cyoa-choice: common5-line-pick
	
	cdog? if
		($ cname .. " 	Scamper para a linha e depois passa completamente.		Horas depois, " .. cname .. "  volta com um pau na " .. chis .. "  boca." $) text!
		1 WEAPON_TREEBRANCH trunk.weapon+
		morale2+ cchar morale--all-except goodfx
		common5-line-camp
	then;
	
	ccat? if
		($ cname .. "  entra correndo na loja, furando a fila. " .. cHe .. "  sai carregando um roedor morto, possivelmente um gerbilo?" $) text!
		2 trunk-food+ morale2+ goodfx
	then;
	
	cpet? if
		($ cname .. "  deita para descansar um pouco. Como um animal, " .. che .. " ' ou nao entende o conceito de fila, ou finge que nao." $) text!
		morale++ goodfx
	then;
	
	cchar oblivious? if
		($ cname .. "  fura a fila completamente. " .. cHe .. " ' nao percebeu que tinha fila, nem zumbis." $) text!
		witsREV attitudeREV common5-line-shop
	then;
	
	ctemper- if
		cloyalty- if
			($ cname .. "  simplesmente pula a fila completamente. " .. cHe .. "  naO respeita as regras!" $) text!
			morale++ aholeREV common5-line-shop
		then;
		($ cname .. "  dura cerca de um minuto na fila antes de perder a " .. chis .. "  paciencia!" $) text!
		cstrength+ if
			"ESPERAR NA FILA E PARA NERDS!" rbold +crtext
			($ cHe .. " ' comeca a socar rapidamente todos os zumbis na fila ate que eles explodam. Sao muitos para socar entao " .. che .. "  se cansa e se desmaia, sudando. Que uma brincadeira!," $) +crtext
			rageREV fitness++ fitnessREV morale++ goodfx
		then;
		($ cHe .. "  comeca a atacar o zumbi que esta frente a " .. chim .. ". Os outros zumbis eventualmente percebem e comecam a atacar. " .. cname .. "  e obrigado a correr!" $) +crtext
		sting-- bitefx temperREV 
	then;
	
	ctemper+ if
		($ cname .. "  aguarda pacientemente na fila. Ela avanca lentamente, mas , " .. che .. "  aguarda calma." $) text!
		temperREV 
		common5-line-shop common5-line-camp
	then;
	
	($ cname .. "  aguarda na fila. Ela avanca, para , " .. cname .. "' grande irritacao dele.'" $) text!
	attitude+roll if 
		($ cname .. " tenta pensar nisso de forma positiva. " .. cHe .. " consegue suportar e entrar na loja!" $) +crtext
		attitudeREV morale-- common5-line-shop
	else
		($ "Depois de horas disso, os zumbis se voltam contra , " .. cname .. " devido a " .. chis .. " queixas constantes. " .. cHe .. " e forcado a fugir!" $) +crtext
		morale2- sting-- bitefx 
	then temperREV common5-line-camp
;

cyoa-bridge: common5-line-pick4
	"Escolha quem vai esperar na fila dos zumbis:" text!
	' common5-line-pick cyoa-team-choice 
;

cyoa-blank: common5-line
	"Ainda tendo que esperar na fila" cyoa-title!
	($ Thegroup .. " ' corre em uma horda de zumbis. Eles estao completamente doceis, apenas esperando do lado de fora de uma loja em uma grande fila unica. Cada zumbi esta olhando diretamente para frente." $) text!
		choice( "Ignore essa esquisitice" )choice
	solo if
		"'A placa da loja esta desbotada e nao pode ser lida." +crtext
		soloer charchoice( ": Espere na fila" )choice: common5-line-pick
	then;
	"Voce pode enviar alguem para entrar na fila, para resolver este misterio. Parece que pode ser uma longa espera." +crtext
	team-count 3 > if
		' common5-line-pick 2 #cyoa-team-choice
		choice( "Escolha alguem outro" )choice: common5-line-pick4
	else
		' common5-line-pick 3 #cyoa-team-choice
	then	
;

// Rock throwing

cyoa-choice: common5-rock-throw
	($ cname .. "  e compelido a jogar uma pedra no infeliz zumbi." $) text!
	cchar oblivious? if
		($ cHe .. "  continua acidentalmente jogando a pedra de maneiras que a fazem retornar para " .. chim .. ". Primeiro foi direto para cima, depois nas poucas vezes que atingiu a cerca. Isso continua por varias horas.		" .. cname .. "  eventualmente joga a pedra por cima da cerca, por pouco. " .. cHe .. "  esta quase morto de exaustao ate entao." $) +crtext
		morale3- sting-- cchar morale--all-except fitness++ strength++ fitnessREV strengthREV witsattREV
		common5-line-camp
	then;
	strength+roll if
		($ cHe .. "  coloca todo " .. chis .. "  pode em um poderoso lancamento para baixo. A pedra erra o zumbi, mas destroi um pequeno gnomo de jardim." $) +crtext
		strengthREV morale++all goodfx
		33 stpick if choosechar ($ cname .. "  percebe que isso e muito ofensivo para " .. chim .. "!" $) +crtext morale4- badfx then
	then;
	cfitness+ if
		($ cHe .. " 1" $) +crtext
		fitnessREV morale2+ cchar morale++all-except goodfx
	then;
	($ cHe .. " 'nao so erra, mas faz uma jogada muito feia' " .. cHe .. " tropeca e quase cai sobre a cerca e fora do penhasco." $) +crtext badfx strengthREV fitnessREV morale-- cchar morale++all-except
;

cyoa: common5-rock
	"A Pedra Perfeita" cyoa-title!
	($ Thegroup .. " encontra uma pedra perfeita para atirar no chao. Ela esta ao lado de uma cerca baixa que protege uma grande queda. No fundo desta queda esta um unico zumbi, parado la, olhando para lugar nenhum em particular." $) text!
	solohuman if choosehuman
		($ cname ..  " 'sente um desejo ardente de atirar a pedra neste zumbi.'" $) +crtext
		choice( "Deixe o zumbi em paz" )choice
		cchar charchoice( "joga pedra!" )choice: common5-rock-throw
	else
		"'E muito bom para ser desprezado. Alguem DEVE lancar essa pedra neste zumbi:" +crtext
		' common5-rock-throw cyoa-human-choice
	then
;

// Drag Race

cyoa-choice: common5-drag-race
	vehicle.which@ 1 < if
		($ Thegroup .. " 'ele nao tem carro nesse momento, portanto, e uma corrida muito distante." $) text!
		"EU VENCHEI, CARA!" rbold +crtext morale3-all badfx
	then;
	($ "A corrida comeca! Ambos os pilotos " .. "De maquina!" fbold $) text!
	car-speed 2 < if
		($ Thegroup .. "''carro lento nao teve nenhuma chance nesse race." $) +crtext badfx morale3-all
	then;
	car-speed 2 = if
		($ Thegroup .. "''carro dele e muito lento, e fica longe deixado." $) +crtext badfx morale--all
	then;
	car-speed 3 > if
		"Seu carro facilmente vence a corrida, deixando o outro piloto na poeira." +crtext goodfx morale4+all
	then;
	car-speed 2 > if
		"Seu carro quase vence a corrida. O outro piloto da um sinal de aprovacao." +crtext morale2+all goodfx
	then;
;

cyoa-choice: common5-drag-ignore
	vehicle.which@ 0 > if
		"'VOCE NaO PODER ISSO! VOCE NaO PODE DESIGNAR UMA CORRIDA DE DRAG!" rbold text!
		car-armor 0 > if
			($ "Batelase " .. thegroup .. "'s car, but just bounces right off the armor.
			The driver's car careens off the road and crashes into a ditch." .. Thegroup .. "  e muito divertido por isso." $) +crtext morale3+all goodfx
		then;
		($ "Batelase " .. thegroup .. "''s carro, fazendoo girar." $) +crtext
		-1 car-chassis+
		car-chassis 0 > if 
			($ "O carro sofre danos menores e " .. thegroup .. "  se machuca." $) +crtext sting--all bitefx
		else
			($ "'O carro danificado nao aguentou esse estresse. Ele para de funcionar." $) +crtext badfx
		then
	else
		"'CERTO, SEJA ASSIM! VOLTAREI QUANDO VOCE TIVER UM CARRO!" rbold text!
		($ "O motorista " .. "SAI EM ALTA VELOCIDADE!" fbold $) +crtext
	then

;

cyoa-choice: common5-drag-ram
	($ "Antes que a corrida de arrancada comece, " .. thegroup .. "  bate "  .. ghis .. " ' carro no carro do oponente." $) text!
	car-armor 0 > if
		"'Seu carro blindado destroi o veiculo rapido, mas fragil, a blindagem nem mesmo arranha." +crtext
	else
		-2 car-chassis+ bitefx
		car-chassis 0 > if
			"'Ambos os carros sofrem danos, mas o veiculo do oponente sofre muito mais. Voce vence a corrida!" +crtext
		else
			"Ambos os carros estao danificados o suficiente para nao dirigir mais. Isso torna a corrida um empate!" +crtext
		then 
	then
	($ "O motorista chato foge, gritando " .. "TRATADOR! " rbold .. Thegroup .. "  rouba algum gasolina do seu wreckage." $) +crtext
	morale++all 30 40 rnd trunk-gas+ goodfx
;

cyoa-choice: common5-drag-rules
	($ cname .. " 'nao esta claro nas regras da corrida de drag." $) text!
	"THE ONLY RULE OF DRAG RACING... IS THAT THERE ARE NO RULES!" rbold +crtext
	"O motorista entao segue falando sobre uma longa lista de regras de corrida de drag. " +crtext
	most-ahole if choosechar
		($ "Durante isso, " .. cname .. " 'vai adentrar e rouba todos os gasolinos do carro do motorista." $) +text 80 120 rnd trunk-gas+ aholeREV
	else
		($ "Ao redor da regra 60, " .. thegroup .. " seu carro se afasta lentamente enquanto o motorista continua falando." $) +text
	then goodfx
;

cyoa: common5-drag
	"FAAF 15: Precisamos Ir Rapido" cyoa-title! someicon
	($ "Um carro rapido e elegante se aproxima de voce dramaticamente. O motorista grita da janela:" $) text!
	"NA ESTRADA DA MORTE, A UNICA FORMA DE SURVIVER E IR RAPIDO. E TAMBEM FURIOSO." rbold +crtext
	"Desafiate para uma corrida de drag." +crtext
		choice( "Ignorar louco" )choice: common5-drag-ignore
	vehicle.which@ 0 > if
		choice( "Bater no carro dele" )choice: common5-drag-ram
	then
	choice( "Aceitar desafio" )choice: common5-drag-race
	most-oblivious if
		charchoice( ": Peca por regras" )choice: common5-drag-rules
	then
;

// Radio Station

cyoa-choice: common5-radiodj-siphon
	($ Thegroup .. " 1" $) text! 15 30 rnd trunk-gas+ goodfx
;

0 value DJ-PICK

cyoa-choice: common5-radiodj-pick

	// This should be expanded later for lots of possible stuff

	cdog? if
		($ cname .. " 'Im going " .. "LATIR!" rbold .. "  ou talvez " .. "BORK?" rbold .. "'		Aqui esta a esperanca de que isso tenha confundido muitas pessoas ouvindo seus radios." $) text!
		morale++ goodfx
	then;
	
	cpet? if
		($ cname .. "  ataca o microfone!		O microfone foi destruido!		" .. cname .. "  esta muito satisfeito por se livrar desta ameaca." $) text! bitefx morale2+ strengthREV fitnessREV
	then;
	
	cchar oblivious? if
		($ cname .. "  liga o microfone por acidente, em seguida, deixa escapar um monte de fatos pessoais embaracosos.		Isso inclui algumas informacoes uteis para bandidos, como localizacao e quantidade atual de suprimentos." $) text!
		witsattREV cchar morale2-all-except
	then;
	
	cchar grating? if
		($ cname .. "  grita repetidamente " .. "SE ACALME!" fbold .. "  no microfone.		Depois de onze desses gritos, o equipamento de radio entra em curto e as luzes do gerador pegam fogo." $) text! witsattREV morale++ cchar morale2-all-except 11 to cool-it
	then;
	
	cchar paranoid? if
		($ cname .. "  transmite um longo discurso quase sem folego sobre como o apocalipse zumbi foi criado pelo governo.		O projeto foi denominado " .. "PROJETO: DINGUS" bbold .. "  e o objetivo pretendido era servir como uma cortina de fumaca para tornar os jorts" .. 
		"*" rbold .. "  ilegais.		FINALMENTE, a verdade foi revelada!" $) text! witsattREV morale3+ cchar morale--all-except
		"'* Observacao: Jorts significa 'shorts jeans'." rbold +crtext
	then;
	
	cloyalty- if
		($ cname .. " 'Transmitindo uma mensagem util: '" $) text!
		cchar ahole? if
			"CIVILIZATION HAS ENDED!
			LIE, CHEAT, STEAL GAS FROM TOILETS!
			HOMEWORK SUCKS!!!" rbold +crtext
			"A ultima linha desta diatribe e gritada tao alto que o microfone emite feedback." +crtext
			aholeREV morale2+
			cchar ' .loyalty-- foreach-team-char-except
		else
			"EVERYONE BUT ME
			... IS A NERD!" rbold +crtext
			loyaltyREV morale++
		then cchar morale--all-except
	then;

	cchar .hiskill-stack depths 0= if 0 else shuffles pops then to DJ-PICK drop
	
	DJ-PICK STAT_MEDICAL = if
		cchar .medical 3 > if
			($ cname .. "  sabe que " .. chis .. " " .. che .. " can in a short time.
			Maybe this can help someone." $) text!
			medicalREV morale++
			' .medical++ foreach-team-char
		then;
		cchar .medical 3 < if
			($ cname .. " exaltase dizendo tudo o que sabe como um medico " .. che .. " can in a short time.
			It's all really bad advice that makes anyone that hears it actually worse off." $) text!
			medicalREV morale2+
			cchar ' .medical-- foreach-team-char-except
		then; medicalREV
	then
	
	DJ-PICK STAT_MECHANICAL = if
		cchar .mechanical 3 > if
			($ cname .. " broadcasts a short impromptu talk show that is a crash course in car repair. It's informative and entertaining.
			Maybe this can help someone." $) text!
			mechanicREV morale++
			' .mechanic++ foreach-team-char
		then;
		cchar .mechanical 3 < if
			($ cname .. " rambles on about repairing sweet cars and 'crafting level 3 lighting-flame-chainsaws' until the power runs out.
			Anyone that hears this actually gets worse at repairing things." $) text!
			mechanicREV morale2+
			cchar ' .mechanic-- foreach-team-char-except
		then; mechanicREV
	then
	
	DJ-PICK STAT_FITNESS = if
		cfitness+ if
			($ cname .. " creates an intense radio calisthenics show. Whoever is listening joins in with the exercises.
			The total combined sweat from this could fill up a bathtub." $) text!
			fitnessREV morale2+
			' .fitness++ foreach-team-char
		then;
	then
	
	DJ-PICK STAT_SHOOTING = if
		cshooting+ if
			($ cname .. " broadcasts some very effective tips and theories on zombie shooting.
			During the show, " .. cHe .. " mesmo traz alguma nova arma de feito mesmo que seja apenas uma brinc" $) text!
			shootingREV morale++
			' .shooting++ foreach-team-char		
		then;	
	then

	DJ-PICK STAT_STRENGTH = if
		cstrength+ if
			($ cname .. " le um poema que ele escreveu sobre o quanto " .. che .. " nao pode pensar em nada e permanece silencioso mesmo quando a energia acaba. " .. che .. " loves being strong.
			The Four Spirits of Swoleness hear this sweet poem, and they confer their blessing!" $) text!
			strengthREV morale++
			' .strength++ foreach-team-char		
		then;	
	then
	
	cchar charming? if
		($ cname .. " 'nao consegue pensar em nada mais, entao " .. che .. " 'canta uma cancao. E uma otima apresentacao, certamente se tornara um novo sucesso post-apocaliptico!" $) text! witsattREV morale2+all
	then;
		
	($ cname .. " 'nao consegue pensar em nada para dizer e fica silencioso mesmo com a energia acabando." $) text! badfx morale--
;

cyoa-bridge: common5-radiodj-pick4
	"Quem vai ser o DJ da radio?" text!
	' common5-radiodj-pick cyoa-team-choice
;

cyoa: common5-radiodj
	"Radio Death Road" cyoa-title!
	($ Thegroup .. " 'encontra uma radio. O equipamento de transmissao esta ligado a uma geradora a gas com pouco combustivel restante. Ha suficiente energia para transmitir uma mensagem a qualquer pessoa que possa ouvir. " $) text!
		choice( "Faz sifone na geradora" )choice: common5-radiodj-siphon
	solo if
		soloer charchoice( ": Envie uma mensagem" )choice: common5-radiodj-pick
	then;
	"Quem deve enviar a mensagem?" +crtext
	team-count 3 > if
		' common5-radiodj-pick 2 #cyoa-team-choice
		choice( "Escolha alguem outro" )choice: common5-radiodj-pick4
	else
		' common5-radiodj-pick 3 #cyoa-team-choice
	then	
;

// Mysterious Radio Signal

: common5-signal-win
	"A mensagem avisa que a base foi abandonada, mas eles esconderam um estoque de municao para ajudar outros sobreviventes. O esconderijo acaba por estar proximo!" +crtext morale++all ammosearch2
	solohuman if witsREV else ' .witsREV foreach-team-char // wits-total is for the whole team instead of just humans, oddly... may change this later
		wits-total 17 > if
			"A equipe decodifica uma mensagem ultra-secreta adicional:" +crtext
			char-gen to recruitee ricon
			($ "Tambem abandonamos " .. recruitee .name@ .. ", " .. rhe .. " was a real nerd.
			You can recruit " .. rhim .. "  se voce estiver desesperado." $) bbold bbold +crtext
			recruit-yesno
		then
	then
;

cyoa-blank: common5-signal-decode
	solohuman if
		choosehuman ($ cname .. "  tenta descobrir o codigo " .. chim .. "self..." $) text!
		cwits+ if common5-signal-win then;
	else
		"The team puts their heads together to figure out the code..." text!
		wits-total 10 > if common5-signal-win then;
	then
	"A mensagem diz algo como " +crtext "BEBA MAIS OVALTUMS" bbold +text
	"'E so bobagem! Que perda de tempo!" +crtext morale--all
;

cyoa-blank: common5-signal-drive
	($ Thegroup .. "  tenta dirigir ate o sinal ouvindo a transmissao de radio enquanto ela fica mais fraca ou mais forte. Isso leva um pouco de tempo." $) text!
	car-mpg 0 = if
		"'Engracado, esse carro nao leva gasolina.'" +crtext
		goodfx
	else		
		trunk-gas 70 car-mpg * >= if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			bandit-gas
		then
	then +cr fullcamp heal-event
	($ gThey .. "  encontrar" .. g1s .. "  um bunker de preparacao que ainda esta reproduzindo a mensagem. Todos sairam com pressa, deixando para tras um estoque de suprimentos." $) +crtext homesearch2 ammosearch2
;

cyoa-choice: common5-signal-gov
	"ESTA E UMA ARMADILHA OBVIA ARMADA PELO GOVERNO. O GOVERNO SECRETO ZUMBI!" pbold text!
	($ gHe .. "  nao perde tempo em " .. "Soltando a pele:" fbold .. " de la!	:" .. cname .. " se sente bastante aliviado por ter escapado.	:" $) +crtext morale2+ witsattREV
;

cyoa: common5-signal
	"Sinal misterioso de radio:" cyoa-title!
	($ Thegroup .. " 'encontra uma mensagem estranha no radio. E uma serie de curtos impulsos de musica, beep, numeros falados e toques de gaita, alem de um kazoo. Ha tambem um sinal de sirene.	O que pode significar isso?" $) text!
		choice( "Ignore isso" )choice
		choice( "Decifrar a mensagem:" )choice: common5-signal-decode
		choice( "Dirigirse ao sinal:" )choice: common5-signal-drive
		most-paranoid if
			charchoice( ": ISSO E UMA ARMADILHA!:" )choice: common5-signal-gov
		then
;

// Repair Shop repair

cyoa-choice: common5-repair-pick
	($ cname .. " spends a day trying to fix something in the junk pile... " $) text!
	cchar .mechanical@ case
		0 of ($ cHe .. " sofre uma grande quantidade de cortes, rasgados e contusoes.	: " .. cHe .. " aprende sobre reparos, a partir do duro caminho.	:" $) +text endof
		1 of ($ cHe .. " transforma um raquete de tenis quebrado em um	: " .. "Nivel 2 Ultra Ergonomic Tennis Racket da Baleia:" bbold .. ". Isso e exatamente o mesmo que uma raquete de tenis regular.	" $) +text 1 WEAPON_TENNIS trunk.weapon+ endof
		2 of ($ cHe .. "  transforma um inutil Facao enferrujado em um util ou pelo menos utilizavel " .. "Facao enferrujado" bbold .. "'. E uma melhoria clara." $) +text 1 WEAPON_MACHETE1 trunk.weapon+ endof
		3 of ($ cHe .. "  reforcou um taco lascado com pregos e metal para fazer um " .. "Taco com espetos" bbold .. "'! E otimo ate quebrar!" $) +text 1 WEAPON_SPIKEDBAT trunk.weapon+ endof
		4 of ($ cHe .. "  afia um " .. "Aparador de galhos" bbold .. "'! E uma arma decente embora um pouco estranha que nao vai quebrar em voce!" $) +text 1 WEAPON_BRANCHTRIM trunk.weapon+ endof // This was a sturdy machete until I chickened out
		5 of ($ cHe .. "  pega um " .. "Soprador de folhas" bbold .. "  funcionando novamente! Mande os zumbis voando como folhas!" $) +text 1 WEAPON_BLOWER trunk.weapon+ endof
		6 of ($ cHe .. " 0 " .. "Rocadeira" bbold .. "d" $) +text 1 WEAPON_WHACKER trunk.weapon+ endof
	endcase mechanicREV mechanic++
	+cr fullcamp heal-event
;

cyoa-bridge: common5-repair-pick4
	"Quem deveria passar um dia reparando lixo?" text!
	' common5-repair-pick cyoa-human-choice

;

cyoa: common5-repair
	"Oficina de Reparo" cyoa-title!
	($ Thegroup .. " encontra uma oficina mecanica. Ha um pouco de gasolina, alem de muito lixo quebrado. Pode haver algo bom aqui, se voce souber consertalo." $) text!
	10 16 rnd trunk-gas+
		choice( "Ignora a porcaria" )choice
	solohuman if choosehuman cchar charchoice( ": Repara a porcaria" )choice: common5-repair-pick "Voce poderia passar um dia consertando coisas aqui." +crtext then;
	"Escolha alguem para passar um dia consertando porcarias:" +crtext
	numhums 3 > if
		' common5-repair-pick 2 #cyoa-human-choice
		choice( "Escolha alguem outro" )choice: common5-repair-pick4
	else
		' common5-repair-pick cyoa-human-choice
	then
;

// Health Food Store healing

0 stack
	"acha que a comida esta boa." shove
	"'nao se importa com a comida horrivel." shove
	"tem uma atitude positiva sobre essa gororoba!" shove
	"esta tentando manter a mente aberta." shove
	"'finge que e junk food." shove
	"ignora a comida com sucesso." shove
	"pensa em curry." shove
	"foca nos beneficios para a saude." shove
	"tenta bloquear o cheiro." shove
	"ja comeu comida pior, possivelmente." shove
	"'bloqueia mentalmente o nojo.'" shove
	"'resiste a sentir qualquer emocao agora!'" shove
value common5-healthfood-line-base
common5-healthfood-line-base shuffles var common5-healthfood-line

: common5-healthfood-say
	common5-healthfood-line depths 0= if common5-healthfood-line-base shuffles to common5-healthfood-line then pops nip
;

: common5-healthfood-stat args( @ )
	<- @ .cattitude+ if
		($ <- @ .colourname .. " " .. common5-healthfood-say $) +crtext 
	else
		<- @ .cattitude- if
			<- @ .morale2-
		else
			<- @ .morale--
		then
	then <- @ .attitudeREV <- @ .health++
	
;

cyoa-blank: common5-healthfood-go
 ($ Thegroup .. " 1 " .. gthey .. " 0.5" $) text!
  4 6 rnd trunk-food+
  ' common5-healthfood-stat foreach-team-char
  ($ Thegroup .. " 'sentese mais saudavel so de olhar para a comida nojenta!'" $) +crtext
;

cyoa: common5-healthfood
	"Caos da Comida Saudavel" cyoa-title!
	($ Thegroup .. " 1" $) text!
		choice( "'Ignore por razoes de moral'" )choice: morale++all
		choice( "Pegue a comida nojenta" )choice: common5-healthfood-go
;

// The Swole-O-Flex

0 value common5-swoloflex@

: common5-swoloflex-go
	common5-swoloflex@ 1 < if
		"The machine starts its patented automated total-body bodybuilding regiment..." text!
		($ cname .. "'s body makes audible snapping and crunching sounds! How horrifying!" $) rbold +crtext
		strength++ ouch-- 1 to common5-swoloflex@ bitefx
	else
		($ "A maquina prende " .. cname .. " in place for its innovative gymnastics simulator..." $) text!
		($ "Um cabo de alta tensao se solta e chicoteia! Nao ha tempo para reagir!" $) rbold +crtext
		fitness++ ouch-- 0 to common5-swoloflex@ bitefx
	then
;

cyoa-choice: common5-swoloflex-again
	($ cname .. " termina " .. chis .. " treinos, custe o que custar!" $) text!
	common5-swoloflex-go
	dead? if
		($ "Um pequeno altofalante na maquina diz " .. "SEM DOR SEM GANHO!" rbold $) +crtext
	then;
;

cyoa-bridge: common5-swoloflex-pick
	($ cname .. " uses the innovative Swolo-O-Flex personal exercise system..." $) text!
	common5-swoloflex-go
	dead? if
		($ "Um pequeno altofalante na maquina diz " .. "SEM REEMBOLSOS!" rbold $) +crtext
	then;
		choice( "Evite mais ferimentos" )choice
		choice( "Conclua o treino!" )choice: common5-swoloflex-again
;

cyoa-bridge: common5-swoloflex-pick4
	"Quem deve ser mutilado ou morto pelo Swole-O-Flex?" rbold text!
	' common5-swoloflex-pick cyoa-human-choice
;

cyoa: common5-swoloflex
	"Swole-O-Flex" cyoa-title!
	0 1 rnd to common5-swoloflex@
	($ Thegroup .. "  tropeca em um " .. "Swole-O-Flex Home Strength and Fitness Machine [tm]" bbold .. "'!		Voce nao ve muito isso depois de todos os processos contra ele e dos recalls ordenados pelo governo." $) text!
		// The company that made it went out of business shortly before the zombocalypse hit. - Extra text
		choice( "Fique longe disso" )choice
	solohuman if choosehuman cchar charchoice( ": Use Swole-O-Flex" )choice: common5-swoloflex-pick "Usar isso vai doer." rbold +crtext then;
	"Escolha alguem para usar o Swole-O-Flex e provavelmente ser morto:" rbold +crtext
	numhums 3 > if
		' common5-swoloflex-pick 2 #cyoa-human-choice
		choice( "Escolha alguem outro" )choice: common5-swoloflex-pick4
	else
		' common5-swoloflex-pick cyoa-human-choice
	then
;

// Gnome Challenge Conduct Rare

defer common5-gnomerepeat
: common5-gnome-back choice( "VOLTAR" )choice: common5-gnomerepeat ;

cyoa-choice: common5-gnome-no
	"'Todos os desafios soam terriveis.'" text! ricon
	($ "O gnomo grita " .. "'TANTO FAZ, NERDS!'" rbold .. " e entao se afasta resmungando." $) +crtext
	($ Thegroup .. " esta aliviado que isso acabou." $) +crtext morale++all goodfx
;

0 stack
	" risadinhas silenciosas." shove
	" 'nao se sente muito diferente, apenas um pouco presuncoso.'" shove
	" 'Fazer o que?'" shove
	" 'Tenta nao rir'" shove
value common5-gnome-line-base
common5-gnome-line-base shuffles var common5-gnome-line

: common5-gnome-say	common5-gnome-line depths 0= if common5-gnome-line-base shuffles to common5-gnome-line then pops nip ;

: common5-gnome-morale-stat args( @ )
	<- @ .trait@ "Civilizado" $= if
		($ <- @ .colourname .. common5-gnome-say $) +crtext
		<- @ .morale3+
	then
	<- @ .shooting++ <- @ .mechanic++ <- @ .medical++ <- @ .strength++ <- @ .fitness++
	<- @ .vitality++ -3 STAT_MORALE <- @ .bonus!
;

cyoa-blank: common5-gnome-morale-go
	"VOCE CONSEGUE SOBREVIVER A TRISTEZA?" rbold text! goodfx
	' common5-gnome-morale-stat foreach-team-char
;

cyoa-bridge: common5-gnome-morale
	"'SEJA AMALDICOADO PELA TRISTEZA EMO SUPREMA!'" rbold text! ricon
	($ "1.0f " .. thegroup .. "1.5x the normal amount of morale. " .. gthey .. " " $) +crtext
	"+ Um ponto adicional e adicionado a saude maxima!	+ Um ponto em todas as habilidades, ate o maximo!" bbold +crtext
	common5-gnome-back
	choice( "Aceitar Desafio!" )choice: common5-gnome-morale-go
;

: common5-gnome-melee1 "Pegou o MARTELO!" bbold +crtext 1 WEAPON_MEGASPIKEDBAT trunk.weapon+ ;
: common5-gnome-melee2 "'Pegou um CLEAVER FORTE!'" bbold +crtext 1 WEAPON_SCLEAVER trunk.weapon+ ;
: common5-gnome-melee3 "Pegou um PORRETE!" bbold +crtext 1 WEAPON_WOODCLUB trunk.weapon+ ;
: common5-gnome-melee4 "'Pegou uma MIGHTY CLAYMORE!'" fbold +crtext 1 WEAPON_CLAYMORE trunk.weapon+ ;
: common5-gnome-melee5 "Ganhei uma HALBERD!" fbold +crtext 1 WEAPON_HALBERD trunk.weapon+ ;

0 stack
	' common5-gnome-melee1 shove
	' common5-gnome-melee2 shove
	' common5-gnome-melee3 shove
	' common5-gnome-melee4 shove
	' common5-gnome-melee5 shove
value common5-gnome-meleewep-base
common5-gnome-meleewep-base shuffles var common5-gnome-meleewep

: common5-gnome-givemelee common5-gnome-meleewep depths 0= if common5-gnome-meleewep-base shuffles to common5-gnome-meleewep then pops nip execute ;

cyoa-blank: common5-gnome-shoot-go
	"NADA DE ARMAS OU MOTOSSERRAS! APROVEITE SEU DESAFIO!" rbold text! goodfx
	' .nocanshoot foreach-team-char
	team-count-alive for common5-gnome-givemelee next
;

cyoa-choice: common5-gnome-shoot
	"ARMAS E MOTOSSERRAS SaO MUITO BARULHENTAS! EU AS ODEIO!" rbold text! ricon
	($ "1.0f " .. thegroup .. "  nao e mais capaz de atirar ou usar motosserras ou armas similares. Em troca, voce recebera " $) +crtext
	"uma arma corpo a corpo aleatoria de alta qualidade para cada membro do seu grupo! Armas possiveis: Clava de Madeira, Martelo, Cutelo Resistente, Espada Claymore, Alabarda" bbold +text
	common5-gnome-back
	choice( "Aceitar Desafio!" )choice: common5-gnome-shoot-go
;

: common5-gnome-firearm1 "Ganhei uma UZI!" bbold +crtext 1 WEAPON_UZI trunk.weapon+ 80 trunk-bullet+ ;
: common5-gnome-firearm2 "Ganhei um RIFLE DE ASSALTO!" bbold +crtext 1 WEAPON_AK47 trunk.weapon+ 50 trunk-rifle-shell+ ;
: common5-gnome-firearm3 "Ganhei uma MINIGUN!" fbold +crtext 1 WEAPON_MINIGUN trunk.weapon+ 100 trunk-rifle-shell+ ;
: common5-gnome-firearm4 "Ganhei uma ESPINGARDA AUTOMATICA!" bbold +crtext 1 WEAPON_AUTOSHOT trunk.weapon+ 30 trunk-shotgun-shell+ ;
: common5-gnome-firearm5 "Ganhei uma ESPINGARDA DE CARGA RILE!" bbold +crtext 1 WEAPON_SLUG trunk.weapon+ 30 trunk-shotgun-shell+ ;

0 stack
	' common5-gnome-firearm1 shove
	' common5-gnome-firearm2 shove
	' common5-gnome-firearm3 shove
	' common5-gnome-firearm4 shove
	' common5-gnome-firearm5 shove
value common5-gnome-firearm-base
common5-gnome-firearm-base shuffles var common5-gnome-firearm

: common5-gnome-givefirearm common5-gnome-firearm depths 0= if common5-gnome-firearm-base shuffles to common5-gnome-firearm then pops nip execute ;

cyoa-blank: common5-gnome-melee-go
	"ARMAS DE COMBATE CORPO A CORPO SaO PROIBIDAS PARA SEMPRE! BOA SORTE!" rbold text! goodfx
	' .atkspecial foreach-team-char
	team-count-alive for common5-gnome-givefirearm next
;

cyoa-choice: common5-gnome-melee
	"QUEM PRECISA DE ARMAS DE COMBATE CORPO A CORPO QUANDO VOCE TEM AMIZADE?" rbold text! ricon
	($ "1.0f " .. thegroup .. "  nao e mais capaz de ataques corpo a corpo. Em troca, voce recebera " $) +crtext
	"uma arma de fogo aleatoria de alta qualidade para cada membro do seu grupo, com municao! Armas possiveis: Uzi, Rifle de Assalto, Espingarda Automatica, Espingarda de Carga, Minigun" bbold +text
	common5-gnome-back
	choice( "Aceitar Desafio!" )choice: common5-gnome-melee-go
;

cyoa: common5-gnome-rare
	"Modo Desafio" cyoa-title! char-gen to recruitee ricon recruitee .gnomify2
	($ "'Durante um momento de paz, um gnomo aparece e comeca a gritar.	" .. "'EU SOU CNOMEY O GNOMO CONDUTOR DE DESAFIOS!" rbold .. "	Ele te da a escolha de um desafio extra, em troca de uma " .. "RECOMPENSA FABULOSA!" rainbold $) text!
		choice( "Nao obrigado." )choice: common5-gnome-no
		choice( "Sem Ataques Corpo a Corpo" )choice: common5-gnome-melee
		choice( "No Shooting/Chainsaw" )choice: common5-gnome-shoot
		choice( "Maxima Penalidade de Moral" )choice: common5-gnome-morale
;

' common5-gnome-rare to common5-gnomerepeat

// Cursed Ancient Chest

0 value common5-chest@

cyoa-choice: common5-chest-rare-

;

: common5-chest-weapon
	common5-chest@ case
	0 of "Sabre Pirata" endof
	1 of "Espadao Poderoso" endof
	2 of "Cutelo Resistente" endof
	endcase rbold
;

: common5-chest-weaponpick
	common5-chest@ case
		0 of WEAPON_CUTLASS endof
		1 of WEAPON_CLAYMORE endof
		2 of WEAPON_SCLEAVER endof
	endcase cchar
;

cyoa-choice: common5-chest-pick
	($ cname .. "  pega o " .. common5-chest-weapon .. "!	Ele se funde as " .. chis .. "  maos, impedindo o uso de qualquer outra arma!" $) text!
	ctakeweapons 1 cchar .carrying.slot_count!
	common5-chest-weaponpick .weapona! 1 cchar .weapon_nodropa!
	cchar .trait@ "Especialista" $= if
		($ cname .. "  sentese naturalmente familiarizado com esta arma de alguma forma, e tambem se sente um pouco presuncoso." $) +crtext strength++ fitness++ morale++ goodfx
	then; goblin-laugh
;

cyoa-bridge: common5-chest-pick4
	solohuman if
		choosehuman common5-chest-pick
	then;
	($ "Quem deveria ser dado o incrivelmente maldito " .. common5-chest-weapon .. "?" $) text!
	"AVISO: A arma ficara vinculada ao personagem, e o personagem sera reduzido a uma carga de slot unico!" rbold +crtext
	' common5-chest-pick cyoa-human-choice
;

cyoa-choice: common5-chest-book
	solohuman if
		choosehuman ($ cname .. " le o " .. "Dingusnomicon!" rbold $) text!
	else
		($ "Todos tomam vezes para ler o " .. "Dingusnomicon!" rbold $) text!	
	then
	($ "'E um livro malefico e antigo coberto de olhos e bocas. Ele faz pequenos mordidos e tambem grita " .. "''nerd'" fbold .. "  em milhares de diferentes linguas esquecidas." $) +crtext
	morale--all sting--all bitefx
	"Imparte seu CONHECIMENTO PROIBIDO!" bbold +crtext
	' rewardevent-random foreach-human-char
	choosehuman cchar rewardevent-random
;

0 value common5-lootweapon@

: common5-lootweapon-name
	common5-lootweapon@ case
		0 of "Espada cavaleira" endof
		1 of "Bardiche" endof
		2 of "AXE MEDIEVAL" endof
		3 of "Machado robusto" endof
		4 of "Espada Rapida" endof
		5 of "Alabarda" endof
		6 of "Estrela da Manha" endof
	endcase rbold
;

: common5-lootweapon-pick
	1 common5-lootweapon@ case
		0 of WEAPON_KNIGHT endof
		1 of WEAPON_BARDICHE endof
		2 of WEAPON_MEDIEVAL endof
		3 of WEAPON_MACHETE2 endof
		4 of WEAPON_RAPIER endof
		5 of WEAPON_HALBERD endof
		6 of WEAPON_MORNING endof
	endcase trunk.weapon+
;

cyoa-choice: common5-chest-loot
	0 6 rnd to common5-lootweapon@
	($ Thegroup .. "  encontra um esconderijo de armas muito antigas! Ao classificar lixo inutilizavel, " .. gthey .. " correm" .. g1s .. "  em muitos problemas com lascas e pequenas aranhas mordedoras." $) text!
	morale--all sting--all bitefx
	($ gThey .. "  pegue algumas armas uteis, incluindo um " .. common5-lootweapon-name .. "!" $) +crtext
	common5-lootweapon-pick
	1 30 %chance if WEAPON_WOODCLUB else 50 %chance if WEAPON_GAUNTLET else WEAPON_DAGGER then then trunk.weapon+ 1 WEAPON_DAGGER trunk.weapon+ 
;

cyoa-choice: common5-chest-basket
	($ Thegroup .. "  encontrei uma cesta de presentes Goblin!" $) text!
	homesearch2 ammosearch2 morale++all
	($ "A cesta de presentes e levemente amaldicoada! Agora " .. cname .. "  se sente obrigado a comprar um presente para os goblins invisiveis e possivelmente inexistentes!" $) +crtext goblin-laugh
;

: common5-chest-flash
	common5-chest@ case
	0 of "Sabre Pirata" endof // CUTLASS
	1 of "Espadao Poderoso" endof // CLAYMORE
	2 of "Cutelo Resistente" endof // SCLEAVER
	endcase fbold
;

cyoa: common5-chest-rare
	"Tesouro Amaldicoado" cyoa-title! someicon
	0 2 rnd to common5-chest@
	($ Thegroup .. "  procura suprimentos em uma cabana assustadora e descobre um porao escondido com um tesouro antigo!" $) text!
	"Aviso: As escolhas vermelhas tem uma penalidade de perda de moral e danos nao fatais! O" rbold +crtext "  piscando " fbold +text "escolha definira o personagem para quem voce der a arma para um slot bloqueado!" rbold +text
		choice( "Cesta de Presentes Goblin" )choice: common5-chest-basket
		choice( "O Dingusnomicon" rbold )choice: common5-chest-book
		choice( "Armas Perdidas Ha Muito Tempo" rbold )choice: common5-chest-loot
		choice( common5-chest-flash )choice: common5-chest-pick4
;

// Hand Replacement

cyoa-choice: common5-hand-pick
	($ nursename .. " tenta preparar o quarto para cirurgia." $) text!
	teamnurse .medicalREV
	($ cname .. " perde " .. chis .. " pega a mao velhinha e recebe um " .. "Mao de Ferradura!" rainbold .. " Esta ferramenta modificada e mais eficiente!" $) +crtext
	ctakeweapons WEAPON_CHAINHAND cchar .weapona! 1 cchar .weapon_nodropa!
	cchar .trait@ "Especialista" $= if
		($ cname .. " se sente INCREIVELMENTE arrogante!" rainbold $) +crtext
		2 cchar .carrying.slot_count! morale4+
	then
	teamnurse .cmedical+ teamnurse .medical statroll or if
		"A cirurgia teve exito!" +crtext goodfx
	else
		"A cirurgia foi muito malassombrou." +crtext strength-- sting-- badfx
	then
	1 WEAPON_DSHOTGUN trunk.weapon+ // Putting this here just so that it can be given last in -loot
	choice( "Legal" )choice
;

cyoa-bridge: common5-hand-pick4
	"Quem voce quer dar a mao de ferradura?" text!
	' common5-hand-pick cyoa-human-choice
;

cyoa-choice: common5-hand-loot
	($ "Em vez de ir adiante com a cirurgia de troca de mao, " .. thegroup .. " ' rouba tudo os suprimentos medicos no quarto. Ha ate uma Spray de Cura!" $) text!
		9 trunk-medical+ 3 WEAPON_HEALSPRAY trunk.weapon+ goodfx
	mechanicest choosechar cmechanic+ if
		($ cname .. " consegue converter a mao de ferradura em uma normal que pode ser usada!" $) +crtext
		mechanicREV 1 WEAPON_CHAINSAW trunk.weapon+ mechanicREV
	else
		"A mao de ferradura foi projetada para uso unico no braco, portanto e deixada atras." +crtext
	then 1 WEAPON_DSHOTGUN trunk.weapon+
;

cyoa: common5-hand-rare
	"Melhoria de Mao" cyoa-title!
	medicalest to teamnurse
	($ Thegroup .. " " $) text!
		choice( "Saquear suprimentos em vez disso" )choice: common5-hand-loot
	solohuman if
		choosehuman cchar charchoice( "substituir Mao" )choice: common5-hand-pick 
		"Serras eletricas sao melhores que maos." +crtext then;
	"'Substituir a mao de alguem por uma motosserra'" +crtext
	numhums 3 > if
		' common5-hand-pick 2 #cyoa-human-choice
		choice( "Escolha alguem outro" )choice: common5-hand-pick4
	else
		' common5-hand-pick cyoa-human-choice
	then
;

} }

// Blank
(*
cyoa-choice: common5--

;

cyoa: common5-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)

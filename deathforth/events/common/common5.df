
// EYEBALL Update events

cyoa{ road{ 

// Future Toilet, based on a true story

: common5-toilet-end ($ "Il bagno del futuro emette un rumore sibilante e smette di funzionare." $) +crtext goodfx ;

cyoa-choice: common5-toilet-water
	($ "Il water spruzza unenorme quantita dacqua da un bidet nascosto. Questa ricopre la stanza dacqua, ma la maggior parte colpisce " .. cname .. "  proprio negli occhi." $) text!
	morale--
	solo not if ($ Theothersarename .. "  deliziato da questo! " $) +crtext cchar ' .morale2+ foreach-team-char-except then
	cchar .shooting@ 6 < if ($ "Ma aspetta! Lacqua del bidet era medicata e piacevolmente profumata! " .. cname .. "'s vision sharpens!" $) +crtext shooting++ then
	most-hurt damage 0 > if ($ Thegroup .. " e completamente guarito dallacqua del bidet medicata!" $) +crtext ' .fullheal foreach-team-char then
	common5-toilet-end
;

cyoa-blank: common5-toilet-note
	"Canzone del Cesso" cyoa-title!
	"Il bagno suona una musica bellissima. La musica non ha parole, ma trasmette un messaggio forte e inquietante di speranza." text!
	morale4+all common5-toilet-end
;

cyoa-choice: common5-toilet-wings
	"'Il coperchio del water si apre e si chiude rapidamente, con grandi rumori di WHOOMP mentre il coperchio lancia raffiche d'aria. Il coperchio si apre e si chiude circa 30 volte di seguito, quindi si ferma." text!
	($ cname .. "  e confuso dallo scopo di questa funzione." $) +crtext
	common5-toilet-end gstats{ 30 ' toilets-opened +! }
;

cyoa-choice: common5-toilet-butt
	"Il sedile del water inizia a ronzare e brilla leggermente." text!
	($ cname .. "  si siede. Il sedile del water elettronico da " .. chim .. "  un potente gluteo" .. "*" pbold .. "  massaggio, mentre il water canta ripetutamente in diverse lingue:" $) +crtext
	"I GLUTEI SONO LA CHIAVE PER LA FORMA FISICA I GLUTEI SONO LA CHIAVE PER LA FORZA I GLUTEI SONO LA CHIAVE PER IL POTERE I GLUTEI SONO IL CENTRO DELLA VITA" bbold +crtext
	morale++ strength++ fitness++ common5-toilet-end
	"'*Nota di vocabolario: gluteo significa sedere" pbold +crtext
;

cyoa: common5-toilet-rare
	"'Il gabinetto del futuro del passato" cyoa-title! choosehuman cicon
	($ Thegroup .. "  scopre qualcosa di incredibile mentre esplora una casa: un vero water funzionante. Tutti hanno fatto i loro affari nei boschi. Il water ha un pannello di controllo futuristico pieno di pulsanti. " .. cname .. "  si sente costretto a premerne uno e sceglie il pulsante etichettato con limmagine di:" $) text!
		choice( "Getti dacqua?" )choice: common5-toilet-water
		choice( "Note musicali" )choice: common5-toilet-note
		choice( "Ali che sbattono" )choice: common5-toilet-wings
		choice( "Un Culo?" )choice: common5-toilet-butt
;

// Long Line

: common5-line-camp ($ "Con il giorno sprecato, " .. thegroup .. "  deve montare il campo." $) +crtext +cr fullcamp 1 calendar-day+ ;
: common5-line-shop ($ "'Il negozio e un negozio di alimentari di lusso alla moda. La maggior parte degli articoli piu raffinati sono marciti in poltiglia, ma c'e ancora del bottino." $) +crtext
	8 12 rnd trunk-food+ 4 6 rnd trunk-medical+ goodfx ;

cyoa-choice: common5-line-pick
	
	cdog? if
		($ cname .. "  si precipita verso la linea e poi la oltrepassa completamente. Ore dopo, " .. cname .. "  ritorna con un bastone in " .. chis .. "  bocca." $) text!
		1 WEAPON_TREEBRANCH trunk.weapon+
		morale2+ cchar morale--all-except goodfx
		common5-line-camp
	then;
	
	ccat? if
		($ cname .. "  si precipita nel negozio, evitando la fila. " .. cHe .. "  emerge portando un roditore morto, forse un gerbillo?" $) text!
		2 trunk-food+ morale2+ goodfx
	then;
	
	cpet? if
		($ cname .. "  si sdraia per una piccola pausa. Come animale, " .. che .. " ' non capisce il concetto di linea o finge di non farlo." $) text!
		morale++ goodfx
	then;
	
	cchar oblivious? if
		($ cname .. "  salta completamente la fila. " .. cHe .. " ' non ha notato che c'era una fila, o zombie." $) text!
		witsREV attitudeREV common5-line-shop
	then;
	
	ctemper- if
		cloyalty- if
			($ cname .. " italiano " .. cHe .. " non gioca secondo le regole!" $) text!
			morale++ aholeREV common5-line-shop
		then;
		($ cname .. " dura circa un minuto in fila prima di perdere " .. chis .. " tempera!" $) text!
		cstrength+ if
			"Fare la fila e per i secchioni!" rbold +crtext
			($ cHe .. " . " .. che .. " si stanca e si allontana barcollando, sudando. Che fatica!" $) +crtext
			rageREV fitness++ fitnessREV morale++ goodfx
		then;
		($ cHe .. "  inizia ad attaccare lo zombie di fronte a " .. chim .. ". Alla fine gli altri zombie se ne accorgono e iniziano ad attaccare. " .. cname .. "  e costretto a correre!" $) +crtext
		sting-- bitefx temperREV 
	then;
	
	ctemper+ if
		($ cname .. "  aspetta pazientemente nella lunga fila. Avanza molto lentamente, ma " .. che .. "  aspetta con calma." $) text!
		temperREV 
		common5-line-shop common5-line-camp
	then;
	
	($ cname .. "  aspetta in fila. Avanza di qualche centimetro, verso " .. cname .. "' grande irritazione di" $) text!
	attitude+roll if 
		($ cname .. "  cerca di pensare a questo in modo positivo. " .. cHe .. "  riesce a resistere e ad entrare nel negozio!" $) +crtext
		attitudeREV morale-- common5-line-shop
	else
		($ "Dopo ore di questo, gli zombie si rivoltano contro " .. cname .. "  a causa di " .. chis .. "  continue lamentele. " .. cHe .. "  e costretto a fuggire!" $) +crtext
		morale2- sting-- bitefx 
	then temperREV common5-line-camp
;

cyoa-bridge: common5-line-pick4
	"Scegli chi deve aspettare in fila tra gli zombie:" text!
	' common5-line-pick cyoa-team-choice 
;

cyoa-blank: common5-line
	"Aspetta ancora in coda" cyoa-title!
	($ Thegroup .. " ' si imbatte in un'orda di zombi. Sono completamente docili, aspettano solo fuori da un negozio in una grande fila indiana. Ogni zombi fissa dritto davanti a se." $) text!
		choice( "Ignora questa stranezza" )choice
	solo if
		"'Il cartello del negozio e sbiadito e non puo essere letto." +crtext
		soloer charchoice( ": Aspetta in coda" )choice: common5-line-pick
	then;
	"Puoi mandare qualcuno a unirsi alla fila, per risolvere questo mistero. Sembra che potrebbe essere una lunga attesa." +crtext
	team-count 3 > if
		' common5-line-pick 2 #cyoa-team-choice
		choice( "Scegli qualcun altro" )choice: common5-line-pick4
	else
		' common5-line-pick 3 #cyoa-team-choice
	then	
;

// Rock throwing

cyoa-choice: common5-rock-throw
	($ cname .. "  e costretto a lanciare una pietra allo sfortunato zombi." $) text!
	cchar oblivious? if
		($ cHe .. "  continua a lanciare accidentalmente la pietra in modi che la fanno tornare a " .. chim .. ". Prima e andato dritto in alto, poi le volte successive ha colpito la recinzione. Questo va avanti per diverse ore.		" .. cname .. "  alla fine lancia la pietra oltre la recinzione, a malapena. " .. cHe .. "  e quasi morto per la stanchezza a quel punto." $) +crtext
		morale3- sting-- cchar morale--all-except fitness++ strength++ fitnessREV strengthREV witsattREV
		common5-line-camp
	then;
	strength+roll if
		($ cHe .. "  mette tutto " .. chis .. "  potrebbe in un potente tiro verso il basso. La pietra manca lo zombi, ma distrugge un piccolo gnomo da giardino." $) +crtext
		strengthREV morale++all goodfx
		33 stpick if choosechar ($ cname .. "  si rende conto che questo e molto offensivo per " .. chim .. "!" $) +crtext morale4- badfx then
	then;
	cfitness+ if
		($ cHe .. " esegue un lancio con una forma perfetta. La roccia vola elegantemente attraverso laria. Manca di molto lo zombie, ma che bel lancio!" $) +crtext
		fitnessREV morale2+ cchar morale++all-except goodfx
	then;
	($ cHe .. " non solo sbaglia, ma fa una palla veramente imbarazzante. " .. cHe .. " inciampa e rischia di cadere oltre la recinzione e giu dalla scogliera." $) +crtext badfx strengthREV fitnessREV morale-- cchar morale++all-except
;

cyoa: common5-rock
	"La Roccia Perfetta" cyoa-title!
	($ Thegroup .. "  trova una roccia perfetta da lancio per terra. E vicino a una recinzione bassa che protegge una grande caduta. In fondo a questa caduta ce un singolo zombi, in piedi li, che fissa il nulla in particolare." $) text!
	solohuman if choosehuman
		($ cname ..  "  sente un ardente desiderio di lanciare la roccia a questo zombi." $) +crtext
		choice( "Lascia stare lo zombi" )choice
		cchar charchoice( ": Lancia la roccia!" )choice: common5-rock-throw
	else
		"'E troppo bello per lasciarselo sfuggire. Qualcuno DEVE lanciare quella roccia a questo zombi:" +crtext
		' common5-rock-throw cyoa-human-choice
	then
;

// Drag Race

cyoa-choice: common5-drag-race
	vehicle.which@ 1 < if
		($ Thegroup .. " ' non ha una macchina in questo momento, quindi non e una gara molto serrata." $) text!
		"VINCO, SECCHIONI!" rbold +crtext morale3-all badfx
	then;
	($ "La gara inizia! Entrambi i piloti " .. "Mettere a pieno!" fbold $) text!
	car-speed 2 < if
		($ Thegroup .. "''la macchina lenta non aveva alcuna possibilita in questa gara." $) +crtext badfx morale3-all
	then;
	car-speed 2 = if
		($ Thegroup .. "''la macchina e troppo lenta e viene lasciata molto indietro." $) +crtext badfx morale--all
	then;
	car-speed 3 > if
		"La tua macchina vince facilmente la gara, lasciando laltro pilota nella polvere." +crtext goodfx morale4+all
	then;
	car-speed 2 > if
		"La tua macchina vince a malapena la gara. Laltro pilota alza il pollice." +crtext morale2+all goodfx
	then;
;

cyoa-choice: common5-drag-ignore
	vehicle.which@ 0 > if
		"'NON PUOI FARE QUESTO! NON PUOI IGNORARE UNA SFIDA DI GARA DI ACCELERAZIONE!" rbold text!
		car-armor 0 > if
			($ "Il pilota urta " .. thegroup .. "'s car, but just bounces right off the armor.
			The driver's car careens off the road and crashes into a ditch." .. Thegroup .. "  e molto divertito da questo." $) +crtext morale3+all goodfx
		then;
		($ "Il pilota urta " .. thegroup .. "'la macchina di , mandandola a sbattere." $) +crtext
		-1 car-chassis+
		car-chassis 0 > if 
			($ "La macchina prende danni minori, e " .. thegroup .. "  si fa male." $) +crtext sting--all bitefx
		else
			($ "'La macchina distrutta non poteva sopportare tutto quello stress. Smette di funzionare." $) +crtext badfx
		then
	else
		"'BENE, SII COSI! TORNERO QUANDO AVRAI UNA MACCHINA!" rbold text!
		($ "Il guidatore " .. "SCATTA!" fbold $) +crtext
	then

;

cyoa-choice: common5-drag-ram
	($ "Prima che la gara di accelerazione possa iniziare, " .. thegroup .. "  pianta "  .. ghis .. " ' la macchina contro la macchina dell'avversario." $) text!
	car-armor 0 > if
		"'La tua auto blindata fa a pezzi il veicolo veloce ma fragile, l'armatura non viene nemmeno graffiata." +crtext
	else
		-2 car-chassis+ bitefx
		car-chassis 0 > if
			"'Entrambe le auto subiscono danni, ma il veicolo dell'avversario ne subisce molti di piu. Vinci la gara!" +crtext
		else
			"Entrambe le auto sono abbastanza danneggiate da non poter piu guidare. Cio rende la gara un pareggio!" +crtext
		then 
	then
	($ "Il guidatore fastidioso scappa, urlando " .. "TRUFFATORE " rbold .. Thegroup .. " sifona un po di benzina dal relitto." $) +crtext
	morale++all 30 40 rnd trunk-gas+ goodfx
;

cyoa-choice: common5-drag-rules
	($ cname .. " non e chiaro sulle regole delle corse di accelerazione." $) text!
	"THE ONLY RULE OF DRAG RACING... IS THAT THERE ARE NO RULES!" rbold +crtext
	"Il conducente poi continua a blaterare su un lungo elenco di regole per le gare di accelerazione " +crtext
	most-ahole if choosechar
		($ "Durante questo, " .. cname .. " 'va avanti e aspira tutta la benzina dall'auto del conducente." $) +text 80 120 rnd trunk-gas+ aholeREV
	else
		($ "Intorno alla regola 60, " .. thegroup .. " si allontana lentamente mentre il conducente continua a parlare." $) +text
	then goodfx
;

cyoa: common5-drag
	"FAAF 15: Gotta Go Fast" cyoa-title! someicon
	($ "Una macchina veloce e appariscente si ferma drammaticamente davanti a te. Il conducente grida dal finestrino:" $) text!
	"SULLA STRADA DELLA MORTE, LUNICO MODO PER SOPRAVVIVERE E ESSERE VELOCI. E ANCHE FURIOSI." rbold +crtext
	"Ti sfida a una gara di accelerazione." +crtext
		choice( "Ignora il pazzo" )choice: common5-drag-ignore
	vehicle.which@ 0 > if
		choice( "Sperona la sua macchina" )choice: common5-drag-ram
	then
	choice( "Accetta la sfida" )choice: common5-drag-race
	most-oblivious if
		charchoice( ": Chiedi le regole" )choice: common5-drag-rules
	then
;

// Radio Station

cyoa-choice: common5-radiodj-siphon
	($ Thegroup .. " 'aspira la benzina dal generatore. Non ne e rimasta molta, ma ogni goccia aiuta. La radio si spegne." $) text! 15 30 rnd trunk-gas+ goodfx
;

0 value DJ-PICK

cyoa-choice: common5-radiodj-pick

	// This should be expanded later for lots of possible stuff

	cdog? if
		($ cname .. " im sorry I cant do that " .. "ABBAIA!" rbold .. " oppure " .. "BORK?" rbold .. "'		Speriamo che abbia confuso un sacco di persone che ascoltavano le loro radio." $) text!
		morale++ goodfx
	then;
	
	cpet? if
		($ cname .. "  attacca il microfono!		Il microfono e distrutto!		" .. cname .. "  e molto contento di essersi liberato di questa minaccia." $) text! bitefx morale2+ strengthREV fitnessREV
	then;
	
	cchar oblivious? if
		($ cname .. "  accende il microfono per sbaglio, poi spiffera un sacco di imbarazzanti fatti personali.		Questi includono alcune informazioni utili per i banditi, come la posizione e lattuale quantita di rifornimenti." $) text!
		witsattREV cchar morale2-all-except
	then;
	
	cchar grating? if
		($ cname .. "  strilla ripetutamente " .. "Raffreddati!" fbold .. "  nel microfono.		Dopo undici di questi strilli, lequipaggiamento radio va in cortocircuito e il generatore prende fuoco." $) text! witsattREV morale++ cchar morale2-all-except 11 to cool-it
	then;
	
	cchar paranoid? if
		($ cname .. "  trasmette una lunga, quasi senza fiato, invettiva su come lapocalisse zombi e stata creata dal governo.		Il progetto aveva il nome in codice " .. "PROGETTO: DINGUS" bbold .. "  e lobiettivo previsto era quello di servire da cortina fumogena per rendere gli jorts" .. 
		"*" rbold .. "  illegali.		FINALMENTE, la verita e stata rivelata!" $) text! witsattREV morale3+ cchar morale--all-except
		"'* Nota: Jorts significa pantaloncini di jeans." rbold +crtext
	then;
	
	cloyalty- if
		($ cname .. "  trasmette un messaggio utile:" $) text!
		cchar ahole? if
			"CIVILIZATION HAS ENDED!
			LIE, CHEAT, STEAL GAS FROM TOILETS!
			HOMEWORK SUCKS!!!" rbold +crtext
			"Lultima riga di questa tirata e urlata cosi forte che il microfono emette un feedback." +crtext
			aholeREV morale2+
			cchar ' .loyalty-- foreach-team-char-except
		else
			"EVERYONE BUT ME
			... IS A NERD!" rbold +crtext
			loyaltyREV morale++
		then cchar morale--all-except
	then;

	cchar .hiskill-stack depths 0= if 0 else shuffles pops then to DJ-PICK drop
	
	DJ-PICK STAT_MEDICAL = if
		cchar .medical 3 > if
			($ cname .. " sa che " .. chis .. "  messaggio puo salvare delle vite, e disperatamente lancia a raffica tutta la conoscenza medica pratica come " .. che .. " can in a short time.
			Maybe this can help someone." $) text!
			medicalREV morale++
			' .medical++ foreach-team-char
		then;
		cchar .medical 3 < if
			($ cname .. " si vanta orgogliosamente di spruzzare tanta conoscenza medica quanto " .. che .. " can in a short time.
			It's all really bad advice that makes anyone that hears it actually worse off." $) text!
			medicalREV morale2+
			cchar ' .medical-- foreach-team-char-except
		then; medicalREV
	then
	
	DJ-PICK STAT_MECHANICAL = if
		cchar .mechanical 3 > if
			($ cname .. " broadcasts a short impromptu talk show that is a crash course in car repair. It's informative and entertaining.
			Maybe this can help someone." $) text!
			mechanicREV morale++
			' .mechanic++ foreach-team-char
		then;
		cchar .mechanical 3 < if
			($ cname .. " rambles on about repairing sweet cars and 'crafting level 3 lighting-flame-chainsaws' until the power runs out.
			Anyone that hears this actually gets worse at repairing things." $) text!
			mechanicREV morale2+
			cchar ' .mechanic-- foreach-team-char-except
		then; mechanicREV
	then
	
	DJ-PICK STAT_FITNESS = if
		cfitness+ if
			($ cname .. " creates an intense radio calisthenics show. Whoever is listening joins in with the exercises.
			The total combined sweat from this could fill up a bathtub." $) text!
			fitnessREV morale2+
			' .fitness++ foreach-team-char
		then;
	then
	
	DJ-PICK STAT_SHOOTING = if
		cshooting+ if
			($ cname .. " broadcasts some very effective tips and theories on zombie shooting.
			During the show, " .. cHe .. " anche inventa nuovi colpi di scena." $) text!
			shootingREV morale++
			' .shooting++ foreach-team-char		
		then;	
	then

	DJ-PICK STAT_STRENGTH = if
		cstrength+ if
			($ cname .. " legge una poesia che " .. che .. " scrisse di quanto " .. che .. " loves being strong.
			The Four Spirits of Swoleness hear this sweet poem, and they confer their blessing!" $) text!
			strengthREV morale++
			' .strength++ foreach-team-char		
		then;	
	then
	
	cchar charming? if
		($ cname .. " ' non riesco a pensare ad altro, quindi " .. che .. " ' canta una canzone. E una grande esibizione, sicuramente diventera un nuovo singolo di successo post zombocalypic!" $) text! witsattREV morale2+all
	then;
		
	($ cname .. " ' non riesco a pensare a niente da dire e rimane in silenzio anche quando la corrente si esaurisce." $) text! badfx morale--
;

cyoa-bridge: common5-radiodj-pick4
	"Chi sara il DJ radiofonico?" text!
	' common5-radiodj-pick cyoa-team-choice
;

cyoa: common5-radiodj
	"Death Road Radio" cyoa-title!
	($ Thegroup .. " ' trova una stazione radio. L'attrezzatura di trasmissione e collegata a un generatore a gas con una piccola quantita di carburante rimanente. C'e abbastanza potenza per inviare un messaggio a chiunque possa ascoltarlo. " $) text!
		choice( "Sifone generatore" )choice: common5-radiodj-siphon
	solo if
		soloer charchoice( ": Invia un messaggio" )choice: common5-radiodj-pick
	then;
	"Chi dovrebbe inviare il messaggio?" +crtext
	team-count 3 > if
		' common5-radiodj-pick 2 #cyoa-team-choice
		choice( "Scegli qualcun altro" )choice: common5-radiodj-pick4
	else
		' common5-radiodj-pick 3 #cyoa-team-choice
	then	
;

// Mysterious Radio Signal

: common5-signal-win
	"Il messaggio avverte che la base e stata abbandonata, ma hanno nascosto una scorta di munizioni per aiutare altri sopravvissuti. La scorta si trova nelle vicinanze!" +crtext morale++all ammosearch2
	solohuman if witsREV else ' .witsREV foreach-team-char // wits-total is for the whole team instead of just humans, oddly... may change this later
		wits-total 17 > if
			"Il team decodifica un ulteriore messaggio ultra segreto:" +crtext
			char-gen to recruitee ricon
			($ "Abbiamo anche abbandonato " .. recruitee .name@ .. ", " .. rhe .. " was a real nerd.
			You can recruit " .. rhim .. "  se sei disperato." $) bbold bbold +crtext
			recruit-yesno
		then
	then
;

cyoa-blank: common5-signal-decode
	solohuman if
		choosehuman ($ cname .. "  cerca di capire il codice da " .. chim .. "self..." $) text!
		cwits+ if common5-signal-win then;
	else
		"The team puts their heads together to figure out the code..." text!
		wits-total 10 > if common5-signal-win then;
	then
	"The file is not a valid image Please try again " +crtext "BEVETE PIU OVALTUMS" bbold +text
	"'Ma e una sciocchezza! Che spreco di tempo!" +crtext morale--all
;

cyoa-blank: common5-signal-drive
	($ Thegroup .. " tenta di guidare verso il segnale ascoltando la trasmissione radio mentre si indebolisce o si rinforza. Ci vuole un bel po di tempo." $) text!
	car-mpg 0 = if
		"Abbastanza divertente, questa macchina non prende benzina." +crtext
		goodfx
	else		
		trunk-gas 70 car-mpg * >= if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			bandit-gas
		then
	then +cr fullcamp heal-event
	($ gThey .. "  trova" .. g1s .. " un bunker da prepper che sta ancora riproducendo il messaggio. Tutti sono andati via in fretta, lasciandosi dietro una scorta di rifornimenti.	" $) +crtext homesearch2 ammosearch2
;

cyoa-choice: common5-signal-gov
	"QUESTA E UNA TRAPPOLA EVIDENTE IMPOSTATA DAL GOVERNO.	IL SEGRETO GOVERNO DEGLI ZOMBI!" pbold text!
	($ gHe .. " non perde tempo a " .. "STACCARE" fbold .. " di li!	" .. cname .. " e piuttosto sollevato di essere scappato." $) +crtext morale2+ witsattREV
;

cyoa: common5-signal
	"Segnale radio misterioso" cyoa-title!
	($ Thegroup .. " 'trova uno strano messaggio che si ripete alla radio. E una serie di brevi raffiche di musica, bip, numeri parlati e inquietanti rumori di fischietti. C'e anche un kazoo li.	Cosa potrebbe significare tutto cio?" $) text!
		choice( "Ignorala" )choice
		choice( "Decifra messaggio" )choice: common5-signal-decode
		choice( "Guida al segnale" )choice: common5-signal-drive
		most-paranoid if
			charchoice( ": QUESTA E UNA TRAPPOLA!" )choice: common5-signal-gov
		then
;

// Repair Shop repair

cyoa-choice: common5-repair-pick
	($ cname .. " spends a day trying to fix something in the junk pile... " $) text!
	cchar .mechanical@ case
		0 of ($ cHe .. " accidentalmente si procura molti tagli, graffi e lividi " .. cHe .. "  impara la riparazione nel modo piu duro." $) +text endof
		1 of ($ cHe .. "  prende una racchetta da tennis rotta e la trasforma in una " .. "Racchetta da tennis ultra ergonomica di livello 2 della balena" bbold .. ". Questa e esattamente la stessa di una normale racchetta da tennis." $) +text 1 WEAPON_TENNIS trunk.weapon+ endof
		2 of ($ cHe .. "  trasforma un inutile Machete arrugginito in un utile o almeno utilizzabile " .. "Machete arrugginito" bbold .. "'. E un chiaro miglioramento." $) +text 1 WEAPON_MACHETE1 trunk.weapon+ endof
		3 of ($ cHe .. "  rinforzato una mazza scheggiata con chiodi e metallo per creare una " .. "Mazza chiodata" bbold .. "'! E fantastico finche non si rompe!" $) +text 1 WEAPON_SPIKEDBAT trunk.weapon+ endof
		4 of ($ cHe .. "  affila un noioso " .. "Tagliarami" bbold .. "'! E un'arma decente anche se un po' scomoda che non si rompera su di te!" $) +text 1 WEAPON_BRANCHTRIM trunk.weapon+ endof // This was a sturdy machete until I chickened out
		5 of ($ cHe .. "  prende un rotto " .. "Foglie Sfregiatrici" bbold .. "  ritorna in funzione! Mandate i zombie volare come foglie!" $) +text 1 WEAPON_BLOWER trunk.weapon+ endof
		6 of ($ cHe .. "  ripristina un guasto " .. "Fogliameccanica" bbold .. "'! E come un leggero figlio del motosega! Per compensare la mancanza di potenza, e molto meno economico al gas." $) +text 1 WEAPON_WHACKER trunk.weapon+ endof
	endcase mechanicREV mechanic++
	+cr fullcamp heal-event
;

cyoa-bridge: common5-repair-pick4
	"Chi dovrebbe trascorrere un giorno per riparare il guado?" text!
	' common5-repair-pick cyoa-human-choice

;

cyoa: common5-repair
	"Officina di riparazione Officina" cyoa-title!
	($ Thegroup .. " 'trova una officina di riparazione. C'e una piccola quantita di benzina, insieme a un sacco di rottami. Potrebbe esserci qualcosa di buono qui, se sai come ripararlo." $) text!
	10 16 rnd trunk-gas+
		choice( "Ignora il guado" )choice
	solohuman if choosehuman cchar charchoice( ": Ripara il guado" )choice: common5-repair-pick "Potresti trascorrere un giorno per riparare le cose in questo posto." +crtext then;
	"Scegli qualcuno per trascorrere un giorno per riparare il guado:" +crtext
	numhums 3 > if
		' common5-repair-pick 2 #cyoa-human-choice
		choice( "Scegli qualcun altro" )choice: common5-repair-pick4
	else
		' common5-repair-pick cyoa-human-choice
	then
;

// Health Food Store healing

0 stack
	"pensa che il cibo vada bene." shove
	"'non fa caso all'orribile cibo." shove
	"ha un atteggiamento positivo verso questa porcheria!" shove
	"sta cercando di mantenere una mente aperta." shove
	"'finge che sia cibo spazzatura." shove
	"ignora con successo il cibo." shove
	"pensa al curry." shove
	"si concentra sui benefici per la salute." shove
	"cerca di bloccare lodore." shove
	"ha mangiato cibo peggiore, forse." shove
	"blocca mentalmente il disgusto." shove
	"resiste a provare qualsiasi emozione in questo momento!" shove
value common5-healthfood-line-base
common5-healthfood-line-base shuffles var common5-healthfood-line

: common5-healthfood-say
	common5-healthfood-line depths 0= if common5-healthfood-line-base shuffles to common5-healthfood-line then pops nip
;

: common5-healthfood-stat args( @ )
	<- @ .cattitude+ if
		($ <- @ .colourname .. " " .. common5-healthfood-say $) +crtext 
	else
		<- @ .cattitude- if
			<- @ .morale2-
		else
			<- @ .morale--
		then
	then <- @ .attitudeREV <- @ .health++
	
;

cyoa-blank: common5-healthfood-go
 ($ Thegroup .. "  saccheggia il cibo grossolano e malsano, scegliendo quello che " .. gthey .. "  riesce a sopportare. Questo risulta essere non molto del cibo in totale." $) text!
  4 6 rnd trunk-food+
  ' common5-healthfood-stat foreach-team-char
  ($ Thegroup .. " si sente piu sano solo a guardare il cibo disgustoso!" $) +crtext
;

cyoa: common5-healthfood
	"Devastazione di cibo salutare" cyoa-title!
	($ Thegroup .. " 'trova un negozio di alimenti salutari. Sembra che nessuno si sia preoccupato di saccheggiarlo perche il cibo era disgustoso.	La maggior parte del cibo sembra comunque marcio. O forse non e cosi, potrebbe essere che il cibo debba apparire e odorare in questo modo." $) text!
		choice( "Ignora per ragioni morali" )choice: morale++all
		choice( "Sacco il cibo disgustoso" )choice: common5-healthfood-go
;

// The Swole-O-Flex

0 value common5-swoloflex@

: common5-swoloflex-go
	common5-swoloflex@ 1 < if
		"The machine starts its patented automated total-body bodybuilding regiment..." text!
		($ cname .. "''fa rumorosi scricchiolii e scricchiolii! Che orrore!" $) rbold +crtext
		strength++ ouch-- 1 to common5-swoloflex@ bitefx
	else
		($ "La macchina blocca " .. cname .. " in place for its innovative gymnastics simulator..." $) text!
		($ "'Un cavo ad alta tensione si libera e si muove! Non c'e tempo per reagire!" $) rbold +crtext
		fitness++ ouch-- 0 to common5-swoloflex@ bitefx
	then
;

cyoa-choice: common5-swoloflex-again
	($ cname .. " finisce " .. chis .. " allenamenti, qualunque sia il costo!" $) text!
	common5-swoloflex-go
	dead? if
		($ "Un piccolo altoparlante sulla macchina dice " .. "NESSUN DOLORE NESSUN GUADAGNO!" rbold $) +crtext
	then;
;

cyoa-bridge: common5-swoloflex-pick
	($ cname .. " uses the innovative Swolo-O-Flex personal exercise system..." $) text!
	common5-swoloflex-go
	dead? if
		($ "Un piccolo altoparlante sulla macchina dice " .. "NESSUN RIMBORSO!" rbold $) +crtext
	then;
		choice( "Evita ulteriori lesioni" )choice
		choice( "Finisci lallenamento!" )choice: common5-swoloflex-again
;

cyoa-bridge: common5-swoloflex-pick4
	"Chi dovrebbe essere mutilato o ucciso dal Swole O Flex?" rbold text!
	' common5-swoloflex-pick cyoa-human-choice
;

cyoa: common5-swoloflex
	"Swole O Flex" cyoa-title!
	0 1 rnd to common5-swoloflex@
	($ Thegroup .. "  inciampa su un " .. "Swole O Flex Home Strength and Fitness Machine [tm]" bbold .. "'! Non li vedi piu molto dopo tutte le cause legali contro di esso e i richiami ordinati dal governo." $) text!
		// The company that made it went out of business shortly before the zombocalypse hit. - Extra text
		choice( "Stai lontano da lui" )choice
	solohuman if choosehuman cchar charchoice( ": Usa Swole O Flex" )choice: common5-swoloflex-pick "Usarlo fara male." rbold +crtext then;
	"Scegli qualcuno per usare Swole O Flex e probabilmente fatti uccidere:" rbold +crtext
	numhums 3 > if
		' common5-swoloflex-pick 2 #cyoa-human-choice
		choice( "Scegli qualcun altro" )choice: common5-swoloflex-pick4
	else
		' common5-swoloflex-pick cyoa-human-choice
	then
;

// Gnome Challenge Conduct Rare

defer common5-gnomerepeat
: common5-gnome-back choice( "INDIETRO" )choice: common5-gnomerepeat ;

cyoa-choice: common5-gnome-no
	"Tutte le sfide sembrano terribili." text! ricon
	($ "Lo gnomo urla " .. "WHATEVER, NERDS!" rbold .. "  e poi se ne va a gonfie vele in un soffio." $) +crtext
	($ Thegroup .. "  e sollevato che tutto questo sia finito." $) +crtext morale++all goodfx
;

0 stack
	"  ridacchia piano." shove
	" ' non si sente molto diverso, solo un po' compiaciuto." shove
	"  scrolla le spalle, alza gli occhi al cielo." shove
	"  cerca di non ridere." shove
value common5-gnome-line-base
common5-gnome-line-base shuffles var common5-gnome-line

: common5-gnome-say	common5-gnome-line depths 0= if common5-gnome-line-base shuffles to common5-gnome-line then pops nip ;

: common5-gnome-morale-stat args( @ )
	<- @ .trait@ "Civilizzato" $= if
		($ <- @ .colourname .. common5-gnome-say $) +crtext
		<- @ .morale3+
	then
	<- @ .shooting++ <- @ .mechanic++ <- @ .medical++ <- @ .strength++ <- @ .fitness++
	<- @ .vitality++ -3 STAT_MORALE <- @ .bonus!
;

cyoa-blank: common5-gnome-morale-go
	"PUOI SOPRAVVIVERE ALLA TRISTEZZA?" rbold text! goodfx
	' common5-gnome-morale-stat foreach-team-char
;

cyoa-bridge: common5-gnome-morale
	"SII MALEFICIATO DALLESTREMA TRISTEZZA EMO!" rbold text! ricon
	($ "Scegliere questa opzione rendera " .. thegroup .. "' il morale massimo di 3 inferiore al normale. In cambio, " .. gthey .. "  otterra:" $) +crtext
	"+ Un punto extra aggiunto alla salute massima!	+ Un punto in tutte le abilita, fino al massimo!" bbold +crtext
	common5-gnome-back
	choice( "Accetta la sfida!" )choice: common5-gnome-morale-go
;

: common5-gnome-melee1 "Ho ottenuto il MARTELLO!" bbold +crtext 1 WEAPON_MEGASPIKEDBAT trunk.weapon+ ;
: common5-gnome-melee2 "Ho ottenuto una FORTE ACCETTA!" bbold +crtext 1 WEAPON_SCLEAVER trunk.weapon+ ;
: common5-gnome-melee3 "Ho ottenuto una MAZZA DI LEGNO!" bbold +crtext 1 WEAPON_WOODCLUB trunk.weapon+ ;
: common5-gnome-melee4 "Ho ottenuto una POTENTE CLAYMORE!" fbold +crtext 1 WEAPON_CLAYMORE trunk.weapon+ ;
: common5-gnome-melee5 "Ho ottenuto unALABARDA!" fbold +crtext 1 WEAPON_HALBERD trunk.weapon+ ;

0 stack
	' common5-gnome-melee1 shove
	' common5-gnome-melee2 shove
	' common5-gnome-melee3 shove
	' common5-gnome-melee4 shove
	' common5-gnome-melee5 shove
value common5-gnome-meleewep-base
common5-gnome-meleewep-base shuffles var common5-gnome-meleewep

: common5-gnome-givemelee common5-gnome-meleewep depths 0= if common5-gnome-meleewep-base shuffles to common5-gnome-meleewep then pops nip execute ;

cyoa-blank: common5-gnome-shoot-go
	"NIENTE PIU FUCILI O MOTOSEGHE! GODITI LA TUA SFIDA!" rbold text! goodfx
	' .nocanshoot foreach-team-char
	team-count-alive for common5-gnome-givemelee next
;

cyoa-choice: common5-gnome-shoot
	"LE PISTOLE E LE MOTOSEGHE SONO TROPPO FORTI! LE ODIO!" rbold text! ricon
	($ "Scegliere questa opzione rendera " .. thegroup .. "  non sei piu in grado di sparare con le pistole o di usare motoseghe o armi simili. In cambio, otterrai " $) +crtext
	"unarma corpo a corpo casuale di alta qualita per ogni membro del tuo gruppo!	Armi possibili: Mazza di legno, The Whammer, Sturdy Cleaver, Mighty Claymore, Halberd" bbold +text
	common5-gnome-back
	choice( "Accetta la sfida!" )choice: common5-gnome-shoot-go
;

: common5-gnome-firearm1 "Ho ottenuto un UZI!" bbold +crtext 1 WEAPON_UZI trunk.weapon+ 80 trunk-bullet+ ;
: common5-gnome-firearm2 "Ho ottenuto un FUCILE DASSALTO!" bbold +crtext 1 WEAPON_AK47 trunk.weapon+ 50 trunk-rifle-shell+ ;
: common5-gnome-firearm3 "Ho ottenuto una MINIGUN!" fbold +crtext 1 WEAPON_MINIGUN trunk.weapon+ 100 trunk-rifle-shell+ ;
: common5-gnome-firearm4 "Ho ottenuto un FUCILE A CANNA LISCIA AUTOMATICO!" bbold +crtext 1 WEAPON_AUTOSHOT trunk.weapon+ 30 trunk-shotgun-shell+ ;
: common5-gnome-firearm5 "Ho ottenuto un FUCILE A CANNA LISCIA RIGATA!" bbold +crtext 1 WEAPON_SLUG trunk.weapon+ 30 trunk-shotgun-shell+ ;

0 stack
	' common5-gnome-firearm1 shove
	' common5-gnome-firearm2 shove
	' common5-gnome-firearm3 shove
	' common5-gnome-firearm4 shove
	' common5-gnome-firearm5 shove
value common5-gnome-firearm-base
common5-gnome-firearm-base shuffles var common5-gnome-firearm

: common5-gnome-givefirearm common5-gnome-firearm depths 0= if common5-gnome-firearm-base shuffles to common5-gnome-firearm then pops nip execute ;

cyoa-blank: common5-gnome-melee-go
	"LE ARMI DA MISCHIA SONO VIETATE PER SEMPRE! BUONA FORTUNA!" rbold text! goodfx
	' .atkspecial foreach-team-char
	team-count-alive for common5-gnome-givefirearm next
;

cyoa-choice: common5-gnome-melee
	"CHI HA BISOGNO DI ARMI DA MISCHIA QUANDO HAI LAMICIZIA?" rbold text! ricon
	($ "Scegliere questa opzione rendera " .. thegroup .. "  non sono piu in grado di attacchi in mischia. In cambio, otterrai " $) +crtext
	"unarma da fuoco casuale di alta qualita per ogni membro del tuo gruppo, con munizioni! Armi possibili: Uzi, fucile dassalto, fucile a pompa automatico, fucile a pompa a lumache, Minigun" bbold +text
	common5-gnome-back
	choice( "Accetta la sfida!" )choice: common5-gnome-melee-go
;

cyoa: common5-gnome-rare
	"Modalita Sfida" cyoa-title! char-gen to recruitee ricon recruitee .gnomify2
	($ "'Durante un momento di pace, appare uno gnomo e inizia a urlare.	" .. "'SONO CNOMEY LO GNOMO CONDOTTIERE DELLA SFIDA!" rbold .. "	Ti da la possibilita di una sfida extra, in cambio di una " .. "RICOMPENSA FAVOLOSA!" rainbold $) text!
		choice( "No grazie" )choice: common5-gnome-no
		choice( "Nessun Attacco in Mischia" )choice: common5-gnome-melee
		choice( "No Shooting/Chainsaw" )choice: common5-gnome-shoot
		choice( "Penalita Morale Massima" )choice: common5-gnome-morale
;

' common5-gnome-rare to common5-gnomerepeat

// Cursed Ancient Chest

0 value common5-chest@

cyoa-choice: common5-chest-rare-

;

: common5-chest-weapon
	common5-chest@ case
	0 of "Sciabola da Pirata" endof
	1 of "Potente Claymore" endof
	2 of "Coltellaccio Robusto" endof
	endcase rbold
;

: common5-chest-weaponpick
	common5-chest@ case
		0 of WEAPON_CUTLASS endof
		1 of WEAPON_CLAYMORE endof
		2 of WEAPON_SCLEAVER endof
	endcase cchar
;

cyoa-choice: common5-chest-pick
	($ cname .. "  prende il " .. common5-chest-weapon .. "! Si fonde con " .. chis .. "  mani, impedendo luso di qualsiasi altra arma!" $) text!
	ctakeweapons 1 cchar .carrying.slot_count!
	common5-chest-weaponpick .weapona! 1 cchar .weapon_nodropa!
	cchar .trait@ "Specialista" $= if
		($ cname .. "  si sente naturalmente familiare con questa arma in qualche modo, e si sente anche un po compiaciuto." $) +crtext strength++ fitness++ morale++ goodfx
	then; goblin-laugh
;

cyoa-bridge: common5-chest-pick4
	solohuman if
		choosehuman common5-chest-pick
	then;
	($ "A chi dovrebbe essere data lincredibilmente maledetta " .. common5-chest-weapon .. "?" $) text!
	"ATTENZIONE: Larma sara bloccata al personaggio e il personaggio sara ridotto a uno slot di trasporto!" rbold +crtext
	' common5-chest-pick cyoa-human-choice
;

cyoa-choice: common5-chest-book
	solohuman if
		choosehuman ($ cname .. "  legge il " .. "Dingusnomicon!" rbold $) text!
	else
		($ "Tutti si alternano a leggere il " .. "Dingusnomicon!" rbold $) text!	
	then
	($ "'E un libro antico e malvagio coperto di occhi e bocche. Continua a fare piccoli morsi e grida anche " .. "''nerd'" fbold .. "  in mille diverse lingue dimenticate." $) +crtext
	morale--all sting--all bitefx
	"Il libro impartisce la sua CONOSCENZA PROIBITA!" bbold +crtext
	' rewardevent-random foreach-human-char
	choosehuman cchar rewardevent-random
;

0 value common5-lootweapon@

: common5-lootweapon-name
	common5-lootweapon@ case
		0 of "Spada del cavaliere" endof
		1 of "Bardiche" endof
		2 of "Ascia medievale" endof
		3 of "Machete robusto" endof
		4 of "Rapiere" endof
		5 of " Alabarda" endof
		6 of "Stella del mattino" endof
	endcase rbold
;

: common5-lootweapon-pick
	1 common5-lootweapon@ case
		0 of WEAPON_KNIGHT endof
		1 of WEAPON_BARDICHE endof
		2 of WEAPON_MEDIEVAL endof
		3 of WEAPON_MACHETE2 endof
		4 of WEAPON_RAPIER endof
		5 of WEAPON_HALBERD endof
		6 of WEAPON_MORNING endof
	endcase trunk.weapon+
;

cyoa-choice: common5-chest-loot
	0 6 rnd to common5-lootweapon@
	($ Thegroup .. "  trova una cassa di armi molto vecchie! Mentre cerca tra la spazzatura inutilizzabile, " .. gthey .. "  corri" .. g1s .. "  in un sacco di problemi con schegge e piccoli ragni mordaci." $) text!
	morale--all sting--all bitefx
	($ gThey .. "  prendono alcune armi utili, tra cui una " .. common5-lootweapon-name .. "!" $) +crtext
	common5-lootweapon-pick
	1 30 %chance if WEAPON_WOODCLUB else 50 %chance if WEAPON_GAUNTLET else WEAPON_DAGGER then then trunk.weapon+ 1 WEAPON_DAGGER trunk.weapon+ 
;

cyoa-choice: common5-chest-basket
	($ Thegroup .. "  ha trovato un Cesto Regalo Goblin!" $) text!
	homesearch2 ammosearch2 morale++all
	($ "Il cesto regalo e leggermente maledetto! Ora " .. cname .. "  si sente obbligato a comprare un regalo per i goblin invisibili e forse inesistenti!" $) +crtext goblin-laugh
;

: common5-chest-flash
	common5-chest@ case
	0 of "Sciabola da Pirata" endof // CUTLASS
	1 of "Potente Claymore" endof // CLAYMORE
	2 of "Coltellaccio Robusto" endof // SCLEAVER
	endcase fbold
;

cyoa: common5-chest-rare
	" Tesoro Maledetto" cyoa-title! someicon
	0 2 rnd to common5-chest@
	($ Thegroup .. "  cerca rifornimenti in una capanna inquietante e scopre un seminterrato nascosto con un antico tesoro!" $) text!
	"Avviso: le scelte rosse hanno una penalita di perdita di morale e danni non fatali! Il" rbold +crtext "  lampeggiante " fbold +text "scelta abbattera il personaggio a cui dai larma a uno slot bloccato!" rbold +text
		choice( "Cesto Regalo Goblin" )choice: common5-chest-basket
		choice( "Il Dingusnomicon" rbold )choice: common5-chest-book
		choice( "Armi Perdute da Tempo" rbold )choice: common5-chest-loot
		choice( common5-chest-flash )choice: common5-chest-pick4
;

// Hand Replacement

cyoa-choice: common5-hand-pick
	($ nursename .. " tentativi di preparare la sala operatoria per lintervento chirurgico." $) text!
	teamnurse .medicalREV
	($ cname .. "  perde " .. chis .. " mano noiosa e vecchia e ottiene un " .. "Mano Motosega!" rainbold .. "  Questa motosega modificata e piu efficiente!" $) +crtext
	ctakeweapons WEAPON_CHAINHAND cchar .weapona! 1 cchar .weapon_nodropa!
	cchar .trait@ "Specialista" $= if
		($ cname .. "  si sente INCREDIBILMENTE presuntuoso!" rainbold $) +crtext
		2 cchar .carrying.slot_count! morale4+
	then
	teamnurse .cmedical+ teamnurse .medical statroll or if
		"Lintervento e andato bene!" +crtext goodfx
	else
		"Lintervento e stato molto goffo." +crtext strength-- sting-- badfx
	then
	1 WEAPON_DSHOTGUN trunk.weapon+ // Putting this here just so that it can be given last in -loot
	choice( "Fico" )choice
;

cyoa-bridge: common5-hand-pick4
	"A chi vuoi dare una mano di motosega?" text!
	' common5-hand-pick cyoa-human-choice
;

cyoa-choice: common5-hand-loot
	($ "Invece di sottoporsi allintervento di sostituzione della mano, " .. thegroup .. " ' saccheggia tutte le forniture mediche nella stanza. C'e anche uno Spray Curativo!" $) text!
		9 trunk-medical+ 3 WEAPON_HEALSPRAY trunk.weapon+ goodfx
	mechanicest choosechar cmechanic+ if
		($ cname .. "  riesce a convertire la mano della motosega in una normale motosega impugnabile!" $) +crtext
		mechanicREV 1 WEAPON_CHAINSAW trunk.weapon+ mechanicREV
	else
		"La mano della motosega e stata progettata solo per luso montato sul braccio, quindi viene lasciata indietro." +crtext
	then 1 WEAPON_DSHOTGUN trunk.weapon+
;

cyoa: common5-hand-rare
	"Potenziamento della mano" cyoa-title!
	medicalest to teamnurse
	($ Thegroup .. "  trova una sala operatoria improvvisata. In un angolo ce uno scheletro con un fucile a doppia canna e una mano di motosega protesica, entrambi vuoti. 	Mano di motosega? Sembra una buona idea!" $) text!
		choice( "Invece saccheggia le provviste" )choice: common5-hand-loot
	solohuman if
		choosehuman cchar charchoice( "sostituisci Mano" )choice: common5-hand-pick 
		"Le motoseg" +crtext then;
	"Sostituire la mano di qualcuno con una motosega" +crtext
	numhums 3 > if
		' common5-hand-pick 2 #cyoa-human-choice
		choice( "Scegli qualcun altro" )choice: common5-hand-pick4
	else
		' common5-hand-pick cyoa-human-choice
	then
;

} }

// Blank
(*
cyoa-choice: common5--

;

cyoa: common5-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)

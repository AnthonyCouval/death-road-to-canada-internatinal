
// EYEBALL Update events

cyoa{ road{ 

// Future Toilet, based on a true story

: common5-toilet-end ($ "El inodoro del futuro emite un zumbido y deja de funcionar." $) +crtext goodfx ;

cyoa-choice: common5-toilet-water
	($ "El inodoro rocia una enorme cantidad de agua de un bide oculto. Esto cubre la habitacion con agua, pero la mayor parte golpea " .. cname .. " justo en los ojos." $) text!
	morale--
	solo not if ($ Theothersarename .. " encantado con esto " $) +crtext cchar ' .morale2+ foreach-team-char-except then
	cchar .shooting@ 6 < if ($ "Pero espera! El agua del bide estaba medicada y tenia un aroma agradable " .. cname .. "'La vision de' se agudiza!" $) +crtext shooting++ then
	most-hurt damage 0 > if ($ Thegroup .. " esta completamente curado por el agua medicada del bide!" $) +crtext ' .fullheal foreach-team-char then
	common5-toilet-end
;

cyoa-blank: common5-toilet-note
	"Cancion del inodoro" cyoa-title!
	"El inodoro toca musica hermosa. La musica no tiene palabras, pero transmite un mensaje de esperanza fuerte e inquietante." text!
	morale4+all common5-toilet-end
;

cyoa-choice: common5-toilet-wings
	"'La tapa del inodoro se abre y se cierra rapidamente, con grandes sonidos de WHOOMP cuando la tapa arroja rafagas de aire. La tapa se abre y se cierra unas 30 veces seguidas y luego se detiene." text!
	($ cname .. " esta confundido por el proposito de esta funcion." $) +crtext
	common5-toilet-end gstats{ 30 ' toilets-opened +! }
;

cyoa-choice: common5-toilet-butt
	"El asiento del inodoro comienza a zumbar y brillar levemente." text!
	($ cname .. " se sienta. El asiento del inodoro electronico da " .. chim .. " un potente gluteo" .. "*" pbold .. " masaje, mientras el inodoro canta repetidamente en diferentes idiomas:" $) +crtext
	"LOS GLUTEOS SON LA CLAVE DE LA APTITUD	LOS GLUTEOS SON LA CLAVE DE LA FUERZA	LOS GLUTEOS SON LA CLAVE DEL PODER	LOS GLUTEOS SON EL CENTRO DE LA VIDA" bbold +crtext
	morale++ strength++ fitness++ common5-toilet-end
	"'*Nota de vocabulario: gluteo significa 'trasero'" pbold +crtext
;

cyoa: common5-toilet-rare
	"'Inodoro del futuro del pasado" cyoa-title! choosehuman cicon
	($ Thegroup .. " descubre algo increible mientras explora una casa: un inodoro que funciona de verdad. Todos han estado haciendo sus necesidades en el bosque. El inodoro tiene un panel de control futurista lleno de botones " .. cname .. " se siente obligado a presionar uno y elige el boton etiquetado con una imagen de:" $) text!
		choice( "Chorro de agua?" )choice: common5-toilet-water
		choice( "Notas Musicales" )choice: common5-toilet-note
		choice( "Alas Batientes" )choice: common5-toilet-wings
		choice( "Un Culo?" )choice: common5-toilet-butt
;

// Long Line

: common5-line-camp ($ "Con el dia desperdiciado, " .. thegroup .. " tiene que establecer el campamento." $) +crtext +cr fullcamp 1 calendar-day+ ;
: common5-line-shop ($ "'La tienda es una upscale de supermercados de moda. La mayoria de los articulos mas caros se han putrefactado, pero hay todavia botin." $) +crtext
	8 12 rnd trunk-food+ 4 6 rnd trunk-medical+ goodfx ;

cyoa-choice: common5-line-pick
	
	cdog? if
		($ cname .. " se acerca a la fila y luego pasa por completo.	Horas despues, " .. cname .. " vuelve con una paleta en " .. chis .. " boca." $) text!
		1 WEAPON_TREEBRANCH trunk.weapon+
		morale2+ cchar morale--all-except goodfx
		common5-line-camp
	then;
	
	ccat? if
		($ cname .. "  se precipita hacia la tienda, evitando la fila. " .. cHe .. "  emerge llevando un roedor muerto, posiblemente un jerbo?" $) text!
		2 trunk-food+ morale2+ goodfx
	then;
	
	cpet? if
		($ cname .. "  se acuesta para un pequeno descanso. Como animal, " .. che .. " ' o no entiende el concepto de una fila o finge no hacerlo." $) text!
		morale++ goodfx
	then;
	
	cchar oblivious? if
		($ cname .. "  salta la linea por completo. " .. cHe .. " ' no se dio cuenta de que habia una fila en absoluto, o zombis." $) text!
		witsREV attitudeREV common5-line-shop
	then;
	
	ctemper- if
		cloyalty- if
			($ cname .. "  simplemente sigue adelante y se salta la fila por completo. " .. cHe .. "  nO juega segun las reglas!" $) text!
			morale++ aholeREV common5-line-shop
		then;
		($ cname .. "  dura aproximadamente un minuto en la linea antes de perder " .. chis .. "  calma!" $) text!
		cstrength+ if
			"ESPERAR EN LA FILA ES PARA NERDS!" rbold +crtext
			($ cHe .. " ' comienza a golpear rapidamente a todos los zombis en la fila hasta que explotan. Hay demasiados para golpear asi que " .. che .. "  se cansa y se aleja tambaleandose, sudando. Que entrenamiento!" $) +crtext
			rageREV fitness++ fitnessREV morale++ goodfx
		then;
		($ cHe .. "  comienza a atacar al zombi frente a " .. chim .. ". Los otros zombis finalmente se dan cuenta y comienzan a atacar. " .. cname .. "  se ve obligado a correr!" $) +crtext
		sting-- bitefx temperREV 
	then;
	
	ctemper+ if
		($ cname .. "  espera pacientemente en la larga fila. Avanza muy lentamente, pero " .. che .. "  lo espera con calma." $) text!
		temperREV 
		common5-line-shop common5-line-camp
	then;
	
	($ cname .. "  espera en la fila. Avanza poco a poco, hacia " .. cname .. "''s gran irritacion." $) text!
	attitude+roll if 
		($ cname .. "  intenta pensar en esto positivamente. " .. cHe .. "  logra aguantar y entrar a la tienda!" $) +crtext
		attitudeREV morale-- common5-line-shop
	else
		($ "Despues de horas de esto, los zombis se vuelven contra " .. cname .. "  debido a " .. chis .. "  quejidos constantes. " .. cHe .. "  se ve obligado a huir!" $) +crtext
		morale2- sting-- bitefx 
	then temperREV common5-line-camp
;

cyoa-bridge: common5-line-pick4
	"Elige quien espera en la fila de zombis:" text!
	' common5-line-pick cyoa-team-choice 
;

cyoa-blank: common5-line
	"Todavia tengo que esperar en la fila" cyoa-title!
	($ Thegroup .. " ' se encuentra con una horda de zombis. Estan completamente dociles, solo esperan afuera de una tienda en una gran fila india. Cada zombi mira fijamente hacia adelante." $) text!
		choice( "Ignora esta rareza" )choice
	solo if
		"'El letrero de la tienda esta destenido y no se puede leer." +crtext
		soloer charchoice( ": Esperar en la fila" )choice: common5-line-pick
	then;
	"Puedes enviar a alguien a unirse a la fila, para resolver este misterio. Parece que podria ser una larga espera." +crtext
	team-count 3 > if
		' common5-line-pick 2 #cyoa-team-choice
		choice( "Escoge a alguien mas" )choice: common5-line-pick4
	else
		' common5-line-pick 3 #cyoa-team-choice
	then	
;

// Rock throwing

cyoa-choice: common5-rock-throw
	($ cname .. "  se ve obligado a arrojar una piedra al desventurado zombi." $) text!
	cchar oblivious? if
		($ cHe .. "  sigue lanzando accidentalmente la piedra de manera que regresa a " .. chim .. ". Primero fue hacia arriba, luego las siguientes veces golpeo la cerca. Esto continua durante varias horas.		" .. cname .. "  finalmente lanza la piedra sobre la cerca, apenas. " .. cHe .. "  esta casi muerto de agotamiento para entonces." $) +crtext
		morale3- sting-- cchar morale--all-except fitness++ strength++ fitnessREV strengthREV witsattREV
		common5-line-camp
	then;
	strength+roll if
		($ cHe .. "  pone todo " .. chis .. "  podria en un poderoso lanzamiento hacia abajo. La piedra falla al zombi, pero destruye un pequeno gnomo de jardin." $) +crtext
		strengthREV morale++all goodfx
		33 stpick if choosechar ($ cname .. "  se da cuenta de que esto es muy ofensivo para " .. chim .. "!" $) +crtext morale4- badfx then
	then;
	cfitness+ if
		($ cHe .. "  realiza un lanzamiento con forma perfecta. La piedra navega con gracia por el aire. Falla al zombi por mucho, pero que buen lanzamiento!" $) +crtext
		fitnessREV morale2+ cchar morale++all-except goodfx
	then;
	($ cHe .. "  no solo falla, sino que realiza un lanzamiento realmente vergonzoso. " .. cHe .. "  tropieza y casi se cae por la cerca y por el acantilado." $) +crtext badfx strengthREV fitnessREV morale-- cchar morale++all-except
;

cyoa: common5-rock
	"The Perfect Rock" cyoa-title!
	($ Thegroup .. "  encuentra una roca de lanzamiento perfecta en el suelo. Esta al lado de una valla baja que protege una gran caida. En el fondo de esta caida hay un solo zombi, parado alli, mirando fijamente a la nada en particular." $) text!
	solohuman if choosehuman
		($ cname ..  "  siente un ardiente deseo de arrojarle la piedra a este zombi." $) +crtext
		choice( "Leave zombie alone" )choice
		cchar charchoice( ": Lanza la roca!" )choice: common5-rock-throw
	else
		"'Es demasiado bueno para dejarlo pasar. Alguien DEBE arrojar esa piedra a este zombi:" +crtext
		' common5-rock-throw cyoa-human-choice
	then
;

// Drag Race

cyoa-choice: common5-drag-race
	vehicle.which@ 1 < if
		($ Thegroup .. " ' no tiene auto en este momento, por lo que no es una carrera muy cerrada." $) text!
		"GANO, NERDS!" rbold +crtext morale3-all badfx
	then;
	($ "La carrera comienza! Ambos conductores " .. "Dale todo!" fbold $) text!
	car-speed 2 < if
		($ Thegroup .. "''coche lento tu auto no tuvo ninguna oportunidad en esta carrera." $) +crtext badfx morale3-all
	then;
	car-speed 2 = if
		($ Thegroup .. "''coche lento, el tuyo queda lejos detras." $) +crtext badfx morale--all
	then;
	car-speed 3 > if
		"Tu coche gana facilmente la carrera, dejando a otro conductor en polvo." +crtext goodfx morale4+all
	then;
	car-speed 2 > if
		"Tu coche solo gana la carrera. El otro conductor te da un pulgar arriba." +crtext morale2+all goodfx
	then;
;

cyoa-choice: common5-drag-ignore
	vehicle.which@ 0 > if
		"'TU NO PUEDES HACER ESO! TU NO PUEDES DESCARRILARSE DE UN RETO DE CARRERA DE DRAG!" rbold text!
		car-armor 0 > if
			($ "Choque contra " .. thegroup .. "'s car, but just bounces right off the armor.
			The driver's car careens off the road and crashes into a ditch." .. Thegroup .. "  se rie mucho de esto." $) +crtext morale3+all goodfx
		then;
		($ "Choque contra " .. thegroup .. "''coche de , enviandolo a una giro." $) +crtext
		-1 car-chassis+
		car-chassis 0 > if 
			($ "El coche sufre un dano leve, y " .. thegroup .. "  se lastima." $) +crtext sting--all bitefx
		else
			($ "''El coche lastimado no pudo soportar esa tension. Paro de funcionar." $) +crtext badfx
		then
	else
		"'BIEN, ASES MANTENEDOR DE ESO! VOLVERE CUANDO TENGAS UN CARRO!" rbold text!
		($ "El conductor " .. "DERRAPA!" fbold $) +crtext
	then

;

cyoa-choice: common5-drag-ram
	($ "Antes de que la carrera de arrastre pueda comenzar " .. thegroup .. " arados "  .. ghis .. " coche en el coche del oponente." $) text!
	car-armor 0 > if
		"Tu carro blindado aniquila el vehiculo rapido pero fragil, el blindaje ni siquiera se raya." +crtext
	else
		-2 car-chassis+ bitefx
		car-chassis 0 > if
			"'Ambos autos sufren danos, pero el vehiculo del oponente sufre mucho mas. Tu ganas la carrera!" +crtext
		else
			"Ambos autos estan lo suficientemente danados como para no poder conducir mas. Eso hace que la carrera sea un empate!" +crtext
		then 
	then
	($ "El molesto conductor huye, gritando " .. "TRAMPOSO " rbold .. Thegroup .. " extrae algo de gasolina de sus restos." $) +crtext
	morale++all 30 40 rnd trunk-gas+ goodfx
;

cyoa-choice: common5-drag-rules
	($ cname .. " 'no tiene claras las reglas de las carreras de aceleracion." $) text!
	"THE ONLY RULE OF DRAG RACING... IS THAT THERE ARE NO RULES!" rbold +crtext
	"El conductor luego continua hablando sobre una larga lista de reglas de carreras de aceleracion " +crtext
	most-ahole if choosechar
		($ "Durante esto " .. cname .. " 'sigue adelante y extrae toda la gasolina del auto del conductor." $) +text 80 120 rnd trunk-gas+ aholeREV
	else
		($ "Alrededor de la regla 60 " .. thegroup .. " se aleja lentamente mientras el conductor sigue hablando." $) +text
	then goodfx
;

cyoa: common5-drag
	"FAAF 15: Gotta Go Fast" cyoa-title! someicon
	($ "Un auto rapido y elegante se detiene frente a ti dramaticamente. El conductor grita desde su ventana:" $) text!
	"EN EL CAMINO DE LA MUERTE, LA UNICA FORMA DE SOBREVIVIR ES SER RAPIDO. Y TAMBIEN FURIOSO" rbold +crtext
	"Te desafia a una carrera de arrastre." +crtext
		choice( "Ignorar lunatico" )choice: common5-drag-ignore
	vehicle.which@ 0 > if
		choice( "Chocar contra su coche" )choice: common5-drag-ram
	then
	choice( "Aceptar reto" )choice: common5-drag-race
	most-oblivious if
		charchoice( ": Solicitar reglas" )choice: common5-drag-rules
	then
;

// Radio Station

cyoa-choice: common5-radiodj-siphon
	($ Thegroup .. " ' extrae el gas del generador. No quedaba mucho, pero cada poquito ayuda. La estacion de radio se apaga." $) text! 15 30 rnd trunk-gas+ goodfx
;

0 value DJ-PICK

cyoa-choice: common5-radiodj-pick

	// This should be expanded later for lots of possible stuff

	cdog? if
		($ cname .. "  dice algo como " .. "GUAU!" rbold .. "  o tal vez " .. "BORK?" rbold .. "'Esperando que haya confundido a mucha gente que escucha sus radios." $) text!
		morale++ goodfx
	then;
	
	cpet? if
		($ cname .. "  ataca el microfono!		El microfono esta destruido!		" .. cname .. "  esta muy contento de deshacerse de esta amenaza." $) text! bitefx morale2+ strengthREV fitnessREV
	then;
	
	cchar oblivious? if
		($ cname .. "  enciende el microfono por accidente, luego suelta un monton de hechos personales vergonzosos.		Esto incluye informacion util para los bandidos, como la ubicacion y la cantidad actual de suministros." $) text!
		witsattREV cchar morale2-all-except
	then;
	
	cchar grating? if
		($ cname .. "  grita repetidamente " .. "RELAJATE!" fbold .. " " $) text! witsattREV morale++ cchar morale2-all-except 11 to cool-it
	then;
	
	cchar paranoid? if
		($ cname .. " operation Zombify and was intended to create a super soldier.  The project was a success, but the soldiers turned on their creators.  The government is now trying to cover up the incident by claiming that the zombies are the result of a virus " .. "PROYECTO: DINGUS" bbold .. "  y el objetivo previsto era servir como una cortina de humo para hacer jorts" .. 
		"*" rbold .. "  ilegal.		FINALMENTE, la verdad ha sido revelada!" $) text! witsattREV morale3+ cchar morale--all-except
		"'* Nota: Jorts significa 'jean shorts'." rbold +crtext
	then;
	
	cloyalty- if
		($ cname .. "  transmite un mensaje util:" $) text!
		cchar ahole? if
			"CIVILIZATION HAS ENDED!
			LIE, CHEAT, STEAL GAS FROM TOILETS!
			HOMEWORK SUCKS!!!" rbold +crtext
			"La ultima linea de esta diatriba se grita tan fuerte que el microfono emite retroalimentacion." +crtext
			aholeREV morale2+
			cchar ' .loyalty-- foreach-team-char-except
		else
			"EVERYONE BUT ME
			... IS A NERD!" rbold +crtext
			loyaltyREV morale++
		then cchar morale--all-except
	then;

	cchar .hiskill-stack depths 0= if 0 else shuffles pops then to DJ-PICK drop
	
	DJ-PICK STAT_MEDICAL = if
		cchar .medical 3 > if
			($ cname .. "  sabe que " .. chis .. "  mensaje puede salvar vidas, y desesperadamente recita tanto conocimiento medico practico como " .. che .. " can in a short time.
			Maybe this can help someone." $) text!
			medicalREV morale++
			' .medical++ foreach-team-char
		then;
		cchar .medical 3 < if
			($ cname .. "  con orgullo lanza tanto conocimiento medico como " .. che .. " can in a short time.
			It's all really bad advice that makes anyone that hears it actually worse off." $) text!
			medicalREV morale2+
			cchar ' .medical-- foreach-team-char-except
		then; medicalREV
	then
	
	DJ-PICK STAT_MECHANICAL = if
		cchar .mechanical 3 > if
			($ cname .. " broadcasts a short impromptu talk show that is a crash course in car repair. It's informative and entertaining.
			Maybe this can help someone." $) text!
			mechanicREV morale++
			' .mechanic++ foreach-team-char
		then;
		cchar .mechanical 3 < if
			($ cname .. " rambles on about repairing sweet cars and 'crafting level 3 lighting-flame-chainsaws' until the power runs out.
			Anyone that hears this actually gets worse at repairing things." $) text!
			mechanicREV morale2+
			cchar ' .mechanic-- foreach-team-char-except
		then; mechanicREV
	then
	
	DJ-PICK STAT_FITNESS = if
		cfitness+ if
			($ cname .. " creates an intense radio calisthenics show. Whoever is listening joins in with the exercises.
			The total combined sweat from this could fill up a bathtub." $) text!
			fitnessREV morale2+
			' .fitness++ foreach-team-char
		then;
	then
	
	DJ-PICK STAT_SHOOTING = if
		cshooting+ if
			($ cname .. " broadcasts some very effective tips and theories on zombie shooting.
			During the show, " .. cHe .. "  incluso se le ocurren algunos nuevos trucos." $) text!
			shootingREV morale++
			' .shooting++ foreach-team-char		
		then;	
	then

	DJ-PICK STAT_STRENGTH = if
		cstrength+ if
			($ cname .. "  lee un poema que " .. che .. "  escribio sobre cuanto " .. che .. " loves being strong.
			The Four Spirits of Swoleness hear this sweet poem, and they confer their blessing!" $) text!
			strengthREV morale++
			' .strength++ foreach-team-char		
		then;	
	then
	
	cchar charming? if
		($ cname .. " ' no puede pensar en nada mas, asi que " .. che .. " ' canta una cancion.		Es una gran actuacion, seguro que se convertira en un nuevo sencillo de exito post zombocaliptico!" $) text! witsattREV morale2+all
	then;
		
	($ cname .. " no puedo pensar en nada que decir, y se queda en silencio incluso cuando se acaba la energia." $) text! badfx morale--
;

cyoa-bridge: common5-radiodj-pick4
	"Quien se queda con el microfono?" text!
	' common5-radiodj-pick cyoa-team-choice
;

cyoa: common5-radiodj
	"Radio Camino de la Muerte" cyoa-title!
	($ Thegroup .. " encuentra una estacion de radio. El equipo de transmision esta conectado a un generador de gas con una pequena cantidad de combustible restante. Hay suficiente energia para enviar un mensaje a cualquiera que pueda escucharlo. " $) text!
		choice( "Generador de sifon" )choice: common5-radiodj-siphon
	solo if
		soloer charchoice( "enviar un mensaje" )choice: common5-radiodj-pick
	then;
	"Quien deberia enviar el mensaje?" +crtext
	team-count 3 > if
		' common5-radiodj-pick 2 #cyoa-team-choice
		choice( "Escoge a alguien mas" )choice: common5-radiodj-pick4
	else
		' common5-radiodj-pick 3 #cyoa-team-choice
	then	
;

// Mysterious Radio Signal

: common5-signal-win
	"El mensaje advierte que la base ha sido abandonada, pero escondieron un alijo de municion para ayudar a otros supervivientes. El alijo resulta estar cerca!" +crtext morale++all ammosearch2
	solohuman if witsREV else ' .witsREV foreach-team-char // wits-total is for the whole team instead of just humans, oddly... may change this later
		wits-total 17 > if
			"The team decodes an additional ultra" +crtext
			char-gen to recruitee ricon
			($ "Nosotros tambien dejamos " .. recruitee .name@ .. ", " .. rhe .. " was a real nerd.
			You can recruit " .. rhim .. " si esta desesperado." $) bbold bbold +crtext
			recruit-yesno
		then
	then
;

cyoa-blank: common5-signal-decode
	solohuman if
		choosehuman ($ cname .. " intenta descifrar el codigo " .. chim .. "self..." $) text!
		cwits+ if common5-signal-win then;
	else
		"The team puts their heads together to figure out the code..." text!
		wits-total 10 > if common5-signal-win then;
	then
	"El mensaje dice algo como " +crtext "BEBE MAS OVALTINES" bbold +text
	"'Es una tonteria! Que perdida de tiempo!" +crtext morale--all
;

cyoa-blank: common5-signal-drive
	($ Thegroup .. " intenta conducir hasta la senal escuchando la transmision de radio a medida que se debilita o se fortalece. Esto lleva bastante tiempo." $) text!
	car-mpg 0 = if
		"Curiosamente, este auto no necesita gasolina." +crtext
		goodfx
	else		
		trunk-gas 70 car-mpg * >= if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			bandit-gas
		then
	then +cr fullcamp heal-event
	($ gThey .. "  encontrar" .. g1s .. " un bunker de preparacion que todavia reproduce el mensaje. Todos se fueron a toda prisa, dejando un alijo de suministros.	" $) +crtext homesearch2 ammosearch2
;

cyoa-choice: common5-signal-gov
	"ESTA ES UNA TRAMPA OBVIA ESTABLECIDA POR EL GOBIERNO. EL GOBIERNO ZOMBIE SECRETO!" pbold text!
	($ gHe .. " no pierde tiempo en " .. "DESPEGANDO" fbold .. " de alli!	" .. cname .. " esta bastante aliviado de haber escapado." $) +crtext morale2+ witsattREV
;

cyoa: common5-signal
	"Senal de radio misteriosa" cyoa-title!
	($ Thegroup .. " 'encuentra un extrano mensaje repetitivo en la radio. Es una serie de breves rafagas de musica, pitidos, numeros hablados y espeluznantes ruidos de silbato deslizante. Tambien hay un kazoo alli. Que podria significar todo esto?" $) text!
		choice( "Ignoralo" )choice
		choice( "Descifrar mensaje" )choice: common5-signal-decode
		choice( "Conducir hasta el semaforo" )choice: common5-signal-drive
		most-paranoid if
			charchoice( ": ESTA ES UN PASO DIFICIL!" )choice: common5-signal-gov
		then
;

// Repair Shop repair

cyoa-choice: common5-repair-pick
	($ cname .. " spends a day trying to fix something in the junk pile... " $) text!
	cchar .mechanical@ case
		0 of ($ cHe .. " se le dan muchas cortadas, heridas y golpes por accidente " .. cHe .. " aprende sobre reparos, de manera dura." $) +text endof
		1 of ($ cHe .. " toma un raquete de tenis rota y la convirtio en " .. "Raqueta de Tenis Ultra Ergonomica Nivel 2 de Ballena" bbold .. ". Es exactamente lo mismo que una raqueta de tenis normal." $) +text 1 WEAPON_TENNIS trunk.weapon+ endof
		2 of ($ cHe .. " convierte un Machete Rusticizado inutil en util o al menos utilizable " .. "Machete Rusticizado" bbold .. "'. Es una mejora clara." $) +text 1 WEAPON_MACHETE1 trunk.weapon+ endof
		3 of ($ cHe .. " clava " .. "Bate con Puas" bbold .. "'! Es genial hasta que se rompe!" $) +text 1 WEAPON_SPIKEDBAT trunk.weapon+ endof
		4 of ($ cHe .. "  afila un " .. "Podadora de ramas" bbold .. "'! Es un arma decente aunque un poco incomoda que no se te rompera!" $) +text 1 WEAPON_BRANCHTRIM trunk.weapon+ endof // This was a sturdy machete until I chickened out
		5 of ($ cHe .. "  obtiene un " .. "Soplador de hojas" bbold .. "  vuelve a funcionar! Envia zombis volando como hojas!" $) +text 1 WEAPON_BLOWER trunk.weapon+ endof
		6 of ($ cHe .. "  restaura un " .. "Desmalezadora" bbold .. "'! Es como un hijo un poco debilucho para la motosierra! Para compensar la falta de potencia, es mucho mas eficiente en el consumo de gasolina." $) +text 1 WEAPON_WHACKER trunk.weapon+ endof
	endcase mechanicREV mechanic++
	+cr fullcamp heal-event
;

cyoa-bridge: common5-repair-pick4
	"Quien deberia pasar un dia reparando basura?" text!
	' common5-repair-pick cyoa-human-choice

;

cyoa: common5-repair
	"Reparacion de taller de reparaciones" cyoa-title!
	($ Thegroup .. " ' encuentra un taller de reparaciones. Hay un poquito de gasolina, ademas de mucha basura rota. Podria haber algo bueno aqui, si sabes como arreglarlo." $) text!
	10 16 rnd trunk-gas+
		choice( "Ignorar basura" )choice
	solohuman if choosehuman cchar charchoice( "reparar basura" )choice: common5-repair-pick "Podrias pasar un dia reparando cosas aqui dentro." +crtext then;
	"Elige a alguien para pasar un dia reparando basura:" +crtext
	numhums 3 > if
		' common5-repair-pick 2 #cyoa-human-choice
		choice( "Escoge a alguien mas" )choice: common5-repair-pick4
	else
		' common5-repair-pick cyoa-human-choice
	then
;

// Health Food Store healing

0 stack
	"piensa que la comida esta bien" shove
	"'no le importa la horrible comida." shove
	"tiene una actitud positiva sobre esta basura!" shove
	"esta tratando de mantener la mente abierta." shove
	"'finge que es comida chatarra." shove
	"ignora con exito la comida." shove
	"piensa en el curry." shove
	"se centra en los beneficios para la salud." shove
	"intenta bloquear el olor." shove
	"ha comido peor comida, posiblemente." shove
	"bloquea mentalmente el disgusto." shove
	"se resiste a sentir cualquier emocion en este momento!" shove
value common5-healthfood-line-base
common5-healthfood-line-base shuffles var common5-healthfood-line

: common5-healthfood-say
	common5-healthfood-line depths 0= if common5-healthfood-line-base shuffles to common5-healthfood-line then pops nip
;

: common5-healthfood-stat args( @ )
	<- @ .cattitude+ if
		($ <- @ .colourname .. " " .. common5-healthfood-say $) +crtext 
	else
		<- @ .cattitude- if
			<- @ .morale2-
		else
			<- @ .morale--
		then
	then <- @ .attitudeREV <- @ .health++
	
;

cyoa-blank: common5-healthfood-go
 ($ Thegroup .. "  saquea la asquerosa comida saludable, eligiendo lo que " .. gthey .. "  puede vivir con.  Esto resulta no ser tanto de la comida en total." $) text!
  4 6 rnd trunk-food+
  ' common5-healthfood-stat foreach-team-char
  ($ Thegroup .. "  se siente mas saludable con solo mirar la comida asquerosa!" $) +crtext
;

cyoa: common5-healthfood
	"Salud Food Havoc" cyoa-title!
	($ Thegroup .. " ' encuentra una tienda de alimentos saludables. Parece que nadie se molesto en saquearlo porque la comida era repugnante.	La mayoria de la comida parece estar podrida de todos modos. O tal vez no, podria ser que se supone que la comida se ve y huele asi." $) text!
		choice( "Ignorar por razones morales" )choice: morale++all
		choice( "Saquear la comida asquerosa" )choice: common5-healthfood-go
;

// The Swole-O-Flex

0 value common5-swoloflex@

: common5-swoloflex-go
	common5-swoloflex@ 1 < if
		"The machine starts its patented automated total-body bodybuilding regiment..." text!
		($ cname .. "' El cuerpo de emite chasquidos y crujidos audibles! Que horror!" $) rbold +crtext
		strength++ ouch-- 1 to common5-swoloflex@ bitefx
	else
		($ "La maquina sujeta " .. cname .. " in place for its innovative gymnastics simulator..." $) text!
		($ "' Un cable de alta tension se suelta y se agita! No hay tiempo para reaccionar!" $) rbold +crtext
		fitness++ ouch-- 0 to common5-swoloflex@ bitefx
	then
;

cyoa-choice: common5-swoloflex-again
	($ cname .. "  termina " .. chis .. "  entrenamientos, cueste lo que cueste!" $) text!
	common5-swoloflex-go
	dead? if
		($ "Un pequeno hablador en la maquina dice " .. "NO HAY DOLOR NO HAY GANANCIA!" rbold $) +crtext
	then;
;

cyoa-bridge: common5-swoloflex-pick
	($ cname .. " uses the innovative Swolo-O-Flex personal exercise system..." $) text!
	common5-swoloflex-go
	dead? if
		($ "Un pequeno hablador en la maquina dice " .. "NO REEMBOLSOS!" rbold $) +crtext
	then;
		choice( "Evitar lesiones adicionales" )choice
		choice( "Termina el entrenamiento!" )choice: common5-swoloflex-again
;

cyoa-bridge: common5-swoloflex-pick4
	"Quien deberia ser mutilado o asesinado por el Swole O Flex?" rbold text!
	' common5-swoloflex-pick cyoa-human-choice
;

cyoa: common5-swoloflex
	"Swole O Flex" cyoa-title!
	0 1 rnd to common5-swoloflex@
	($ Thegroup .. "  tropieza con un " .. "Maquina de fuerza y fitness para el hogar Swole O Flex [tm]" bbold .. "'!		Ya no se ven mucho despues de todas las demandas en su contra y los retiros ordenados por el gobierno." $) text!
		// The company that made it went out of business shortly before the zombocalypse hit. - Extra text
		choice( "Mantente alejado de el" )choice
	solohuman if choosehuman cchar charchoice( ": Usa Swole O Flex" )choice: common5-swoloflex-pick "Usar esto dolera." rbold +crtext then;
	"Elige a alguien para que use Swole O Flex y probablemente muera:" rbold +crtext
	numhums 3 > if
		' common5-swoloflex-pick 2 #cyoa-human-choice
		choice( "Escoge a alguien mas" )choice: common5-swoloflex-pick4
	else
		' common5-swoloflex-pick cyoa-human-choice
	then
;

// Gnome Challenge Conduct Rare

defer common5-gnomerepeat
: common5-gnome-back choice( "VOLVER" )choice: common5-gnomerepeat ;

cyoa-choice: common5-gnome-no
	"Todos los desafios suenan horribles." text! ricon
	($ "El gnomo chilla " .. "LO QUE SEA, NERDS!" rbold .. "  y luego se aleja enfurrunado." $) +crtext
	($ Thegroup .. "  esta aliviado de que todo haya terminado." $) +crtext morale++all goodfx
;

0 stack
	"  se rie en voz baja." shove
	" ' no se siente muy diferente, solo algo presumido." shove
	"  se encoge de hombros, pone los ojos en blanco." shove
	"  intenta no reir." shove
value common5-gnome-line-base
common5-gnome-line-base shuffles var common5-gnome-line

: common5-gnome-say	common5-gnome-line depths 0= if common5-gnome-line-base shuffles to common5-gnome-line then pops nip ;

: common5-gnome-morale-stat args( @ )
	<- @ .trait@ "Civilizado" $= if
		($ <- @ .colourname .. common5-gnome-say $) +crtext
		<- @ .morale3+
	then
	<- @ .shooting++ <- @ .mechanic++ <- @ .medical++ <- @ .strength++ <- @ .fitness++
	<- @ .vitality++ -3 STAT_MORALE <- @ .bonus!
;

cyoa-blank: common5-gnome-morale-go
	"PUEDES SOBREVIVIR A LA TRISTEZA?" rbold text! goodfx
	' common5-gnome-morale-stat foreach-team-char
;

cyoa-bridge: common5-gnome-morale
	"SE MALDECIDO POR LA TRISTEZA EMO DEFINITIVA!" rbold text! ricon
	($ "Elegir esto hara que " .. thegroup .. "'el maximo de moral sea 3 mas bajo de lo normal. A cambio, " .. gthey .. "  obtendra:" $) +crtext
	"+ Un punto extra anadido a la salud maxima! + Un punto en todas las habilidades, hasta cada maximo!" bbold +crtext
	common5-gnome-back
	choice( "Acepta el desafio!" )choice: common5-gnome-morale-go
;

: common5-gnome-melee1 "Tengo el WHAMMER!" bbold +crtext 1 WEAPON_MEGASPIKEDBAT trunk.weapon+ ;
: common5-gnome-melee2 "Tengo un FUERTE CUCHILLO!" bbold +crtext 1 WEAPON_SCLEAVER trunk.weapon+ ;
: common5-gnome-melee3 "Tengo un GARROTE DE MADERA!" bbold +crtext 1 WEAPON_WOODCLUB trunk.weapon+ ;
: common5-gnome-melee4 "Obtuviste una MIGHTY CLAYMORE!" fbold +crtext 1 WEAPON_CLAYMORE trunk.weapon+ ;
: common5-gnome-melee5 "Consegui una ALABARDA!" fbold +crtext 1 WEAPON_HALBERD trunk.weapon+ ;

0 stack
	' common5-gnome-melee1 shove
	' common5-gnome-melee2 shove
	' common5-gnome-melee3 shove
	' common5-gnome-melee4 shove
	' common5-gnome-melee5 shove
value common5-gnome-meleewep-base
common5-gnome-meleewep-base shuffles var common5-gnome-meleewep

: common5-gnome-givemelee common5-gnome-meleewep depths 0= if common5-gnome-meleewep-base shuffles to common5-gnome-meleewep then pops nip execute ;

cyoa-blank: common5-gnome-shoot-go
	"NO MAS PISTOLAS O MOTOSIERRAS! DISFRUTA TU DESAFIO!" rbold text! goodfx
	' .nocanshoot foreach-team-char
	team-count-alive for common5-gnome-givemelee next
;

cyoa-choice: common5-gnome-shoot
	"PISTOLAS Y MOTOSIERRAS SON DEMASIADO RUIDOSAS! LAS ODIO!" rbold text! ricon
	($ "Elegir esto hara que " .. thegroup .. " no longer capable of shooting guns or using chainsaws or similar weapons. In exchange, you will get     points. " $) +crtext
	"weapons" bbold +text
	common5-gnome-back
	choice( "Acepta el desafio!" )choice: common5-gnome-shoot-go
;

: common5-gnome-firearm1 "Obtuve una UZI!" bbold +crtext 1 WEAPON_UZI trunk.weapon+ 80 trunk-bullet+ ;
: common5-gnome-firearm2 "Obtuve un FUSIL DE ASALTO!" bbold +crtext 1 WEAPON_AK47 trunk.weapon+ 50 trunk-rifle-shell+ ;
: common5-gnome-firearm3 "Obtuve una MINIGUNA!" fbold +crtext 1 WEAPON_MINIGUN trunk.weapon+ 100 trunk-rifle-shell+ ;
: common5-gnome-firearm4 "Obtuve UN CANON AUTOMATICO DE HABITAT!" bbold +crtext 1 WEAPON_AUTOSHOT trunk.weapon+ 30 trunk-shotgun-shell+ ;
: common5-gnome-firearm5 "Obtuve un CANON DE GUERRA DE PROYECTILES DE LIDER" bbold +crtext 1 WEAPON_SLUG trunk.weapon+ 30 trunk-shotgun-shell+ ;

0 stack
	' common5-gnome-firearm1 shove
	' common5-gnome-firearm2 shove
	' common5-gnome-firearm3 shove
	' common5-gnome-firearm4 shove
	' common5-gnome-firearm5 shove
value common5-gnome-firearm-base
common5-gnome-firearm-base shuffles var common5-gnome-firearm

: common5-gnome-givefirearm common5-gnome-firearm depths 0= if common5-gnome-firearm-base shuffles to common5-gnome-firearm then pops nip execute ;

cyoa-blank: common5-gnome-melee-go
	"LAS ARMAS DE CUESTE A MANOS ESTAN PROHIBIDAS FOREVER! BUENA SUERTE!" rbold text! goodfx
	' .atkspecial foreach-team-char
	team-count-alive for common5-gnome-givefirearm next
;

cyoa-choice: common5-gnome-melee
	"QUE NECESITAS ARMAS DE CUESTE A MANOS CUANDO TENES LA AMISTAD?" rbold text! ricon
	($ "Elegir esto hara que " .. thegroup .. " ya no estas capaz de realizar ataques de cuerpo a cuerpo. En compensacion, obtendras " $) +crtext
	"un arma de fuego aleatoria de alta calidad para cada miembro de tu grupo, con municiones!	Possible guns: Uzi, Assault Rifle, Automatic Shotgun, Slug Shotgun, Minigun" bbold +text
	common5-gnome-back
	choice( "Acepta el desafio!" )choice: common5-gnome-melee-go
;

cyoa: common5-gnome-rare
	"Modo Desafio" cyoa-title! char-gen to recruitee ricon recruitee .gnomify2
	($ "'En un momento de tranquilidad, un gnomo aparece y comienza a gritar.	" .. "'SOY CNOMEY EL GNOMO DE LA CONDUCCION DE LOS DESAFIOS!" rbold .. "x multiplier to your score " .. "RECOMPENSA FABULOSA!" rainbold $) text!
		choice( "No gracias" )choice: common5-gnome-no
		choice( "No Ataques de Cuerpo a Cuerpo" )choice: common5-gnome-melee
		choice( "No Shooting/Chainsaw" )choice: common5-gnome-shoot
		choice( "Max Morale Penalty" )choice: common5-gnome-morale
;

' common5-gnome-rare to common5-gnomerepeat

// Cursed Ancient Chest

0 value common5-chest@

cyoa-choice: common5-chest-rare-

;

: common5-chest-weapon
	common5-chest@ case
	0 of "Pirate Cutlass" endof
	1 of "Mighty Claymore" endof
	2 of "Sturdy Cleaver" endof
	endcase rbold
;

: common5-chest-weaponpick
	common5-chest@ case
		0 of WEAPON_CUTLASS endof
		1 of WEAPON_CLAYMORE endof
		2 of WEAPON_SCLEAVER endof
	endcase cchar
;

cyoa-choice: common5-chest-pick
	($ cname .. "  recoge el " .. common5-chest-weapon .. "!	Se fusiona con " .. chis .. "  manos, impidiendo el uso de cualquier otra arma!" $) text!
	ctakeweapons 1 cchar .carrying.slot_count!
	common5-chest-weaponpick .weapona! 1 cchar .weapon_nodropa!
	cchar .trait@ "Especialista" $= if
		($ cname .. "  se siente naturalmente familiarizado con esta arma de alguna manera, y tambien se siente un poco engreido." $) +crtext strength++ fitness++ morale++ goodfx
	then; goblin-laugh
;

cyoa-bridge: common5-chest-pick4
	solohuman if
		choosehuman common5-chest-pick
	then;
	($ "A quien se le debe dar el increiblemente maldito " .. common5-chest-weapon .. "?" $) text!
	"ADVERTENCIA: El arma quedara bloqueada para el personaje, y el personaje se reducira a un espacio de transporte!" rbold +crtext
	' common5-chest-pick cyoa-human-choice
;

cyoa-choice: common5-chest-book
	solohuman if
		choosehuman ($ cname .. "  lee el " .. "Dingusnomicon!" rbold $) text!
	else
		($ "Todos se turnan para leer el " .. "Dingusnomicon!" rbold $) text!	
	then
	($ "'Es un libro antiguo y malvado cubierto de ojos y bocas. Sigue dando pequenos mordiscos y tambien grita " .. "''nerd'" fbold .. "  en mil diferentes lenguas olvidadas." $) +crtext
	morale--all sting--all bitefx
	"El libro imparte su CONOCIMIENTO PROHIBIDO!" bbold +crtext
	' rewardevent-random foreach-human-char
	choosehuman cchar rewardevent-random
;

0 value common5-lootweapon@

: common5-lootweapon-name
	common5-lootweapon@ case
		0 of "Espada Caballero" endof
		1 of "Bardiche" endof
		2 of "Hacha medieval" endof
		3 of "Machete resistente" endof
		4 of "Florete" endof
		5 of "Alabarda" endof
		6 of "Jarro de estrella de la manana" endof
	endcase rbold
;

: common5-lootweapon-pick
	1 common5-lootweapon@ case
		0 of WEAPON_KNIGHT endof
		1 of WEAPON_BARDICHE endof
		2 of WEAPON_MEDIEVAL endof
		3 of WEAPON_MACHETE2 endof
		4 of WEAPON_RAPIER endof
		5 of WEAPON_HALBERD endof
		6 of WEAPON_MORNING endof
	endcase trunk.weapon+
;

cyoa-choice: common5-chest-loot
	0 6 rnd to common5-lootweapon@
	($ Thegroup .. "  encuentra un escondite de armas muy viejas! Mientras clasifica la basura inutilizable, " .. gthey .. "  correr" .. g1s .. "  en un monton de problemas con astillas y pequenas aranas mordedoras." $) text!
	morale--all sting--all bitefx
	($ gThey .. "  toma algunas armas utiles, incluyendo un " .. common5-lootweapon-name .. "!" $) +crtext
	common5-lootweapon-pick
	1 30 %chance if WEAPON_WOODCLUB else 50 %chance if WEAPON_GAUNTLET else WEAPON_DAGGER then then trunk.weapon+ 1 WEAPON_DAGGER trunk.weapon+ 
;

cyoa-choice: common5-chest-basket
	($ Thegroup .. "  encontro una cesta de regalo Goblin!" $) text!
	homesearch2 ammosearch2 morale++all
	($ "La cesta de regalo esta levemente maldita! Ahora " .. cname .. "  se siente obligado a comprar un regalo para los goblins invisibles y posiblemente inexistentes!" $) +crtext goblin-laugh
;

: common5-chest-flash
	common5-chest@ case
	0 of "Pirate Cutlass" endof // CUTLASS
	1 of "Mighty Claymore" endof // CLAYMORE
	2 of "Sturdy Cleaver" endof // SCLEAVER
	endcase fbold
;

cyoa: common5-chest-rare
	"Tesoro maldito" cyoa-title! someicon
	0 2 rnd to common5-chest@
	($ Thegroup .. "  busca suministros en una cabana espeluznante y descubre un sotano oculto con un antiguo tesoro!" $) text!
	"Is not needed" rbold +crtext " parpadeo " fbold +text "la opcion hara que el personaje al que le des el arma se quede con una ranura bloqueada" rbold +text
		choice( "Cesta de regalo goblin" )choice: common5-chest-basket
		choice( "El Dingusnomicon" rbold )choice: common5-chest-book
		choice( "Armas perdidas hace mucho tiempo" rbold )choice: common5-chest-loot
		choice( common5-chest-flash )choice: common5-chest-pick4
;

// Hand Replacement

cyoa-choice: common5-hand-pick
	($ nursename .. " intenta preparar la habitacion para la cirugia." $) text!
	teamnurse .medicalREV
	($ cname .. "  pierde " .. chis .. " vieja y aburrida mano y obtiene una " .. "Mano de motosierra!" rainbold .. " Esta motosierra modificada es mas eficiente!" $) +crtext
	ctakeweapons WEAPON_CHAINHAND cchar .weapona! 1 cchar .weapon_nodropa!
	cchar .trait@ "Especialista" $= if
		($ cname .. " se siente INCREIBLEMENTE engreido!" rainbold $) +crtext
		2 cchar .carrying.slot_count! morale4+
	then
	teamnurse .cmedical+ teamnurse .medical statroll or if
		"La cirugia salio bien!" +crtext goodfx
	else
		"La cirugia fue muy torpe." +crtext strength-- sting-- badfx
	then
	1 WEAPON_DSHOTGUN trunk.weapon+ // Putting this here just so that it can be given last in -loot
	choice( "Genial" )choice
;

cyoa-bridge: common5-hand-pick4
	"A quien quieres darle una mano de motosierra?" text!
	' common5-hand-pick cyoa-human-choice
;

cyoa-choice: common5-hand-loot
	($ "En lugar de seguir adelante con la cirugia de reemplazo de mano " .. thegroup .. " saquea todos los suministros medicos de la habitacion. Incluso hay un Spray Curativo!" $) text!
		9 trunk-medical+ 3 WEAPON_HEALSPRAY trunk.weapon+ goodfx
	mechanicest choosechar cmechanic+ if
		($ cname .. " logra convertir la motosierra en una motosierra normal que se puede empunar!" $) +crtext
		mechanicREV 1 WEAPON_CHAINSAW trunk.weapon+ mechanicREV
	else
		"La mano de motosierra fue disenada para usarse solo en el brazo, por lo que se deja atras." +crtext
	then 1 WEAPON_DSHOTGUN trunk.weapon+
;

cyoa: common5-hand-rare
	"Mejora de la mano" cyoa-title!
	medicalest to teamnurse
	($ Thegroup .. "  encuentra una sala de cirugia improvisada. En una esquina hay un esqueleto con una escopeta de doble canon y una mano de motosierra protesica, ambas vacias. Mano de motosierra? Parece una buena idea!" $) text!
		choice( "Saquear suministros en su lugar" )choice: common5-hand-loot
	solohuman if
		choosehuman cchar charchoice( ": Reemplazar mano" )choice: common5-hand-pick 
		"Las motosierras son mejores que las manos." +crtext then;
	"'Reemplazar la mano de alguien con una motosierra:" +crtext
	numhums 3 > if
		' common5-hand-pick 2 #cyoa-human-choice
		choice( "Escoge a alguien mas" )choice: common5-hand-pick4
	else
		' common5-hand-pick cyoa-human-choice
	then
;

} }

// Blank
(*
cyoa-choice: common5--

;

cyoa: common5-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)

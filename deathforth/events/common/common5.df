
// EYEBALL Update events

cyoa{ road{ 

// Future Toilet, based on a true story

: common5-toilet-end ($ "O banheiro do futuro emite um ruido giratorio e para de funcionar." $) +crtext goodfx ;

cyoa-choice: common5-toilet-water
	($ "O banheiro espirra uma quantidade enorme de agua de um bide escondido. Isso cobre o quarto com agua, mas a maior parte acerta " .. cname .. " bem nos olhos." $) text!
	morale--
	solo not if ($ Theothersarename .. " fica encantado com isso " $) +crtext cchar ' .morale2+ foreach-team-char-except then
	cchar .shooting@ 6 < if ($ "Mas espere! A agua do bide era medicada e tinha um cheiro agradavel " .. cname .. "'a visao de fica mais nitida!" $) +crtext shooting++ then
	most-hurt damage 0 > if ($ Thegroup .. " fica totalmente curado pela agua medicada do bide!" $) +crtext ' .fullheal foreach-team-char then
	common5-toilet-end
;

cyoa-blank: common5-toilet-note
	"Cancao do Vaso Sanitario" cyoa-title!
	"O banheiro toca uma musica linda. A musica nao tem palavras, mas transmite uma mensagem de esperanca forte e assombrosa." text!
	morale4+all common5-toilet-end
;

cyoa-choice: common5-toilet-wings
	"'A tampa do banheiro abre e fecha rapidamente, com grandes sons de WHOOMP enquanto a tampa joga rajadas de ar. A tampa abre e fecha cerca de 30 vezes seguidas e depois para." text!
	($ cname .. " fica confuso com o proposito desta funcao." $) +crtext
	common5-toilet-end gstats{ 30 ' toilets-opened +! }
;

cyoa-choice: common5-toilet-butt
	"O assento do banheiro comeca a zunir e brilhar levemente." text!
	($ cname .. " senta. O assento eletronico do banheiro da " .. chim .. " uma poderosa massagem glutea" .. "*" pbold .. " enquanto o banheiro canta repetidamente em diferentes idiomas:" $) +crtext
	"OS GLUTEOS SAO A CHAVE PARA A BOA FORMA	OS GLUTEOS SAO A CHAVE PARA A FORCA	OS GLUTEOS SAO A CHAVE PARA O PODER	OS GLUTEOS SAO O CENTRO DA VIDA" bbold +crtext
	morale++ strength++ fitness++ common5-toilet-end
	"'*Nota de vocabulario: Gluteo significa 'bunda'" pbold +crtext
;

cyoa: common5-toilet-rare
	"'Banheiro do Futuro do Passado" cyoa-title! choosehuman cicon
	($ Thegroup .. " descobre algo incrivel enquanto explora uma casa: Um banheiro realmente funcionando. Todo mundo tem feito suas necessidades no mato.	O banheiro tem um painel de controle futurista cheio de botoes " .. cname .. " sente se compelido a apertar um e escolhe o botao rotulado com uma imagem de:" $) text!
		choice( "Jatos de Agua?" )choice: common5-toilet-water
		choice( "Notas Musicais" )choice: common5-toilet-note
		choice( "Asas Batendo" )choice: common5-toilet-wings
		choice( "Uma Bunda?" )choice: common5-toilet-butt
;

// Long Line

: common5-line-camp ($ "Com o dia desperdicado " .. thegroup .. " tem que montar acampamento." $) +crtext +cr fullcamp 1 calendar-day+ ;
: common5-line-shop ($ "'A loja e um mercado chique e sofisticado. A maioria dos itens mais sofisticados apodreceu, mas ainda ha alguns saques." $) +crtext
	8 12 rnd trunk-food+ 4 6 rnd trunk-medical+ goodfx ;

cyoa-choice: common5-line-pick
	
	cdog? if
		($ cname .. " corre em direcao a linha e entao a ultrapassa completamente.		Horas depois " .. cname .. " retorna com um graveto " .. chis .. "  boca." $) text!
		1 WEAPON_TREEBRANCH trunk.weapon+
		morale2+ cchar morale--all-except goodfx
		common5-line-camp
	then;
	
	ccat? if
		($ cname .. "  entra correndo na loja, evitando a fila. " .. cHe .. "  sai carregando um roedor morto, possivelmente um gerbilo?" $) text!
		2 trunk-food+ morale2+ goodfx
	then;
	
	cpet? if
		($ cname .. "  deita para um descanso. Como animal, " .. che .. " ' ou nao entende o conceito de fila, ou finge que nao entende." $) text!
		morale++ goodfx
	then;
	
	cchar oblivious? if
		($ cname .. "  pula a fila completamente. " .. cHe .. " ' nao percebeu que tinha fila, ou zumbis." $) text!
		witsREV attitudeREV common5-line-shop
	then;
	
	ctemper- if
		cloyalty- if
			($ cname .. "  vai em frente e pula a fila completamente. " .. cHe .. "  nao joga pelas regras!" $) text!
			morale++ aholeREV common5-line-shop
		then;
		($ cname .. "  dura cerca de um minuto na fila antes de perder a " .. chis .. "  paciencia!" $) text!
		cstrength+ if
			"ESPERAR NA FILA E PARA NERDS!" rbold +crtext
			($ cHe .. " ' comeca a dar socos rapidos em todos os zumbis da fila ate que eles explodam. Ha muitos para socar entao " .. che .. "  fica cansado e tropeca para longe, suando. Que treino!" $) +crtext
			rageREV fitness++ fitnessREV morale++ goodfx
		then;
		($ cHe .. " comeca a atacar o zumbi na frente de " .. chim .. ". Os outros zumbis eventualmente percebem e comecam a atacar. " .. cname .. " e forcado a correr!" $) +crtext
		sting-- bitefx temperREV 
	then;
	
	ctemper+ if
		($ cname .. " pacientemente espera na longa fila. Ela avanca muito devagar, mas " .. che .. " espera calmamente." $) text!
		temperREV 
		common5-line-shop common5-line-camp
	then;
	
	($ cname .. " espera na fila. Ela avanca aos poucos, para " .. cname .. "'grande irritacao de." $) text!
	attitude+roll if 
		($ cname .. " tenta pensar sobre isso positivamente. " .. cHe .. " consegue aguentar e entrar na loja!" $) +crtext
		attitudeREV morale-- common5-line-shop
	else
		($ "Depois de horas disso, os zumbis ligam para " .. cname .. " devido a " .. chis .. " choramingos constantes. " .. cHe .. " e forcado a fugir!" $) +crtext
		morale2- sting-- bitefx 
	then temperREV common5-line-camp
;

cyoa-bridge: common5-line-pick4
	"Escolha quem fica na fila de zumbis:" text!
	' common5-line-pick cyoa-team-choice 
;

cyoa-blank: common5-line
	"Ainda tendo que esperar na fila" cyoa-title!
	($ Thegroup .. " ' corre para uma horda de zumbis. Eles estao completamente doceis, apenas esperando do lado de fora de uma loja em uma grande fila unica. Cada zumbi esta olhando para frente." $) text!
		choice( "Ignore esta estranheza" )choice
	solo if
		"'O letreiro da loja esta desbotado e nao pode ser lido." +crtext
		soloer charchoice( ": Espere na fila" )choice: common5-line-pick
	then;
	"Voce pode enviar alguem para entrar na fila, para resolver este misterio. Parece que pode ser uma longa espera." +crtext
	team-count 3 > if
		' common5-line-pick 2 #cyoa-team-choice
		choice( "Escolha outra pessoa" )choice: common5-line-pick4
	else
		' common5-line-pick 3 #cyoa-team-choice
	then	
;

// Rock throwing

cyoa-choice: common5-rock-throw
	($ cname .. "  e compelido a jogar uma pedra no infeliz zumbi." $) text!
	cchar oblivious? if
		($ cHe .. "  continua acidentalmente jogando a pedra de maneiras que a fazem retornar para " .. chim .. ". Primeiro foi direto para cima, depois nas proximas vezes atingiu a cerca. Isso continua por varias horas.		" .. cname .. "  eventualmente joga a pedra por cima da cerca, por pouco. " .. cHe .. "  esta quase morto de exaustao ate entao." $) +crtext
		morale3- sting-- cchar morale--all-except fitness++ strength++ fitnessREV strengthREV witsattREV
		common5-line-camp
	then;
	strength+roll if
		($ cHe .. "  coloca tudo " .. chis .. "  pode em um poderoso lancamento para baixo. A pedra erra o zumbi, mas destroi um pequeno gnomo de jardim." $) +crtext
		strengthREV morale++all goodfx
		33 stpick if choosechar ($ cname .. "  percebe que isso e muito ofensivo para " .. chim .. "!" $) +crtext morale4- badfx then
	then;
	cfitness+ if
		($ cHe .. "  executa um lancamento com forma perfeita. A pedra navega graciosamente pelo ar. Ela erra o zumbi por muito, mas que lancamento bonito!" $) +crtext
		fitnessREV morale2+ cchar morale++all-except goodfx
	then;
	($ cHe .. "  nao apenas erra, mas realiza um lancamento realmente embaracoso " .. cHe .. "  tropeca e quase cai sobre a cerca e do penhasco." $) +crtext badfx strengthREV fitnessREV morale-- cchar morale++all-except
;

cyoa: common5-rock
	"A Pedra Perfeita" cyoa-title!
	($ Thegroup .. "  encontra uma pedra de arremesso perfeita no chao. Ela esta ao lado de uma cerca baixa guardando uma grande queda. No fundo desta queda esta um unico zumbi, parado la, olhando para nada em particular." $) text!
	solohuman if choosehuman
		($ cname ..  "  sente um desejo ardente de jogar a pedra neste zumbi." $) +crtext
		choice( "Deixe o zumbi em paz" )choice
		cchar charchoice( ": Jogue a pedra!" )choice: common5-rock-throw
	else
		"'E bom demais para deixar passar. Alguem DEVE jogar aquela pedra neste zumbi:" +crtext
		' common5-rock-throw cyoa-human-choice
	then
;

// Drag Race

cyoa-choice: common5-drag-race
	vehicle.which@ 1 < if
		($ Thegroup .. " ' nao tem carro agora, entao nao e uma corrida muito acirrada." $) text!
		"EU GANHEI, NERDS!" rbold +crtext morale3-all badfx
	then;
	($ "A corrida comeca! Ambos os pilotos " .. "DESACELER A VELOCIDADE!" fbold $) text!
	car-speed 2 < if
		($ Thegroup .. "' carro lento nao teve chance alguma nesta corrida." $) +crtext badfx morale3-all
	then;
	car-speed 2 = if
		($ Thegroup .. "' carro e muito lento e fica muito para tras." $) +crtext badfx morale--all
	then;
	car-speed 3 > if
		"Seu carro vence facilmente a corrida, deixando o outro motorista na poeira." +crtext goodfx morale4+all
	then;
	car-speed 2 > if
		"Seu carro quase vence a corrida. O outro motorista da um polegar para cima." +crtext morale2+all goodfx
	then;
;

cyoa-choice: common5-drag-ignore
	vehicle.which@ 0 > if
		"'VOCE NAO PODE FAZER ISSO! VOCE NAO PODE IGNORAR UM DESAFIO DE CORRIDA DE ARRANQUE!" rbold text!
		car-armor 0 > if
			($ "O motorista bate no carro de " .. thegroup .. "'s car, but just bounces right off the armor.
			The driver's car careens off the road and crashes into a ditch." .. Thegroup .. "  esta muito divertido com isso." $) +crtext morale3+all goodfx
		then;
		($ "O motorista bate no carro de " .. thegroup .. "', fazendo o girar." $) +crtext
		-1 car-chassis+
		car-chassis 0 > if 
			($ "O carro sofre pequenos danos e " .. thegroup .. "  se machuca." $) +crtext sting--all bitefx
		else
			($ "'O carro destruido nao aguentou essa tensao. Ele para de funcionar." $) +crtext badfx
		then
	else
		"'CERTO, SEJA ASSIM! VOLTAREI QUANDO VOCE TIVER UM CARRO!" rbold text!
		($ "O motorista " .. "SAI COM TUDO!" fbold $) +crtext
	then

;

cyoa-choice: common5-drag-ram
	($ "Antes que a corrida de arrancada comece, " .. thegroup .. "  joga "  .. ghis .. " ' o carro no carro do oponente." $) text!
	car-armor 0 > if
		"'Seu carro blindado destroi o veiculo rapido, mas fragil, a blindagem nem mesmo arranha." +crtext
	else
		-2 car-chassis+ bitefx
		car-chassis 0 > if
			"'Ambos os carros sofrem danos, mas o veiculo do oponente leva muito mais. Voce vence a corrida!" +crtext
		else
			"Ambos os carros estao danificados o suficiente para nao poderem mais dirigir. Isso torna a corrida um empate!" +crtext
		then 
	then
	($ "O motorista chato foge gritando " .. "TRAMPACEIRO! " rbold .. Thegroup .. "  suga um pouco de gasolina de seus destrocos." $) +crtext
	morale++all 30 40 rnd trunk-gas+ goodfx
;

cyoa-choice: common5-drag-rules
	($ cname .. " ' nao esta claro sobre as regras de arrancadas." $) text!
	"THE ONLY RULE OF DRAG RACING... IS THAT THERE ARE NO RULES!" rbold +crtext
	"O motorista entao continua a falar sobre uma longa lista de regras de arrancadas. " +crtext
	most-ahole if choosechar
		($ "Durante isso, " .. cname .. " ' vai em frente e suga toda a gasolina do carro do motorista." $) +text 80 120 rnd trunk-gas+ aholeREV
	else
		($ "Por volta da regra 60, " .. thegroup .. "  afasta se lentamente enquanto o motorista continua falando." $) +text
	then goodfx
;

cyoa: common5-drag
	"FAAF 15: Tenho que ir rapido" cyoa-title! someicon
	($ "Um carro rapido e chique chega ate voce dramaticamente. O motorista grita da janela dele:" $) text!
	"ON THE DEATH ROAD, THE ONLY WAY TO SURVIVE IS TO BE FAST. AND ALSO FURIOUS." rbold +crtext
	"Ele te desafia para uma corrida de arrancada." +crtext
		choice( "Ignorar lunatico" )choice: common5-drag-ignore
	vehicle.which@ 0 > if
		choice( "Bater no carro dele" )choice: common5-drag-ram
	then
	choice( "Aceitar desafio" )choice: common5-drag-race
	most-oblivious if
		charchoice( ": Perguntar por regras" )choice: common5-drag-rules
	then
;

// Radio Station

cyoa-choice: common5-radiodj-siphon
	($ Thegroup .. " ' sifona a gasolina do gerador. Nao havia muito restante, mas cada pequena ajuda ajuda. A energia da estacao de radio e desligada." $) text! 15 30 rnd trunk-gas+ goodfx
;

0 value DJ-PICK

cyoa-choice: common5-radiodj-pick

	// This should be expanded later for lots of possible stuff

	cdog? if
		($ cname .. "  diz algo como " .. "BARK!" rbold .. "  ou talvez " .. "BORK?" rbold .. "' ataca o microfone O microfone e destruido" $) text!
		morale++ goodfx
	then;
	
	cpet? if
		($ cname .. "  ataca o microfone!		O microfone foi destruido!		" .. cname .. "  esta muito satisfeito por se livrar desta ameaca." $) text! bitefx morale2+ strengthREV fitnessREV
	then;
	
	cchar oblivious? if
		($ cname .. "  liga o microfone por acidente, depois deixa escapar muitos fatos pessoais embaracosos. Isso inclui algumas informacoes uteis para bandidos, como localizacao e quantidade atual de suprimentos." $) text!
		witsattREV cchar morale2-all-except
	then;
	
	cchar grating? if
		($ cname .. "  grita repetidamente " .. "CALMA AI!" fbold .. "  no microfone. Depois de onze desses gritos, o equipamento de radio entra em curto e o gerador pega fogo." $) text! witsattREV morale++ cchar morale2-all-except 11 to cool-it
	then;
	
	cchar paranoid? if
		($ cname .. "  divulga um longo, quase desesperado discurso sobre como a apocalipse de zumbis foi criada pelo governo.		O projeto se chamava " .. " PROJETO: DINGUS" bbold .. "  e tinha a intencao de servir como uma cortina de fumo para fazer ilegalFIM DE SE a verdade foi revelada" .. 
		"*" rbold .. "   nota Jorts significam calcas de jeans." $) text! witsattREV morale3+ cchar morale--all-except
		"' divulga uma mensagem util:" rbold +crtext
	then;
	
	cloyalty- if
		($ cname .. "  transmite uma mensagem util:" $) text!
		cchar ahole? if
			"CIVILIZATION HAS ENDED!
			LIE, CHEAT, STEAL GAS FROM TOILETS!
			HOMEWORK SUCKS!!!" rbold +crtext
			"A ultima linha deste discurso e gritada tao alto que o microfone produz feedback." +crtext
			aholeREV morale2+
			cchar ' .loyalty-- foreach-team-char-except
		else
			"EVERYONE BUT ME
			... IS A NERD!" rbold +crtext
			loyaltyREV morale++
		then cchar morale--all-except
	then;

	cchar .hiskill-stack depths 0= if 0 else shuffles pops then to DJ-PICK drop
	
	DJ-PICK STAT_MEDICAL = if
		cchar .medical 3 > if
			($ cname .. "  sabe que " .. chis .. "  mensagem pode salvar vidas, e com grande ansiedade raspou o maximo de conhecimento medico pratico que " .. che .. " can in a short time.
			Maybe this can help someone." $) text!
			medicalREV morale++
			' .medical++ foreach-team-char
		then;
		cchar .medical 3 < if
			($ cname .. "  se orgulha de espalhar como muito conhecimento medico que " .. che .. " can in a short time.
			It's all really bad advice that makes anyone that hears it actually worse off." $) text!
			medicalREV morale2+
			cchar ' .medical-- foreach-team-char-except
		then; medicalREV
	then
	
	DJ-PICK STAT_MECHANICAL = if
		cchar .mechanical 3 > if
			($ cname .. " broadcasts a short impromptu talk show that is a crash course in car repair. It's informative and entertaining.
			Maybe this can help someone." $) text!
			mechanicREV morale++
			' .mechanic++ foreach-team-char
		then;
		cchar .mechanical 3 < if
			($ cname .. " rambles on about repairing sweet cars and 'crafting level 3 lighting-flame-chainsaws' until the power runs out.
			Anyone that hears this actually gets worse at repairing things." $) text!
			mechanicREV morale2+
			cchar ' .mechanic-- foreach-team-char-except
		then; mechanicREV
	then
	
	DJ-PICK STAT_FITNESS = if
		cfitness+ if
			($ cname .. " creates an intense radio calisthenics show. Whoever is listening joins in with the exercises.
			The total combined sweat from this could fill up a bathtub." $) text!
			fitnessREV morale2+
			' .fitness++ foreach-team-char
		then;
	then
	
	DJ-PICK STAT_SHOOTING = if
		cshooting+ if
			($ cname .. " broadcasts some very effective tips and theories on zombie shooting.
			During the show, " .. cHe .. "  mesmo chega a inventar alguns ganchos de truques." $) text!
			shootingREV morale++
			' .shooting++ foreach-team-char		
		then;	
	then

	DJ-PICK STAT_STRENGTH = if
		cstrength+ if
			($ cname .. "  le um poema que " .. che .. " escreveu sobre o quanto " .. che .. " loves being strong.
			The Four Spirits of Swoleness hear this sweet poem, and they confer their blessing!" $) text!
			strengthREV morale++
			' .strength++ foreach-team-char		
		then;	
	then
	
	cchar charming? if
		($ cname .. " nao consigo pensar em mais nada, entao " .. che .. " ' canta uma musica. E uma otima apresentacao, com certeza se tornara um novo hit pos zumbocalipse!" $) text! witsattREV morale2+all
	then;
		
	($ cname .. " ' nao consegue pensar em nada para dizer e permanece em silencio mesmo quando a energia acaba." $) text! badfx morale--
;

cyoa-bridge: common5-radiodj-pick4
	"Quem vai ser o DJ da radio?" text!
	' common5-radiodj-pick cyoa-team-choice
;

cyoa: common5-radiodj
	"Radio Estrada da Morte" cyoa-title!
	($ Thegroup .. " ' encontra uma estacao de radio. O equipamento de transmissao esta ligado a um gerador a gas com uma pequena quantidade de combustivel restante. Ha energia suficiente para enviar uma mensagem para qualquer um que possa ouvi la. " $) text!
		choice( "Sifao do gerador" )choice: common5-radiodj-siphon
	solo if
		soloer charchoice( ": Enviar uma mensagem" )choice: common5-radiodj-pick
	then;
	"Quem deve enviar a mensagem?" +crtext
	team-count 3 > if
		' common5-radiodj-pick 2 #cyoa-team-choice
		choice( "Escolha outra pessoa" )choice: common5-radiodj-pick4
	else
		' common5-radiodj-pick 3 #cyoa-team-choice
	then	
;

// Mysterious Radio Signal

: common5-signal-win
	"A mensagem avisa que a base foi abandonada, mas eles esconderam um estoque de municao para ajudar outros sobreviventes. O estoque acaba por estar por perto!" +crtext morale++all ammosearch2
	solohuman if witsREV else ' .witsREV foreach-team-char // wits-total is for the whole team instead of just humans, oddly... may change this later
		wits-total 17 > if
			"A equipe decodifica uma mensagem ultra secreta adicional:" +crtext
			char-gen to recruitee ricon
			($ "Tambem abandonamos " .. recruitee .name@ .. ", " .. rhe .. " was a real nerd.
			You can recruit " .. rhim .. "  se voce estiver desesperado." $) bbold bbold +crtext
			recruit-yesno
		then
	then
;

cyoa-blank: common5-signal-decode
	solohuman if
		choosehuman ($ cname .. "  tenta descobrir o codigo por " .. chim .. "self..." $) text!
		cwits+ if common5-signal-win then;
	else
		"The team puts their heads together to figure out the code..." text!
		wits-total 10 > if common5-signal-win then;
	then
	"Portugues " +crtext "DRINK MORE OVALTINE" bbold +text
	"E so baboseira! Que perda de tempo!" +crtext morale--all
;

cyoa-blank: common5-signal-drive
	($ Thegroup .. " tentativas de dirigir ate o sinal ouvindo a transmissao de radio conforme ela fica mais fraca ou mais forte. Isso leva um bom tempo." $) text!
	car-mpg 0 = if
		"Engracado, esse carro nao consome gasolina." +crtext
		goodfx
	else		
		trunk-gas 70 car-mpg * >= if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			bandit-gas
		then
	then +cr fullcamp heal-event
	($ gThey .. "  encontrar" .. g1s .. " um abrigo de sobrevivencia que ainda esta reproduzindo a mensagem. Todos sairam as pressas, deixando para tras um estoque de suprimentos." $) +crtext homesearch2 ammosearch2
;

cyoa-choice: common5-signal-gov
	"ESTA E UMA ARMADILHA OBVIA ARMADA PELO GOVERNO.	O GOVERNO ZUMBI SECRETO!" pbold text!
	($ gHe .. "  nao perde tempo em " .. "DESTACANDO" fbold .. "  de la!	" .. cname .. "  esta muito aliviado por ter escapado." $) +crtext morale2+ witsattREV
;

cyoa: common5-signal
	"Sinal de Radio Misterioso" cyoa-title!
	($ Thegroup .. " ' encontra uma estranha mensagem repetitiva no radio. E uma serie de curtas rajadas de musica, bipes, numeros falados e assombrosos ruidos de apito deslizante. Ha tambem um kazoo la.	O que tudo isso poderia significar?" $) text!
		choice( "Ignore" )choice
		choice( "Decifrar mensagem" )choice: common5-signal-decode
		choice( "Dirija para o sinal" )choice: common5-signal-drive
		most-paranoid if
			charchoice( ": ESTA E UMA ARMADILHA!" )choice: common5-signal-gov
		then
;

// Repair Shop repair

cyoa-choice: common5-repair-pick
	($ cname .. " spends a day trying to fix something in the junk pile... " $) text!
	cchar .mechanical@ case
		0 of ($ cHe .. "  acidentalmente recebe muitos cortes, arranhoes e hematomas. " .. cHe .. "  aprende sobre conserto, da maneira mais dificil." $) +text endof
		1 of ($ cHe .. "  pega uma raquete de tenis quebrada e a transformou em uma " .. "Raquete de Tenis Ultra Ergonomica Nivel 2 da Baleia" bbold .. ". Isto e exatamente o mesmo que uma raquete de tenis normal." $) +text 1 WEAPON_TENNIS trunk.weapon+ endof
		2 of ($ cHe .. "  transforma um inutil " .. "Machete Enferrujado" bbold .. "'. E uma clara melhoria." $) +text 1 WEAPON_MACHETE1 trunk.weapon+ endof
		3 of ($ cHe .. "  reforcou um taco de basebol lascado com pregos e metal para fazer um " .. "Taco de Basebol Cravado" bbold .. "'! E otimo ate se partir!" $) +text 1 WEAPON_SPIKEDBAT trunk.weapon+ endof
		4 of ($ cHe .. "  afia um " .. "Aparador de Ramos" bbold .. "'! E uma arma decente embora ligeiramente estranha que nao se parte!" $) +text 1 WEAPON_BRANCHTRIM trunk.weapon+ endof // This was a sturdy machete until I chickened out
		5 of ($ cHe .. "  consegue um " .. "Soprador de Folhas" bbold .. "  a funcionar novamente! Manda os zombies voar como folhas!" $) +text 1 WEAPON_BLOWER trunk.weapon+ endof
		6 of ($ cHe .. "  restaura uma falha " .. "Cortador de ervas daninhas" bbold .. "'! E como um filho um pouco fraco para a motosserra! Para compensar a falta de potencia, e muito mais eficiente em termos de gas." $) +text 1 WEAPON_WHACKER trunk.weapon+ endof
	endcase mechanicREV mechanic++
	+cr fullcamp heal-event
;

cyoa-bridge: common5-repair-pick4
	"Quem deveria passar um dia consertando lixo?" text!
	' common5-repair-pick cyoa-human-choice

;

cyoa: common5-repair
	"Reparo de oficina mecanica" cyoa-title!
	($ Thegroup .. " ' encontra uma oficina mecanica. Ha um pouquinho de gas, alem de muito lixo quebrado. Pode haver algo bom aqui, se voce souber como consertar." $) text!
	10 16 rnd trunk-gas+
		choice( "Ignorar lixo" )choice
	solohuman if choosehuman cchar charchoice( ": Consertar lixo" )choice: common5-repair-pick "Voce poderia passar um dia consertando coisas aqui." +crtext then;
	"Escolha alguem para passar um dia consertando lixo:" +crtext
	numhums 3 > if
		' common5-repair-pick 2 #cyoa-human-choice
		choice( "Escolha outra pessoa" )choice: common5-repair-pick4
	else
		' common5-repair-pick cyoa-human-choice
	then
;

// Health Food Store healing

0 stack
	"acha que a comida e boa." shove
	"'nao se importa com a comida horrivel." shove
	"tem uma atitude positiva sobre esta sujeira!" shove
	"esta tentando manter a mente aberta." shove
	"'finge que e junk food." shove
	"ignora a comida com sucesso." shove
	"pensa em curry." shove
	"foca nos beneficios para a saude." shove
	"tenta bloquear o cheiro." shove
	"ja comeu comida pior, possivelmente." shove
	"bloqueia mentalmente o nojo." shove
	"resiste a sentir qualquer emocao agora mesmo!" shove
value common5-healthfood-line-base
common5-healthfood-line-base shuffles var common5-healthfood-line

: common5-healthfood-say
	common5-healthfood-line depths 0= if common5-healthfood-line-base shuffles to common5-healthfood-line then pops nip
;

: common5-healthfood-stat args( @ )
	<- @ .cattitude+ if
		($ <- @ .colourname .. " " .. common5-healthfood-say $) +crtext 
	else
		<- @ .cattitude- if
			<- @ .morale2-
		else
			<- @ .morale--
		then
	then <- @ .attitudeREV <- @ .health++
	
;

cyoa-blank: common5-healthfood-go
 ($ Thegroup .. "  saquea a comida nojenta e pega o que " .. gthey .. "  pode viver com. Isso acaba nao sendo muito da comida no total." $) text!
  4 6 rnd trunk-food+
  ' common5-healthfood-stat foreach-team-char
  ($ Thegroup .. "  sente se mais saudavel so de olhar para a comida nojenta!" $) +crtext
;

cyoa: common5-healthfood
	"Saude da Alimentacao" cyoa-title!
	($ Thegroup .. " ' encontra uma loja de alimentos saudaveis. Parece que ninguem se incomodou em saquea la porque a comida era nojenta. A maior parte da comida parece estar estragando de qualquer maneira. Ou talvez nao, pode ser que a comida deva ter essa aparencia e esse cheiro." $) text!
		choice( "Ignorar por razoes morais" )choice: morale++all
		choice( "Saquear a comida bruta" )choice: common5-healthfood-go
;

// The Swole-O-Flex

0 value common5-swoloflex@

: common5-swoloflex-go
	common5-swoloflex@ 1 < if
		"The machine starts its patented automated total-body bodybuilding regiment..." text!
		($ cname .. "''s corpo faz estalos e rangidos audiveis! Quao horrivel!" $) rbold +crtext
		strength++ ouch-- 1 to common5-swoloflex@ bitefx
	else
		($ "A maquina prende " .. cname .. " in place for its innovative gymnastics simulator..." $) text!
		($ "'Um cabo de alta tensao se solta e chicoteia! Nao ha tempo para reagir a ele!" $) rbold +crtext
		fitness++ ouch-- 0 to common5-swoloflex@ bitefx
	then
;

cyoa-choice: common5-swoloflex-again
	($ cname .. "  termina " .. chis .. "  treinos, custe o que custar!" $) text!
	common5-swoloflex-go
	dead? if
		($ "Um pequeno alto falante na maquina diz " .. "SEM DOR SEM GANHO!" rbold $) +crtext
	then;
;

cyoa-bridge: common5-swoloflex-pick
	($ cname .. " uses the innovative Swolo-O-Flex personal exercise system..." $) text!
	common5-swoloflex-go
	dead? if
		($ "Um pequeno alto falante na maquina diz " .. "SEM REEMBOLSOS!" rbold $) +crtext
	then;
		choice( "Evite mais lesoes" )choice
		choice( "Termine o treino!" )choice: common5-swoloflex-again
;

cyoa-bridge: common5-swoloflex-pick4
	"Quem deve ser mutilado ou morto pelo Swole O Flex?" rbold text!
	' common5-swoloflex-pick cyoa-human-choice
;

cyoa: common5-swoloflex
	"Swole O Flex" cyoa-title!
	0 1 rnd to common5-swoloflex@
	($ Thegroup .. "  tropeca em um " .. "Maquina de Ginastica e Fortalecimento Caseiro Swole O Flex [tm]" bbold .. "'!		Voce nao ve muito disso depois de todos os processos contra ele e dos recalls ordenados pelo governo." $) text!
		// The company that made it went out of business shortly before the zombocalypse hit. - Extra text
		choice( "Fique longe disso" )choice
	solohuman if choosehuman cchar charchoice( ": Use Swole O Flex" )choice: common5-swoloflex-pick "Usar isso vai doer." rbold +crtext then;
	"Escolha alguem para usar o Swole O Flex e provavelmente ser morto:" rbold +crtext
	numhums 3 > if
		' common5-swoloflex-pick 2 #cyoa-human-choice
		choice( "Escolha outra pessoa" )choice: common5-swoloflex-pick4
	else
		' common5-swoloflex-pick cyoa-human-choice
	then
;

// Gnome Challenge Conduct Rare

defer common5-gnomerepeat
: common5-gnome-back choice( "VOLTAR" )choice: common5-gnomerepeat ;

cyoa-choice: common5-gnome-no
	"Todos os desafios parecem horriveis." text! ricon
	($ "O gnomo grita " .. "QUAISQUER, NERDS!" rbold .. "  e entao se afasta bufando." $) +crtext
	($ Thegroup .. "  fica aliviado que tudo acabou." $) +crtext morale++all goodfx
;

0 stack
	"  ri baixinho." shove
	" ' nao se sente muito diferente, apenas meio presuncoso." shove
	"  da de ombros, revira os olhos." shove
	" tenta nao rir." shove
value common5-gnome-line-base
common5-gnome-line-base shuffles var common5-gnome-line

: common5-gnome-say	common5-gnome-line depths 0= if common5-gnome-line-base shuffles to common5-gnome-line then pops nip ;

: common5-gnome-morale-stat args( @ )
	<- @ .trait@ "Civilizado" $= if
		($ <- @ .colourname .. common5-gnome-say $) +crtext
		<- @ .morale3+
	then
	<- @ .shooting++ <- @ .mechanic++ <- @ .medical++ <- @ .strength++ <- @ .fitness++
	<- @ .vitality++ -3 STAT_MORALE <- @ .bonus!
;

cyoa-blank: common5-gnome-morale-go
	"VOCE CONSEGUE SOBREVIVER A TRISTEZA?" rbold text! goodfx
	' common5-gnome-morale-stat foreach-team-char
;

cyoa-bridge: common5-gnome-morale
	"SEJA AMALDICOADO PELA MAXIMA TRISTEZA EMO!" rbold text! ricon
	($ "Escolher isso fara com que " .. thegroup .. "'o moral maximo de seja 3 mais baixo que o normal. Em troca, " .. gthey .. "  recebera:" $) +crtext
	"+ Um ponto extra adicionado a saude maxima! + Um ponto em todas as habilidades, ate o maximo de cada uma!" bbold +crtext
	common5-gnome-back
	choice( "Aceitar o desafio!" )choice: common5-gnome-morale-go
;

: common5-gnome-melee1 "Peguei o MARTELO!" bbold +crtext 1 WEAPON_MEGASPIKEDBAT trunk.weapon+ ;
: common5-gnome-melee2 "Peguei uma FACA FORTE!" bbold +crtext 1 WEAPON_SCLEAVER trunk.weapon+ ;
: common5-gnome-melee3 "Peguei uma CLAVA DE MADEIRA!" bbold +crtext 1 WEAPON_WOODCLUB trunk.weapon+ ;
: common5-gnome-melee4 "Peguei uma CLAYMORE PODEROSA!" fbold +crtext 1 WEAPON_CLAYMORE trunk.weapon+ ;
: common5-gnome-melee5 "Peguei uma HALBERD!" fbold +crtext 1 WEAPON_HALBERD trunk.weapon+ ;

0 stack
	' common5-gnome-melee1 shove
	' common5-gnome-melee2 shove
	' common5-gnome-melee3 shove
	' common5-gnome-melee4 shove
	' common5-gnome-melee5 shove
value common5-gnome-meleewep-base
common5-gnome-meleewep-base shuffles var common5-gnome-meleewep

: common5-gnome-givemelee common5-gnome-meleewep depths 0= if common5-gnome-meleewep-base shuffles to common5-gnome-meleewep then pops nip execute ;

cyoa-blank: common5-gnome-shoot-go
	"NADA MAIS DE ARMAS OU MOTOSSERRAS! APROVEITE SEU DESAFIO!" rbold text! goodfx
	' .nocanshoot foreach-team-char
	team-count-alive for common5-gnome-givemelee next
;

cyoa-choice: common5-gnome-shoot
	"GUNS E ESMAGADEIRAS SAO TROPPELOS! ODEIO ELAS!  Guns and chainsaws are too loud! I hate them!" rbold text! ricon
	($ "Escolher isso fara com que " .. thegroup .. " nao e mais capaz de disparar armas de fogo ou usar esmagadeiras ou armas semelhantes. Em troca, voce recebera  no longer capable of shooting guns or using chainsaws or similar weapons. In exchange, you will get " $) +crtext
	"um facao aleatorio para cada membro de seu grupo! Possiveis facoes: Bate de Madeira, The Whammer, Facao Solido, Claymore, Alabarda  one random high quality melee weapon for each member of your group!	Possible weapons: Wooden Club, The Whammer, Sturdy Cleaver, Mighty Claymore, Halberd" bbold +text
	common5-gnome-back
	choice( "Aceitar o desafio!" )choice: common5-gnome-shoot-go
;

: common5-gnome-firearm1 "Gotei um UZI!  Got an UZI!" bbold +crtext 1 WEAPON_UZI trunk.weapon+ 80 trunk-bullet+ ;
: common5-gnome-firearm2 "Gotei uma FUSAO DE ASSALTO!  Got an assault rifle!" bbold +crtext 1 WEAPON_AK47 trunk.weapon+ 50 trunk-rifle-shell+ ;
: common5-gnome-firearm3 "Gotei um MINIGUN!  Got a minigun!" fbold +crtext 1 WEAPON_MINIGUN trunk.weapon+ 100 trunk-rifle-shell+ ;
: common5-gnome-firearm4 "Gotei uma ESpingarda Automatica!  Got an automatic shotgun!" bbold +crtext 1 WEAPON_AUTOSHOT trunk.weapon+ 30 trunk-shotgun-shell+ ;
: common5-gnome-firearm5 "Gotei uma Espingarda de Balas Rifadas!  Got a rifled slug shotgun!" bbold +crtext 1 WEAPON_SLUG trunk.weapon+ 30 trunk-shotgun-shell+ ;

0 stack
	' common5-gnome-firearm1 shove
	' common5-gnome-firearm2 shove
	' common5-gnome-firearm3 shove
	' common5-gnome-firearm4 shove
	' common5-gnome-firearm5 shove
value common5-gnome-firearm-base
common5-gnome-firearm-base shuffles var common5-gnome-firearm

: common5-gnome-givefirearm common5-gnome-firearm depths 0= if common5-gnome-firearm-base shuffles to common5-gnome-firearm then pops nip execute ;

cyoa-blank: common5-gnome-melee-go
	"ESPADAS DE CORPO A CORPO SAO PROIBIDAS PARA SEMPRE! BOA SORTE!  Melee weapons are forbidden forever! Good luck!" rbold text! goodfx
	' .atkspecial foreach-team-char
	team-count-alive for common5-gnome-givefirearm next
;

cyoa-choice: common5-gnome-melee
	"QUEM NECESITA ESPADAS DE CORPO A CORPO Quando VOCE TEM AMIGOS?  Who needs melee weapons when you have friends?" rbold text! ricon
	($ "Escolher isso fara com que " .. thegroup .. " nao e mais capaz de realizar ataques corpo a corpo. Em troca, voce recebera  no longer capable of melee attacks. In exchange, you will get " $) +crtext
	"um fuzil aleatorio para cada membro de seu grupo, com municao!	Possiveis armas: Uzi, Fusao de Assalto, Espingarda Automatica, Espingarda de Balas Rifadas, Minigun  one random high quality firearm for each member of your group, with ammo!	Possible guns: Uzi, Assault Rifle, Automatic Shotgun, Slug Shotgun, Minigun" bbold +text
	common5-gnome-back
	choice( "Aceitar o desafio!" )choice: common5-gnome-melee-go
;

cyoa: common5-gnome-rare
	"Modo Desafio" cyoa-title! char-gen to recruitee ricon recruitee .gnomify2
	($ "'Durante um momento de paz, um gnomo aparece e comeca a gritar.	" .. "'SOU O CNOMEY O GNOMO CONDUTOR DO DESAFIO!" rbold .. "	Ele lhe da a opcao de um desafio extra, em troca de uma " .. "RECOMPENSA FABULOSA!" rainbold $) text!
		choice( "Nao obrigado" )choice: common5-gnome-no
		choice( "Sem Ataques Corpo a Corpo" )choice: common5-gnome-melee
		choice( "No Shooting/Chainsaw" )choice: common5-gnome-shoot
		choice( "Penalidade Maxima de Moral" )choice: common5-gnome-morale
;

' common5-gnome-rare to common5-gnomerepeat

// Cursed Ancient Chest

0 value common5-chest@

cyoa-choice: common5-chest-rare-

;

: common5-chest-weapon
	common5-chest@ case
	0 of "Sabre Pirata" endof
	1 of "Espada Larga Poderosa" endof
	2 of "Cutelo Robusto" endof
	endcase rbold
;

: common5-chest-weaponpick
	common5-chest@ case
		0 of WEAPON_CUTLASS endof
		1 of WEAPON_CLAYMORE endof
		2 of WEAPON_SCLEAVER endof
	endcase cchar
;

cyoa-choice: common5-chest-pick
	($ cname .. "  pega o " .. common5-chest-weapon .. "!	Ele se funde as " .. chis .. "  maos, impedindo o uso de qualquer outra arma!" $) text!
	ctakeweapons 1 cchar .carrying.slot_count!
	common5-chest-weaponpick .weapona! 1 cchar .weapon_nodropa!
	cchar .trait@ "Especialista" $= if
		($ cname .. "  se sente naturalmente familiarizado com esta arma de alguma forma, e tambem se sente um pouco presuncoso." $) +crtext strength++ fitness++ morale++ goodfx
	then; goblin-laugh
;

cyoa-bridge: common5-chest-pick4
	solohuman if
		choosehuman common5-chest-pick
	then;
	($ "Quem deveria receber a " .. common5-chest-weapon .. "?" $) text!
	"AVISO: A arma sera bloqueada para o personagem, e o personagem sera reduzido a um slot de transporte!" rbold +crtext
	' common5-chest-pick cyoa-human-choice
;

cyoa-choice: common5-chest-book
	solohuman if
		choosehuman ($ cname .. "  le o " .. "Dingusnomicon!" rbold $) text!
	else
		($ "Todos se revezam lendo o " .. "Dingusnomicon!" rbold $) text!	
	then
	($ "'E um livro antigo e maligno coberto de olhos e bocas. Ele continua dando pequenas mordidas e tambem grita " .. "''nerd'" fbold .. "  em mil linguas esquecidas diferentes." $) +crtext
	morale--all sting--all bitefx
	"O livro transmite seu CONHECIMENTO PROIBIDO!" bbold +crtext
	' rewardevent-random foreach-human-char
	choosehuman cchar rewardevent-random
;

0 value common5-lootweapon@

: common5-lootweapon-name
	common5-lootweapon@ case
		0 of "Espada de cavaleiro" endof
		1 of "Bardiche" endof
		2 of "Machado Medieval" endof
		3 of "Facao Robusto" endof
		4 of "Espada Ropera" endof
		5 of "Halberd" endof
		6 of "Estrela da Manha" endof
	endcase rbold
;

: common5-lootweapon-pick
	1 common5-lootweapon@ case
		0 of WEAPON_KNIGHT endof
		1 of WEAPON_BARDICHE endof
		2 of WEAPON_MEDIEVAL endof
		3 of WEAPON_MACHETE2 endof
		4 of WEAPON_RAPIER endof
		5 of WEAPON_HALBERD endof
		6 of WEAPON_MORNING endof
	endcase trunk.weapon+
;

cyoa-choice: common5-chest-loot
	0 6 rnd to common5-lootweapon@
	($ Thegroup .. "  encontra um esconderijo de armas muito antigas! Enquanto selecionava lixo inutilizavel, " .. gthey .. " correr" .. g1s .. "  muitos problemas com farpas e pequenas aranhas mordedoras." $) text!
	morale--all sting--all bitefx
	($ gThey .. "  pegar algumas armas uteis, incluindo um " .. common5-lootweapon-name .. "!" $) +crtext
	common5-lootweapon-pick
	1 30 %chance if WEAPON_WOODCLUB else 50 %chance if WEAPON_GAUNTLET else WEAPON_DAGGER then then trunk.weapon+ 1 WEAPON_DAGGER trunk.weapon+ 
;

cyoa-choice: common5-chest-basket
	($ Thegroup .. "  encontrou uma Cesta de Presentes Goblin!" $) text!
	homesearch2 ammosearch2 morale++all
	($ "A cesta de presentes esta levemente amaldicoada! Agora " .. cname .. "  se sente obrigado a comprar um presente para os goblins invisiveis e possivelmente inexistentes!" $) +crtext goblin-laugh
;

: common5-chest-flash
	common5-chest@ case
	0 of "Sabre Pirata" endof // CUTLASS
	1 of "Espada Larga Poderosa" endof // CLAYMORE
	2 of "Cutelo Robusto" endof // SCLEAVER
	endcase fbold
;

cyoa: common5-chest-rare
	"Tesouro Amaldicoado" cyoa-title! someicon
	0 2 rnd to common5-chest@
	($ Thegroup .. "  procura suprimentos em uma cabana assustadora e descobre um porao escondido com um tesouro antigo!" $) text!
	"Aviso: As escolhas vermelhas tem uma penalidade de perda de moral e danos nao fatais! A" rbold +crtext "  piscando " fbold +text "escolha definira o personagem para o qual voce da a arma com um slot bloqueado!" rbold +text
		choice( "Cesta de Presentes Goblin" )choice: common5-chest-basket
		choice( "O Dingusnomicon" rbold )choice: common5-chest-book
		choice( "Armas Perdidas Ha Muito Tempo" rbold )choice: common5-chest-loot
		choice( common5-chest-flash )choice: common5-chest-pick4
;

// Hand Replacement

cyoa-choice: common5-hand-pick
	($ nursename .. "  tenta preparar a sala para a cirurgia." $) text!
	teamnurse .medicalREV
	($ cname .. " perde " .. chis .. "  velha mao chata e recebe um " .. "Mao de Motosserra!" rainbold .. "  Esta motosserra modificada e mais eficiente!" $) +crtext
	ctakeweapons WEAPON_CHAINHAND cchar .weapona! 1 cchar .weapon_nodropa!
	cchar .trait@ "Especialista" $= if
		($ cname .. "  sente se INCRIVELMENTE presuncoso!" rainbold $) +crtext
		2 cchar .carrying.slot_count! morale4+
	then
	teamnurse .cmedical+ teamnurse .medical statroll or if
		"A cirurgia correu bem!" +crtext goodfx
	else
		"A cirurgia foi muito desajeitada." +crtext strength-- sting-- badfx
	then
	1 WEAPON_DSHOTGUN trunk.weapon+ // Putting this here just so that it can be given last in -loot
	choice( "Legal" )choice
;

cyoa-bridge: common5-hand-pick4
	"Para quem voce quer dar uma mao de motosserra?" text!
	' common5-hand-pick cyoa-human-choice
;

cyoa-choice: common5-hand-loot
	($ "Em vez de fazer a cirurgia de substituicao da mao, " .. thegroup .. " ' saquea todos os suprimentos medicos da sala. Ha ate um Spray de Cura!" $) text!
		9 trunk-medical+ 3 WEAPON_HEALSPRAY trunk.weapon+ goodfx
	mechanicest choosechar cmechanic+ if
		($ cname .. "  consegue converter a mao de motosserra em uma motosserra normal utilizavel!" $) +crtext
		mechanicREV 1 WEAPON_CHAINSAW trunk.weapon+ mechanicREV
	else
		"A mao da motosserra foi projetada apenas para uso montado no braco, por isso e deixada para tras." +crtext
	then 1 WEAPON_DSHOTGUN trunk.weapon+
;

cyoa: common5-hand-rare
	"Atualizacao de Mao" cyoa-title!
	medicalest to teamnurse
	($ Thegroup .. " 	Chainsaw hand? That seems like a good idea! " $) text!
		choice( "Pilhar suprimentos em vez de saquear" )choice: common5-hand-loot
	solohuman if
		choosehuman cchar charchoice( "substituir Mao" )choice: common5-hand-pick 
		"Serras eletricas sao melhores que maos." +crtext then;
	"Substituir a mao de alguem por uma motosserra" +crtext
	numhums 3 > if
		' common5-hand-pick 2 #cyoa-human-choice
		choice( "Escolha outra pessoa" )choice: common5-hand-pick4
	else
		' common5-hand-pick cyoa-human-choice
	then
;

} }

// Blank
(*
cyoa-choice: common5--

;

cyoa: common5-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)

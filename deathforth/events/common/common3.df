
cyoa{ road{ 


cyoa-choice: common3-molotov-2
	($ cname .. "  macht 2 Molotowcocktails mit 20 Gas! Sei vorsichtig mit denen!" $) text! 2 WEAPON_MOLOTOV trunk.weapon+ -20 trunk-gas+
;

cyoa-choice: common3-molotov-4
	($ cname .. "  macht 4 Molotowcocktails mit 40 Gas! Genug um ein paar Zombies zu rosten!" $) text! 4 WEAPON_MOLOTOV trunk.weapon+ -40 trunk-gas+
;

cyoa-choice: common3-molotov-6
	($ cname .. "  macht 6 Molotowcocktails mit 60 Gas! Das sollte einen groen Unterschied machen!" $) text! 6 WEAPON_MOLOTOV trunk.weapon+ -60 trunk-gas+
;

cyoa-choice: common3-molotov-bottle
	($ cname .. "  nimmt 6 leere Flaschen mit " .. chim .. ". Sie konnten sich als nutzlich erweisen?" $) text! 6 WEAPON_BOTTLE trunk.weapon+
;

cyoa: common3-molotov
	smartest choosechar cicon "Molotowherstellung" cyoa-title!
	($ Thegroup .. "  findet ein verwustetes Haus mit ein paar Flaschen und zerfetzten Lappen. Es schien Zeitverschwendung zu sein, es zu durchsuchen, aber " .. cname .. "  hat eine Idee:" $) text!
	cchar .wits 2 > if
		($ gThey .. "  konnte Molotows herstellen, wenn " .. gthey .. "  benzin ubrig hat!" $) goodfx
	else
		($ gThey .. "  konnte die Flaschen mitnehmen " .. ghim .. ", um sie auf Zombies und andere Feinde zu werfen!" $) badfx
	then +crtext witsREV
		
		cchar .wits 2 > if
			trunk-gas 20 < if
				"'Leider gibt es nicht genug Benzin ubrig." +crtext
			then
			trunk-gas 19 > if
				choice( "Mache 2 Molotows   20 Gas" )choice: common3-molotov-2
			then
			trunk-gas 39 > if
				choice( "Mache 4 Molotows   40 Gas" )choice: common3-molotov-4
			then
			trunk-gas 59 > if
				choice( "Mache 6 Molotows   60 Gas" )choice: common3-molotov-6
			then
		else
			choice( "Nimm die Flaschen" )choice: common3-molotov-bottle
		then
		choice( "Verlasse das Haus" )choice
;

cyoa-choice: common3-pipe-go
	($ cname .. "  versucht die Bomben fertigzustellen." $) text!
	cmechanic+ if
		($ cHe .. "  beendet sie problemlos und erhalt am Ende 6 Rohrbomben." $) +crtext goodfx 6 WEAPON_PIPEBOMB trunk.weapon+
	else
		mechanicroll if
			($ cHe .. "  schafft es sich nicht " .. chim .. "selbst in die Luft zu jagen, teilweise durch Gluck. " .. cHe .. "  erhalt am Ende 3 Rohrbomben und ruiniert die Halfte davon." $) +crtext 3 WEAPON_PIPEBOMB trunk.weapon+ goodfx
		else
			($ cHe .. " ' fuhlt sich bei solchen Arbeiten nicht wohl und macht einen Fehler. Hoppla!" $) +crtext ouch2- cchar ' .ouch01rnd-- foreach-team-char-except bitefx
		then
	then mechanicREV alive? if mechanic++ then
	
;

cyoa: common3-pipe
	someicon "Bomber Zombie" cyoa-title!
	($ Thegroup .. " ' findet ein verlassenes Versteck. Darin befindet sich ein wenig Munition." $) text!
	ammosearch1
	($ "Bei weiterer Suche werden einige unfertige Rohrbomben im Gebaude gefunden. Jemand musste wohl in Eile verschwinden. " .. "Mit ihnen zu spielen konnte wirklich gefahrlich sein!" rbold $) +crtext
		choice( "Lass sie in Ruhe!" )choice
		mechanicest charchoice( ": Mach sie fertig" )choice: common3-pipe-go
;


cyoa-choice: common3-bridgekeep-plow
	someicon
	($ Thegroup .. "  ignoriert einfach den gepanzerten Mann der auf der Brucke steht und kracht hindurch. " .. "DONNER!" rbold $) text!
	car-plowthrough
;

cyoa-choice: common3-bridgekeep-rage
	($ cname .. "  sturmt direkt auf den Sport Ausrustungs Panzermann zu, packt ihn an der Taille und wirft ihn uber die Brucke. " .. "PLATSCH!" fbold $) text! goodfx rageREV morale++
;

cyoa-bridge: common3-bridgekeep-push
	someicon
	($ Thegroup .. "  steigt aus dem Auto und versucht den Bruckenwachter von der Brucke zu stoen." $) text!
	strength-total fitness-total + 15 > if
		($ "Nach einigem Ringen, " .. thegroup .. "  schafft es ihn von der Brucke zu werfen!" $) +crtext goodfx
	else
		($ "'Es ist zwecklos! Der gepanzerte Mann schlagt einfach " .. thegroup .. "  mit seinen Boxhandschuhen! " $) +crtext
			vehicle.which@ 0= if ($ gThey .. "  laufen" .. g1s .. "  an ihm vorbei nach der Tracht Prugel." $) +text then
		sting--all bitefx
		vehicle.which@ 0 > if choice( "Uberfahren Sie ihn mit dem Auto" )choice: common3-bridgekeep-plow then
		 
	then
	' .strengthREV foreach-team-char ' .fitnessREV foreach-team-char
;

: common3-bridgekeep-choice
	choice( "Schubse ihn von der Brucke" )choice: common3-bridgekeep-push
	vehicle.which@ 0 > if choice( "Durchpflugen mit dem Auto" )choice: common3-bridgekeep-plow then
	most-angrystrong if
		charchoice( "kampfe eins gegen eins gegen ihn!" )choice: common3-bridgekeep-rage
	then
;

cyoa-bridge: common3-bridgekeep-coolit
	cpet? if
		($ cname .. " " .. "furze" pbold .. "  in die allgemeine Richtung des Bruckenschutzes." $)
	else
		($ cname .. "  steht am Rand der Brucke und schreit den Bruckenschutz an um " .. "COOL IT." pbold $)
	then text!
	cool-it++
	($ "'Der gepanzerte Mann starrt weiterhin geradeaus. Es ist nicht klar ob er " .. cname .. "." $) +crtext badfx
	common3-bridgekeep-choice
;

cyoa: common3-bridgekeep
	"Silent Guardian" cyoa-title!
	($ "'Ein Mann in provisorischer Rustung aus Sportbekleidung steht Wache in der Mitte einer schmalen Holzbrucke auf der Strae. Er tragt Boxhandschuhe, starrt das Auto an und schweigt, wahrend er den Weg versperrt." $) text!
		common3-bridgekeep-choice
		most-grating if
			charchoice( ": Sagen Sie dem Wachposten er soll sich beruhigen" )choice: common3-bridgekeep-coolit
		then
;

// Ranting raving ambush

cyoa-choice: common3-ranter-rage
	($ "Anstatt von dem Hinterhalt uberrascht zu werden, " .. cname .. " ' sturzt sich auf den Mann als er schimpft und schlagt ihn nieder. Er bekommt nicht einmal Zeit zu reagieren." $) text! goodfx rageREV morale++
	most-ahole if choosechar
		($ cname .. "  nutzt die Gelegenheit um seine Sachen zu stehlen!" $) +crtext
		homesearch1 ammosearch1 rob-guilt2
	then
;

cyoa-choice: common3-ranter-obliv
	($ cname .. " ' nimmt den schimpfenden Mann uberhaupt nicht wahr. Er wird dadurch so verwirrt dass er ruhig wird und davonzieht." .. cname .. " ' hat die ganze Zeit nicht aufgepasst und wird mehr in " .. chis .. "  eigene kleine Welt hineingezogen." $) text! goodfx witsattREV attitude++ wits--
;

cyoa-bridge: common3-ranter-coolit
	cpet? if
		($ cname .. " ' unterbricht die Schimpftiraden des Mannes indem er standig " .. "FARTING" rbold .. "  wenn er versucht zu sprechen." $)
	else
		($ cname .. " ' unterbricht die Schimpftirade des Mannes um ihm zu sagen dass " .. "'SAG ES, SPRUH ES NICHT!" pbold $)
	then text!
	cool-it-7 if
		($ "'Ha! Du bist in Ordnung!" pbold .. " er bietet an seine Fahigkeiten dem Team zur Verfugung zu stellen!" $) +crtext
		cooled-it goodfx recruit-yesno morale3+all
	else cool-it++
		($ "Oh! Sieht so aus als hatten wir einen Zwischenrufer!" pbold .. " er verprugelt " .. cname .. "  mit einem " .. "VERHEEREND EINSICHTSVOLLE SCHIMPFTIRADE!" fbold .. "  Die Hintern Spreng Schimpftirade erzeugt eine kleine Schockwelle!" $) +crtext
		morale4- ouch-- badfx
	then witsattREV 
;

cyoa-choice: common3-ranter-charm
	($ cname .. "  hort dem schimpfenden Mann zu und versucht, mit ihm zu argumentieren. Der Mann beruhigt sich und erklart, dass er dachte, " $) text! 
	solo if
		($ cname .. "  sei ein Bandit!" $)
	else
		"die Gruppe waren Banditen!"
	then  +text goodfx witsattREV morale++
	most-ahole if choosechar
		($ cname .. "  zeigt ihm dass " .. gtheyare .. "  bandit" gaVSs .. "!" $) +crtext
		homesearch1 ammosearch1 rob-guilt2
	then
;

cyoa-choice: common3-ranter-fight ricon
	($ Thegroup .. " ' bekampft den streitsuchtigen Mann! Es ist sehr schnell vorbei, aber er hat noch etwas Schaden angerichtet, bevor er weggelaufen ist." $) text! bitefx ouch--all
;

cyoa-choice: common3-ranter-run ricon
	($ Thegroup .. "  rennt weg! Der streitsuchtige Mann jagt ihnen eine ganze Weile hinterher und verteilt einige " .. "BRUTZELNDE SCHIMPFWORTER" rbold .. "  bevor " .. thegroup .. "  ihn verliert." $) text! badfx morale2-all
;

cyoa: common3-ranter
	"Schimpfender Mann" cyoa-title!
	char-gen to recruitee "George" recruitee .name!
	26 recruitee .specialtype! ricon
	6 recruitee .wits! 0 recruitee .attitude! 0 recruitee .composure! 6 recruitee .loyalty!
	2 recruitee .morale! 6 recruitee .shooting! 6 recruitee .medical! 6 recruitee .mechanical!
	($ Thegroup .. " ' wird von einem schreienden Mann uberfallen. Er schimpft und tobt! Er sieht nicht besonders gefahrlich aus, aber diese Schimpfworter fangen an zu " .. "brutzeln." pbold $) text!
		most-angrystrong if
			charchoice( ": Hau ihn um" )choice: common3-ranter-rage
		then
		most-grating if
			charchoice( "': SAG ES, SPRUHE ES NICHT" fcoolit )choice: common3-ranter-coolit
		then
		most-oblivious if
			charchoice( ": Ignoriere ihn komplett" )choice: common3-ranter-obliv
		then
		most-charming if
			charchoice( ": Hore ihm zu" )choice: common3-ranter-charm
		then
		choice( "Lauf weg!" )choice: common3-ranter-run
		choice( "Bekampfe ihn!" )choice: common3-ranter-fight
;

// BEE PROBLEMS

: swatsoreats cpet? if "isst" else "klatscht" then ;

cyoa-choice: common3-bee-swat
	($ cname .. " " .. swatsoreats .. "  die Biene! Es lost das Bienenproblem, aber " .. che .. "  wird gestochen!" $) text!
	ouch-- temperREV bitefx
	alive? not if ($ cHis .. "  opfer wird niemals vergessen!" $) +crtext then
	
;

cyoa-choice: common3-bee-coolit
	cpet? if 
		($ cname .. "  gahnt und es klingt ein bisschen wie " .. "COOL IT." pbold $)
	else 
		($ cname .. "  sagt der Biene sie solle " .. "COOL IT." pbold $)
	then text!
	($ "Die Biene fliegt direkt in " .. cname .. "'s Maul! " .. cHe .. "  wird gestochen! " .. "Es tut sehr weh!" rbold $) +crtext
	ouch-- witsattREV bitefx morale3- cool-it++
	alive? not if ($ cHis .. "  opfer wird niemals vergessen!" $) +crtext then
;

cyoa-choice: common3-bee-obliv
	($ cname .. "  ignoriert die Biene vollig! " .. cHe .. " ' hat es irgendwie nicht bemerkt. " $) text!
	notsolo if ($ Everyone .. "  folgt " .. chis .. "  beispiel und ignoriere auch" .. g2s .. "  die Biene." $) +text then
	"Die Biene summt noch Monate nach ihrer Aussetzung um das Auto herum." +crtext goodfx witsattREV
;

cyoa-choice: common3-bee-panic
	($ Thegroup .. "  gerat in Panik! Das Auto gerat auer Kontrolle und pflugt dann in einen Baum." $) text!
	car-plowthrough
	"Irgendwann entkam die Biene durch das Fenster." +crtext
;

cyoa-choice: common3-bee-beeman
	($ cname .. " ' singt dem Bienchen ein beruhigendes Lied.	Das Bienchen fliegt aus dem Fenster.	Ich hatte nicht gedacht, dass ich die Biene jemals aus dem Auto verschwinden sehe, aber es ist wirklich passiert." $) text!
	morale2+all goodfx
;

cyoa: common3-bee
	"Bienen Arger!" cyoa-title! angriest choosechar cicon
	vehicle.which@ 0= if 
		($ "Eine Biene belastigt standig " .. thegroup .. "!		Es dauert ewig, bis es weg ist. Wenn Sie nur ein Auto hatten, waren Sie sicherlich vor Bienen geschutzt!" $) text! morale2-all
	then;
	($ "'Eine Biene fliegt ins Auto! Auch bei geoffnetem Fenster geht sie nicht weg. Sie fliegt immer wieder direkt ins " .. thegroup .. "''s Gesicht" .. gs .. "!	Das konnte die bisher grote Herausforderung sein!" $) text!
		choice( "PANIK!" )choice: common3-bee-panic
		most-grating if
			charchoice( "  sagt der Biene sie soll sich beruhigen" )choice: common3-bee-coolit
		then
		most-oblivious if
			charchoice( "  ignoriert die Biene" )choice: common3-bee-obliv
		then
		angriest charchoice( " " .. swatsoreats .. "  die Biene" )choice: common3-bee-swat
		20 stpick if
			charchoice( "  singt Bienenlied" )choice: common3-bee-beeman
		then
		
;

// Gloomy Weather

: common3-gloom-attcheck args( @ )
	<- @ .attitudeREV
	<- @ .cattitude+ if
		($ <- @ .colourname .. "  behalt eine positive Einstellung bei!" $) +crtext
	then;
	<- @ .cattitude- if
		<- @ .morale2-
	else
		<- @ .morale--
	then	
;

cyoa-choice: common3-gloomy-pick
	($ cname .. "  fangt an zu reden, um die Stimmung zu verbessern." $) text!
	cpet? if
		($ cHe .. "  macht " .. cdogcat .. " ' Gerausche. Es ist eine ziemlich gute Ablenkung vom Wetter!" $) +crtext morale++	goodfx
	then;
	cchar grating? if
		($ cHe .. " sagt " .. everyone .. "  an " .. "  KUHL ES." pbold $) +crtext
		cool-it-7 if
			($ Everyone .. "  lacht herzlich! Dieser Slogan wird nie alt!" $) +text
			morale2+all goodfx
		else cool-it++
			"Dies kommt uberhaupt NICHT gut an." +text morale3-all badfx
		then witsattREV
	then;
	ctemper- if
		($ cHe .. "  verliert " .. chis .. "  temperament und fangt an uber das Wetter zu schimpfen " .. cHe .. "  fuhlt sich besser aber " .. everyone .. "  muss die Schimpftiraden ertragen." $) +crtext badfx
		temperREV morale++ cchar ' .morale-- foreach-team-char-except
	then;
	cattitude+ if
		($ cHe .. "  schafft es abzulenken " .. everyone .. "  vom beschissenen Wetter!" $) +crtext goodfx attitudeREV
	then;
	cattitude- if
		($ cHe .. "  jammert und jammert, sowohl uber das Wetter als auch uber andere nicht verwandte Themen." $) +crtext badfx
		attitudeREV morale2-all
	then;
	"'Es hilft nicht so viel." +crtext badfx
	' common3-gloom-attcheck foreach-team-char
;

cyoa: common3-gloomy
	"Dusteres Wettertrip" cyoa-title! someicon
	($ "'Das Wetter ist grau und trub und es wird wirklich " .. thegroup .. "  herunterkommen. " $) text!
	solo if
		($ cHe .. "  hat niemandem zu klagen uber das Wetter." $) +text
		soloer common3-gloom-attcheck
	else
		"Jemand entscheidet sich, aufzusprechen, um den Laune" +crtext
		' common3-gloomy-pick cyoa-team-choice
	then 0.4 1 frnd weather! -1 1 frnd wind_force!

;

// Fart Mystery


cyoa-choice: common3-fart-blame
	($ cname .. "  zu heben" $) text!
	cpet? if
		($ "Wow! Was fur ein schlechter " .. cdogcat .. "!" $) +crtext morale-- badfx
	then;
	cattitude+ if ($ cHe .. "  schuttet in einer gutenartigen Weise auf." $) +crtext attitudeREV else morale2- then
	" Ist fur den Faulnisri verantwortlich" +crtext
	cchar ' .morale-- foreach-team-char-except badfx
	"Was this the real culprit? Who can say..." +crtext
;

cyoa-choice: common3-fart-coolit
	cpet? if
		($ cname .. "  lasst einen " .. "SEHR DEUTLICHER ZWEITER FURZ." rbold $) text!
		"Geheimnis aufgeklart!" +crtext
	else
		($ cname .. " sagt " .. everyone .. "  an " .. "COOL IT." pbold $) text!
		($ "PERLEN AUS NERVENSCHWEISS" rbold .. "  rollen " .. cname .. "''s Gesicht herunter. Dennoch kann nichts bewiesen werden!" $) +crtext
	then cool-it++ cchar ' .morale3- foreach-team-char-except witsattREV badfx
;

cyoa-choice: common3-fart-smart
	notcchar to victim
	($ cname .. "  erklart eine komplizierte Reihe von Schlussfolgerungen die zu einer Schlussfolgerung fuhren:" $) text!
	cattitude+ if
	($ "Der Furz wurde von einem " .. "GHOST!!! WWWWHHHHOAAAAAAAAA!" fbold $) +crtext witsattREV morale++all goodfx
	then;
	($ "Es war " .. vname .. "  der den Furz gelassen hat!" $) +crtext victim .morale2- badfx
	cloyalty- if
		($ "Insgeheim war es tatsachlich " .. cname .. "  der " .. "furzmonster war!" rbold $) +crtext
		loyaltyREV
	then witsREV morale++
;

cyoa: common3-fart
	"Das Geheimnis des Geisterfurzes" cyoa-title!
	image-bad-smell 33 to cyoaicon
	($ "'Es ist nur ein weiterer Tag auf dem Todesweg als jemand " .. "im Auto furzt." rbold $) text!
	solo if
		"Wer konnte das getan haben? Wir werden es vielleicht nie erfahren." +crtext 1 trunk-gas+ badfx
	then;
	solohuman if choosepet
		($ "Wer konnte das getan haben? Es war wahrscheinlich " .. cname .. ". Was fur eine schlechter " .. cdogcat .. "!" $) +crtext morale-- badfx
	then;
		"Drama und Geheimnis verzehren das Team.		Wahle jemanden den du beschuldigen kannst:" +crtext
		most-grating if
			charchoice( " sagt " .. everyone .. "  soll sich BERUHIGEN" )choice: common3-fart-coolit
		then
		smartest-pass if 
			smartest charchoice( "  lost das Geheimnis" )choice: common3-fart-smart
		then
		' common3-fart-blame cyoa-team-choice
;

// Alligator

: common3-gator-hotwire
	mechanicest choosechar cmechanic+ if cicon
		($ cname .. "  schafft es das Auto zu uberbrucken!" $) +crtext mechanicREV goodfx
	else
		($ "'Ohne einen guten Mechaniker gibt es fur " .. thegroup .. "  keine andere Moglichkeit das Auto zu starten!" $) +crtext
		car-abandon 1 to gator-ate-key
	then
;

cyoa-choice: common3-gator-str
	($ cname .. "  ringt mit dem Alligator um die Schlussel!" $) text!
	strength+roll if
		($ "Der Alligator wird von " .. cname .. "'s roher Starke besiegt! Er spuckt die Schlussel aus." $) +crtext goodfx strength++
	else
		($ "Der Alligator gewinnt und beit " .. cname .. " schlielich ein groes Stuck aus ihm heraus! Er lasst die Schlussel fallen." $) +crtext ouch2- bitefx
	then alive? if strengthREV then
;

cyoa-choice: common3-gator-fit
	($ cname .. "  belastigt den Alligator, indem er ihn jagt und vor ihm davonlauft, in der Hoffnung, dass er die Schlussel fallen lasst." $) text!
	fitness+roll if
		($ "Der Alligator ist mude von " .. cname .. "''s Energie! Die Schlussel gleiten schlielich aus seinem Maul." $) +crtext goodfx fitness++
	else
		($ "Der Alligator sturzt sich hinein und kaut " .. cname .. "  wenn " .. che .. "  wird mude! Der Alligator lasst die Schlussel fallen." $) +crtext ouch2- bitefx
	then alive? if fitnessREV then
;

cyoa-choice: common3-gator-shoot
	($ cname .. "  schiet auf den Alligator!" $) text!
	shooting+roll if
		($ "Der Alligator wird von " .. cname .. "''s gutem Zielen gesprengt! Er lasst die Autoschlussel fallen." $) +crtext goodfx shooting++
		shootingREV
	else
		($ cname .. "  verfehlt den Schuss. Der Alligator schluckt die Schlussel und watschelt davon." $) +crtext
		shootingREV common3-gator-hotwire
	then
;

cyoa-choice: common3-gator-obliv
	($ cname .. " ' geht einfach hin und schnappt sich die Autoschlussel direkt aus dem Maul des Alligators! Kein Problem! Der Alligator hatte diese beilaufige Aktion uberhaupt nicht erwartet. Er ist so uberrascht dass er seine Kiefer nicht zusammenklappte. " .. Thegroup .. "  fahrt mit dem Auto davon!" $) text! goodfx witsattREV
;

cyoa-choice: common3-gator-mech
	someicon
	($ Thegroup .. " ' entscheidet, dass die Autoschlussel es nicht wert sind, sich mit einem extrem gefahrlichen Alligator anzulegen. Der Alligator ist traurig, dass seine Strategie nicht funktioniert, und verliert einen Punkt Moral."  $) text!
	common3-gator-hotwire
;

cyoa: common3-gator
	"Deutsch sein" cyoa-title!
	($ Thegroup .. " ' kann den Autoschlussel nicht finden! Nach einer Weile " .. gthey .. "  stelle" .. g1s .. "  die Schlussel in einem " .. "'aufgesperrten Maul eines Alligators liegen" rbold .. "'. Nachdem die meisten Menschen verschwunden waren, breiteten sich die Alligatoren an der gesamten Ostkuste aus. Es wird gemunkelt, dass die Alligatoren Zombies und Autoschlussel fressen konnten." $) text!
		strongest charchoice( "  ringt mit dem Alligator" )choice: common3-gator-str
		fittest charchoice( "  ermudet den Alligator" )choice: common3-gator-fit
		most-oblivious if
			charchoice( "  schnappt sich einfach die Schlussel" )choice: common3-gator-obliv
		then
		roadtrip-has-firearm? if
			shootest charchoice( "  erschiet den Alligator" )choice: common3-gator-shoot
		then
		choice( "Lass die Autoschlussel im Stich" )choice: common3-gator-mech
;

// Trap Town

cyoa-choice: common3-traptown-pick
	($ cname .. "  beginnt, die Stadt zu durchsuchen und vermeidet dabei Fallen so gut wie moglich " .. chis .. "  fahigkeit." $) text!
	cchar paranoid? if
		($ cHe .. " ' ist extrem paranoid in Bezug auf jeden Schritt und jede Handlung. Es geht langsam voran, viele Fallen gehen los. " .. cname .. "  ist auf jeden einzelnen vorbereitet und vermeidet Verletzungen." $) +crtext goodfx witsattREV mechanic++ ammosearch2 homesearch2
	then;
	cchar oblivious? if
		($ cHe .. " die meisten Fallen treffen. " .. chim .. ". " .. cname .. " uberlebt irgendwie diesen langen Prozess!" $) +crtext bitefx witsattREV sting2- morale2-
		"Es war lehrreich so viele Fallen in Aktion zu sehen!" +crtext
		' .mechanic++ foreach-team-char ammosearch2 homesearch2
	then;
	wits+roll if
		($ cHe .. "  ist vorsichtig wohin " .. che .. "' geht. Viele Fallen werden entdeckt und sicher ausgelost. " .. cname .. "  vermeidet Verletzungen!" $) +crtext goodfx witsREV mechanic++ ammosearch2 homesearch2
	then;
	mechanic+roll if
		($ cHe .. "  benutzt " .. chis .. "  erfahrung mit mechanischen Dingen um herauszufinden wo " .. che .. "  fallen stellen wurde wenn " .. che .. "  eine Art Spinner war. " .. cname .. "  vermeidet oder entscharft auf diese Weise viele Fallen!" $) +crtext goodfx mechanicREV mechanic++ ammosearch2 homesearch2
	then;
		($ cHis .. " ' Fahigkeit Fallen auszuweichen ist nicht so toll. " .. cname .. "  wird bose geschreddert! " $) +crtext
		witsREV mechanicREV ouch2- bitefx
;

cyoa-bridge: common3-traptown-pick4
	someicon
	"Wer sollte die mit Sprengfallen versehene Stadt auskundschaften?" cyoa-text!
	' common3-traptown-pick cyoa-team-choice
;

cyoa: common3-traptown
	"Stadt der Fallen" cyoa-title!
	($ Thegroup .. "  findet eine Strae in einer Stadt die uberall Fallen hat. Gruben mit Decken daruber. Uberall Schlingen. Spikes auf Fassern. Eine Tur mit einer offensichtlichen Guillotine daruber. Und das sind nur die sichtbaren." $) text!
		solo if
			($ "Die Erkundung dieses Gebiets nach Beute konnte " .. "sehr gefahrlich" rbold .. ", vor allem weil " .. soloname .. "  ist allein!" $) +crtext
			soloer charchoice( "  versucht es zu erkunden" )choice: common3-traptown-pick
		else
			"Soll ich einen Spaher losschicken um nach Beute zu suchen?" +crtext
			team-count 3 > if
				' common3-traptown-pick 2 #cyoa-team-choice
				choice( "Wahle jemand anderen" )choice: common3-traptown-pick4
			else
				' common3-traptown-pick 3 #cyoa-team-choice
			then
		then
		choice( "Fahr weg!" )choice
		
;

// Creepy Store

cyoa-choice: common3-creepy-go
	($ cname .. "  betritt den dunklen und gruseligen Laden." $) text!
	cchar paranoid? if
		($ cHis .. "  das ubliche Gefuhl von Paranoia schlagt fehl " .. cHe .. "  bekommt riesige Angst. " .. cname .. "  sieht eine winzige Maus und fangt dann an " .. "zu kreischen wie ein YouTuber" fbold .. "!" $) +crtext morale2- witsattREV sfx{ creepy }
	then;
	cchar oblivious? if
		($ cHe .. " ' bemerkt dort nichts Beangstigendes. Nicht einmal als ein " .. "GRUSELIGES TANZENDES SKELETT" fbold .. "  von der Decke fallt und zu klappern beginnt!" $) +crtext
		witsattREV homesearch2 goodfx
	then;
	temper+roll if
		($ cHe .. "  bleibt ruhig, nimmt die Beute " .. che .. "  findet und kommt da raus!" $) +crtext
		temperREV homesearch2 goodfx
	then;
		($ cHe .. "  verliert " .. chis .. "  ruhig und rennt von dort raus und behauptet, es gabe keine Beute!" $) +crtext
		temperREV morale-- badfx
;

cyoa-choice: common3-creepy-allgo
	someicon ($ Thegroup .. "  betritt den dunklen und gruseligen Laden." $) text!
	angriest-pass if angriest choosechar cicon
		($ cname .. "  verwechselt eine Schaufensterpuppe mit einem Zombie und greift an! Das lasst " .. everyone .. "  ausflippen und auch blindlings angreifen. Am Ende stand keine Schaufensterpuppe mehr." $) +crtext temperREV
		ouch--all bitefx
	else
		"Alle bleiben ziemlich ruhig! Der Laden wird ohne Zwischenfalle geplundert." +crtext goodfx
	then ' .temperREV foreach-team-char homesearch2
;

cyoa-choice: common3-creepy-pick
	"Wer sollte den gruseligen Laden betreten?" text!
	' common3-creepy-go cyoa-team-choice
;

cyoa: common3-creepy
	"Dunkler und gruseliger Laden" cyoa-title! image-female-zombie-1
	($ Thegroup .. " ' findet einen durchnassten Laden. Drinnen sind viele Schaufensterpuppen, die in der Dunkelheit kaum zu sehen sind. Der ganze Laden ist besonders gruselig. Es konnten noch ein paar schimmelige Vorrate drin sein." $) text!
		choice( "Ignoriere diesen Ort" )choice
		solo if
			soloer charchoice( "  geht in den Laden" )choice: common3-creepy-go
		else
			choice( "Schicke jemanden rein" )choice: common3-creepy-pick
			choice( "Schicke alle rein!" )choice: common3-creepy-allgo
		then
;

// Bad Weather Siege

cyoa-choice: common3-badweather-drive
	smartest choosechar
	($ Thegroup .. "  wirft alle Vorsicht uber Bord und beschliet weiterzufahren." $) text!
	cchar .wits 3 skillcheck? if 
		($ cname .. "  anstrengungen " .. chis .. "  augen und schafft es knapp zu vermeiden in etwas zu sturzen!" $) +crtext goodfx witsREV
	else
		car-armor 0 > if
			"Das Auto stot durch einige unsichtbare Hindernisse im Regen. Ein Teil seiner Rustung fallt ab." +cr +text goodfx
			-1 ' car-armor +!
		else
			-2 -3 rnd car-chassis+ badfx
			car-chassis 0 > if
				($ "Das Auto wird durch einige Trummer beschadigt " .. thegroup .. " ' sah es nicht rechtzeitig, aber es lauft noch." $) +crtext
			else
				($ "Das Auto pflugt direkt in die Seite eines ruinierten Autos auf der Strae und wird zerstort. " .. Thegroup .. "  beginnt zu laufen." $) +crtext
			then 
		then
	then
;

cyoa-choice: common3-badweather-fight
	($ Thegroup .. " ' kommt nicht einmal in diesem Haus zur Ruhe bevor sie feststellen dass sich eine groe Horde aus dem stromenden Regen nahert." $) text!
	2.3 15 20 rnd 1 siegecast // 1.7
	"events/siege/region/midopen1.df" $load-region
	choice( "Versuche zu uberleben" )choice: premission
;

cyoa-choice: common3-badweather-camp
	someicon ($ Thegroup .. "  nimmt die weniger befahrene Seitenstrae und findet einen Ort zum Campen bis das Wetter aufklart." $) text! chowday
	"Es dauert einen zusatzlichen Tag bis sich das Wetter so weit verbessert dass die Reise fortgesetzt werden kann. " +crtext
	+cr fullcamp heal-event 1 calendar-day+
;

cyoa: common3-badweather
	"Fahren bei schlechtem Wetter" cyoa-title!
	image-water-driving 73 to cyoaicon
	($ "'Der Regen beginnt zu stromen. Es ist schwer zu sehen, und die Gestalten von Zombies lauern direkt am Rande der Dunkelheit. " .. Thegroup .. " ' ist sich nicht sicher, was schlimmer ware: weiterzumachen oder hier anzuhalten." $) text!
	vehicle.which@ 0 > if choice( "Weiterfahren" )choice: common3-badweather-drive then
	choice( "Sucht Unterschlupf im Haus" )choice: common3-badweather-fight
	fullmeal if
		choice( "Fahrt die Seitenstrae runter, lagert" )choice: common3-badweather-camp
	then 1.4 2 frnd weather! -2 2 frnd wind_force!
;

cyoa-choice: common3-horde-plow
	($ Thegroup .. "  beschliet zu versuchen durch die Horde zu fahren!" $) text! car-plowthrough
;

cyoa-choice: common3-horde-fight
	($ Thegroup .. "  versteckt sich in einem nahe gelegenen Haus um darauf zu warten dass sich die Horde auflost. Die Zombies werden am Ende unruhig und greifen an!" $) text!
	2.4 15 20 rnd 1 siegecast // 1.7
	"events/siege/region/midopen1.df" $load-region
	choice( "Versuche zu uberleben" )choice: premission
;

cyoa: common3-horde
	"Starker Verkehr" cyoa-title!
	image-zombie-hoard-1 9 to cyoaicon
	($ "'Es gibt eine groe Horde Zombies auf der Strae vor uns " .. Thegroup .. " ' ist nicht sicher ob " .. gthey .. "  durch sie durchkommen konnte oder nicht." $) text!
	choice( "Sucht Unterschlupf im Haus" )choice: common3-horde-fight
	vehicle.which@ 0 > if 
		choice( "Einen Umweg nehmen" )choice: car-detour
		choice( "Durchpflugen!" )choice: common3-horde-plow 
	then
;


// Blank
(*
cyoa-choice: common3--

;

cyoa: common3-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)
} }

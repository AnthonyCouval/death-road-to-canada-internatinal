
cyoa{ road{ 


cyoa-choice: common3-molotov-2
	($ cname .. "  faz 2 coqueteis molotov com 20 gas! Tenha cuidado com eles!" $) text! 2 WEAPON_MOLOTOV trunk.weapon+ -20 trunk-gas+
;

cyoa-choice: common3-molotov-4
	($ cname .. "  faz 4 coqueteis molotov com 40 gas! O suficiente para brindar um monte de zumbis!" $) text! 4 WEAPON_MOLOTOV trunk.weapon+ -40 trunk-gas+
;

cyoa-choice: common3-molotov-6
	($ cname .. "  faz 6 coqueteis molotov com 60 gas! Isso deve fazer uma grande diferenca!" $) text! 6 WEAPON_MOLOTOV trunk.weapon+ -60 trunk-gas+
;

cyoa-choice: common3-molotov-bottle
	($ cname .. "  pega 6 garrafas vazias com " .. chim .. ". Elas podem ser uteis?" $) text! 6 WEAPON_BOTTLE trunk.weapon+
;

cyoa: common3-molotov
	smartest choosechar cicon "Fabricacao de Coqueteis Molotov" cyoa-title!
	($ Thegroup .. "  encontra uma casa destruida com algumas garrafas e trapos esfarrapados. Parecia perda de tempo procurar, mas " .. cname .. "  teve uma ideia:" $) text!
	cchar .wits 2 > if
		($ gThey .. "  poderia fazer coqueteis molotov, se " .. gthey .. "  puder poupar a gasolina!" $) goodfx
	else
		($ gThey .. "  poderia levar as garrafas com " .. ghim .. ", para jogar em zumbis e outros inimigos!" $) badfx
	then +crtext witsREV
		
		cchar .wits 2 > if
			trunk-gas 20 < if
				"'Infelizmente, nao ha gasolina suficiente para poupar." +crtext
			then
			trunk-gas 19 > if
				choice( "Fazer 2 coqueteis molotov   20 de gasolina" )choice: common3-molotov-2
			then
			trunk-gas 39 > if
				choice( "Fazer 4 coqueteis molotov   40 de gasolina" )choice: common3-molotov-4
			then
			trunk-gas 59 > if
				choice( "Fazer 6 coqueteis molotov   60 de gasolina" )choice: common3-molotov-6
			then
		else
			choice( "Pegar as garrafas" )choice: common3-molotov-bottle
		then
		choice( "Saia da casa" )choice
;

cyoa-choice: common3-pipe-go
	($ cname .. "  tenta terminar de fazer as bombas." $) text!
	cmechanic+ if
		($ cHe .. "  as termina facilmente e acaba com 6 bombas caseiras." $) +crtext goodfx 6 WEAPON_PIPEBOMB trunk.weapon+
	else
		mechanicroll if
			($ cHe .. "  consegue nao se explodir parcialmente por sorte " .. chim .. " acaba com  bombas caseiras, estragando metade delas " .. cHe .. "  acaba com 3 bombas caseiras, arruinando metade delas." $) +crtext 3 WEAPON_PIPEBOMB trunk.weapon+ goodfx
		else
			($ cHe .. " ' nao esta confortavel o suficiente com um trabalho como este e comete um erro. Opa!" $) +crtext ouch2- cchar ' .ouch01rnd-- foreach-team-char-except bitefx
		then
	then mechanicREV alive? if mechanic++ then
	
;

cyoa: common3-pipe
	someicon "Bombardeiro Zumbi" cyoa-title!
	($ Thegroup .. " ' encontra um esconderijo abandonado. Ha um pouco de municao la dentro." $) text!
	ammosearch1
	($ "Em uma busca mais aprofundada, algumas bombas caseiras inacabadas estao no predio. Alguem deve ter tido que sair com pressa. " .. "Mexer com eles pode ser muito perigoso!" rbold $) +crtext
		choice( "Deixe os em paz!" )choice
		mechanicest charchoice( ": Termine de faze las" )choice: common3-pipe-go
;


cyoa-choice: common3-bridgekeep-plow
	someicon
	($ Thegroup .. "  simplesmente ignora o homem blindado parado na ponte e passa direto. " .. "THUNK!" rbold $) text!
	car-plowthrough
;

cyoa-choice: common3-bridgekeep-rage
	($ cname .. " " .. "SPLASH!" fbold $) text! goodfx rageREV morale++
;

cyoa-bridge: common3-bridgekeep-push
	someicon
	($ Thegroup .. " sai do carro e tenta empurrar o guarda da ponte para fora da ponte." $) text!
	strength-total fitness-total + 15 > if
		($ "Depois de lutar um pouco, " .. thegroup .. " consegue arremessa lo do pontal!" $) +crtext goodfx
	else
		($ "'Nao serve! O homem em bronze so bate " .. thegroup .. "  nele com seus guantes de boxe " $) +crtext
			vehicle.which@ 0= if ($ gThey .. " correr" .. g1s .. " passar por ele apos a derrota." $) +text then
		sting--all bitefx
		vehicle.which@ 0 > if choice( "Corra o sobre com o carro" )choice: common3-bridgekeep-plow then
		 
	then
	' .strengthREV foreach-team-char ' .fitnessREV foreach-team-char
;

: common3-bridgekeep-choice
	choice( "Jogue o do pontal" )choice: common3-bridgekeep-push
	vehicle.which@ 0 > if choice( "Arranque o com o carro" )choice: common3-bridgekeep-plow then
	most-angrystrong if
		charchoice( ": Lutar contra ele um a um!" )choice: common3-bridgekeep-rage
	then
;

cyoa-bridge: common3-bridgekeep-coolit
	cpet? if
		($ cname .. " caminha ate o bordo do pontal, gira se e " .. "farta" pbold .. " na direcao geral do guarda da ponte." $)
	else
		($ cname .. " " .. "SE ACALMAR." pbold $)
	then text!
	cool-it++
	($ "O homem de armadura continua olhando para frente. Nao esta claro se ele ouviu sequer " .. cname .. "." $) +crtext badfx
	common3-bridgekeep-choice
;

cyoa: common3-bridgekeep
	"Guardiao Silencioso" cyoa-title!
	($ "'Um homem com uma armadura improvisada, feita de equipamento atletico, esta de guarda no meio de uma ponte estreita de madeira na estrada. Ele esta usando luvas de boxe, olhando para a frente para o carro, silencioso enquanto bloqueia o caminho." $) text!
		common3-bridgekeep-choice
		most-grating if
			charchoice( ": Diga ao sentinela para SE ACALMAR" )choice: common3-bridgekeep-coolit
		then
;

// Ranting raving ambush

cyoa-choice: common3-ranter-rage
	($ "Em vez de ficar surpreso com a emboscada, " .. cname .. " ' corre para o homem enquanto ele esta reclamando e o nocauteia. Ele nem mesmo tem tempo para reagir." $) text! goodfx rageREV morale++
	most-ahole if choosechar
		($ cname .. "  aproveita a oportunidade para roubar suas coisas!" $) +crtext
		homesearch1 ammosearch1 rob-guilt2
	then
;

cyoa-choice: common3-ranter-obliv
	($ cname .. " ' nao reconhece o homem que reclama. Ele fica tao confuso com isso que fica quieto e vai embora. 	" .. cname .. " ' nao estava prestando atencao o tempo todo, e se envolve mais em " .. chis .. "  proprio mundinho." $) text! goodfx witsattREV attitude++ wits--
;

cyoa-bridge: common3-ranter-coolit
	cpet? if
		($ cname .. " ' interrompe os discursos do homem continuamente " .. "PEIDANDO" rbold .. "  quando ele tenta falar." $)
	else
		($ cname .. " ' interrompe os discursos do homem para dizer a ele para " .. "'DIGA, NAO BORRIFFE!" pbold $)
	then text!
	cool-it-7 if
		($ "'Ha! Voce esta bem!" pbold .. "ele se oferece para emprestar suas habilidades para a equipe!" $) +crtext
		cooled-it goodfx recruit-yesno morale3+all
	else cool-it++
		($ "Oh! Parece que temos um cara vaiando!" pbold .. "		Ele detona " .. cname .. "  com um " .. "DISCURSO DEVASTADORAMENTE INSIGHTFUL!" fbold .. "  O discurso explosivo faz uma pequena onda de choque!" $) +crtext
		morale4- ouch-- badfx
	then witsattREV 
;

cyoa-choice: common3-ranter-charm
	($ cname .. "  ouve o homem furioso e tenta argumentar com ele. O homem se acalma e explica que ele achou que " $) text! 
	solo if
		($ cname .. "  era um bandido!" $)
	else
		"o grupo eram bandidos!"
	then  +text goodfx witsattREV morale++
	most-ahole if choosechar
		($ cname .. "  mostra a ele que " .. gtheyare .. "  bandido" gaVSs .. "!" $) +crtext
		homesearch1 ammosearch1 rob-guilt2
	then
;

cyoa-choice: common3-ranter-fight ricon
	($ Thegroup .. " ' luta contra o homem rabugento! Acabou muito rapido, mas ele acabou causando alguns danos antes de fugir." $) text! bitefx ouch--all
;

cyoa-choice: common3-ranter-run ricon
	($ Thegroup .. "  foge! O homem rabugento os persegue por um bom tempo, entregando alguns " .. "DISCURSOS CHIRRANTES" rbold .. "  antes de " .. thegroup .. "  perde o." $) text! badfx morale2-all
;

cyoa: common3-ranter
	"Homem Zangado" cyoa-title!
	char-gen to recruitee "George" recruitee .name!
	26 recruitee .specialtype! ricon
	6 recruitee .wits! 0 recruitee .attitude! 0 recruitee .composure! 6 recruitee .loyalty!
	2 recruitee .morale! 6 recruitee .shooting! 6 recruitee .medical! 6 recruitee .mechanical!
	($ Thegroup .. " ' e emboscado por um homem gritando. Ele esta reclamando e delirando! Ele nao parece particularmente perigoso, mas esses discursos estao comecando a " .. "chiar." pbold $) text!
		most-angrystrong if
			charchoice( ": Nocauteie o" )choice: common3-ranter-rage
		then
		most-grating if
			charchoice( "': DIGA, NAO BORRIFFE" fcoolit )choice: common3-ranter-coolit
		then
		most-oblivious if
			charchoice( ": Ignore o completamente" )choice: common3-ranter-obliv
		then
		most-charming if
			charchoice( ": Ouca o" )choice: common3-ranter-charm
		then
		choice( "Fuja!" )choice: common3-ranter-run
		choice( "Lute contra ele!" )choice: common3-ranter-fight
;

// BEE PROBLEMS

: swatsoreats cpet? if "come" else "esbofeteia" then ;

cyoa-choice: common3-bee-swat
	($ cname .. " " .. swatsoreats .. "  a abelha! Ele resolve o problema da abelha, mas " .. che .. "  e picado!" $) text!
	ouch-- temperREV bitefx
	alive? not if ($ cHis .. "  sacrificio nunca sera esquecido!" $) +crtext then
	
;

cyoa-choice: common3-bee-coolit
	cpet? if 
		($ cname .. "  boceja e parece " .. "SE ACALMAR." pbold $)
	else 
		($ cname .. "  diz para a abelha " .. "SE ACALMAR." pbold $)
	then text!
	($ "A abelha voa direto para " .. cname .. "' boca! " .. cHe .. "  e picado! " .. "Doi muito!" rbold $) +crtext
	ouch-- witsattREV bitefx morale3- cool-it++
	alive? not if ($ cHis .. "  sacrificio nunca sera esquecido!" $) +crtext then
;

cyoa-choice: common3-bee-obliv
	($ cname .. "  ignora completamente a abelha! " .. cHe .. " ' de alguma forma nao percebeu. " $) text!
	notsolo if ($ Everyone .. "  segue " .. chis .. "  exemplo e tambem ignore" .. g2s .. "  a abelha." $) +text then
	"A abelha ainda esta zumbindo ao redor do carro meses depois de ser abandonado." +crtext goodfx witsattREV
;

cyoa-choice: common3-bee-panic
	($ Thegroup .. "  entra em panico! O carro perde o controle e bate em uma arvore." $) text!
	car-plowthrough
	"Em algum momento, a abelha escapou pela janela." +crtext
;

cyoa-choice: common3-bee-beeman
	($ cname .. " ' canta uma cancao relaxante para a abelha.	A abelha voa para fora da janela.	Eu nao pensei que eu veria a abelha deixar o carro, mas isso realmente aconteceu." $) text!
	morale2+all goodfx
;

cyoa: common3-bee
	"Problema com abelha!" cyoa-title! angriest choosechar cicon
	vehicle.which@ 0= if 
		($ "Uma abelha continua assediando " .. thegroup .. "!		Leva uma eternidade para ela sair. Se voce tivesse um carro, certamente teria sido protegido das abelhas!" $) text! morale2-all
	then;
	($ "'Uma abelha voa para dentro do carro! Mesmo com a janela aberta, ela nao sai. Ela continua voando direto para o rosto de " .. thegroup .. "' rosto de" .. gs .. "!	Este pode ser o maior desafio ate agora!" $) text!
		choice( "PANICO!" )choice: common3-bee-panic
		most-grating if
			charchoice( "  diz para a abelha SE ACALMAR" )choice: common3-bee-coolit
		then
		most-oblivious if
			charchoice( "  ignora a abelha" )choice: common3-bee-obliv
		then
		angriest charchoice( " " .. swatsoreats .. "  a abelha" )choice: common3-bee-swat
		20 stpick if
			charchoice( "  canta cancao da abelha" )choice: common3-bee-beeman
		then
		
;

// Gloomy Weather

: common3-gloom-attcheck args( @ )
	<- @ .attitudeREV
	<- @ .cattitude+ if
		($ <- @ .colourname .. "  mantem uma atitude positiva!" $) +crtext
	then;
	<- @ .cattitude- if
		<- @ .morale2-
	else
		<- @ .morale--
	then	
;

cyoa-choice: common3-gloomy-pick
	($ cname .. "  comeca a falar, para melhorar o clima." $) text!
	cpet? if
		($ cHe .. "  faz " .. cdogcat .. " ' barulhos. E uma distracao suficientemente decente do tempo!" $) +crtext morale++	goodfx
	then;
	cchar grating? if
		($ cHe .. "  conta " .. everyone .. "  para " .. "  CALMA." pbold $) +crtext
		cool-it-7 if
			($ Everyone .. "  ri muito! Esta frase de efeito nunca envelhece!" $) +text
			morale2+all goodfx
		else cool-it++
			"Isso NAO vai bem de jeito nenhum." +text morale3-all badfx
		then witsattREV
	then;
	ctemper- if
		($ cHe .. " perde " .. chis .. "  temperamento e comeca a desabafar sobre o tempo " .. cHe .. "  sente se melhor mas " .. everyone .. "  tem de suportar os discursos." $) +crtext badfx
		temperREV morale++ cchar ' .morale-- foreach-team-char-except
	then;
	cattitude+ if
		($ cHe .. "  consegue distrair " .. everyone .. "  do tempo de merda!" $) +crtext goodfx attitudeREV
	then;
	cattitude- if
		($ cHe .. "  lamenta e lamenta, tanto sobre o tempo como sobre outros topicos nao relacionados." $) +crtext badfx
		attitudeREV morale2-all
	then;
	"'Nao ajuda muito." +crtext badfx
	' common3-gloom-attcheck foreach-team-char
;

cyoa: common3-gloomy
	"Viagem de Tempo Sombrio" cyoa-title! someicon
	($ "'O tempo esta cinzento e sombrio e esta mesmo a ficar " .. thegroup .. "  para baixo. " $) text!
	solo if
		($ cHe .. "  nao tem ninguem para reclamar sobre o tempo." $) +text
		soloer common3-gloom-attcheck
	else
		"Alguem decide falar, para ajudar o clima:" +crtext
		' common3-gloomy-pick cyoa-team-choice
	then 0.4 1 frnd weather! -1 1 frnd wind_force!

;

// Fart Mystery


cyoa-choice: common3-fart-blame
	($ cname .. "  e culpado pelo peido." $) text!
	cpet? if
		($ "Uau! Que " .. cdogcat .. "!" $) +crtext morale-- badfx
	then;
	cattitude+ if ($ cHe .. "  encolhe os ombros de uma forma bem humorada." $) +crtext attitudeREV else morale2- then
	"O cheiro permanece!" +crtext
	cchar ' .morale-- foreach-team-char-except badfx
	"Was this the real culprit? Who can say..." +crtext
;

cyoa-choice: common3-fart-coolit
	cpet? if
		($ cname .. "  emite um " .. "SEGUNDO PEIDO MUITO CLARO." rbold $) text!
		"Misterio resolvido!" +crtext
	else
		($ cname .. "  conta " .. everyone .. "  para " .. "SE ACALMAR." pbold $) text!
		($ "GOTAS DE SUOR NERVOSO" rbold .. "  rolam " .. cname .. "''s rosto. No entanto nada pode ser provado!" $) +crtext
	then cool-it++ cchar ' .morale3- foreach-team-char-except witsattREV badfx
;

cyoa-choice: common3-fart-smart
	notcchar to victim
	($ cname .. "  explica uma sequencia complicada de deducao que chega a uma conclusao:" $) text!
	cattitude+ if
	($ "O peido foi feito por um " .. "GHOST!!! WWWWHHHHOAAAAAAAAA!" fbold $) +crtext witsattREV morale++all goodfx
	then;
	($ "Era o " .. vname .. "  que peidou!" $) +crtext victim .morale2- badfx
	cloyalty- if
		($ "Na verdade, em segredo, era o " .. cname .. "  que era o " .. "monstro peidorreiro!" rbold $) +crtext
		loyaltyREV
	then witsREV morale++
;

cyoa: common3-fart
	"O Misterio do Peido Fantasma" cyoa-title!
	image-bad-smell 33 to cyoaicon
	($ "'E so mais um dia na Estrada da Morte quando alguem " .. "peida no carro." rbold $) text!
	solo if
		"Quem poderia ter feito isso? Talvez nunca saibamos." +crtext 1 trunk-gas+ badfx
	then;
	solohuman if choosepet
		($ "Quem poderia ter feito isso? Provavelmente foi o " .. cname .. ". Que " .. cdogcat .. "!" $) +crtext morale-- badfx
	then;
		"O drama e o misterio consomem a equipe. Escolha alguem para culpar:" +crtext
		most-grating if
			charchoice( "  conta " .. everyone .. "  para SE ACALMAR" )choice: common3-fart-coolit
		then
		smartest-pass if 
			smartest charchoice( "  resolve o misterio" )choice: common3-fart-smart
		then
		' common3-fart-blame cyoa-team-choice
;

// Alligator

: common3-gator-hotwire
	mechanicest choosechar cmechanic+ if cicon
		($ cname .. "  consegue ligar o carro!" $) +crtext mechanicREV goodfx
	else
		($ "'Sem um bom mecanico, nao ha outra maneira de " .. thegroup .. "  ligar o carro!" $) +crtext
		car-abandon 1 to gator-ate-key
	then
;

cyoa-choice: common3-gator-str
	($ cname .. "  luta com o jacare pelas chaves!" $) text!
	strength+roll if
		($ "O jacare e derrotado por " .. cname .. "' forca bruta de ! Ele cospe as chaves." $) +crtext goodfx strength++
	else
		($ "O jacare vence e acaba dando uma grande mordida em " .. cname .. "! Ele deixa cair as chaves." $) +crtext ouch2- bitefx
	then alive? if strengthREV then
;

cyoa-choice: common3-gator-fit
	($ cname .. "  incomoda o jacare perseguindo o e fugindo dele, na esperanca de que ele deixe cair as chaves." $) text!
	fitness+roll if
		($ "O jacare esta cansado de " .. cname .. "' energia! As chaves eventualmente escapam de sua boca." $) +crtext goodfx fitness++
	else
		($ "O jacare se lanca e morde " .. cname .. "  quando " .. che .. "  fica cansado! O jacare deixa cair as chaves." $) +crtext ouch2- bitefx
	then alive? if fitnessREV then
;

cyoa-choice: common3-gator-shoot
	($ cname .. "  atira no jacare!" $) text!
	shooting+roll if
		($ "O jacare e atingido pela " .. cname .. "' boa mira! Ele deixa cair as chaves do carro." $) +crtext goodfx shooting++
		shootingREV
	else
		($ cname .. "  erra o tiro. O jacare engole as chaves e se afasta." $) +crtext
		shootingREV common3-gator-hotwire
	then
;

cyoa-choice: common3-gator-obliv
	($ cname .. " ' simplesmente pega as chaves do carro da boca do jacare! Sem problemas! O jacare nao estava esperando essa acao casual. Ele fica tao surpreso que nao fechou suas mandibulas " .. Thegroup .. "  dirige o carro para longe!" $) text! goodfx witsattREV
;

cyoa-choice: common3-gator-mech
	someicon
	($ Thegroup .. " ' decide que as chaves do carro nao valem a pena mexer com um jacare extremamente perigoso. O jacare fica triste porque sua estrategia nao funciona e perde um ponto de moral."  $) text!
	common3-gator-hotwire
;

cyoa: common3-gator
	"Chaves Perdidas" cyoa-title!
	($ Thegroup .. " ' nao consegue encontrar as chaves do carro! Depois de um tempo, " .. gthey .. "  ve" .. g1s .. "  as chaves dentro da " .. "' boca aberta de um jacare" rbold .. "'. Depois que a maioria dos humanos desapareceu, os jacares comecaram a se espalhar por toda a costa leste. Ha rumores de que os jacares podem comer zumbis e chaves de carro." $) text!
		strongest charchoice( "  luta com o jacare" )choice: common3-gator-str
		fittest charchoice( "  cansa o jacare" )choice: common3-gator-fit
		most-oblivious if
			charchoice( "  pega as chaves" )choice: common3-gator-obliv
		then
		roadtrip-has-firearm? if
			shootest charchoice( "  atira no jacare" )choice: common3-gator-shoot
		then
		choice( "Abandone as chaves do carro" )choice: common3-gator-mech
;

// Trap Town

cyoa-choice: common3-traptown-pick
	($ cname .. "  comeca a vasculhar a cidade, evitando armadilhas da melhor forma possivel " .. chis .. "  habilidade." $) text!
	cchar paranoid? if
		($ cHe .. " ' e extremamente paranoico sobre cada passo e acao. E lento, muitas armadilhas sao ativadas " .. cname .. "  esta pronto para cada uma delas e evita ferimentos." $) +crtext goodfx witsattREV mechanic++ ammosearch2 homesearch2
	then;
	cchar oblivious? if
		($ cHe .. "  passa direto. A maioria das armadilhas atinge " .. chim .. ". " .. cname .. "  de alguma forma sobrevive a esse longo processo!" $) +crtext bitefx witsattREV sting2- morale2-
		"Ver tantas armadilhas em acao foi educativo!" +crtext
		' .mechanic++ foreach-team-char ammosearch2 homesearch2
	then;
	wits+roll if
		($ cHe .. "  e cuidadoso sobre onde " .. che .. "' esta indo. Muitas armadilhas sao detectadas e acionadas com seguranca " .. cname .. "  evita ferimentos!" $) +crtext goodfx witsREV mechanic++ ammosearch2 homesearch2
	then;
	mechanic+roll if
		($ cHe .. "  usa " .. chis .. "  experiencia com coisas mecanicas para descobrir onde " .. che .. "  colocaria armadilhas se " .. che .. "  fosse algum tipo de noz. " .. cname .. "  evita ou desarma muitas armadilhas desta forma!" $) +crtext goodfx mechanicREV mechanic++ ammosearch2 homesearch2
	then;
		($ cHis .. " ' capacidade de evitar armadilhas nao e tao boa. " .. cname .. "  fica muito picado! " $) +crtext
		witsREV mechanicREV ouch2- bitefx
;

cyoa-bridge: common3-traptown-pick4
	someicon
	"Quem deveria explorar a cidade com armadilhas?" cyoa-text!
	' common3-traptown-pick cyoa-team-choice
;

cyoa: common3-traptown
	"Cidade das Armadilhas" cyoa-title!
	($ Thegroup .. "  encontra uma rua em uma cidade que tem armadilhas por toda parte. Pocos com cobertores colocados por cima. Armadilhas por toda parte. Espinhos em barris. Uma porta com uma guilhotina obvia sobre ela. E essas sao apenas as visiveis." $) text!
		solo if
			($ "Explorar esta area em busca de saque pode ser " .. "muito perigoso" rbold .. ", especialmente porque " .. soloname .. "  esta sozinho!" $) +crtext
			soloer charchoice( " tenta explorar" )choice: common3-traptown-pick
		else
			"Envie um escoteiro para explorar por tesouros?" +crtext
			team-count 3 > if
				' common3-traptown-pick 2 #cyoa-team-choice
				choice( "Escolha outra pessoa" )choice: common3-traptown-pick4
			else
				' common3-traptown-pick 3 #cyoa-team-choice
			then
		then
		choice( "Despeje o!" )choice
		
;

// Creepy Store

cyoa-choice: common3-creepy-go
	($ cname .. " entra no armazem escuro e assombroso." $) text!
	cchar paranoid? if
		($ cHis .. " sentimento de paranoia normalmente rebate " .. cHe .. " fica muito espantado. " .. cname .. " ve uma ratozinha e entao comeca a " .. "gritando como um youtuber" fbold .. "!" $) +crtext morale2- witsattREV sfx{ creepy }
	then;
	cchar oblivious? if
		($ cHe .. " 'nao percebe nada assustador la. Mesmo quando um " .. "Esqueleto dancante espelhafato" fbold .. " cai do teto e comeca a balancar!" $) +crtext
		witsattREV homesearch2 goodfx
	then;
	temper+roll if
		($ cHe .. " se mantem calmo, pega o tesouro " .. che .. " acha e sai fora!" $) +crtext
		temperREV homesearch2 goodfx
	then;
		($ cHe .. " perde " .. chis .. " calma e sai de la, alegando que nao havia saque!" $) +crtext
		temperREV morale-- badfx
;

cyoa-choice: common3-creepy-allgo
	someicon ($ Thegroup .. " entra no armazem escuro e assombroso." $) text!
	angriest-pass if angriest choosechar cicon
		($ cname .. " " .. everyone .. " " $) +crtext temperREV
		ouch--all bitefx
	else
		"Todos mantem a calma! A loja e saqueada sem incidentes." +crtext goodfx
	then ' .temperREV foreach-team-char homesearch2
;

cyoa-choice: common3-creepy-pick
	"Quem deve entrar na loja assustadora?" text!
	' common3-creepy-go cyoa-team-choice
;

cyoa: common3-creepy
	"Loja escura e assustadora" cyoa-title! image-female-zombie-1
	($ Thegroup .. " ' encontra uma loja alagada. Ha muitos manequins la dentro, mal visiveis na escuridao. A loja inteira e especialmente assustadora. Pode haver alguns suprimentos mofados la dentro." $) text!
		choice( "Ignore este lugar" )choice
		solo if
			soloer charchoice( "  vai ate a loja" )choice: common3-creepy-go
		else
			choice( "Envie alguem para dentro" )choice: common3-creepy-pick
			choice( "Envie todos para dentro!" )choice: common3-creepy-allgo
		then
;

// Bad Weather Siege

cyoa-choice: common3-badweather-drive
	smartest choosechar
	($ Thegroup .. "  joga a cautela ao vento e decide continuar dirigindo." $) text!
	cchar .wits 3 skillcheck? if 
		($ cname .. "  forca os " .. chis .. "  olhos e consegue evitar por pouco bater em alguma coisa!" $) +crtext goodfx witsREV
	else
		car-armor 0 > if
			"O carro bate em alguns obstaculos invisiveis na chuva. Parte de sua armadura acaba caindo." +cr +text goodfx
			-1 ' car-armor +!
		else
			-2 -3 rnd car-chassis+ badfx
			car-chassis 0 > if
				($ "O carro e danificado ao passar por alguns destrocos " .. thegroup .. " ' nao viu a tempo, mas ainda esta funcionando." $) +crtext
			else
				($ "O carro bate direto na lateral de um carro destruido na estrada e fica destruido. " .. Thegroup .. "  comeca a andar." $) +crtext
			then 
		then
	then
;

cyoa-choice: common3-badweather-fight
	($ Thegroup .. " ' nem mesmo se instala nesta casa antes de perceber que uma grande horda esta se aproximando da chuva torrencial." $) text!
	2.3 15 20 rnd 1 siegecast // 1.7
	"events/siege/region/midopen1.df" $load-region
	choice( "Tente Sobreviver" )choice: premission
;

cyoa-choice: common3-badweather-camp
	someicon ($ Thegroup .. "  pega a estrada secundaria menos movimentada e encontra um lugar para acampar ate que o tempo melhore." $) text! chowday
	"Demora um dia extra para que o tempo melhore o suficiente para continuar a jornada. " +crtext
	+cr fullcamp heal-event 1 calendar-day+
;

cyoa: common3-badweather
	"Dirigindo em Mau Tempo" cyoa-title!
	image-water-driving 73 to cyoaicon
	($ "'A chuva comeca a cair. E dificil ver, e as formas de zumbis espreitam bem na beira da escuridao. " .. Thegroup .. " ' nao tem certeza do que seria pior: continuar ou parar aqui." $) text!
	vehicle.which@ 0 > if choice( "Continue dirigindo" )choice: common3-badweather-drive then
	choice( "Procure abrigo em casa" )choice: common3-badweather-fight
	fullmeal if
		choice( "Va pela estrada secundaria, acampamento" )choice: common3-badweather-camp
	then 1.4 2 frnd weather! -2 2 frnd wind_force!
;

cyoa-choice: common3-horde-plow
	($ Thegroup .. "  decide tentar atravessar a horda!" $) text! car-plowthrough
;

cyoa-choice: common3-horde-fight
	($ Thegroup .. "  se esconde em uma casa proxima para esperar a horda se dispersar. Os zumbis acabam ficando agitados e atacam!" $) text!
	2.4 15 20 rnd 1 siegecast // 1.7
	"events/siege/region/midopen1.df" $load-region
	choice( "Tente Sobreviver" )choice: premission
;

cyoa: common3-horde
	"Trafego Pesado" cyoa-title!
	image-zombie-hoard-1 9 to cyoaicon
	($ "'Ha uma grande horda de zumbis na estrada a frente. " .. Thegroup .. " ' nao tem certeza se " .. gthey .. "  conseguiria passar por eles ou nao." $) text!
	choice( "Procure abrigo em casa" )choice: common3-horde-fight
	vehicle.which@ 0 > if 
		choice( "Fazer um desvio" )choice: car-detour
		choice( "Atravesse!" )choice: common3-horde-plow 
	then
;


// Blank
(*
cyoa-choice: common3--

;

cyoa: common3-
	" " cyoa-title!
	($ " " $) text!
		choice( " " )choice
		choice( " " )choice
;
*)
} }


cyoa{ road{ 

// Twinkie Truck

: common1-twinkie-get
	($ Thegroup .. " ' sammelt die Twinkies ein die nicht alle zertrummert und von Ameisen uberfallen wurden nachdem der Lastwagen umgekippt war. " $) +cr +text 8 trunk-food+ goodfx
;

cyoa-choice: common1-twinkie-mech
	($ cname .. "  fangt an mit dem Schloss herumzuspielen. " $) cyoa-text!
		cmechanic+ if
			($ "'Es ist kein Problem fur " .. chim .. "  sich zu offnen." $) +text common1-twinkie-get
		then;
		mechanicroll if
			"Es dauert etwas, aber schlielich springt der Schlussel auf." +text common1-twinkie-get mechanic++
		else
			($ cHe .. " kommt nicht weiter und gibt auf aus Frustration." $) +text
			badfx morale--
		then mechanicREV
;

cyoa-choice: common1-twinkie-smash
	cpet? if
		($ cname .. " beginnt den Schlussel zu zerbeissern " $) 
	else
		($ cname .. " findet ein groes Stein und schwinger dann hart gegen den Schlussel " $) 
	then cyoa-text!
		cstrength+ if
			($ "WHAAAAAAM!" rbold .. " Der Schlussel bricht " $) +text common1-twinkie-get
		then;
		strengthroll if
			"Der Schlussel bricht!" +text common1-twinkie-get strength++
		else
			cpet? if
				($ cHe .. " verrat sich " .. chis .. " leiht seinen Zahnen gegen den festen Metall!" $)
			else
				($ cHe .. " schlagt es anstatt das Lkw aufzuklappen " .. chis .. " beutelt seine Hand recht schlimm auf statt das Lkw aufzuziehen!" $) 
			then +text bitefx sting-- strength--
		then strengthREV
;

cyoa-choice: common1-twinkie-shoot
	($ cname .. " nimmt Schuss auf den Schlussel am Heck des Lkw " $) cyoa-text!
		cshooting+ if
			"Volltreffer! Der Hebel springt auf " +text common1-twinkie-get
		then;
		shootingroll if
			($ cHe .. " schafft es, den Schuss abzugeben, das Schloss zu zerstoren!" $) +text common1-twinkie-get shooting++
		else
			($ "The shot misses and goes through the back.
			Sparks from the bullet ignite the " .. "EXTREM ENTZUNDLICH" rbold .. "  goo drinnen. Der Lastwagen wird von einer gewaltigen Explosion verzehrt!" $) +text
			bitefx sting--
		then shootingREV
;

cyoa: common1-twinkie
	image-twinkie
	"Twinkie Truckin" cyoa-title!
	($ "'Auf der Strae steht ein Twinkie Lastwagen. Er steht auf dem Kopf, und die Tur zum Heck ist fest verschlossen. " .. Thegroup .. "  leckt sich die Lippen bei dem Gedanken an die Menge an verarbeitetem Maissirup die sich darin befinden muss." $) cyoa-text!
		choice( "Ignoriere es aus gesundheitlichen Grunden" )choice
		mechanicest charchoice( "  versucht es aufzubrechen" )choice: common1-twinkie-mech
		strongest charchoice( "  schlagt mit etwas Schwerem darauf ein" )choice: common1-twinkie-smash
		shootest charchoice( "  schiet auf das Schloss" )choice: common1-twinkie-shoot
;

// Deer Hunter

: common1-deer-eat
	($ Thegroup .. " ' wei nicht viel uber die Konservierung von Wild, aber " .. gthey .. "  kocht" .. g1s .. "  etwas Fleisch um es in das Auto zu packen." $) +cr +text 8 12 rnd trunk-food+ goodfx
;

cyoa-choice: common1-deer-leave
	($ "Der Hirsch rennt schnell davon. Die ruhige Begegnung hebt " .. thegroup .. "''s Stimmung!" $) cyoa-text! morale++all
;

cyoa-choice: common1-deer-runover
	choosehuman
	($ cname .. "  tritt aufs Gas und fahrt mit dem Auto durch den Hirsch." $) cyoa-text! 
	common1-deer-eat car-plowthrough
;

cyoa-choice: common1-deer-shoot
	($ cname .. " schiet auf das Reh" $) cyoa-text!
	vehicle.which@ 0 > if " aus einem der Autofenster. " else ". " then +text
		shooting+roll if
			($ cHe .. "  macht den Schuss" $) +crtext 
			vehicle.which@ 0 > if 
				", das Hirsch erlegt hat, wahrend es auch jegliche Ethik des Jagers verletzt."
			else
				"!"
			then +text
			shooting++ common1-deer-eat
		else
			($ cHe .. "  komplett verfehlt" $) +crtext badfx
		then shootingREV
;

cyoa: common1-deer
	image-deer-in-road
	"Hirschubergang" cyoa-title!
	($ "Ein Hirsch steht in der Mitte der Strae. " $) cyoa-text!
	vehicle.which@ 0 > if "Es schaut auf das Auto." else ($ "Schaut auf " .. thegroup .. "." $) then +text
		choice( "Lass es alleine" )choice: common1-deer-leave
		vehicle.which@ 0 > if
			choice( "Das Hirsch uberfahren" )choice: common1-deer-runover
		then
		roadtrip-has-firearm? if
			shootest charchoice( "  versucht es zu schieen" )choice: common1-deer-shoot
		then
;

// Cauterize or stitch wound

cyoa-choice: common1-stitches-no
	($ cname .. "  entscheidet sich " .. che .. " 'keine Nahte brauchen, und dass es besser ist, das Problem einfach zu ignorieren. " .. cHe .. "  blutet uberall und der Arm heilt nie vollstandig." $) cyoa-text! badfx strength-- sting-- 
;

cyoa-choice: common1-stitches-med
	($ cname .. "  versucht zu nahen " .. vname .. "'s arme Kind" $) cyoa-text!
		medical+roll if
			trunk-medical 1 < if
				($ "Ohne medizinische Versorgung " .. cname .. " macht das Beste " .. che .. " es gibt einige Probleme spater mit einer Infektion." $) +crtext victim .morale--
			else
				($ cHe .. "  sticht den Arm mit minimalen Schmerzen." $) +crtext -1 trunk-medical+ medical++
			then medicalREV
		then;
		($ cHe .. "  verpfuscht die Stiche und verschlimmert die Wunde. Die Blutung hort auf, aber " .. vname .. "' Arm heilt schlecht." $) +crtext badfx victim .strength-- victim .morale-- medicalREV
;

cyoa-choice: common1-stitches-cauter
	($ cname .. "  ahmt etwas nach " .. che .. " ' sah als er den Film 'Rambeux' sah. " .. cHe .. " ' erhitzt ein Messer richtig und legt es dann direkt auf den Schnitt, um die Wunde zu kauterisieren. Es war eine ziemlich schlechte Idee! Der Schmerz ist extrem, obwohl die Blutung aufhort und keine Kraft im Arm verloren geht." $) cyoa-text! badfx morale3-
;

cyoa: common1-stitches
	image-arm-gash
	" Tiefer Schnitt" cyoa-title!
	choosehuman cicon
	cchar to victim
	($ cname .. "  schneidet " .. chim .. " selbst ziemlich schlecht wahrend er in der Postapokalypse herumalbert. " .. cHe .. "  wird wahrscheinlich Stiche brauchen fur " .. chis .. "  arm." $) cyoa-text!
		choice( "Ignore it / Walk it off" )choice: common1-stitches-no
		medicalest charchoice( "  versucht es zu nahen" )choice: common1-stitches-med
		cchar charchoice( "  kauterisiert es" )choice: common1-stitches-cauter
;

// Lonely Gas Station

: common1-gas-get 
	($ Thegroup .. "  sammelt dieses kostbare Gas " $) +crtext 70 110 rnd trunk-gas+ goodfx
;

cyoa-choice: common1-gas-mech
	($ cname .. " versucht die Pumpen zu reparieren. " $) cyoa-text!
		cmechanic+ if
			($ cHe .. " bringt sie im Handumdrehen zum Pumpen." $) +text common1-gas-get
		then;
		mechanicroll if
			($ "Es braucht etwas Fummelei, aber " .. che .. " bringt die Pumpen zum Laufen." $) +text common1-gas-get mechanic++
		else
			($ cHe .. " versucht es, kommt aber nicht weiter." $) +text
			badfx morale--
		then mechanicREV
;

cyoa-choice: common1-gas-climb
	($ cname .. " meldet sich freiwillig in die Tanks zu klettern. " $) cyoa-text!
		cpet? if
			($ cHe .. " bleibt dort stecken und es dauert lange bis er wieder herauskommt. " .. cname .. " taucht vollig mit Benzin durchnasst auf." $) +text
			morale2- sting-- 
			notsolo if
				($ notcname .. " wringt " .. chim .. " aus und sammelt das Benzin!" $)
				+crtext 70 110 rnd trunk-gas+ goodfx
			then
		then;
		fitnessroll if
			($ "'Es gibt eine Menge Kampf und blindes Gluck, aber " .. che .. " quetscht sich in den Tank." $) +text common1-gas-get fitness++
		else
			($ cHe .. " stellt fest dass das Klettern in den Tank vollig erschopfend ist. " .. cname .. " steigt in den Tank, wird aber auf dem Weg verletzt. Rauszukommen ist eine andere Sache." $) +text
			badfx morale-- ouch--
		then fitnessREV
;

cyoa: common1-gas
	image-lonely-gas
	"Lonely Gas Station" cyoa-title!
	($ Thegroup .. " ' findet eine Tankstelle weit abseits der Hauptstrae. Jemand hat den Laden geplundert, und es sind keine Zombies in Sicht. Die Zapfsaulen sind alt und scheinen nicht zu funktionieren." $) cyoa-text!
		choice( "Pumpe in Ruhe lassen" )choice
		mechanicest charchoice( "  versucht die Pumpen zu reparieren" )choice: common1-gas-mech
		dumbest charchoice( "  kriecht in den Tank" )choice: common1-gas-climb
;

// Jon's tooth pain event and revamped version of his radio event

: cardJ4-mcheck 
	medicalroll if
		($ cname .. "  wusste wirklich, was sie taten. Mit den verfugbaren Werkzeugen und medizinischen Hilfsmitteln " .. che .. "  kann den Zahn sauber und mit relativ wenig Schmerzen ziehen. " $) cyoa-text! goodfx
		medical++
	then;
	
		($ cname .. "  hatte am Ende groe Probleme. Die Zahnextraktion war chaotisch, ungeschickt und schmerzhaft. Um es noch schlimmer zu machen, " .. che .. "  zog den falschen Zahn und musste es noch einmal machen.  " $) cyoa-text! bitefx ouch-- morale3-
		
	dead? if
		+cr ($ "Nach dem Ziehen des zweiten Zahnes " .. cname .. "  horte man ausrufen " .. "YEAH SCREW THIS NOISE" pbold .. "  und dann " .. che .. "  starb spontan." $) +text
	then
	
;

cyoa-choice: cardJ4-dentist cardJ4-mcheck medicalREV ;

: cardJ4-mecheck 
	mechanicroll if
		 ($ "Mit dem zusatzlichen Hebel " .. che .. "  knallt die Autotur zu und zieht den Zahn sauber heraus." $) +text goodfx
		mechanic++ 
	then;
	
		($ "Unvorbereitet auf die plotzliche Kraft des Zuschlagens der Tur " .. che .. "  wird zum Auto gezogen und schlagt " .. chis .. "  kopf gegen die Tur. " $) +text bitefx ouch-- morale--
		
	dead? if
		+cr ($ "Dieses genaue Szenario war tatsachlich eine ziemlich verbreitete Todesart vor der Zombie Apokalypse. Diese Unfalle wurden aus den Nachrichten herausgehalten aufgrund von " .. "GROSSE AUTOKONZERNE" rbold .. "." $) +text
	then
;

cyoa-choice: cardJ4-mechanical
	($ cname .. ", inspiriert von alten Zeichentrickserien, beschliet, " .. chis .. "  zahn an die Autotur zu binden." $) cyoa-text! 
		+cr cardJ4-mecheck mechanicREV
;

: cardJ4-scheck 
	cpet? if
		($ cname .. "  versucht, den kaputten Zahn loszuwerden, indem er auf einen Stein beit. Ein paar Zahne fallen heraus und der Schmerz ist unertraglich. Aber zumindest ist auch der schlechte Zahn ausgefallen." $) cyoa-text! badfx
		nonfatal if
			ouch--
		then morale3- attitude--
	then;
	strengthroll if
		($ cname .. "  schnappt sich eine Zange und geht in die Stadt, zieht den Zahn so fest wie moglich. Mit nichts als Willen, den Schmerz zu bekampfen, " .. che .. "  bekommt den Zahn schnell und mit uberraschend wenig Muhe heraus." $) cyoa-text! goodfx
		strength++ 
	then;
		($ cname .. " ' schnappt sich eine Zange und geht in die Stadt. Die Extraktion verlauft nicht gut. Nach viel Schmerz und Blut kommt der Zahn schlielich heraus." $) cyoa-text! bitefx ouch-- morale2-
		
		dead? if
			+cr ($ "Irgendwie " .. cname .. " stirbt an diesem." $) +text
		then
		
;

cyoa-choice: cardJ4-force cardJ4-scheck strengthREV ;

cyoa: cardJ4-test
	"Mein Hut" cyoa-title!
	choosehuman cicon
	($ cname .. " hat fast lahmende Zahnschmerzen. Ohne Zugang zu den Prazisionswerkzeugen die benotigt werden ist die einzige Option den Zahn herauszuziehen. " .. cHe .. " wagt die aktuellen Optionen ab." $) cyoa-text! basic-tone
	
		choice( "Zwing es raus!" )choice: cardJ4-force
		cnotpet? if
			choice( "Ich war mal ein Zahnarzt!" )choice: cardJ4-dentist
			vehicle.which@ 0 > if choice( "Das hab ich mal in einem Cartoon gesehen!" )choice: cardJ4-mechanical then
		then
;

: cardJ2-broke "Der CD Player ist immer noch kaputt und nervt." +crtext badfx morale--all ;

cyoa-choice: cardJ2-fix
	"Repariere den CD Player" cyoa-title!
	chumany? not if
		cpetderp cyoa-text! cardJ2-broke
	then;
	cwits- cattitude- notsolo and and if
		cpet? if
			($ cname .. " 'lasst einen wirklich langen Furz, der uberhaupt nicht von der Musik der feststeckenden CD ablenkt. Es repariert auch nicht den CD Player." $) cyoa-text! badfx morale2-all
		else
				($ cname .. " sagt " .. everyone .. " zu" .. " KUHL ES!" pbold $) cyoa-text!
			cool-it-7 if
				($ cname .. "'nasale Stimme kommt genau zur richtigen Zeit wahrend des sich wiederholenden Songs. Eine neue " .. "HIT HIP HOP SONG" rbold .. " 'wird versehentlich erfunden und steigert die Moral der Gruppe erheblich!" $) +crtext goodfx
				morale3+all
			else cool-it++
				"'Das hilft naturlich uberhaupt nicht." +crtext cardJ2-broke
			then
		then
	then;	
	cmechanic+ if
		($ cname .. " repariert den CD Player mit minimaler Schwierigkeit!" $) cyoa-text! goodfx morale++all
	then;
	ctemper- cstrength+ and if
		($ cname .. " problem gelost!" $) cyoa-text! goodfx morale++all strengthREV
	then;
	($ cname .. " entfernt den CD Player und grubelt " $) cyoa-text! 
	mechanicroll if
		($ cHe .. "  herausfindet, was falsch war, und repariert es!" $) +crtext goodfx mechanic++ morale++all 
	else
		($ cHe .. " ' schafft es nicht es zu reparieren." $) +text cardJ2-broke
	then mechanicREV
;

cyoa-choice: cardJ2-quit
	($ Thegroup .. "  beschliet einfach den CD Player auszuschalten. Das war einfach!" $) cyoa-text!
;

cyoa-bridge: cardJ2-fix4
	someicon
	"Wer sollte versuchen den CD Player zu reparieren?" cyoa-text!
	' cardJ2-fix cyoa-team-choice
;

cyoa: cardJ2-test 
	vehicle.which@ 0 = if 
		image-road-end
		"Zermurbender Marsch" cyoa-title! someicon
		($ Thegroup .. "  geht unerbittlich weiter. Es gibt keine sicheren Verstecke, daher ware es zu gefahrlich, eine Pause einzulegen." $) text! 
		(*	solo if ($ cname .. "  wird sehr mude! " $) else "Alle werden sehr mude! " then +crtext
		($ gThey .. "  wird im nachsten Kampf erschopft sein." $) +text
		nosleep *) morale--all badfx
	then;
	"Audioreparatur" cyoa-title!
	($ Thegroup .. "  ist frustriert, weil der CD Player im Auto verklemmt ist und immer wieder dasselbe Lied abspielt. Wer sollte versuchen, es zu reparieren?" $) cyoa-text! 4 500 tone 
		team-count 3 > if
			' cardJ2-fix 2 #cyoa-team-choice
			choice( "Wahle jemand anderen" )choice: cardJ2-fix4
		else
			' cardJ2-fix 3 #cyoa-team-choice
		then
		choice( "Schalte es einfach aus" )choice: cardJ2-quit
;

// END jon events

// Reading related events

cyoa-choice: magazine1-butts
	"Enttauschung" cyoa-title!
	"" cyoa-text!
	solo not if ($ "Diese seltsame Lesart stort " .. theothers .. ". Trotzdem geht es ihm nichts an und " $) +text then
	cchar dsyp? if
		"Sicherheit bei Fakalien" cyoa-title!
		($ cname .. " kickt sich hinterher und liest Giant Farts Monthly. Es unterrichtet ihn vieles uber sicheres Fakalienflatulieren, und es wird jetzt schwerer fur ihn, die Hose zu verdrehen.," $) +text goodfx
		statmod{ -5 to dsyp-farted }
	else
		($ cname .. " kickt sich hinterher und liest Giant Farts Monthly. Es outet sich jedoch als trockener medizinischer Fachzeitschrift mit irrefuhrendem Namen." $) +text 2 400 tone medical++
	then
		attitude-- cchar morale--all-except
;

cyoa-choice: magazine1-guns
	"American Feuerwaffen" cyoa-title!
	($ "Das Magazin ist voller Waffenbilder, Waffeninformationen, Waffentipps und seltsame hyperpatriotische Rants. " .. cname .. "  absorbiert alle diese Dinge." $) cyoa-text! 1 400 tone
		temper-- shooting++
;

cyoa-choice: magazine1-mech
	"Umschalt  & Ready2Go" cyoa-title!
	($ "Dies war DAS Auto Magazin vor der Gesellschaftsexplosion. Die aufschlureichen Artikel haben ruhigende Wirkung auf " .. cname .. "." $) cyoa-text! 2 300 tone
		temper++ mechanic++
;

cyoa-choice: magazine1-cats
	" katzenvideobilder" cyoa-title!
	"'Das heit, was der Titel sagt Nur Katzenvideobilder. Es war sinnlos in besseren Zeiten, aber nachdem das Internet abgesturzt ist, hat es groe Popularitat erlangt." cyoa-text! goodfx
		cdog? if
			($ cname .. "  ist " .. "VOLLIG DEMORALISIERT" fbold .. " by reading this!
			It fills " .. chim .. " mit Wut!" $) +crtext
			morale2- attitude-- temper-- WEAPON_FIERCE cchar .weapon_default!
		else
			ccat? if
				($ cname .. "  ist " .. "VOLLIG FASZINIERT" fbold .. " by this magazine!
				" .. cHe .. " wie winzig die Katzen von damals waren!" $) +crtext
				morale4+
			else
				morale2+ attitude++
			then 
		then		
;

cyoa: magazine1-cyoa
	"Zeitschriften: Beliebt nach der Apokalypse" cyoa-title!
	choosehuman cicon
	($ Thegroup .. " versucht einen Lebensmittelladen zu plundern, aber er wurde absolut leergeraumt " .. cname-He .. " " .. "	Wahle ein Magazin aus:" $) cyoa-text! basic-tone
		choice( "American Feuerwaffen" )choice: magazine1-guns
		choice( "Umschalt  & Ready2Go" )choice: magazine1-mech
		choice( " katzenvideobilder" )choice: magazine1-cats
		choice( "MONATSZEITSCHRIFT FUR RIESENFURZE" )choice: magazine1-butts
;

// END Magazines: Popular after the Apocalpyse

// Dentist Office

: magazine2-loot morale++ 2 LOOT_FOOD trunk.loot+ 2 3 rnd LOOT_MEDICAL trunk.loot+ ;

cyoa-choice: magazine2-glu
	"Erbauliche Anekdoten" cyoa-title!
	($ cname .. "  erkennt, dass diese Zeitschrift schon immer kitschig war, geniet aber immer noch die optimistischen Geschichten. Eine Geschichte handelt von einer Katze, die ihren Besitzer vor dem Ersticken an einem Buffet rettet. Inspirierend." $) cyoa-text! 2 300 tone attitudeREV attitude++ magazine2-loot
;	
cyoa-choice: magazine2-sci
	"Fragwurdige Wissenschaft" cyoa-title!
	($ cname .. " ' ist pessimistisch gegenuber der Vorhersage der Zeitschrift, dass in ein paar Jahren fliegende Hovercars im Einsatz sein werden, jetzt, da die Zivilisation mehr oder weniger zerstort ist. Es war trotzdem ein ziemlich unterhaltsames Magazin." $) cyoa-text! 1 300 tone attitudeREV attitude-- magazine2-loot
;
cyoa-choice: magazine2-celeb
	"Promi News" cyoa-title!
	($ "Diese Zeitschrift war immer unglaublich gemein, moglicherweise noch mehr, da die meisten Leute darin jetzt aufgefressen sind " .. cname .. "  ist ein etwas schlechterer Mensch nur weil er sie liest." $) cyoa-text! 2 300 tone loyaltyREV loyalty-- witsREV wits-- morale2+ magazine2-loot
;	
cyoa: magazine2-cyoa
	"Zahnarztpraxis" cyoa-title!
	choosehuman cicon
	($ Thegroup .. "  plundert eine Zahnarztpraxis und findet einige ubrig gebliebene medizinische Hilfsmittel und viele technisch essbare Tuben Zahnpasta " .. cname-He .. "  geht in den Warteraum und schnappt sich ein Magazin aus der sparlichen Auswahl." .. "	Wahle ein Magazin aus:" $) cyoa-text! basic-tone
		choice( "Schnulze fur die sensible Seele" )choice: magazine2-glu
		choice( "Fragwurdige Wissenschaft" )choice: magazine2-sci
		choice( "CELEBZ Nachrichten" )choice: magazine2-celeb
;

// END Dentist Office

// Prepper Magazine - Person Pick CYOA if you have people, Instant Result if you don't.

: mag3roll 30 %chance if .medical++ else 50 %chance if .shooting++ else .mechanic++ then then ;
: mag3share cyoa-good-dialog "Jeder reicht die Zeitschrift herum und liest, welche Artikel sie ansprechen. Wie immer bekommt irgendwann jemand Erdnussbutterflecken auf das Magazin, bis zu dem Punkt, an dem es unlesbar wird." cyoa-text! 
	' mag3roll foreach-team-char someone 50 70 rnd LOOT_GAS trunk.loot+ ; 

: mag3solo shooting++ medical++ mechanic++ 50 70 rnd LOOT_GAS trunk.loot+ ;
: mag3result cyoa-good-dialog
	($ "Dies war eine hypothetische, damals aktuelle Sonderausgabe zur Zombie Apokalypse " .. cname .. " studiert die Schusswaffentipps, den Artikel uber Notfallmedizin und ungewohnliche, aber effektive allgemeine Reparaturtricks." $) cyoa-text! cicon mag3solo
;

cyoa-bridge: mag3share-pick 
	someicon
	"Wer sollte das Prepared Preppers Magazine lesen?" cyoa-text!
	' mag3result cyoa-team-choice
;

: magazine3-cyoa
	cyoa-start "Prepared Preppers Magazine" cyoa-title! 
	0 to cyoaicon cyoaicon
	($ Thegroup .. " 'zapft Benzin aus einigen verlassenen Fahrzeugen ab und entdeckt dann eine Zeitschrift in einem der Autos. Es ist eine Ausgabe von Prepared Preppers, die Sonderausgabe, in neuwertigem Zustand " $) cyoa-text!
	solo if 
		soloer choosechar ($ cHe .. " studiert seine jetzt berechtigte Weisheit." $) +text 
		mag3solo goodfx
	then;
	+cr "Wer darf es lesen?" +text basic-tone
	team-count 3 > if
		' mag3result 2 #cyoa-team-choice
		choice( "Wahle jemand anderen" )choice: mag3share-pick 
	else
		' mag3result 3 #cyoa-team-choice
	then
	choice( "Mit allen teilen" )choice: mag3share 
; 

// END Prepper Magazine

// Burnt Library

cyoa-choice: books-are-for-nerds 
	"BUCHER SIND FUR NERDS" cyoa-title!
	cpet? if
		($ cname .. " springt auf, schnappt sich das Buch in " .. chis .. " kiefer und " .. "ZERFETZT ES IN STUCKE" rbold .. " deutsch sein " .. che .. " schuttelt das Buch hin und her. Nimm DAS, Nerd." $) 
	else
		($ cname .. " greift das Buch und " .. "reit es in zwei Halften" rbold .. " wahrend er viel grunzt " .. "'JA DAS DENKE ICH UBER BUCHER." pbold $) 
	then cyoa-text! goodfx
	morale2+ temperREV strengthREV temper-- strength++ cchar morale--all-except
;
: book1-helproll 50 %chance if .attitude++ else .temper++ then ;
: book1-mechroll 50 %chance if .mechanic++ then ;
cyoa-choice: book1-improve
	"Verandere Dein Leben durch Positivitat" cyoa-title!
	($ Thegroup .. " lernt viel uber die Kraft positiv zu bleiben!" $) cyoa-text! 1 300 tone
		attitude++ temper++ morale++ cchar ' book1-helproll foreach-team-char-except
;
cyoa-choice: book1-mechanic
	"Enzyklopadie des Bastelns" cyoa-title!
	"'Dieses Buch enthalt Informationen zur Reparatur oder Modifikation vieler Alltagsgegenstande. Es ist wahrscheinlich auch schwer genug jemanden damit zu toten. Bonus!" cyoa-text! 2 300 tone
		mechanic++ cchar ' book1-mechroll foreach-team-char-except
;
cyoa-choice: book1-morale
	"'Dr. Dinks Reimzeit" cyoa-title!
	($ "Dieses Buch ist voller bunter Bilder und eignet sich hervorragend fur Kinder jeden Alters und " .. thegroup .. " freut sich sehr, etwas Neues zum Lesen zu haben." $) cyoa-text! goodfx morale2+ cchar morale++all-except
;
: book1-swole 
	// cchar angrystrong-revealed? not if then;	cchar angrystrong? if
		most-angrystrong if
			charchoice( ": BUCH WUT" )choice: books-are-for-nerds
	then
;
cyoa: book1-cyoa
	"Verbrannte Bibliothek" cyoa-title!
	choosehuman cicon
	($ Thegroup .. " begegnet einer Bibliothek in einer Stadt die grotenteils abgebrannt zu sein scheint " .. cname-He .. " findet ein einzelnes Buch das nicht wirklich durch das Feuer beschadigt wurde. Was war das Buch?" $) cyoa-text! basic-tone
		book1-swole
		choice( "Gigantisches Reparaturhandbuch" )choice: book1-mechanic
		choice( "Selbsthilfebuch uber Positivitat" )choice: book1-improve
		choice( "'Reimendes Kinderbuch" )choice: book1-morale
;

// END Burnt Library

// END reading

} }


cyoa{ road{ 

// Twinkie Truck

: common1-twinkie-get
	($ Thegroup .. " ' coleta os twinkies que nao foram todos esmagados e invadidos por formigas depois que o caminhao capotou. " $) +cr +text 8 trunk-food+ goodfx
;

cyoa-choice: common1-twinkie-mech
	($ cname .. "  comeca a mexer na fechadura. " $) cyoa-text!
		cmechanic+ if
			($ "'Nao e problema para " .. chim .. "  abrir." $) +text common1-twinkie-get
		then;
		mechanicroll if
			"Demora um pouco, mas eventualmente a fechadura se abre." +text common1-twinkie-get mechanic++
		else
			($ cHe .. "  nao chega a lugar nenhum e desiste frustrado." $) +text
			badfx morale--
		then mechanicREV
;

cyoa-choice: common1-twinkie-smash
	cpet? if
		($ cname .. "  comeca a mastigar a fechadura. " $) 
	else
		($ cname .. "  encontra uma pedra grande e entao balanca forte na fechadura. " $) 
	then cyoa-text!
		cstrength+ if
			($ "WHAAAAAAM!" rbold .. "  A fechadura se despedaca. " $) +text common1-twinkie-get
		then;
		strengthroll if
			"A fechadura quebra!" +text common1-twinkie-get strength++
		else
			cpet? if
				($ cHe .. "  estraga " .. chis .. " dentes se colocam contra o metal solido!" $)
			else
				($ cHe .. " acaba por quebrar " .. chis .. " deixa a mao ficar ruim em vez de abrir o caminhao." $) 
			then +text bitefx sting-- strength--
		then strengthREV
;

cyoa-choice: common1-twinkie-shoot
	($ cname .. " tomando alvo da travessa no fundo do caminhao. " $) cyoa-text!
		cshooting+ if
			"Tiro perfeito! A travessa popa. " +text common1-twinkie-get
		then;
		shootingroll if
			($ cHe .. " consegue fazer o tiro, destruindo a travessa!" $) +text common1-twinkie-get shooting++
		else
			($ "The shot misses and goes through the back.
			Sparks from the bullet ignite the " .. "EXTERMINADAMENTE INFLAVEL" rbold .. " barata dentro. O caminhao e consumido por uma explosao enorme!" $) +text
			bitefx sting--
		then shootingREV
;

cyoa: common1-twinkie
	image-twinkie
	"Twinkie Trucking" cyoa-title!
	($ "'Ha um caminhao de Twinkies na estrada. Ele esta de cabeca para baixo, e a porta do fundo esta travada firmemente. " .. Thegroup .. " saliva sobre o butim de xarope de milho processado que deve estar dentro." $) cyoa-text!
		choice( "Ignoralo por razoes de saude" )choice
		mechanicest charchoice( "  tenta arrombalo" )choice: common1-twinkie-mech
		strongest charchoice( "  esmagao com algo pesado" )choice: common1-twinkie-smash
		shootest charchoice( "  atira na fechadura" )choice: common1-twinkie-shoot
;

// Deer Hunter

: common1-deer-eat
	($ Thegroup .. " ' nao sabe muito sobre preservacao de caca, mas " .. gthey .. "  cozinhar desajeitadamente" .. g1s .. "  alguma carne para colocar no carro." $) +cr +text 8 12 rnd trunk-food+ goodfx
;

cyoa-choice: common1-deer-leave
	($ "O cervo foge rapidamente. O encontro tranquilo levanta " .. thegroup .. "''s espiritos!" $) cyoa-text! morale++all
;

cyoa-choice: common1-deer-runover
	choosehuman
	($ cname .. "  pisa no acelerador e bate o carro no cervo." $) cyoa-text! 
	common1-deer-eat car-plowthrough
;

cyoa-choice: common1-deer-shoot
	($ cname .. "  atira no cervo" $) cyoa-text!
	vehicle.which@ 0 > if "  de uma das janelas do carro. " else ". " then +text
		shooting+roll if
			($ cHe .. "  acerta o tiro" $) +crtext 
			vehicle.which@ 0 > if 
				", ensacando o cervo e tambem indo contra qualquer senso de etica do cacador."
			else
				"!"
			then +text
			shooting++ common1-deer-eat
		else
			($ cHe .. "  erra completamente." $) +crtext badfx
		then shootingREV
;

cyoa: common1-deer
	image-deer-in-road
	"Passagem de Cervos" cyoa-title!
	($ " Um veado parou no meio da estrada. " $) cyoa-text!
	vehicle.which@ 0 > if "Ele olha para o carro." else ($ "Olha para ele " .. thegroup .. "." $) then +text
		choice( "Deixeo sozinho" )choice: common1-deer-leave
		vehicle.which@ 0 > if
			choice( "Corre sobre o veado" )choice: common1-deer-runover
		then
		roadtrip-has-firearm? if
			shootest charchoice( " tenta atingilo com uma arma" )choice: common1-deer-shoot
		then
;

// Cauterize or stitch wound

cyoa-choice: common1-stitches-no
	($ cname .. " decide " .. che .. " 'nao precisa de nenhuma costura, e que e melhor ignorar o problema.', " .. cHe .. " acaba sanguessugo por todos os lados, e o braco nunca se cura totalmente.," $) cyoa-text! badfx strength-- sting-- 
;

cyoa-choice: common1-stitches-med
	($ cname .. " tenta coselar " .. vname .. "'seu braco.'" $) cyoa-text!
		medical+roll if
			trunk-medical 1 < if
				($ "Sem suprimentos medicos, , " .. cname .. " " .. che .. " acaba sofrendo com infeccoes." $) +crtext victim .morale--
			else
				($ cHe .. " 'costura o braco com dor minima.'" $) +crtext -1 trunk-medical+ medical++
			then medicalREV
		then;
		($ cHe .. "  the wound is now infected. " .. vname .. "'s arm heals poorly." $) +crtext badfx victim .strength-- victim .morale-- medicalREV
;

cyoa-choice: common1-stitches-cauter
	($ cname .. "  imita algo " .. che .. "  vi enquanto assistia ao filme 'Rambeux'. " .. cHe .. " 1" $) cyoa-text! badfx morale3-
;

cyoa: common1-stitches
	image-arm-gash
	"Corte Profundo" cyoa-title!
	choosehuman cicon
	cchar to victim
	($ cname .. " cortes " .. chim .. " auto bonito ruim enquanto goofing fora no pos-apocalipse. " .. cHe .. "  provavelmente precisara de pontos para " .. chis .. "  braco." $) cyoa-text!
		choice( "Ignore it / Walk it off" )choice: common1-stitches-no
		medicalest charchoice( "  tenta costurar" )choice: common1-stitches-med
		cchar charchoice( "  cauteriza isso" )choice: common1-stitches-cauter
;

// Lonely Gas Station

: common1-gas-get 
	($ Thegroup .. "  coleta aquele gas precioso. " $) +crtext 70 110 rnd trunk-gas+ goodfx
;

cyoa-choice: common1-gas-mech
	($ cname .. "  tenta consertar as bombas. " $) cyoa-text!
		cmechanic+ if
			($ cHe .. "  comeca a bombear em pouco tempo." $) +text common1-gas-get
		then;
		mechanicroll if
			($ " Leva um pouco de confusao, mas " .. che .. "  consegue fazer as bombas funcionarem." $) +text common1-gas-get mechanic++
		else
			($ cHe .. "  tenta, mas nao chega a lugar nenhum com eles." $) +text
			badfx morale--
		then mechanicREV
;

cyoa-choice: common1-gas-climb
	($ cname .. "  se oferece para subir nos tanques. " $) cyoa-text!
		cpet? if
			($ cHe .. "  fica preso la e leva muito tempo para sair. " .. cname .. "  emerge completamente encharcado em gas." $) +text
			morale2- sting-- 
			notsolo if
				($ notcname .. "  torce " .. chim .. " 'sai e recolhe o gas!'" $)
				+crtext 70 110 rnd trunk-gas+ goodfx
			then
		then;
		fitnessroll if
			($ "'Ha muita luta e sorte cega envolvida, mas' " .. che .. " se contorce para dentro do tanque." $) +text common1-gas-get fitness++
		else
			($ cHe .. "  descobre que subir para o tanque e completamente exaustivo. " .. cname .. " 'entra no tanque, mas se fere no caminho. Sair e outra historia.'" $) +text
			badfx morale-- ouch--
		then fitnessREV
;

cyoa: common1-gas
	image-lonely-gas
	"Posto de Gasolina Solitario" cyoa-title!
	($ Thegroup .. " 1" $) cyoa-text!
		choice( "Deixe as bombas em paz" )choice
		mechanicest charchoice( " tentativas de consertar bombas" )choice: common1-gas-mech
		dumbest charchoice( " entra no tanque" )choice: common1-gas-climb
;

// Jon's tooth pain event and revamped version of his radio event

: cardJ4-mcheck 
	medicalroll if
		($ cname .. " realmente sabiamos o que estamos fazendo. Usando as ferramentas e suprimentos medicos disponiveis, " .. che .. " consegue tirar a dente limpamente e com relativamente pouca dor. " $) cyoa-text! goodfx
		medical++
	then;
	
		($ cname .. " acabou tendo muita dificuldade. A extracao de dente foi suja, desajeitada e dolorosa. Para piorar, " .. che .. " tirou a dente errada e teve que fazer novamente. " $) cyoa-text! bitefx ouch-- morale3-
		
	dead? if
		+cr ($ "Depois de tirar a segunda dente " .. cname .. " foi ouvido gritar " .. "ESSA MERDA QUEBRA" pbold .. " e depois " .. che .. " morreu repentinamente." $) +text
	then
	
;

cyoa-choice: cardJ4-dentist cardJ4-mcheck medicalREV ;

: cardJ4-mecheck 
	mechanicroll if
		 ($ "Usando a forca adicionada, " .. che .. " fecha a porta do carro, tirando limpamente o dente." $) +text goodfx
		mechanic++ 
	then;
	
		($ "Despreparado para a forca subita de bater a porta " .. che .. "  e puxado em direcao ao carro e bate " .. chis .. " " $) +text bitefx ouch-- morale--
		
	dead? if
		+cr ($ "Este cenario exato era na verdade uma maneira muito comum de morrer antes do apocalipse zumbi. Esses acidentes foram mantidos fora das noticias devido a " .. "BIG AUTO CORPORATIONS" rbold .. "." $) +text
	then
;

cyoa-choice: cardJ4-mechanical
	($ cname .. ", usando programas de desenho animado antigos como inspiracao, decide amarrar " .. chis .. " 'dente na porta do carro'" $) cyoa-text! 
		+cr cardJ4-mecheck mechanicREV
;

: cardJ4-scheck 
	cpet? if
		($ cname .. "  Tenta se livrar do dente podre mordendo uma pedra. Alguns dentes caem, e a dor e excruciante. Mas pelo menos o dente ruim caiu tambem." $) cyoa-text! badfx
		nonfatal if
			ouch--
		then morale3- attitude--
	then;
	strengthroll if
		($ cname .. "  pega um alicate e vai para a cidade, puxando o dente com toda a forca possivel. Usando nada alem de vontade para lutar contra a dor, " .. che .. "  tira o dente rapidamente e com surpreendentemente pouco problema." $) cyoa-text! goodfx
		strength++ 
	then;
		($ cname .. " ' pega um alicate e vai para a cidade. A extracao nao vai bem. Depois de muita dor e sangue o dente finalmente sai." $) cyoa-text! bitefx ouch-- morale2-
		
		dead? if
			+cr ($ "De alguma forma " .. cname .. "  morre com isso." $) +text
		then
		
;

cyoa-choice: cardJ4-force cardJ4-scheck strengthREV ;

cyoa: cardJ4-test
	"Ma Hooth" cyoa-title!
	choosehuman cicon
	($ cname .. "  esta tendo uma dor de dente quase paralisante. Sem acesso as ferramentas de precisao necessarias a unica opcao e arrancar o dente. " .. cHe .. "  pondera as opcoes atuais." $) cyoa-text! basic-tone
	
		choice( "Forceo para fora!" )choice: cardJ4-force
		cnotpet? if
			choice( "Eu costumava ser dentista!" )choice: cardJ4-dentist
			vehicle.which@ 0 > if choice( "Eu vi isso uma vez em um desenho animado!" )choice: cardJ4-mechanical then
		then
;

: cardJ2-broke "O CD player permanece quebrado e irritante." +crtext badfx morale--all ;

cyoa-choice: cardJ2-fix
	"Consertando o CD Player" cyoa-title!
	chumany? not if
		cpetderp cyoa-text! cardJ2-broke
	then;
	cwits- cattitude- notsolo and and if
		cpet? if
			($ cname .. " ' da um peido muito longo, o que nao distrai em nada a musica do CD emperrado. Tambem nao conserta o CD player." $) cyoa-text! badfx morale2-all
		else
				($ cname .. "  conta " .. everyone .. " para" .. " CALMA!" pbold $) cyoa-text!
			cool-it-7 if
				($ cname .. "'uma voz nasal acontece exatamente ao tempo certo durante a musica em loop do grupo. Um novo HIT DE HIPHOP e acidentalmente inventado aumentando consideravelmente a moral do grupo! " .. "CANCaO DE HIP-HOP INVENTADA A CASUALIDADE" rbold .. " 'isso, porem, nao ajuda nada na situacao." $) +crtext goodfx
				morale3+all
			else cool-it++
				"'Isso, claro, nao ajuda em nada a situacao.'" +crtext cardJ2-broke
			then
		then
	then;	
	cmechanic+ if
		($ cname .. " consegue reparar o tocador de CD com facilidade!" $) cyoa-text! goodfx morale++all
	then;
	ctemper- cstrength+ and if
		($ cname .. " tira o tocador de CD do painel e depois o joga fora da janela.	Problema resolvido!" $) cyoa-text! goodfx morale++all strengthREV
	then;
	($ cname .. " retira o tocador de CD e se preocupa com como reparalo. " $) cyoa-text! 
	mechanicroll if
		($ cHe .. " consegue descobrir o que estava errado e o repara!" $) +crtext goodfx mechanic++ morale++all 
	else
		($ cHe .. " 'nao consegue reparar." $) +text cardJ2-broke
	then mechanicREV
;

cyoa-choice: cardJ2-quit
	($ Thegroup .. " simplesmente desliga o tocador de CD.	Isso foi facil!" $) cyoa-text!
;

cyoa-bridge: cardJ2-fix4
	someicon
	"Quem deveria tentar reparar o tocador de CD?" cyoa-text!
	' cardJ2-fix cyoa-team-choice
;

cyoa: cardJ2-test 
	vehicle.which@ 0 = if 
		image-road-end
		"Marcha Extenuante" cyoa-title! someicon
		($ Thegroup .. " andar a um passo imperio. Nao ha lugares seguros para se parar, entao parar para descansar seria muito perigoso." $) text! 
		(*	solo if ($ cname .. "  fica muito cansado! " $) else "Todo mundo fica muito cansado! " then +crtext
		($ gThey .. "  ficara exausto durante a proxima luta." $) +text
		nosleep *) morale--all badfx
	then;
	"Reparo de audio" cyoa-title!
	($ Thegroup .. "  fica frustrado porque o CD player do carro esta emperrado, tocando a mesma musica repetidamente. Quem deveria tentar consertalo?" $) cyoa-text! 4 500 tone 
		team-count 3 > if
			' cardJ2-fix 2 #cyoa-team-choice
			choice( "Escolha alguem outro" )choice: cardJ2-fix4
		else
			' cardJ2-fix 3 #cyoa-team-choice
		then
		choice( "Basta desligalo" )choice: cardJ2-quit
;

// END jon events

// Reading related events

cyoa-choice: magazine1-butts
	"Decepcao" cyoa-title!
	"" cyoa-text!
	solo not if ($ "Esta escolha estranha de leitura ofende " .. theothers .. ". Imperturbavel, " $) +text then
	cchar dsyp? if
		"Seguranca de peidos" cyoa-title!
		($ cname .. "  relaxa e le Giant Farts Monthly. Isso lhe ensina muito sobre peidar com seguranca, e agora sera mais dificil para ele fazer coco nas calcas." $) +text goodfx
		statmod{ -5 to dsyp-farted }
	else
		($ cname .. "  relaxa e le Giant Farts Monthly. Acontece que e uma revista medica seca com um nome enganoso." $) +text 2 400 tone medical++
	then
		attitude-- cchar morale--all-except
;

cyoa-choice: magazine1-guns
	"American G*U*N*S" cyoa-title!
	($ "A revista esta cheia de fotos de armas, informacoes sobre armas, dicas sobre armas e discursos hiperpatrioticos bizarros. " .. cname .. " absorve todas essas coisas." $) cyoa-text! 1 400 tone
		temper-- shooting++
;

cyoa-choice: magazine1-mech
	"Pronto para uso" cyoa-title!
	($ "Este era A revista de carros antes que a sociedade explodisse. As artigos perspicazes tem um efeito tranquilizador sobre " .. cname .. "." $) cyoa-text! 2 300 tone
		temper++ mechanic++
;

cyoa-choice: magazine1-cats
	"-videos de gatos imagens-" cyoa-title!
	"'Isso e o que diz o titulo, apenas imagens de videos de gatos. Foi desnecessario em tempos melhores, mas se tornou muito popular apos a internet cair.'," cyoa-text! goodfx
		cdog? if
			($ cname .. "  e " .. "DESMORONADO" fbold .. " by reading this!
			It fills " .. chim .. "  com raiva!" $) +crtext
			morale2- attitude-- temper-- WEAPON_FIERCE cchar .weapon_default!
		else
			ccat? if
				($ cname .. "  e " .. "INTRIGADO" fbold .. " by this magazine!
				" .. cHe .. " pensa como as gatos antigas eram tao pequenas!" $) +crtext
				morale4+
			else
				morale2+ attitude++
			then 
		then		
;

cyoa: magazine1-cyoa
	"Revistas: Popular apos o Apocalipse" cyoa-title!
	choosehuman cicon
	($ Thegroup .. " tenta roubar uma loja de supermercados, mas ela havia sido esvaziada absolutamente., " .. cname-He .. " encontra um estante de revistas e decide pegar uma para ler.," .. "escolha uma revista:" $) cyoa-text! basic-tone
		choice( "American G*U*N*S" )choice: magazine1-guns
		choice( "Pronto para uso" )choice: magazine1-mech
		choice( "-videos de gatos imagens-" )choice: magazine1-cats
		choice( "GIANTES FARTOS ANUALMENTE" )choice: magazine1-butts
;

// END Magazines: Popular after the Apocalpyse

// Dentist Office

: magazine2-loot morale++ 2 LOOT_FOOD trunk.loot+ 2 3 rnd LOOT_MEDICAL trunk.loot+ ;

cyoa-choice: magazine2-glu
	"Anedotas edificantes" cyoa-title!
	($ cname .. "  percebe que esta revista sempre foi piegas, mas ainda gosta das historias otimistas. Uma historia e sobre um gato que salva seu dono de engasgar em um bufe. Inspirador." $) cyoa-text! 2 300 tone attitudeREV attitude++ magazine2-loot
;	
cyoa-choice: magazine2-sci
	"Ciencia questionavel" cyoa-title!
	($ cname .. " ' sentese pessimista sobre a previsao da revista de hovercars voadores em alguns anos, agora que a civilizacao esta mais ou menos destruida. Ainda era uma revista muito divertida." $) cyoa-text! 1 300 tone attitudeREV attitude-- magazine2-loot
;
cyoa-choice: magazine2-celeb
	"Noticias sobre celebridades" cyoa-title!
	($ "Esta revista sempre foi incrivelmente maldosa, possivelmente mais ainda agora que a maioria das pessoas aqui ja foi comida. " .. cname .. "  e uma pessoa um pouco pior por ate mesmo lelo." $) cyoa-text! 2 300 tone loyaltyREV loyalty-- witsREV wits-- morale2+ magazine2-loot
;	
cyoa: magazine2-cyoa
	"Consultorio odontologico" cyoa-title!
	choosehuman cicon
	($ Thegroup .. "  saqueia um consultorio odontologico, encontrando alguns suprimentos medicos restantes e muitos tubos de pasta de dente tecnicamente comestiveis. " .. cname-He .. "  vai para a sala de espera e pega uma revista de sua escassa selecao." .. "escolha uma revista:" $) cyoa-text! basic-tone
		choice( "Glurge for the Sensitive Soul" )choice: magazine2-glu
		choice( "Ciencia questionavel" )choice: magazine2-sci
		choice( "Noticias da CELEBZ" )choice: magazine2-celeb
;

// END Dentist Office

// Prepper Magazine - Person Pick CYOA if you have people, Instant Result if you don't.

: mag3roll 30 %chance if .medical++ else 50 %chance if .shooting++ else .mechanic++ then then ;
: mag3share cyoa-good-dialog "Everyone passes around the magazine, reading what articles appeal to them. Like always, eventually someone gets peanut butter stains all over the mag, to the point where it becomes unreadable." cyoa-text! 
	' mag3roll foreach-team-char someone 50 70 rnd LOOT_GAS trunk.loot+ ; 

: mag3solo shooting++ medical++ mechanic++ 50 70 rnd LOOT_GAS trunk.loot+ ;
: mag3result cyoa-good-dialog
	($ "Isso foi uma edicao hipotetica, aquela altura, sobre um apocalipse de zumbis. " .. cname .. "  Estuda de perto as dicas de armas de fogo, artigo de medicina de emergencia e truques de reparo comuns incomuns, mas eficazes." $) cyoa-text! cicon mag3solo
;

cyoa-bridge: mag3share-pick 
	someicon
	"Quem deveria ler a revista Preppared Preppers?" cyoa-text!
	' mag3result cyoa-team-choice
;

: magazine3-cyoa
	cyoa-start "Revista Preparados Preparados" cyoa-title! 
	0 to cyoaicon cyoaicon
	($ Thegroup .. "  Sifona gasolina de alguns veiculos abandonados e entao avista uma revista em um dos carros. E uma edicao especial de Preparados Preppers, em perfeitas condicoes! " $) cyoa-text!
	solo if 
		soloer choosechar ($ cHe .. " estuda agora sua sabedoria justificada." $) +text 
		mag3solo goodfx
	then;
	+cr "Quem vai ler isso?" +text basic-tone
	team-count 3 > if
		' mag3result 2 #cyoa-team-choice
		choice( "Escolha alguem outro" )choice: mag3share-pick 
	else
		' mag3result 3 #cyoa-team-choice
	then
	choice( "Compartilhe com todos" )choice: mag3share 
; 

// END Prepper Magazine

// Burnt Library

cyoa-choice: books-are-for-nerds 
	"LIVROS SaO PARA NERDS" cyoa-title!
	cpet? if
		($ cname .. " pula, pega o livro e " .. chis .. " abre a boca e " .. "QUEBRALO EM PECAS" rbold .. " como " .. che .. " agita o livro de lado a lado. Faca isso, nerd." $) 
	else
		($ cname .. " pega o livro e " .. "quebrao em meio" rbold .. " enquanto grunhida muito. " .. "'		ISSO E O QUE PENSAM SOBRE LIVROS." pbold $) 
	then cyoa-text! goodfx
	morale2+ temperREV strengthREV temper-- strength++ cchar morale--all-except
;
: book1-helproll 50 %chance if .attitude++ else .temper++ then ;
: book1-mechroll 50 %chance if .mechanic++ then ;
cyoa-choice: book1-improve
	"Mude Sua Vida atraves da Positividade" cyoa-title!
	($ Thegroup .. " aprende muito" $) cyoa-text! 1 300 tone
		attitude++ temper++ morale++ cchar ' book1-helproll foreach-team-char-except
;
cyoa-choice: book1-mechanic
	"Enciclopedia do Gambiarra" cyoa-title!
	"'Este livro esta cheio de informacoes sobre reparar ou modificar muitas coisas comuns a cada dia. Ele provavelmente e pesado o suficiente para matar alguem com isso. Bonus!" cyoa-text! 2 300 tone
		mechanic++ cchar ' book1-mechroll foreach-team-char-except
;
cyoa-choice: book1-morale
	"'Dr. Dink no Tempo do Ritmo" cyoa-title!
	($ "Este livro e cheio de imagens coloridas e e otimo para criancas de todas as idades. " .. thegroup .. " 'realmente gosta de ter algo, qualquer coisa, nova para ler.'" $) cyoa-text! goodfx morale2+ cchar morale++all-except
;
: book1-swole 
	// cchar angrystrong-revealed? not if then;	cchar angrystrong? if
		most-angrystrong if
			charchoice( "rAIVA DE LIVRO" )choice: books-are-for-nerds
	then
;
cyoa: book1-cyoa
	"Biblioteca Queimada" cyoa-title!
	choosehuman cicon
	($ Thegroup .. "  encontra uma biblioteca em uma cidade que parece ter sido em grande parte queimada. " .. cname-He .. " 'The Bible'" $) cyoa-text! basic-tone
		book1-swole
		choice( "Manual de reparacao gigantesco" )choice: book1-mechanic
		choice( "Livro autoral de autoajuda sobre positividade" )choice: book1-improve
		choice( "'Livro infantil em verso" )choice: book1-morale
;

// END Burnt Library

// END reading

} }

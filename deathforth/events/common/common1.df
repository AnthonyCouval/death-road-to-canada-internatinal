
cyoa{ road{ 

// Twinkie Truck

: common1-twinkie-get
	($ Thegroup .. " recupere les twinkies qui n'etaient pas tous ecrases et saccages par des fourmis apres que le camion a renverse. " $) +cr +text 8 trunk-food+ goodfx
;

cyoa-choice: common1-twinkie-mech
	($ cname .. " il commence a travailler sur le verrou. " $) cyoa-text!
		cmechanic+ if
			($ "C'est tout a fait possible pour moi. " .. chim .. " ouvrez-le pour le faire fonctionner." $) +text common1-twinkie-get
		then;
		mechanicroll if
			"Cela prend un peu de temps, mais le verrou finit par s'ouvrir." +text common1-twinkie-get mechanic++
		else
			($ cHe .. " il ne parvient pas a avancer et se rend compte qu'il est incapable de continuer." $) +text
			badfx morale--
		then mechanicREV
;

cyoa-choice: common1-twinkie-smash
	cpet? if
		($ cname .. " mord a la serrure. " $) 
	else
		($ cname .. " trouve une grande pierre et frappe durement sur le verrou. " $) 
	then cyoa-text!
		cstrength+ if
			($ "WHAAAAAAM!" rbold .. " La serrure se brise. " $) +text common1-twinkie-get
		then;
		strengthroll if
			"La serrure cede !" +text common1-twinkie-get strength++
		else
			cpet? if
				($ cHe .. " gache les choses " .. chis .. " les dents contre l'acier dur!" $)
			else
				($ cHe .. " finit par ecraser " .. chis .. " leve les mains mal avec peine au lieu d'ouvrir le camion !" $) 
			then +text bitefx sting-- strength--
		then strengthREV
;

cyoa-choice: common1-twinkie-shoot
	($ cname .. " visadelle verrou en arriere du camion. " $) cyoa-text!
		cshooting+ if
			"Une photo parfaite ! le relache s'ouvre. " +text common1-twinkie-get
		then;
		shootingroll if
			($ cHe .. " parvient a faire le tir, detruisant le verrou !" $) +text common1-twinkie-get shooting++
		else
			($ "The shot misses and goes through the back.
			Sparks from the bullet ignite the " .. "TRES INFLAMMABLE" rbold .. " aller interieurement. le camion est devore par une immense explosion !" $) +text
			bitefx sting--
		then shootingREV
;

cyoa: common1-twinkie
	image-twinkie
	"Camion de twinkies en route" cyoa-title!
	($ "Il y a une camionnette twinkie sur la route. elle est renversee et la porte arriere est verrouillee etanchement. " .. Thegroup .. " salue abondamment le gateau de sirop de mais traite qui doit se trouver dedans." $) cyoa-text!
		choice( "Ignorer pour des raisons de sante" )choice
		mechanicest charchoice( " inserer l'effet de teleportation ici" )choice: common1-twinkie-mech
		strongest charchoice( " il le brise avec quelque chose lourd" )choice: common1-twinkie-smash
		shootest charchoice( " fonce sur le verrou" )choice: common1-twinkie-shoot
;

// Deer Hunter

: common1-deer-eat
	($ Thegroup .. " n'en sait pas beaucoup concernant la conservation de jeu. " .. gthey .. " preparer maladroitivement" .. g1s .. " des viandes a emballer dans la voiture." $) +cr +text 8 12 rnd trunk-food+ goodfx
;

cyoa-choice: common1-deer-leave
	($ "Le cerf s'enfuit rapidement. la rencontre silencieuse le fait fuir. " .. thegroup .. "mon dieu !" $) cyoa-text! morale++all
;

cyoa-choice: common1-deer-runover
	choosehuman
	($ cname .. " accelere brutalement et percute le cerf avec la voiture." $) cyoa-text! 
	common1-deer-eat car-plowthrough
;

cyoa-choice: common1-deer-shoot
	($ cname .. " prend une potache au cerf" $) cyoa-text!
	vehicle.which@ 0 > if " depuis une des fenetres d'une voiture. " else ". " then +text
		shooting+roll if
			($ cHe .. " fait le tir" $) +crtext 
			vehicle.which@ 0 > if 
				"capturer une faon en depit de toute ethique de chasseur."
			else
				"!"
			then +text
			shooting++ common1-deer-eat
		else
			($ cHe .. " rate totalement rate." $) +crtext badfx
		then shootingREV
;

cyoa: common1-deer
	image-deer-in-road
	"Passage de mouflons" cyoa-title!
	($ "Un cerf s'est arrete au milieu de la route. " $) cyoa-text!
	vehicle.which@ 0 > if "Il regarde la voiture." else ($ "C'elabore sur toi. " .. thegroup .. "." $) then +text
		choice( "Laissez-le tranquille" )choice: common1-deer-leave
		vehicle.which@ 0 > if
			choice( "Courir sur une faon" )choice: common1-deer-runover
		then
		roadtrip-has-firearm? if
			shootest charchoice( " essaye de le tirer." )choice: common1-deer-shoot
		then
;

// Cauterize or stitch wound

cyoa-choice: common1-stitches-no
	($ cname .. " decide " .. che .. " il n'a pas besoin de points de couture et il est preferable de ne rien faire face au probleme. " .. cHe .. " sa blessure saigne partout et son bras ne guerit jamais completement." $) cyoa-text! badfx strength-- sting-- 
;

cyoa-choice: common1-stitches-med
	($ cname .. " essaie de le suturer " .. vname .. "musee" $) cyoa-text!
		medical+roll if
			trunk-medical 1 < if
				($ "Sans approvisionnement en matiere de fournitures medicales, " .. cname .. " faites le meilleur. " .. che .. " il y a des problemes plus tard avec l'infection." $) +crtext victim .morale--
			else
				($ cHe .. " fait des pointes sur le bras avec un minimum de douleur." $) +crtext -1 trunk-medical+ medical++
			then medicalREV
		then;
		($ cHe .. " la blessure se termine par envenimer les points de couture et aggraver la plaie. l'hemorragie s'arrete, mais " .. vname .. "son bras guerit mal." $) +crtext badfx victim .strength-- victim .morale-- medicalREV
;

cyoa-choice: common1-stitches-cauter
	($ cname .. " imite quelque chose " .. che .. " j'ai vu la scene dans le film 'rambeaux'. " .. cHe .. " idee maladroite : chauffer un couteau jusqu'a ce qu'il soit reellement chaud et l'appliquer sur la blessure pour essayer de la coaguler... cela etait tres mal! bien que le bourrissage soit efficace et arrete la saignee, la douleur est extreme." $) cyoa-text! badfx morale3-
;

cyoa: common1-stitches
	image-arm-gash
	"Coupe profonde" cyoa-title!
	choosehuman cicon
	cchar to victim
	($ cname .. " coupe " .. chim .. "sois tres mauvais quand on se detend dans l'apres-apocalypses. " .. cHe .. " aura probablement besoin de sutures " .. chis .. " armez-vous." $) cyoa-text!
		choice( "Ignore it / Walk it off" )choice: common1-stitches-no
		medicalest charchoice( " essaie de la reparer en la collant ensemble." )choice: common1-stitches-med
		cchar charchoice( " guerit ce lui par une cauterisation." )choice: common1-stitches-cauter
;

// Lonely Gas Station

: common1-gas-get 
	($ Thegroup .. " recueillez cette precieuse gaz avec soin. " $) +crtext 70 110 rnd trunk-gas+ goodfx
;

cyoa-choice: common1-gas-mech
	($ cname .. " essaie de reparer les pompes. " $) cyoa-text!
		cmechanic+ if
			($ cHe .. " mettront le rythme en few minutes." $) +text common1-gas-get
		then;
		mechanicroll if
			($ "C'est un peu genant, mais " .. che .. " active les pompes." $) +text common1-gas-get mechanic++
		else
			($ cHe .. " essaie, mais ne parvient pas avec eux." $) +text
			badfx morale--
		then mechanicREV
;

cyoa-choice: common1-gas-climb
	($ cname .. " instructions : monter dans les reservoirs. " $) cyoa-text!
		cpet? if
			($ cHe .. " s'y retrouve bloque et cela prend du temps pour en sortir. " .. cname .. " emerge totalement imbibe de gaz." $) +text
			morale2- sting-- 
			notsolo if
				($ notcname .. " s'enroule " .. chim .. " sortez et ramassez le gaz !" $)
				+crtext 70 110 rnd trunk-gas+ goodfx
			then
		then;
		fitnessroll if
			($ "Il y a beaucoup de peine et de malchance involvees. " .. che .. " se fait echouer dans le bassin." $) +text common1-gas-get fitness++
		else
			($ cHe .. " montre que la montee dans le reservoir est totalement epuisante. " .. cname .. " montrant son corps blesse, il entre dans le reservoir, mais se blesse en y entrant. sortir en est une autre affaire." $) +text
			badfx morale-- ouch--
		then fitnessREV
;

cyoa: common1-gas
	image-lonely-gas
	"Gare service isolee" cyoa-title!
	($ Thegroup .. " une station-service eloignee de la route principale. quelqu'un a vole le magasin, pas de zombis en vue. ... les pompes a essence sont anciennes et ne semblent pas fonctionner." $) cyoa-text!
		choice( "Laissez les pompes tranquilles" )choice
		mechanicest charchoice( " essaie de reparer les pompes." )choice: common1-gas-mech
		dumbest charchoice( " se fait introduire dans le reservoir." )choice: common1-gas-climb
;

// Jon's tooth pain event and revamped version of his radio event

: cardJ4-mcheck 
	medicalroll if
		($ cname .. " ils avaient reellement la maitrise de leurs taches. en utilisant les outils et les fournitures medicales a leur disposition, ils ont pu faire preuve d'une grande efficacite. " .. che .. " puisable de sortir nettement et avec un peu de douleur relatif la dents. " $) cyoa-text! goodfx
		medical++
	then;
	
		($ cname .. " eut des ennuis a grande echelle. l'extraction dentaire s'est averee geneuse, maladroite et douloureuse. pour aggraver les choses, " .. che .. " a eu mauvais raison de tirer la mauvaise dent et j'en ai du refaire une nouvelle. " $) cyoa-text! bitefx ouch-- morale3-
		
	dead? if
		+cr ($ "Apres avoir tire la deuxieme dent. " .. cname .. " a dit : a ete entendu hurler " .. "C'EST LA MERDE, JE VAIS PRENDRE CELA A LA GUEULE." pbold .. " et puis... " .. che .. " est decede de maniere spontanee." $) +text
	then
	
;

cyoa-choice: cardJ4-dentist cardJ4-mcheck medicalREV ;

: cardJ4-mecheck 
	mechanicroll if
		 ($ "En utilisant l'avantage supplementaire : " .. che .. " ferme violemment la porte de la voiture, extrayant nettement la dents." $) +text goodfx
		mechanic++ 
	then;
	
		($ "Sans preparation pour la brusque poussee de la porte qui s'est fermee brutalement, " .. che .. " est tire vers la voiture et heurte " .. chis .. " tete contre la porte. " $) +text bitefx ouch-- morale--
		
	dead? if
		+cr ($ "Ceci etait effectivement une situation courante avant l'apocalypse zombie pour deceder. ces accidents n'ont jamais fait la une a cause de : " .. "LES GRANDES ENTREPRISES AUTOMOBILES" rbold .. "." $) +text
	then
;

cyoa-choice: cardJ4-mechanical
	($ cname .. "un knot de television en se inspirant de dessins animes anciens pour lier. " .. chis .. " la dent sur la porte de voiture." $) cyoa-text! 
		+cr cardJ4-mecheck mechanicREV
;

: cardJ4-scheck 
	cpet? if
		($ cname .. " essaye de se debarrasser de la dent cassee en machonnant sur une pierre. quelques dents tombent a leur tour, et la douleur est extremement forte. mais au moins la dent mauvaise est egalement sortie." $) cyoa-text! badfx
		nonfatal if
			ouch--
		then morale3- attitude--
	then;
	strengthroll if
		($ cname .. " s'empare d'une paire de pince et va de l'avant, tirant sur la dent avec toute sa force. en utilisant seulement son volonte pour se battre contre la douleur. " .. che .. " retire la dent rapidement et avec une facilite etonnante." $) cyoa-text! goodfx
		strength++ 
	then;
		($ cname .. " extraction d'un dents avec des pincettes, mais le processus est difficile. il y a beaucoup de douleur et de saignement avant que le dent ne sorte finalement." $) cyoa-text! bitefx ouch-- morale2-
		
		dead? if
			+cr ($ "Quelque facon " .. cname .. " meurt de cela." $) +text
		then
		
;

cyoa-choice: cardJ4-force cardJ4-scheck strengthREV ;

cyoa: cardJ4-test
	"Ma Hooth" cyoa-title!
	choosehuman cicon
	($ cname .. " la personne souffre d'une douleur dentaire presque paralysante. l'option la plus viable sans l'acces aux outils de precision necessaires est de tirer la molaire. " .. cHe .. " pese en equilibre les options actuelles." $) cyoa-text! basic-tone
	
		choice( "Forcer cela hors!" )choice: cardJ4-force
		cnotpet? if
			choice( "Soins de sante appliques sur char" )choice: cardJ4-dentist
			vehicle.which@ 0 > if choice( "Je l'ai vu une fois dans un dessin anime !" )choice: cardJ4-mechanical then
		then
;

: cardJ2-broke "Le lecteur de cd reste casse et genant." +crtext badfx morale--all ;

cyoa-choice: cardJ2-fix
	"Reparation du lecteur de cd" cyoa-title!
	chumany? not if
		cpetderp cyoa-text! cardJ2-broke
	then;
	cwits- cattitude- notsolo and and if
		cpet? if
			($ cname .. " une personne emet un long et puissant pet, qui ne detourne pas du tout de la musique enregistree sur le cd bloque. cependant, cela ne repare pas non plus le lecteur cd." $) cyoa-text! badfx morale2-all
		else
				($ cname .. " instructe : c'est une " .. everyone .. " vers" .. " ENTREPRISE INITECH" pbold $) cyoa-text!
			cool-it-7 if
				($ cname .. "voix nasale de 'l a l'heure juste lors de la boucle de la chanson. " .. "FAITES FRAPPER UNE CHANSON HIP-HOP" rbold .. " une invention heureuse et inattendue s'est produite, augmentant considerablement le moral du groupe !" $) +crtext goodfx
				morale3+all
			else cool-it++
				"Cela ne contribue nullement a ameliorer la situation." +crtext cardJ2-broke
			then
		then
	then;	
	cmechanic+ if
		($ cname .. " regle le lecteur de cd a peine avec difficulte !" $) cyoa-text! goodfx morale++all
	then;
	ctemper- cstrength+ and if
		($ cname .. " arretez de demonter le lecteur audio de tableau de bord et l'expulsez par la fenetre... . 		cette solution fonctionne !" $) cyoa-text! goodfx morale++all strengthREV
	then;
	($ cname .. " retire le lecteur de cd et se penche sur la reparation. " $) cyoa-text! 
	mechanicroll if
		($ cHe .. " repare ce qui est mal fonctionnant !" $) +crtext goodfx mechanic++ morale++all 
	else
		($ cHe .. " ne reussit pas a le reparer." $) +text cardJ2-broke
	then mechanicREV
;

cyoa-choice: cardJ2-quit
	($ Thegroup .. " il a decide de mettre fin a la lecture du lecteur cd. 	" $) cyoa-text!
;

cyoa-bridge: cardJ2-fix4
	someicon
	"Quelqu'un devrait essayer de reparer le lecteur cd." cyoa-text!
	' cardJ2-fix cyoa-team-choice
;

cyoa: cardJ2-test 
	vehicle.which@ 0 = if 
		image-road-end
		"Marche eremitique" cyoa-title! someicon
		($ Thegroup .. " marche avec une impetuosite inebranlable. il n'y a aucun endroit sur pour se refugier, donc prendre une pause serait trop risque." $) text! 
		(*	solo if ($ cname .. " se fatigue beaucoup ! " $) else "Tous deviennent extremement fatigues ! " then +crtext
		($ gThey .. " durant le prochain combat, les reserves seront epuisees." $) +text
		nosleep *) morale--all badfx
	then;
	"Reparation d'enregistrement audio" cyoa-title!
	($ Thegroup .. " il devrait essayer de le reparer quelqu'un ayant des connaissances en matiere d'electronique ou etant un technicien automobile." $) cyoa-text! 4 500 tone 
		team-count 3 > if
			' cardJ2-fix 2 #cyoa-team-choice
			choice( "Choisissez quelqu'un d'autre." )choice: cardJ2-fix4
		else
			' cardJ2-fix 3 #cyoa-team-choice
		then
		choice( "Deconnectez-le simplement." )choice: cardJ2-quit
;

// END jon events

// Reading related events

cyoa-choice: magazine1-butts
	"Desappointement." cyoa-title!
	"" cyoa-text!
	solo not if ($ "Ceci choisie de lire derange : " .. theothers .. "sans detourner, " $) +text then
	cchar dsyp? if
		"Securite aux flatulences" cyoa-title!
		($ cname .. " litige de grandeurs : il jette en arriere et lit le geant des flatulations mensuelles. cela lui enseigne beaucoup sur la fartaison en securite, et c'est maintenant plus difficile pour lui de cracher ses pantalons." $) +text goodfx
		statmod{ -5 to dsyp-farted }
	else
		($ cname .. " lise et retourne une passe en arriere avec le giant farts monthly. il s'avere finalement etre un journal medical seche avec un nom trompeur." $) +text 2 400 tone medical++
	then
		attitude-- cchar morale--all-except
;

cyoa-choice: magazine1-guns
	"Armes americaines" cyoa-title!
	($ "La revue est pleine de photographies d'armes, d'informations sur les armes, de conseils d'armes et de discours hyper-patriotiques bizarres. " .. cname .. " absorption des choses entieres." $) cyoa-text! 1 400 tone
		temper-- shooting++
;

cyoa-choice: magazine1-mech
	"Tour-cle et pret-a-aller" cyoa-title!
	($ "Ceci etait le magazine d'automobile avant que la societe explose. les articles instruits ont un effet apaisant. " .. cname .. "." $) cyoa-text! 2 300 tone
		temper++ mechanic++
;

cyoa-choice: magazine1-cats
	"videos et images de chat." cyoa-title!
	"Ceci n'est pas plus qu'une collection d'images de videos chat. cela n'avait jamais beaucoup de sens dans des temps meilleurs, mais est devenu tres populaire apres que l'internet ait ete mis hors service." cyoa-text! goodfx
		cdog? if
			($ cname .. " est " .. "DEMORALISE AU POINT DE L'ETRE INTERIEURMENT EBRANLE" fbold .. " by reading this!
			It fills " .. chim .. " a la folie avec la colere !" $) +crtext
			morale2- attitude-- temper-- WEAPON_FIERCE cchar .weapon_default!
		else
			ccat? if
				($ cname .. " est " .. "MEFIANTEMENT INTERESSE" fbold .. " by this magazine!
				" .. cHe .. " regardez comment insignifiante etaient les chats anciens !" $) +crtext
				morale4+
			else
				morale2+ attitude++
			then 
		then		
;

cyoa: magazine1-cyoa
	"Magasins populaires apres l'apocalypse" cyoa-title!
	choosehuman cicon
	($ Thegroup .. " essaye de voler un magasin d'alimentation, mais il a ete completement depouille. " .. cname-He .. " prend un magazine depuis le tiroir a magazines et decide de s'en servir pour lire." .. "choisissez un magazine :" $) cyoa-text! basic-tone
		choice( "Armes americaines" )choice: magazine1-guns
		choice( "Tour-cle et pret-a-aller" )choice: magazine1-mech
		choice( "videos et images de chat." )choice: magazine1-cats
		choice( "MOIS HEBDOMADAIRE GEANTS FLATULENCES" )choice: magazine1-butts
;

// END Magazines: Popular after the Apocalpyse

// Dentist Office

: magazine2-loot morale++ 2 LOOT_FOOD trunk.loot+ 2 3 rnd LOOT_MEDICAL trunk.loot+ ;

cyoa-choice: magazine2-glu
	"Histoires dechirantes a l'esprit" cyoa-title!
	($ cname .. " cela realise que ce magazine a toujours ete mordant, mais apprecie encore les histoires optimistes. l'une d'elles est sur un chat sauvant son proprietaire en chocant au buffet. inspiratrice." $) cyoa-text! 2 300 tone attitudeREV attitude++ magazine2-loot
;	
cyoa-choice: magazine2-sci
	"Sciences contestees" cyoa-title!
	($ cname .. " se sent pessimiste quant a la prediction du magazine d'automobiles volantes en suspension a quelques annees, maintenant que la civilisation est quelque peu detruite. cependant, elle restait encore une bonne lecture." $) cyoa-text! 1 300 tone attitudeREV attitude-- magazine2-loot
;
cyoa-choice: magazine2-celeb
	"Nouvelles sur les celebrites" cyoa-title!
	($ "Ce magazine etait toujours particulierement mechant, peut-etre plus encore maintenant que la majorite des personnes figurant dans ses pages ont ete consommees. " .. cname .. " c'est une personne un peu moins agreable a simplement la lire." $) cyoa-text! 2 300 tone loyaltyREV loyalty-- witsREV wits-- morale2+ magazine2-loot
;	
cyoa: magazine2-cyoa
	"Bureau dentiste" cyoa-title!
	choosehuman cicon
	($ Thegroup .. " raide un cabinet dentaire, decouvrant des fournitures medicales restantes et beaucoup de tubes de toothpaste techniquement comestibles. " .. cname-He .. " aller dans le salon d'attente et prendre un magazine parmi celles-ci." .. "choisissez un magazine :" $) cyoa-text! basic-tone
		choice( "Pour l'ame sensible :" )choice: magazine2-glu
		choice( "Sciences contestees" )choice: magazine2-sci
		choice( "Les dernieres nouvelles de celebz." )choice: magazine2-celeb
;

// END Dentist Office

// Prepper Magazine - Person Pick CYOA if you have people, Instant Result if you don't.

: mag3roll 30 %chance if .medical++ else 50 %chance if .shooting++ else .mechanic++ then then ;
: mag3share cyoa-good-dialog "Chaque personne passe le magazine autour, lisant quels articles leur plaisent. comme d'habitude, quelqu'un finit par y avoir des taches de beurre de cacahouete qui rendent la revue illisible." cyoa-text! 
	' mag3roll foreach-team-char someone 50 70 rnd LOOT_GAS trunk.loot+ ; 

: mag3solo shooting++ medical++ mechanic++ 50 70 rnd LOOT_GAS trunk.loot+ ;
: mag3result cyoa-good-dialog
	($ "Cet edition speciale etait une hypothese, a l'epoque, consacree a une apocalypse de zombies. " .. cname .. " etudie soigneusement les conseils sur le tir a l'arme a feu, l'article de premiere aidemedicale d'urgence et les astuces rares mais efficaces de reparation courante." $) cyoa-text! cicon mag3solo
;

cyoa-bridge: mag3share-pick 
	someicon
	"Tous ceux qui s'interessent a la preparation aux evenements d'urgence ou catastrophiques et veulent etre prepares." cyoa-text!
	' mag3result cyoa-team-choice
;

: magazine3-cyoa
	cyoa-start "Prepares, le magazine des preparateurs prets" cyoa-title! 
	0 to cyoaicon cyoaicon
	($ Thegroup .. " desame gaz depuis quelques vehicules abandonnes, puis je decouvre un numero de prepared preppers, edition speciale, en etat neuf dans l'un des voitures ! " $) cyoa-text!
	solo if 
		soloer choosechar ($ cHe .. " etudie maintenant sa sagesse justifiee." $) +text 
		mag3solo goodfx
	then;
	+cr "Qui en lit ?" +text basic-tone
	team-count 3 > if
		' mag3result 2 #cyoa-team-choice
		choice( "Choisissez quelqu'un d'autre." )choice: mag3share-pick 
	else
		' mag3result 3 #cyoa-team-choice
	then
	choice( "Partagez-le avec tout le monde" )choice: mag3share 
; 

// END Prepper Magazine

// Burnt Library

cyoa-choice: books-are-for-nerds 
	"FITNESS : SANTE PHYSIQUE." cyoa-title!
	cpet? if
		($ cname .. " saute en avant, attrape le livre. " .. chis .. " machoires et dentures. " .. "FAITES-LE EN MORCEAUX." rbold .. " comme " .. che .. " fais glisser le livre de cote a cote. que ca te touche, nerde." $) 
	else
		($ cname .. " prends le livre. " .. "coupe-le en deux parties." rbold .. " en roulant des groans intenses. " .. "ce n'est pas ce que je pense reellement a propos des livres." pbold $) 
	then cyoa-text! goodfx
	morale2+ temperREV strengthREV temper-- strength++ cchar morale--all-except
;
: book1-helproll 50 %chance if .attitude++ else .temper++ then ;
: book1-mechroll 50 %chance if .mechanic++ then ;
cyoa-choice: book1-improve
	"Changer ta vie a travers de la positivite" cyoa-title!
	($ Thegroup .. " apprend beaucoup sur le pouvoir de rester positif !" $) cyoa-text! 1 300 tone
		attitude++ temper++ morale++ cchar ' book1-helproll foreach-team-char-except
;
cyoa-choice: book1-mechanic
	"Enciclopedie de l'ingenierie creative" cyoa-title!
	"Ce livre est rempli d'informations sur la reparation ou la modification de beaucoup de choses courantes quotidiennes. il peut egalement etre assez lourd pour tuer quelqu'un. c'est un agreable bonus !" cyoa-text! 2 300 tone
		mechanic++ cchar ' book1-mechroll foreach-team-char-except
;
cyoa-choice: book1-morale
	"Temps de rimer de dr. dink" cyoa-title!
	($ "Ce livre est plein de images couleurees et ideal pour les enfants de tous ages. " .. thegroup .. " vraiment aime avoir quelque chose neuf a lire." $) cyoa-text! goodfx morale2+ cchar morale++all-except
;
: book1-swole 
	// cchar angrystrong-revealed? not if then;	cchar angrystrong? if
		most-angrystrong if
			charchoice( "VOITURE VOLEE !" )choice: books-are-for-nerds
	then
;
cyoa: book1-cyoa
	"Bibliotheque brulee" cyoa-title!
	choosehuman cicon
	($ Thegroup .. " rencontre une bibliotheque dans une ville qui parait avoir brule majoritairement. " .. cname-He .. " le livre qui a echappe reellement aux dommages de l'incendie... 	was it, in fact, the one?" $) cyoa-text! basic-tone
		book1-swole
		choice( "Manuel de reparation gigantesque" )choice: book1-mechanic
		choice( "Livre auto-formation sur la positivite" )choice: book1-improve
		choice( "Livre pour enfants qui rime" )choice: book1-morale
;

// END Burnt Library

// END reading

} }

uses base.df
uses charfilt.df
uses charstats.df
uses road.df

(* Basic Template
: EFFECT cyoa-good-dialog ($ cname .. " furzt auf "  .. someone .colourname .. "." $) cyoa-text!
: OLDERSTYLE "KARTE 1A EREIGNISBEISPIEL" $cyoa-ok-dialog 0 cyoa-image-icon! basic-tone ;
: card1-cyoa
	cyoa-start
	" " cyoa-title!
	0 to cyoaicon cyoaicon
	" " cyoa-text! basic-tone
	choice( "BESCHREIBUNG" )choice: EFFECT
	// NOTE that only 4 choices are shown max, with choices on top/first taking priority.
	; 
*)

(* CYOA standard types:
1. INSTANT RESULT - Example: Muscle Fan
No choice! Something happens instantly. Can do simple or complex rolls to modify the result. The most board-game like option. Keep these rare.
2. PERSON PICK - Example: Candy Finding
You're given an action and then have to pick a person to perform that action. The check/roll can be simple or complex. It should usually be a choice of up to 3 people, then a choice for an alternate group option. This makes things more interesting if you're currently solo.
3. ACTION PICK - Example: Traffic Jam, Quiet Town, Abandoned Gun Range
You're given a situation and provided 2-3 choices as to what to do. The choices then pick a person to carry out that action based on stats, or use the whole group. Gun Range has one of its options change to PERSON PICK.
4. SPOTLIGHT - Example: Parkouuur
A situation is presented and then a person is chosen from your group at random. You're given two, rarely three options for that person to perform. They can be pretty complex rolls or checks. The stats used for the rolls are then revealed, whether the check was good or bad.

REVEALED STATS
It's up to the CYOA writer as to whether or not the result should reveal stats to the group. Generally, loyalty is really rare to reveal, where something like making a really great shot in CYOA reveals your shooting stat outright.

REVEALED STAT SPECIAL OPTIONS - Examples: Parkouuur
All CYOA types have the option of checking your party for combinations of high, or really low, revealed stats. If the checks are successful, additional options can be added to the list of choices. Generally these should use combinations of stats instead of single stats, to make them rarer to trigger. If you can think of one or two of these to include in a CYOA, do so, but overall it should be hard for a single character to get a lot of these in succession.

See charstats.df and charfilt.df for details on what the words do

*)


cyoa{ road{

// Abandoned Gun Range
// Example of a GRAB BAG style CYOA. Give someone a point of shooting, get some ammo instead, or gamble health.
 
 // ammosearch1 gives ammo. See the bottom of chartstats.df for loot words.
: card1-search ($ Thegroup .. "  findet eine kleine Menge Munition." $) $cyoa-ok-dialog ammosearch1 goodfx cyoaicon ;

// One selected person shoots
cyoa-choice: card1-picked
	"Schietraining" cyoa-title!
	
	chumany? if
		($ cname .. "  bekommt etwas Zeit zum Uben des Schieens. 		Die Ziele waren sogar Zombie Themen!" $) cyoa-text! +cr goodfx
		shootingREV shooting++
	else
		($ cname .. "  nagt eine Weile an einer alten verrosteten Waffe. 		Es wird sabbernd." $) cyoa-text! morale2+ goodfx
	then
;

cyoa-choice: card1-one 
"Wer sollte etwas Zeit mit Schieubungen verbringen?" cyoa-text! 
	' card1-picked cyoa-team-choice
;
// For entire group, individually rolls a 40% chance of gaining a point in shooting skill.
: card1-spray 
	40 %chance if 
		dup humany? if 
			.shooting++ 
		else 
			.morale-- 
		then
	else 
		.ouch-- 
	then 
;

cyoa-choice: card1-group "Alle beschlieen, die Ziele auf einmal in der dunklen Schiehalle zu sprengen. Es wird nicht daruber nachgedacht, ob dies eine gute Idee ist oder nicht. WAS AUCH IMMER." cyoa-text! badfx
	' card1-spray foreach-team-char 
	// Note syntax: ' <single word, make shortcut if more> foreach-team-char
	someone .ouch--
;

cyoa: card1-cyoa
	"Verlassener Schiestand" cyoa-title!
	0 to cyoaicon cyoaicon // picks a CYOA image, displays it using "cyoaicon"
	($ Thegroup .. " ' findet einen Schiestand. Er ist leer und grotenteils von Vorraten befreit. Allerdings " .. gthey .. " findet" .. g1s .. "  einige Schieziele und eine funktionierende Pistole." $) cyoa-text! basic-tone
	1 WEAPON_PISTOL trunk.weapon+
	choice( "Suche nach Munition" )choice: card1-search
	// SOLO CHOICE, cancels group choices after it if solo
	solo if
		soloer charchoice( "  trainiert Schieen" )choice: card1-picked
	then;
	choice( "Train Group Haphazardly" )choice: card1-group
	choice( "Train One Person Carefully" )choice: card1-one
;

// END Abandoned Gun Range

// Muscle Fan
// Example of an INSTANT RESULT CYOA with no choices.

: card2-charm
	cpet? if
		cdog? if
			($ cname .. "  macht Welpenaugen bei der Frau." $)
		else
			($ cname .. "  rollt sich herum und tut ganz niedlich." $)
		then +crtext ($ "Sie gibt " .. cname .. "  einen Haufen Essen!" $) +crtext	7 10 rnd trunk-food+
	else
		($ cname .. "  uberzeugt die Frau mehr Details zu geben. Stattdessen gibt sie ein paar tolle Ubungs Tipps." $) +crtext
		witsREV attitudeREV morale++ strength++ strengthREV
	then
;
: card2-jerk 
	cpet? if
		($ cname .. "  hektisch " .. cbarkmeow .. " bei der Frau." $) +crtext	
	else
		($ cname .. "  nennt die Frau grausam einen MUSKEL NERD." $) +crtext
	then
	aholeREV morale2+
;
: card2-pumped ($ cname .. "  fuhlt sich von dieser Geschichte GEpumpt und stemmt einen Baumstamm in der Nahe." $) +text cyoa-text-cr
	strengthREV	strength++ morale++
;
cyoa: card2-cyoa ($ someone .colourname .. " ' trifft einen anderen Uberlebenden, der daruber schimpft, dass er einen eingeolten Bodybuilder gesehen hat, der zu aufgepumpt war, um sich zu kontrollieren, und Zombies in zwei Halften gerissen hat, nachdem er sie gedruckt hatte." $) cyoa-text! +cr 1 cyoa-image-icon! 4 500 tone 

	strongest-pass
	if strongest choosechar card2-pumped then;

	most-charming
	if choosechar card2-charm then;
	
	most-ahole
	if choosechar card2-jerk then;

	solo if ($ soloer .colourname .. "  fragt sich was das alles sollte." $) +text then;

	"Alle in deiner Gruppe schauen sich nervos an." +text
;

// END Muscle Fan

// Quiet Town
// Rest up if you have food, find some ammo for no penalty, or get more supplies in exchange for health if you don't have a paranoid character.

: card3-3rest 60 %chance if .morale2+ else .morale3+ then ;
cyoa-choice: card3-3day ($ Thegroup .. " ' wahlt eines der Hauser zum Barrikadieren aus und ruht sich dort fur ein paar Tage aus. Es ist eine willkommene Pause, auch wenn es gegen Ende langweilig wurde." $) cyoa-text! basic-tone 
	sleep team-count -6 * LOOT_FOOD trunk.loot+ 3 calendar-day+
	' card3-3rest foreach-team-char heal-event
;
cyoa-choice: card3-1day ($ Thegroup .. "  verbarrikadiert eines der Hauser und bekommt dringend benotigte Ruhe." $) cyoa-text! basic-tone 
	sleep team-count -2 *  LOOT_FOOD trunk.loot+ 1 calendar-day+
	' .morale++ foreach-team-char heal-event
;
cyoa-choice: card3-qloot ($ Thegroup .. "  durchsucht schnell die offensichtlichsten Orte um Vorrate zu verstauen. Es wird etwas Munition gefunden." $) cyoa-text! basic-tone ammosearch1 ;

: card3-zomb 
	most-paranoid
	if choosechar
		($ cname .. " ist immer paranoid, wird von dem Zombie nicht uberrascht und weicht ihm leicht aus." $) +text 50 %chance if witsREV else attitudeREV then goodfx
	else
		someone choosechar ($ "Leider fand der Zombie " .. cname .. "  zuerst." $)
		+text ouch2- bitefx
	then
	cicon // forces the icon to be the chosen character
;
cyoa-choice: card3-search ($ Thegroup .. "  durchkammt die Stadt nach zuruckgelassenen Vorraten, wird aber von einem einzigen Zombie uberfallen. " $) cyoa-text! card3-zomb ammosearch1 homesearch1 ;

cyoa: card3-cyoa
	"Ruhige Stadt" cyoa-title!
	"'Es ist so ruhig hier. Es gibt keine Anzeichen von Leben oder Untoten. Es scheint als hatten alle genommen was sie konnten und diese Stadt sofort evakuiert." cyoa-text! 0 to cyoaicon cyoaicon basic-tone
		choice( "Stadt grundlich plundern" )choice: card3-search
		choice( "Schnell plundern und rennen" )choice: card3-qloot
		trunk-food team-count 2 * 1 - > if 
			choice( "Einen Tag ausruhen" )choice: card3-1day
		then
		trunk-food team-count 6 * 1 - > if 
			choice( "Drei Tage ausruhen" )choice: card3-3day 
		then
;

// END Quiet Town

// Lost Stronghold
// Ultra simple DO A TOUGH CITY EVENT OR DON'T choice.

cyoa: card4-cyoa
	"Verlorene Festung" cyoa-title!

	"Dieses Wohnhaus wurde in eine verbarrikadierte Festung verwandelt. Es wurde komplett von einer Horde Zombies uberrannt. Positiv ist, dass sie im Moment ziemlich zahm zu sein scheinen." cyoa-text!
	1.3 1.7 frnd 9 15 frnd 0 1 rnd zforecast
	"events/inner/region/hellhotel.df" $load-region
		choice( "Greift die Festung an" )choice: premission
		gas-draw
;

// END Lost Stronghold

// Traffic Jam
// Examples of manual stat checks in this one. Mostly we should use the shortcuts like cwits+

: card5-wacheck cchar .attitude 4 < cchar .wits 3 > and ;

cyoa-choice: card5-walk good
	($ Thegroup .. "  lasst das alte Auto stehen und geht los, um auf dem Weg in Autos nach Vorraten zu suchen." $) cyoa-text! car-abandon ammosearch1
;

: card5-detadd
	card5-wacheck if
		good "Der Umweg lauft unerwartet gut!"
	then;

	""

	cchar .attitude 3 > if 
		bad "Diesen Weg zu nehmen war zu optimistisch, er dauerte Tage! " $cat
	then 

	cchar .wits 4 < if 
		bad "Der Weg stellte sich als TOTALER MIST heraus und das Auto wurde beschadigt." $cat
	then
;

: card5-detadd2
	card5-wacheck if
		morale++
	then;

	cchar .wits 4 < if 
		 morale-- -2 car-chassis+
	then

	cchar .attitude 3 > if 
		+cr fullcamp 
		trunk-gas 69 > if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			car-mpg 0 > if bandit-gas then
		then heal-event
	then;

;

cyoa-choice: card5-detour

	chumany? if 
		($ cname .. "  beschliet einen Umweg zu machen. " .. card5-detadd $) 
	else
		($ cname .. "  bellt aus dem Fenster auf einen versteckten, uberwucherten Pfad! " .. card5-detadd $)
	then cyoa-text!

	witsREV	attitudeREV	card5-detadd2
;

cyoa-choice: card5-bench
	($ cname .. "  stemmt ein zerstortes Auto. Das bringt wenig, um das Wrack zu beseitigen, und dein Auto muss trotzdem zuruckgelassen werden." $) cyoa-text! goodfx
	car-abandon
	morale++ strengthREV strength++ 
;

: card5-mudadd2 cwits+ if morale++ witsREV then;
	morale-- ouch--all -3 car-chassis+ ;

: card5-mudadd cwits+ if
		good "'Mit einer Kombination aus Wut und Wahrnehmung sturzt das Auto nicht ab."
	then;

	bad "'Es lauft gut bis das Auto gegen einen Felsen prallt."
;

cyoa-choice: card5-mudding
	($ cname .. "  pflugt durch den Seitenstreifen in der Nahe der Strae. " $) card5-mudadd $cat cyoa-text!
	temperREV
	card5-mudadd2
;

cyoa: card5-cyoa
	"Stau" cyoa-title!
	"Ein Meer aus Autos blockiert die Strae vor uns. Die Karambolage ist so schlimm dass es keinen offensichtlichen Weg hindurch gibt." cyoa-text!
		vehicle.which@ 0 = if 
			($ "' Ohne Auto ist es kein Problem, sich zu Fu durchzuquetschen! " .. Thegroup .. "  uberpruft unterwegs die Autos auf Vorrate." $) +crtext ammosearch1 goodfx
		then;
		angriest-pass if
			angriest choosechar chumany? if cchar charchoice( ": Aggressives Offroading!" )choice: card5-mudding then
		then

		strongest-pass if
			strongest charchoice( ": Drucke ein Auto auf die Bank" )choice: card5-bench
		then

		smartest charchoice( ": Finde eine neue Route" )choice: card5-detour

		choice( " Auto stehen lassen und gehen" )choice: card5-walk
;

// END Traffic Jam

// Candy finding
// Pretty similar GRAB BAG to Abandoned Gun Shop, but with interesting solo options.

: candysharesolo soloer .morale-- soloer .strength++ soloer .temper-- soloer .attitude-- ;
: candyshareroll args( @ )
	<- @ .loyaltyREV 
	<- @ .loyalty 4 < if
		<- @ .morale--
	else
		<- @ .morale++
	then
;

cyoa-choice: candyshare
solo if 
	bad ($ soloname .. "  ist wirklich verargert, dass die Suigkeiten mit Freunden geteilt werden mussen, obwohl sie imaginar sind. " .. soloer xHe .. "  tobt und stampft auf die Suigkeiten. Jetzt kann niemand sie mehr haben!" $) cyoa-text! candysharesolo
else 
	good ($ " Jeder lernt eine wertvolle Lektion uber das Teilen indem er versucht einen bereits winzigen Schokoriegel zu teilen. Vielleicht. Irgendwie." $) cyoa-text! ' candyshareroll foreach-team-char
then
;

cyoa-choice: candyresult
	" Es ist wirklich in Spagroe" cyoa-title!
	cdog? if
		($ "' Der Schokoriegel war Schokolade! Hunde durfen keine Schokolade essen! Was hast du getan?" $) text!
		bitefx ouch-- morale2+
	then;
	($ cname .. "  isst diese Suigkeit, groe Zeit. Die zuckrige Sue macht " .. chim .. "  fuhle mich ein bisschen besser " .. chis .. "  aktuelles Leben im Nicht Kanada." $) cyoa-text! cyoa-text-cr goodfx
	solo if 
		cyoa-text-cr ($ cHe .. "  fuhlt sich ziemlich selbstgefallig daruber dass er nicht teilen muss." $) +text attitude++ attitudeREV
	then
	morale2+ cyoa-text-cr 
;

cyoa-bridge: candyresult-pick 
	someicon
	"Wer sollte den winzigen Schokoriegel essen?" cyoa-text!
	' candyresult cyoa-team-choice
;

cyoa: card6-cyoa
	"Glucklicher Schokoriegel" cyoa-title! 
	0 to cyoaicon cyoaicon
	($ Thegroup .. " ' findet einen Schokoriegel am Straenrand! Er ist 'spagro'. Wer darf ihn essen?" $) cyoa-text! basic-tone
	team-count 3 > if
		' candyresult 2 #cyoa-team-choice
		choice( "Wahle jemand anderen" )choice: candyresult-pick 
	else
		' candyresult 3 #cyoa-team-choice
	then
	choice( "Mit allen teilen" )choice: candyshare 
;

// END Candy finding

// Parkouuur - A Spotlight CYOA. Also has an extra TRUMP option if you have revealed stats.
// Pretty standard layout for stat checks that we should reuse frequently. Result if over 4, result if under 2, if average do a stat roll which amounts to a weighted coin toss.

: card7-pcheck 
	cpet? if
		($ cname .. " ' macht eine sue Parkour Bewegung bei der er einfach nur durch die Beine der Zombies geht. Was auch immer!" $) cyoa-text! goodfx morale++
	then;
	
	cfitness+ if
		($ cname .. "  macht ein" .. RED .. " KRANKER PARKOUR FLIP" .. WHITE .. " ' aus etwas Mull und uber den Zaun. Keine groe Sache. Es ist alles zu trivial ein Zug fur " .. cname .. "." $) cyoa-text! goodfx morale++ 
	then;
	
	cfitness- if
		($ cname .. "  versucht unbeholfen den Zaun zu erklimmen ist aber viel zu auer Form um das zu tun. Die Zombies greifen an." $) cyoa-text! ouch-- bitefx
	then;
	
	fitnessroll if
		($ cname .. "  schafft es, rechtzeitig auf den Zaun zu klettern und vermeidet es, vorerst zerrissen zu werden. Gott segne Parkouring." $) cyoa-text! goodfx
		fitness++ 
	then;
	
		($ cname .. "  kampft darum den Zaun zu erklimmen. Einem Zombie gelingt es in einen Beinbiss zu gelangen." $) cyoa-text! bitefx sting--
;
cyoa-choice: card7-parkour card7-pcheck fitnessREV ;

// Doesn't check Strength for high/low, just does a roll no matter what. Advantage: Can use it to go from 5 to 6 str. Disadvantage: Pointless and dangerous at 6.
: card7-scheck 
	cpet? if
		($ cname .. " ' versteht das Konzept des Werfens, ist aber aufgrund fehlender Daumen nicht wirklich gut darin." $) cyoa-text! bitefx ouch--
	then;
	strengthroll if
		($ cname .. "  wirft mehrere Zombies um indem er Mulltonnen wirft " .. cHe .. "  entkommt und bekommt gleichzeitig ein groartiges Training." $) cyoa-text! goodfx
		strength++ morale++
	then;
		($ cname .. " 'versucht, die Zombies mit Mulltonnen abzuwehren, schafft es aber nicht, genug zu werfen, um ihren Angriff aufzuhalten." $) cyoa-text! bitefx ouch--
;
cyoa-choice: card7-throw card7-scheck strengthREV ;

cyoa-choice: card7-flip
($ cname .. " 'dreht VOLLKOMMEN durch und fangt an Dinge auf Zombies zu werfen und dann auf sie einzutreten. Es ist sowohl effektiv als auch sehr therapeutisch fur " .. cname .. "." $)
cyoa-text! goodfx
	morale++ temper++ strength++ temperREV strengthREV
;
: card7-tryflip 
	cchar angrystrong-revealed? not if then;
	cchar angrystrong? if
		choice( "Dreht VOLLKOMMEN durch" )choice: card7-flip
	then
;

cyoa: card7-cyoa
	"Kampfen oder Durchdrehen" cyoa-title!
	choosehuman cicon
	($ cname .. " macht Fehler beim Auskundschaften einer Stadt und wird von einer groen Gruppe Zombies in die Enge getrieben, wahrend er unbewaffnet ist." .. cHe .. " ist in einer Gasse gefangen. Am Ende der Gasse befinden sich schwerer Mull und ein groer Zaun." $) cyoa-text! basic-tone
		card7-tryflip
		choice( "Parkour Fahigkeiten einsetzen" )choice: card7-parkour
		choice( "Mull werfen" )choice: card7-throw
;

// END Parkouuur

} }

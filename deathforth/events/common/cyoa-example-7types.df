uses base.df
uses charfilt.df
uses charstats.df
uses road.df

(* Basic Template
: EFFECT cyoa-good-dialog ($ cname .. " 'apestardea a "  .. someone .colourname .. "." $) cyoa-text!
: OLDERSTYLE "TARJETA 1A EVENTO EJEMPLO" $cyoa-ok-dialog 0 cyoa-image-icon! basic-tone ;
: card1-cyoa
	cyoa-start
	" " cyoa-title!
	0 to cyoaicon cyoaicon
	" " cyoa-text! basic-tone
	choice( "DESCRIPCION" )choice: EFFECT
	// NOTE that only 4 choices are shown max, with choices on top/first taking priority.
	; 
*)

(* CYOA standard types:
1. INSTANT RESULT - Example: Muscle Fan
No choice! Something happens instantly. Can do simple or complex rolls to modify the result. The most board-game like option. Keep these rare.
2. PERSON PICK - Example: Candy Finding
You're given an action and then have to pick a person to perform that action. The check/roll can be simple or complex. It should usually be a choice of up to 3 people, then a choice for an alternate group option. This makes things more interesting if you're currently solo.
3. ACTION PICK - Example: Traffic Jam, Quiet Town, Abandoned Gun Range
You're given a situation and provided 2-3 choices as to what to do. The choices then pick a person to carry out that action based on stats, or use the whole group. Gun Range has one of its options change to PERSON PICK.
4. SPOTLIGHT - Example: Parkouuur
A situation is presented and then a person is chosen from your group at random. You're given two, rarely three options for that person to perform. They can be pretty complex rolls or checks. The stats used for the rolls are then revealed, whether the check was good or bad.

REVEALED STATS
It's up to the CYOA writer as to whether or not the result should reveal stats to the group. Generally, loyalty is really rare to reveal, where something like making a really great shot in CYOA reveals your shooting stat outright.

REVEALED STAT SPECIAL OPTIONS - Examples: Parkouuur
All CYOA types have the option of checking your party for combinations of high, or really low, revealed stats. If the checks are successful, additional options can be added to the list of choices. Generally these should use combinations of stats instead of single stats, to make them rarer to trigger. If you can think of one or two of these to include in a CYOA, do so, but overall it should be hard for a single character to get a lot of these in succession.

See charstats.df and charfilt.df for details on what the words do

*)


cyoa{ road{

// Abandoned Gun Range
// Example of a GRAB BAG style CYOA. Give someone a point of shooting, get some ammo instead, or gamble health.
 
 // ammosearch1 gives ammo. See the bottom of chartstats.df for loot words.
: card1-search ($ Thegroup .. " 'encuentra una pequena cantidad de municion." $) $cyoa-ok-dialog ammosearch1 goodfx cyoaicon ;

// One selected person shoots
cyoa-choice: card1-picked
	"Practica de tiro" cyoa-title!
	
	chumany? if
		($ cname .. " d" $) cyoa-text! +cr goodfx
		shootingREV shooting++
	else
		($ cname .. " gnaws on an old rusted gun for awhile. 		It gets drooly." $) cyoa-text! morale2+ goodfx
	then
;

cyoa-choice: card1-one 
"Quien deberia pasar un tiempo practicando tiro?" cyoa-text! 
	' card1-picked cyoa-team-choice
;
// For entire group, individually rolls a 40% chance of gaining a point in shooting skill.
: card1-spray 
	40 %chance if 
		dup humany? if 
			.shooting++ 
		else 
			.morale-- 
		then
	else 
		.ouch-- 
	then 
;

cyoa-choice: card1-group "Todos deciden disparar a los objetivos a la vez, en el campo de tiro oscuro. No se piensa si esto es una buena idea. LO QUE SEA." cyoa-text! badfx
	' card1-spray foreach-team-char 
	// Note syntax: ' <single word, make shortcut if more> foreach-team-char
	someone .ouch--
;

cyoa: card1-cyoa
	"Campo de Tiro Abandonado" cyoa-title!
	0 to cyoaicon cyoaicon // picks a CYOA image, displays it using "elige tu propia aventura"
	($ Thegroup .. " 1 " .. gthey .. " 'encontrar" .. g1s .. " 'algunos blancos de tiro y un pistola que funciona." $) cyoa-text! basic-tone
	1 WEAPON_PISTOL trunk.weapon+
	choice( "Buscar municion" )choice: card1-search
	// SOLO CHOICE, cancels group choices after it if solo
	solo if
		soloer charchoice( " 'Trenes disparando" )choice: card1-picked
	then;
	choice( "Grupo de trenes a la desordenada" )choice: card1-group
	choice( "Entrenar cuidadosamente a una persona" )choice: card1-one
;

// END Abandoned Gun Range

// Muscle Fan
// Example of an INSTANT RESULT CYOA with no choices.

: card2-charm
	cpet? if
		cdog? if
			($ cname .. " 'hace ojos tontos a la mujer." $)
		else
			($ cname .. " 'se dobla y actua todo dulce." $)
		then +crtext ($ "Ella le da " .. cname .. " 'una gran cantidad de comida!" $) +crtext	7 10 rnd trunk-food+
	else
		($ cname .. " 'convence a la mujer de dar mas detalles. Ella en cambio le proporciona excelentes consejos deportivos." $) +crtext
		witsREV attitudeREV morale++ strength++ strengthREV
	then
;
: card2-jerk 
	cpet? if
		($ cname .. " 'se desmaya " .. cbarkmeow .. "ante ella." $) +crtext	
	else
		($ cname .. " 'cruelmente llama a la mujer una NERD DE MUSCULO" $) +crtext
	then
	aholeREV morale2+
;
: card2-pumped ($ cname .. " se siente emocionado por esta historia y se sienta en un tronco cercano." $) +text cyoa-text-cr
	strengthREV	strength++ morale++
;
cyoa: card2-cyoa ($ someone .colourname .. " 'Encuentra a otro superviviente que se queja de haber visto a un culturista aceitado que estaba 'demasiado hinchado para controlar', desgarrando zombis por la mitad despues de hacerles press de banca." $) cyoa-text! +cr 1 cyoa-image-icon! 4 500 tone 

	strongest-pass
	if strongest choosechar card2-pumped then;

	most-charming
	if choosechar card2-charm then;
	
	most-ahole
	if choosechar card2-jerk then;

	solo if ($ soloer .colourname .. " se pregunta que fue todo eso." $) +text then;

	"Todos en tu grupo se miran nerviosos." +text
;

// END Muscle Fan

// Quiet Town
// Rest up if you have food, find some ammo for no penalty, or get more supplies in exchange for health if you don't have a paranoid character.

: card3-3rest 60 %chance if .morale2+ else .morale3+ then ;
cyoa-choice: card3-3day ($ Thegroup .. " 1" $) cyoa-text! basic-tone 
	sleep team-count -6 * LOOT_FOOD trunk.loot+ 3 calendar-day+
	' card3-3rest foreach-team-char heal-event
;
cyoa-choice: card3-1day ($ Thegroup .. " barricades one of the houses and gets some much needed rest." $) cyoa-text! basic-tone 
	sleep team-count -2 *  LOOT_FOOD trunk.loot+ 1 calendar-day+
	' .morale++ foreach-team-char heal-event
;
cyoa-choice: card3-qloot ($ Thegroup .. " 'Hace una busqueda rapida de los lugares mas obvios para esconder suministros. Se encuentra algo de municion." $) cyoa-text! basic-tone ammosearch1 ;

: card3-zomb 
	most-paranoid
	if choosechar
		($ cname .. ", siempre paranoico, no se sorprende por el zombi y lo evita facilmente." $) +text 50 %chance if witsREV else attitudeREV then goodfx
	else
		someone choosechar ($ "Desafortunadamente el zombie encontro' " .. cname .. " ' primero." $)
		+text ouch2- bitefx
	then
	cicon // forces the icon to be the chosen character
;
cyoa-choice: card3-search ($ Thegroup .. " ' peina el pueblo en busca de suministros abandonados, pero es emboscado por un solo zombi. " $) cyoa-text! card3-zomb ammosearch1 homesearch1 ;

cyoa: card3-cyoa
	"Pueblo Tranquilo" cyoa-title!
	"Es tan tranquilo aqui. No hay senales de vida o no vida. Parece que todos se llevaron lo que pudieron y evacuaron este pueblo de inmediato." cyoa-text! 0 to cyoaicon cyoaicon basic-tone
		choice( "Saquear a fondo el pueblo" )choice: card3-search
		choice( "Saquear rapidamente y huir" )choice: card3-qloot
		trunk-food team-count 2 * 1 - > if 
			choice( "Descansar un dia" )choice: card3-1day
		then
		trunk-food team-count 6 * 1 - > if 
			choice( "Descansar tres dias" )choice: card3-3day 
		then
;

// END Quiet Town

// Lost Stronghold
// Ultra simple DO A TOUGH CITY EVENT OR DON'T choice.

cyoa: card4-cyoa
	"Fortaleza perdida" cyoa-title!

	"Este edificio de apartamentos se convirtio en una fortaleza acordonada. Ha sido completamente invadido por una horda de zombis. Por el lado positivo, parecen bastante dociles por el momento." cyoa-text!
	1.3 1.7 frnd 9 15 frnd 0 1 rnd zforecast
	"events/inner/region/hellhotel.df" $load-region
		choice( "Atacar la Fortaleza" )choice: premission
		gas-draw
;

// END Lost Stronghold

// Traffic Jam
// Examples of manual stat checks in this one. Mostly we should use the shortcuts like cwits+

: card5-wacheck cchar .attitude 4 < cchar .wits 3 > and ;

cyoa-choice: card5-walk good
	($ Thegroup .. " ' abandona el viejo coche y comienza a caminar a traves de el, buscando suministros en los coches por el camino." $) cyoa-text! car-abandon ammosearch1
;

: card5-detadd
	card5-wacheck if
		good "El desvio resulta inesperadamente bien!"
	then;

	""

	cchar .attitude 3 > if 
		bad "Tomar esta ruta fue demasiado optimista, duro dias! " $cat
	then 

	cchar .wits 4 < if 
		bad "La ruta termino siendo una BASURA TOTAL, y el coche quedo danado." $cat
	then
;

: card5-detadd2
	card5-wacheck if
		morale++
	then;

	cchar .wits 4 < if 
		 morale-- -2 car-chassis+
	then

	cchar .attitude 3 > if 
		+cr fullcamp 
		trunk-gas 69 > if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			car-mpg 0 > if bandit-gas then
		then heal-event
	then;

;

cyoa-choice: card5-detour

	chumany? if 
		($ cname .. " decide hacer un desvio. " .. card5-detadd $) 
	else
		($ cname .. "  ladra por la ventana a un camino oc " .. card5-detadd $)
	then cyoa-text!

	witsREV	attitudeREV	card5-detadd2
;

cyoa-choice: card5-bench
	($ cname .. " 0" $) cyoa-text! goodfx
	car-abandon
	morale++ strengthREV strength++ 
;

: card5-mudadd2 cwits+ if morale++ witsREV then;
	morale-- ouch--all -3 car-chassis+ ;

: card5-mudadd cwits+ if
		good "Con una combinacion de estar cabreado y perceptivo, el coche no choca."
	then;

	bad "Va bien hasta que el coche choca contra una roca."
;

cyoa-choice: card5-mudding
	($ cname .. "  ara el hombro cerca de la carretera. " $) card5-mudadd $cat cyoa-text!
	temperREV
	card5-mudadd2
;

cyoa: card5-cyoa
	"Atasco" cyoa-title!
	"Un mar de coches obstaculiza la carretera por delante. La cola es tan mala que no hay clara forma de avanzar." cyoa-text!
		vehicle.which@ 0 = if 
			($ "Sin coche, no hay problema para pasar a pie! " .. Thegroup .. " 'revisa los coches en busca de suministros en el camino." $) +crtext ammosearch1 goodfx
		then;
		angriest-pass if
			angriest choosechar chumany? if cchar charchoice( ": Ofrecer una resistencia agresiva al terreno!" )choice: card5-mudding then
		then

		strongest-pass if
			strongest charchoice( ": Presionar el banco un coche." )choice: card5-bench
		then

		smartest charchoice( ": Encontrar una nueva ruta" )choice: card5-detour

		choice( "Abandona el coche y camina" )choice: card5-walk
;

// END Traffic Jam

// Candy finding
// Pretty similar GRAB BAG to Abandoned Gun Shop, but with interesting solo options.

: candysharesolo soloer .morale-- soloer .strength++ soloer .temper-- soloer .attitude-- ;
: candyshareroll args( @ )
	<- @ .loyaltyREV 
	<- @ .loyalty 4 < if
		<- @ .morale--
	else
		<- @ .morale++
	then
;

cyoa-choice: candyshare
solo if 
	bad ($ soloname .. " 'esta realmente molesto de que la dulce tiene que compartirse con amigos, aun asi son imaginarios. " .. soloer xHe .. " 'enfurece y pisa la dulce. Ahora nadie puede tenerla!" $) cyoa-text! candysharesolo
else 
	good ($ "Todos aprenden una leccion valiosa sobre compartir al intentar dividir una barra de dulce ya pequena. Tal vez. Un poco." $) cyoa-text! ' candyshareroll foreach-team-char
then
;

cyoa-choice: candyresult
	"Es Realmente De Tamano Ideal" cyoa-title!
	cdog? if
		($ "La barrita de dulce era de chocolate! Perros no estan supuestos comer chocolate! Que has hecho?" $) text!
		bitefx ouch-- morale2+
	then;
	($ cname .. "  come alive. " .. chim .. "  sentirse un poco mejor acerca de " .. chis .. " 'vida actual en no-Canada" $) cyoa-text! cyoa-text-cr goodfx
	solo if 
		cyoa-text-cr ($ cHe .. " se siente bastante satisfecho de no tener que compartir." $) +text attitude++ attitudeREV
	then
	morale2+ cyoa-text-cr 
;

cyoa-bridge: candyresult-pick 
	someicon
	"Quien debe comer la barra de caramelo pequena?" cyoa-text!
	' candyresult cyoa-team-choice
;

cyoa: card6-cyoa
	"Barra de Caramelo de la Suerte" cyoa-title! 
	0 to cyoaicon cyoaicon
	($ Thegroup .. " quien se lo come?" $) cyoa-text! basic-tone
	team-count 3 > if
		' candyresult 2 #cyoa-team-choice
		choice( "Elige a alguien mas" )choice: candyresult-pick 
	else
		' candyresult 3 #cyoa-team-choice
	then
	choice( "Compartir con todos" )choice: candyshare 
;

// END Candy finding

// Parkouuur - A Spotlight CYOA. Also has an extra TRUMP option if you have revealed stats.
// Pretty standard layout for stat checks that we should reuse frequently. Result if over 4, result if under 2, if average do a stat roll which amounts to a weighted coin toss.

: card7-pcheck 
	cpet? if
		($ cname .. " 'Hace un dulce movimiento de parkour que implica simplemente caminar entre las piernas de los zombis. Lo que sea!" $) cyoa-text! goodfx morale++
	then;
	
	cfitness+ if
		($ cname .. " ' hace un" .. RED .. " SICK PARKOUR FLIP" .. WHITE .. "  to be a problem. " .. cname .. "." $) cyoa-text! goodfx morale++ 
	then;
	
	cfitness- if
		($ cname .. " 'Trata torpemente de escalar la valla, pero esta demasiado fuera de forma para hacerlo. Los zombies atacan." $) cyoa-text! ouch-- bitefx
	then;
	
	fitnessroll if
		($ cname .. " 'logra subir la valla a tiempo, evitando ser despedazado por ahora. Bendito parkour." $) cyoa-text! goodfx
		fitness++ 
	then;
	
		($ cname .. " 1 wound." $) cyoa-text! bitefx sting--
;
cyoa-choice: card7-parkour card7-pcheck fitnessREV ;

// Doesn't check Strength for high/low, just does a roll no matter what. Advantage: Can use it to go from 5 to 6 str. Disadvantage: Pointless and dangerous at 6.
: card7-scheck 
	cpet? if
		($ cname .. " entiende el concepto de lanzar cosas, pero realmente no es bueno en eso debido a que no tiene pulgares." $) cyoa-text! bitefx ouch--
	then;
	strengthroll if
		($ cname .. "  derriba a varios zombies lanzando botes de basura. " .. cHe .. " escapa y hace un gran entrenamiento al mismo tiempo." $) cyoa-text! goodfx
		strength++ morale++
	then;
		($ cname .. "  intenta luchar contra los zombies arrojandoles botes de basura, pero no logra arrojar suficientes para detener su ataque." $) cyoa-text! bitefx ouch--
;
cyoa-choice: card7-throw card7-scheck strengthREV ;

cyoa-choice: card7-flip
($ cname .. " 1 turn. " .. cname .. "." $)
cyoa-text! goodfx
	morale++ temper++ strength++ temperREV strengthREV
;
: card7-tryflip 
	cchar angrystrong-revealed? not if then;
	cchar angrystrong? if
		choice( "VOLVERSE LOCO" )choice: card7-flip
	then
;

cyoa: card7-cyoa
	"Lucha o Voltea" cyoa-title!
	choosehuman cicon
	($ cname .. " 	Comete errores mientras explora un pueblo, y es acorralado por un gran grupo de zombis mientras esta desarmado." .. cHe .. " 'esta atrapado en un callejon. Al final del callejon hay basura pesada y una gran valla." $) cyoa-text! basic-tone
		card7-tryflip
		choice( "Usa habilidades de Parkour" )choice: card7-parkour
		choice( "Tira basura" )choice: card7-throw
;

// END Parkouuur

} }

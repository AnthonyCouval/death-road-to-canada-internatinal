uses base.df
uses charfilt.df
uses charstats.df
uses road.df

(* Basic Template
: EFFECT cyoa-good-dialog ($ cname .. "  pete sur "  .. someone .colourname .. "." $) cyoa-text!
: OLDERSTYLE "EXEMPLE DEVENEMENT DE CARTE 1A" $cyoa-ok-dialog 0 cyoa-image-icon! basic-tone ;
: card1-cyoa
	cyoa-start
	" " cyoa-title!
	0 to cyoaicon cyoaicon
	" " cyoa-text! basic-tone
	choice( "DESCRIPTION" )choice: EFFECT
	// NOTE that only 4 choices are shown max, with choices on top/first taking priority.
	; 
*)

(* CYOA standard types:
1. INSTANT RESULT - Example: Muscle Fan
No choice! Something happens instantly. Can do simple or complex rolls to modify the result. The most board-game like option. Keep these rare.
2. PERSON PICK - Example: Candy Finding
You're given an action and then have to pick a person to perform that action. The check/roll can be simple or complex. It should usually be a choice of up to 3 people, then a choice for an alternate group option. This makes things more interesting if you're currently solo.
3. ACTION PICK - Example: Traffic Jam, Quiet Town, Abandoned Gun Range
You're given a situation and provided 2-3 choices as to what to do. The choices then pick a person to carry out that action based on stats, or use the whole group. Gun Range has one of its options change to PERSON PICK.
4. SPOTLIGHT - Example: Parkouuur
A situation is presented and then a person is chosen from your group at random. You're given two, rarely three options for that person to perform. They can be pretty complex rolls or checks. The stats used for the rolls are then revealed, whether the check was good or bad.

REVEALED STATS
It's up to the CYOA writer as to whether or not the result should reveal stats to the group. Generally, loyalty is really rare to reveal, where something like making a really great shot in CYOA reveals your shooting stat outright.

REVEALED STAT SPECIAL OPTIONS - Examples: Parkouuur
All CYOA types have the option of checking your party for combinations of high, or really low, revealed stats. If the checks are successful, additional options can be added to the list of choices. Generally these should use combinations of stats instead of single stats, to make them rarer to trigger. If you can think of one or two of these to include in a CYOA, do so, but overall it should be hard for a single character to get a lot of these in succession.

See charstats.df and charfilt.df for details on what the words do

*)


cyoa{ road{

// Abandoned Gun Range
// Example of a GRAB BAG style CYOA. Give someone a point of shooting, get some ammo instead, or gamble health.
 
 // ammosearch1 gives ammo. See the bottom of chartstats.df for loot words.
: card1-search ($ Thegroup .. "  trouve une petite quantite de munitions." $) $cyoa-ok-dialog ammosearch1 goodfx cyoaicon ;

// One selected person shoots
cyoa-choice: card1-picked
	"Entrainement au tir" cyoa-title!
	
	chumany? if
		($ cname .. "  a un peu de temps pour sentrainer au tir. Les cibles etaient meme sur le theme des zombies!" $) cyoa-text! +cr goodfx
		shootingREV shooting++
	else
		($ cname .. " gnawing on an old rusted gun for awhile. It gets drooly." $) cyoa-text! morale2+ goodfx
	then
;

cyoa-choice: card1-one 
"Qui devrait passer un peu de temps a sentrainer au tir?" cyoa-text! 
	' card1-picked cyoa-team-choice
;
// For entire group, individually rolls a 40% chance of gaining a point in shooting skill.
: card1-spray 
	40 %chance if 
		dup humany? if 
			.shooting++ 
		else 
			.morale-- 
		then
	else 
		.ouch-- 
	then 
;

cyoa-choice: card1-group "Everyone decides to all blast the targets at once, in the dark gun range. No thought is given as to whether or not this is a good idea. WHATEVER." cyoa-text! badfx
	' card1-spray foreach-team-char 
	// Note syntax: ' <single word, make shortcut if more> foreach-team-char
	someone .ouch--
;

cyoa: card1-cyoa
	"Champ de tir abandonne" cyoa-title!
	0 to cyoaicon cyoaicon // picks a CYOA image, displays it using "cyoaicon"
	($ Thegroup .. " ' trouve un champ de tir. Il est vide et la plupart des fournitures ont ete ramassees. Cependant, " .. gthey .. "  trouver" .. g1s .. "  quelques cibles de tir et un pistolet en etat de marche." $) cyoa-text! basic-tone
	1 WEAPON_PISTOL trunk.weapon+
	choice( "Rechercher des munitions" )choice: card1-search
	// SOLO CHOICE, cancels group choices after it if solo
	solo if
		soloer charchoice( "  sentraine au tir" )choice: card1-picked
	then;
	choice( "Entrainer le groupe au hasard" )choice: card1-group
	choice( "Entrainer soigneusement une personne" )choice: card1-one
;

// END Abandoned Gun Range

// Muscle Fan
// Example of an INSTANT RESULT CYOA with no choices.

: card2-charm
	cpet? if
		cdog? if
			($ cname .. "  fait les yeux de chiot a la femme." $)
		else
			($ cname .. "  se retourne et agit tout mignon." $)
		then +crtext ($ "Elle donne " .. cname .. "  un tas de nourriture !" $) +crtext	7 10 rnd trunk-food+
	else
		($ cname .. "  convainc la femme de donner plus de details. Elle fournit plutot dexcellents conseils dexercice." $) +crtext
		witsREV attitudeREV morale++ strength++ strengthREV
	then
;
: card2-jerk 
	cpet? if
		($ cname .. "  frenetiquement " .. cbarkmeow .. "a la femme." $) +crtext	
	else
		($ cname .. "  appelle cruellement la femme une NERD MUSCLEE." $) +crtext
	then
	aholeREV morale2+
;
: card2-pumped ($ cname .. "  se sent GONFLE a propos de cette histoire et souleve un journal a proximite." $) +text cyoa-text-cr
	strengthREV	strength++ morale++
;
cyoa: card2-cyoa ($ someone .colourname .. " ' rencontre un autre survivant qui raconte avoir vu un bodybuilder huile qui etait 'trop gonfle pour etre controle', dechirant des zombies en deux apres les avoir souleves au developpe couche." $) cyoa-text! +cr 1 cyoa-image-icon! 4 500 tone 

	strongest-pass
	if strongest choosechar card2-pumped then;

	most-charming
	if choosechar card2-charm then;
	
	most-ahole
	if choosechar card2-jerk then;

	solo if ($ soloer .colourname .. "  se demande de quoi il sagissait." $) +text then;

	"Tout le monde dans votre groupe se regarde nerveusement." +text
;

// END Muscle Fan

// Quiet Town
// Rest up if you have food, find some ammo for no penalty, or get more supplies in exchange for health if you don't have a paranoid character.

: card3-3rest 60 %chance if .morale2+ else .morale3+ then ;
cyoa-choice: card3-3day ($ Thegroup .. " ' choisit l'une des maisons pour les barricades, puis s'y repose pendant quelques jours. C'est une pause bienvenue, meme si cela devenait ennuyeux vers la fin." $) cyoa-text! basic-tone 
	sleep team-count -6 * LOOT_FOOD trunk.loot+ 3 calendar-day+
	' card3-3rest foreach-team-char heal-event
;
cyoa-choice: card3-1day ($ Thegroup .. "  barricade lune des maisons et se repose bien." $) cyoa-text! basic-tone 
	sleep team-count -2 *  LOOT_FOOD trunk.loot+ 1 calendar-day+
	' .morale++ foreach-team-char heal-event
;
cyoa-choice: card3-qloot ($ Thegroup .. "  fait une recherche rapide des endroits les plus evidents pour cacher des fournitures. Des munitions sont trouvees." $) cyoa-text! basic-tone ammosearch1 ;

: card3-zomb 
	most-paranoid
	if choosechar
		($ cname .. ", toujours paranoiaque, nest pas surpris par le zombie et levite facilement." $) +text 50 %chance if witsREV else attitudeREV then goodfx
	else
		someone choosechar ($ "Malheureusement le zombie a trouve " .. cname .. "  dabord." $)
		+text ouch2- bitefx
	then
	cicon // forces the icon to be the chosen character
;
cyoa-choice: card3-search ($ Thegroup .. "  ratisse la ville a la recherche de fournitures abandonnees, mais est pris en embuscade par un seul zombie " $) cyoa-text! card3-zomb ammosearch1 homesearch1 ;

cyoa: card3-cyoa
	"Ville tranquille" cyoa-title!
	"'C'est tellement calme ici. Il n'y a aucun signe de vie ou de non vie. On dirait que tout le monde a pris ce qu'il pouvait et a evacue cette ville immediatement." cyoa-text! 0 to cyoaicon cyoaicon basic-tone
		choice( "Fouiller la ville a fond" )choice: card3-search
		choice( "Piller rapidement et fuir" )choice: card3-qloot
		trunk-food team-count 2 * 1 - > if 
			choice( "Se reposer pendant une journee" )choice: card3-1day
		then
		trunk-food team-count 6 * 1 - > if 
			choice( "Se reposer pendant trois jours" )choice: card3-3day 
		then
;

// END Quiet Town

// Lost Stronghold
// Ultra simple DO A TOUGH CITY EVENT OR DON'T choice.

cyoa: card4-cyoa
	"Forteresse perdue" cyoa-title!

	"Cet immeuble dappartements a ete transforme en une forteresse barricadee. Il a ete completement envahi par une horde de zombies. Du cote positif, ils semblent assez dociles pour le moment." cyoa-text!
	1.3 1.7 frnd 9 15 frnd 0 1 rnd zforecast
	"events/inner/region/hellhotel.df" $load-region
		choice( "Attaquer la forteresse" )choice: premission
		gas-draw
;

// END Lost Stronghold

// Traffic Jam
// Examples of manual stat checks in this one. Mostly we should use the shortcuts like cwits+

: card5-wacheck cchar .attitude 4 < cchar .wits 3 > and ;

cyoa-choice: card5-walk good
	($ Thegroup .. "  abandonne la vieille voiture et commence a marcher a travers, fouillant les voitures pour trouver des fournitures sur le chemin." $) cyoa-text! car-abandon ammosearch1
;

: card5-detadd
	card5-wacheck if
		good "Le detour se passe etonnamment bien !"
	then;

	""

	cchar .attitude 3 > if 
		bad "Prendre cette route etait trop optimiste, ca a dure des jours " $cat
	then 

	cchar .wits 4 < if 
		bad "La route sest revelee etre une POUBELLE TOTALE, et la voiture a ete endommagee." $cat
	then
;

: card5-detadd2
	card5-wacheck if
		morale++
	then;

	cchar .wits 4 < if 
		 morale-- -2 car-chassis+
	then

	cchar .attitude 3 > if 
		+cr fullcamp 
		trunk-gas 69 > if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			car-mpg 0 > if bandit-gas then
		then heal-event
	then;

;

cyoa-choice: card5-detour

	chumany? if 
		($ cname .. "  decide de faire un detour. " .. card5-detadd $) 
	else
		($ cname .. "  aboie par la fenetre sur un chemin cache et envahi par la vegetation " .. card5-detadd $)
	then cyoa-text!

	witsREV	attitudeREV	card5-detadd2
;

cyoa-choice: card5-bench
	($ cname .. " " $) cyoa-text! goodfx
	car-abandon
	morale++ strengthREV strength++ 
;

: card5-mudadd2 cwits+ if morale++ witsREV then;
	morale-- ouch--all -3 car-chassis+ ;

: card5-mudadd cwits+ if
		good "Avec un melange de colere et de perspicacite, la voiture ne s'ecrase pas."
	then;

	bad "Ca se passe bien jusqu'a ce que la voiture percute un rocher."
;

cyoa-choice: card5-mudding
	($ cname .. " laboure lepaule pres de la route. " $) card5-mudadd $cat cyoa-text!
	temperREV
	card5-mudadd2
;

cyoa: card5-cyoa
	"Embouteillage" cyoa-title!
	"Une mer de voitures bloque la route devant. Laccident est si grave quil ny a pas de moyen evident de passer." cyoa-text!
		vehicle.which@ 0 = if 
			($ "'Sans voiture, ce n'est pas un probleme de se faufiler a pied " .. Thegroup .. "  verifie les voitures pour les fournitures sur le chemin." $) +crtext ammosearch1 goodfx
		then;
		angriest-pass if
			angriest choosechar chumany? if cchar charchoice( ": Conduite tout terrain agressive !" )choice: card5-mudding then
		then

		strongest-pass if
			strongest charchoice( ": Developpe couche une voiture" )choice: card5-bench
		then

		smartest charchoice( ": Trouver un nouvel itineraire" )choice: card5-detour

		choice( "Abandonner la voiture et marcher" )choice: card5-walk
;

// END Traffic Jam

// Candy finding
// Pretty similar GRAB BAG to Abandoned Gun Shop, but with interesting solo options.

: candysharesolo soloer .morale-- soloer .strength++ soloer .temper-- soloer .attitude-- ;
: candyshareroll args( @ )
	<- @ .loyaltyREV 
	<- @ .loyalty 4 < if
		<- @ .morale--
	else
		<- @ .morale++
	then
;

cyoa-choice: candyshare
solo if 
	bad ($ soloname .. "  est vraiment ennuye que le bonbon doive etre partage avec des amis, meme sils sont imaginaires. " .. soloer xHe .. "  fait rage et pietine le bonbon. Maintenant personne ne peut lavoir !" $) cyoa-text! candysharesolo
else 
	good ($ "Tout le monde apprend une precieuse lecon sur le partage en essayant de diviser une barre chocolatee deja minuscule. Peut etre. En quelque sorte." $) cyoa-text! ' candyshareroll foreach-team-char
then
;

cyoa-choice: candyresult
	"Cest vraiment amusant" cyoa-title!
	cdog? if
		($ "'La barre chocolatee etait au chocolat ! Les chiens ne sont pas censes manger de chocolat ! Qu'avez vous fait ?" $) text!
		bitefx ouch-- morale2+
	then;
	($ cname .. "  mange ce bonbon, a fond. La douceur sucree fait " .. chim .. "  se sentir un peu mieux dans " .. chis .. "  la vie actuelle au non Canada." $) cyoa-text! cyoa-text-cr goodfx
	solo if 
		cyoa-text-cr ($ cHe .. "  ressent assez confiant" $) +text attitude++ attitudeREV
	then
	morale2+ cyoa-text-cr 
;

cyoa-bridge: candyresult-pick 
	someicon
	"Qui devrait manger la petite barre de chocolat ?" cyoa-text!
	' candyresult cyoa-team-choice
;

cyoa: card6-cyoa
	"Barre de chocolat heureuse" cyoa-title! 
	0 to cyoaicon cyoaicon
	($ Thegroup .. " 'trouve une barre de chocolat sur le bord de la route ! C'est 'fun size'. Qui en mangera ?" $) cyoa-text! basic-tone
	team-count 3 > if
		' candyresult 2 #cyoa-team-choice
		choice( "Choisissez quelquun dautre" )choice: candyresult-pick 
	else
		' candyresult 3 #cyoa-team-choice
	then
	choice( "Partagez avec tout le monde" )choice: candyshare 
;

// END Candy finding

// Parkouuur - A Spotlight CYOA. Also has an extra TRUMP option if you have revealed stats.
// Pretty standard layout for stat checks that we should reuse frequently. Result if over 4, result if under 2, if average do a stat roll which amounts to a weighted coin toss.

: card7-pcheck 
	cpet? if
		($ cname .. " 'fait une belle passe de parkour en passant juste entre les jambes des zombies. Quelqu'un peut manger ca !" $) cyoa-text! goodfx morale++
	then;
	
	cfitness+ if
		($ cname .. " fait" .. RED .. " FLIP PARKOUR SPECTACULAIRE" .. WHITE .. " 'saute sur quelque chose et saute au dessus du mur. Rien de bien important. C'est trop trivial pour " .. cname .. "." $) cyoa-text! goodfx morale++ 
	then;
	
	cfitness- if
		($ cname .. " tente maladroitement descalader le mur mais est trop mal en forme pour le faire. Les zombies attaquent." $) cyoa-text! ouch-- bitefx
	then;
	
	fitnessroll if
		($ cname .. " arrive a grimper au dessus du mur a temps, evitant detre dechire en morceaux pour linstant. Que vive le parkour" $) cyoa-text! goodfx
		fitness++ 
	then;
	
		($ cname .. " peut malgre tout grimper au dessus du mur. Un zombie parvient cependant a mordre une jambe." $) cyoa-text! bitefx sting--
;
cyoa-choice: card7-parkour card7-pcheck fitnessREV ;

// Doesn't check Strength for high/low, just does a roll no matter what. Advantage: Can use it to go from 5 to 6 str. Disadvantage: Pointless and dangerous at 6.
: card7-scheck 
	cpet? if
		($ cname .. " 'comprend le concept de jeter des choses, mais reellement n''est pas tres bon a ca en raison de ne pas avoir de doigts." $) cyoa-text! bitefx ouch--
	then;
	strengthroll if
		($ cname .. " renverse plusieurs zombies en lancant des poubelles. " .. cHe .. " echappe et fait un bon exercice en meme temps." $) cyoa-text! goodfx
		strength++ morale++
	then;
		($ cname .. " tente de se defendre en lancant des poubelles sur les zombies, mais n'en lance pas assez pour arreter leur attaque." $) cyoa-text! bitefx ouch--
;
cyoa-choice: card7-throw card7-scheck strengthREV ;

cyoa-choice: card7-flip
($ cname .. " .x damage to zombies .x damage to self. " .. cname .. "." $)
cyoa-text! goodfx
	morale++ temper++ strength++ temperREV strengthREV
;
: card7-tryflip 
	cchar angrystrong-revealed? not if then;
	cchar angrystrong? if
		choice( "PETER UN CABLE" )choice: card7-flip
	then
;

cyoa: card7-cyoa
	"Combat ou Fuite" cyoa-title!
	choosehuman cicon
	($ cname .. "  fait des erreurs en eclaireur dans une ville, et se fait coincer par un grand groupe de zombies sans arme. 	" .. cHe .. "  est coince dans une ruelle. Au bout de la ruelle se trouvent de lourdes poubelles et une grande cloture." $) cyoa-text! basic-tone
		card7-tryflip
		choice( "Utiliser les capacites de Parkour" )choice: card7-parkour
		choice( "Jeter des ordures" )choice: card7-throw
;

// END Parkouuur

} }

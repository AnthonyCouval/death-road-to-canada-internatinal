uses base.df
uses charfilt.df
uses charstats.df
uses road.df

(* Basic Template
: EFFECT cyoa-good-dialog ($ cname .. " le groupe "  .. someone .colourname .. "." $) cyoa-text!
: OLDERSTYLE "EXEMPLE D'EVENEMENT : CARTE 1A" $cyoa-ok-dialog 0 cyoa-image-icon! basic-tone ;
: card1-cyoa
	cyoa-start
	" " cyoa-title!
	0 to cyoaicon cyoaicon
	" " cyoa-text! basic-tone
	choice( "CETTE SECTION CONTIENT UNE DESCRIPTION DE L'APPLICATION OU DU PRODUIT EN QUESTION." )choice: EFFECT
	// NOTE that only 4 choices are shown max, with choices on top/first taking priority.
	; 
*)

(* CYOA standard types:
1. INSTANT RESULT - Example: Muscle Fan
No choice! Something happens instantly. Can do simple or complex rolls to modify the result. The most board-game like option. Keep these rare.
2. PERSON PICK - Example: Candy Finding
You're given an action and then have to pick a person to perform that action. The check/roll can be simple or complex. It should usually be a choice of up to 3 people, then a choice for an alternate group option. This makes things more interesting if you're currently solo.
3. ACTION PICK - Example: Traffic Jam, Quiet Town, Abandoned Gun Range
You're given a situation and provided 2-3 choices as to what to do. The choices then pick a person to carry out that action based on stats, or use the whole group. Gun Range has one of its options change to PERSON PICK.
4. SPOTLIGHT - Example: Parkouuur
A situation is presented and then a person is chosen from your group at random. You're given two, rarely three options for that person to perform. They can be pretty complex rolls or checks. The stats used for the rolls are then revealed, whether the check was good or bad.

REVEALED STATS
It's up to the CYOA writer as to whether or not the result should reveal stats to the group. Generally, loyalty is really rare to reveal, where something like making a really great shot in CYOA reveals your shooting stat outright.

REVEALED STAT SPECIAL OPTIONS - Examples: Parkouuur
All CYOA types have the option of checking your party for combinations of high, or really low, revealed stats. If the checks are successful, additional options can be added to the list of choices. Generally these should use combinations of stats instead of single stats, to make them rarer to trigger. If you can think of one or two of these to include in a CYOA, do so, but overall it should be hard for a single character to get a lot of these in succession.

See charstats.df and charfilt.df for details on what the words do

*)


cyoa{ road{

// Abandoned Gun Range
// Example of a GRAB BAG style CYOA. Give someone a point of shooting, get some ammo instead, or gamble health.
 
 // ammosearch1 gives ammo. See the bottom of chartstats.df for loot words.
: card1-search ($ Thegroup .. " trouve une petite quantite de munitions." $) $cyoa-ok-dialog ammosearch1 goodfx cyoaicon ;

// One selected person shoots
cyoa-choice: card1-picked
	"Pratiquer la cible" cyoa-title!
	
	chumany? if
		($ cname .. " pratiquez un peu le tir a l'arc, ... 		les cibles etaient meme thematisees zombies !" $) cyoa-text! +cr goodfx
		shootingREV shooting++
	else
		($ cname .. " mord sur un vieux fusil rouille pendant quelque temps ... il s'etreint." $) cyoa-text! morale2+ goodfx
	then
;

cyoa-choice: card1-one 
"Quelqu'un devrait passer du temps a la pratique de tir." cyoa-text! 
	' card1-picked cyoa-team-choice
;
// For entire group, individually rolls a 40% chance of gaining a point in shooting skill.
: card1-spray 
	40 %chance if 
		dup humany? if 
			.shooting++ 
		else 
			.morale-- 
		then
	else 
		.ouch-- 
	then 
;

cyoa-choice: card1-group "Tous decident de faire exploser simultanement les cibles dans le caveau a feu au noir. aucune pensee n'est faite pour savoir si c'est une bonne idee. faites-les exploser tout de meme." cyoa-text! badfx
	' card1-spray foreach-team-char 
	// Note syntax: ' <single word, make shortcut if more> foreach-team-char
	someone .ouch--
;

cyoa: card1-cyoa
	"Plage d'armes abandonnee" cyoa-title!
	0 to cyoaicon cyoaicon // picks a CYOA image, displays it using "ceci n'est qu'une chaine de caracteres"
	($ Thegroup .. " recherche une plage de tir a feu. elle est vide et presque depouillee de fournitures. cependant, il y a encore quelques choses laissees derriere. " .. gthey .. " trouver" .. g1s .. " des cibles de tir et une pistolet en fonctionnement." $) cyoa-text! basic-tone
	1 WEAPON_PISTOL trunk.weapon+
	choice( "Rechercher de la munition" )choice: card1-search
	// SOLO CHOICE, cancels group choices after it if solo
	solo if
		soloer charchoice( " tir sur les trains" )choice: card1-picked
	then;
	choice( "Formerment le groupe aleatoirement :" )choice: card1-group
	choice( "Formerement instruire une seule personne." )choice: card1-one
;

// END Abandoned Gun Range

// Muscle Fan
// Example of an INSTANT RESULT CYOA with no choices.

: card2-charm
	cpet? if
		cdog? if
			($ cname .. " regarde la femme avec des yeux de chien amoureux." $)
		else
			($ cname .. " se renverse et se comporte tout adorablement." $)
		then +crtext ($ "Elle donne. " .. cname .. " une grande quantite de nourriture !" $) +crtext	7 10 rnd trunk-food+
	else
		($ cname .. " convainc-la de vous fournir des details supplementaires. au lieu de cela, elle vous offre des excellents conseils d'exercice." $) +crtext
		witsREV attitudeREV morale++ strength++ strengthREV
	then
;
: card2-jerk 
	cpet? if
		($ cname .. " courbe en avant, de maniere nerveuse et intense. " .. cbarkmeow .. "elle est la femme." $) +crtext	
	else
		($ cname .. " cruellement appelle la femme musclard" $) +crtext
	then
	aholeREV morale2+
;
: card2-pumped ($ cname .. " se sent excitement a l'egard de cette histoire et s'assoit sur un tronc de bois proche." $) +text cyoa-text-cr
	strengthREV	strength++ morale++
;
cyoa: card2-cyoa ($ someone .colourname .. " un survivant vous rencontre qui blabille a propos d'une personne goudronnee vue, trop musclee pour maitriser, qui arrache les zombies en deux apres avoir presse sur eux au banc." $) cyoa-text! +cr 1 cyoa-image-icon! 4 500 tone 

	strongest-pass
	if strongest choosechar card2-pumped then;

	most-charming
	if choosechar card2-charm then;
	
	most-ahole
	if choosechar card2-jerk then;

	solo if ($ soloer .colourname .. " ce qu'il s'est passe, cela reste un mystere pour lui." $) +text then;

	"Tous les membres de votre groupe regardent les uns les autres avec inquietude." +text
;

// END Muscle Fan

// Quiet Town
// Rest up if you have food, find some ammo for no penalty, or get more supplies in exchange for health if you don't have a paranoid character.

: card3-3rest 60 %chance if .morale2+ else .morale3+ then ;
cyoa-choice: card3-3day ($ Thegroup .. " se choisit une des maisons pour barricader, puis y repos pendant quelques jours. c'est une pause agreable, bien qu'elle devienne un peu ennuyeuse en fin de compte." $) cyoa-text! basic-tone 
	sleep team-count -6 * LOOT_FOOD trunk.loot+ 3 calendar-day+
	' card3-3rest foreach-team-char heal-event
;
cyoa-choice: card3-1day ($ Thegroup .. " seulement une des maisons est barree et il y a une pause pour qu'il puisse se reposer assez." $) cyoa-text! basic-tone 
	sleep team-count -2 *  LOOT_FOOD trunk.loot+ 1 calendar-day+
	' .morale++ foreach-team-char heal-event
;
cyoa-choice: card3-qloot ($ Thegroup .. " effectue une recherche rapide des endroits les plus evidents pour stocker des approvisionnements. des munitions sont decouvertes." $) cyoa-text! basic-tone ammosearch1 ;

: card3-zomb 
	most-paranoid
	if choosechar
		($ cname .. "il ne lui laisse pas le temps de s'etonner et parvient a l'eviter facilement." $) +text 50 %chance if witsREV else attitudeREV then goodfx
	else
		someone choosechar ($ "Desole, le zombie a trouve. " .. cname .. " premierement." $)
		+text ouch2- bitefx
	then
	cicon // forces the icon to be the chosen character
;
cyoa-choice: card3-search ($ Thegroup .. " il parcourt la ville a la recherche de fournitures abandonnees, mais tombe dans un piege tendu par un seul zombie. " $) cyoa-text! card3-zomb ammosearch1 homesearch1 ;

cyoa: card3-cyoa
	"Ville tranquille" cyoa-title!
	"Il est si tranquille ici. il n'y a pas de signes de vie ou de vie inanimee. il semble que tout le monde ait pris ce qu'il pouvait et ait evacue cette ville des que possible." cyoa-text! 0 to cyoaicon cyoaicon basic-tone
		choice( "Explorer la ville avec soin et determination, ne laissez rien de cote." )choice: card3-search
		choice( "Rapprocher rapidement et fuir" )choice: card3-qloot
		trunk-food team-count 2 * 1 - > if 
			choice( "Repeter une journee de repos" )choice: card3-1day
		then
		trunk-food team-count 6 * 1 - > if 
			choice( "Repetez une pause de trois jours." )choice: card3-3day 
		then
;

// END Quiet Town

// Lost Stronghold
// Ultra simple DO A TOUGH CITY EVENT OR DON'T choice.

cyoa: card4-cyoa
	"Le chateau perdu" cyoa-title!

	"Cet immeuble d'appartements a ete transforme en forteresse barree. il a ete totalement envahi par une horde de zombies. le point positif, ils semblent assez apaises pour le moment." cyoa-text!
	1.3 1.7 frnd 9 15 frnd 0 1 rnd zforecast
	"events/inner/region/hellhotel.df" $load-region
		choice( "Assaut au fort" )choice: premission
		gas-draw
;

// END Lost Stronghold

// Traffic Jam
// Examples of manual stat checks in this one. Mostly we should use the shortcuts like cwits+

: card5-wacheck cchar .attitude 4 < cchar .wits 3 > and ;

cyoa-choice: card5-walk good
	($ Thegroup .. " abandonne la voiture ancienne et commence a marcher parmi les voitures, a chercher des approvisionnements en chemin." $) cyoa-text! car-abandon ammosearch1
;

: card5-detadd
	card5-wacheck if
		good "La detour fonctionne mieux que prevu !"
	then;

	""

	cchar .attitude 3 > if 
		bad "Cette voie etait trop optimiste, elle a dure des jours ! " $cat
	then 

	cchar .wits 4 < if 
		bad "La route s'est revelee entourageux de dechiques et le voiture a subi des dommages." $cat
	then
;

: card5-detadd2
	card5-wacheck if
		morale++
	then;

	cchar .wits 4 < if 
		 morale-- -2 car-chassis+
	then

	cchar .attitude 3 > if 
		+cr fullcamp 
		trunk-gas 69 > if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			car-mpg 0 > if bandit-gas then
		then heal-event
	then;

;

cyoa-choice: card5-detour

	chumany? if 
		($ cname .. " prend la decision de faire un detour. " .. card5-detadd $) 
	else
		($ cname .. " aboie a travers la fenetre sur un sentier cache et enfrichi ! " .. card5-detadd $)
	then cyoa-text!

	witsREV	attitudeREV	card5-detadd2
;

cyoa-choice: card5-bench
	($ cname .. " pressions du vehicule detruit au banc. cela apporte peu de choses pour eliminer les debris, et votre voiture doit toujours etre abandonnee." $) cyoa-text! goodfx
	car-abandon
	morale++ strengthREV strength++ 
;

: card5-mudadd2 cwits+ if morale++ witsREV then;
	morale-- ouch--all -3 car-chassis+ ;

: card5-mudadd cwits+ if
		good "Avec un melange de colere et d'intuition, la voiture ne s'accidente pas."
	then;

	bad "Ca marche bien jusqu'a ce que la voiture heurte une roche."
;

cyoa-choice: card5-mudding
	($ cname .. " traverse le epaule pres de la route. " $) card5-mudadd $cat cyoa-text!
	temperREV
	card5-mudadd2
;

cyoa: card5-cyoa
	"Embouteillage routier." cyoa-title!
	"Une mer de voitures obstrue la route devant vous. le barrage est si important que il n'y a pas clairement une issue evidente." cyoa-text!
		vehicle.which@ 0 = if 
			($ "Sans voiture, c'est pas un probleme pour passer a pied! " .. Thegroup .. " verifie les voitures pour approvisionnements sur la route." $) +crtext ammosearch1 goodfx
		then;
		angriest-pass if
			angriest choosechar chumany? if cchar charchoice( ": conduite agressive en terrain difficile !" )choice: card5-mudding then
		then

		strongest-pass if
			strongest charchoice( ": pressage d'une voiture au banc" )choice: card5-bench
		then

		smartest charchoice( "trouvez une nouvelle itineraire." )choice: card5-detour

		choice( "Quitter la voiture et marcher a pied." )choice: card5-walk
;

// END Traffic Jam

// Candy finding
// Pretty similar GRAB BAG to Abandoned Gun Shop, but with interesting solo options.

: candysharesolo soloer .morale-- soloer .strength++ soloer .temper-- soloer .attitude-- ;
: candyshareroll args( @ )
	<- @ .loyaltyREV 
	<- @ .loyalty 4 < if
		<- @ .morale--
	else
		<- @ .morale++
	then
;

cyoa-choice: candyshare
solo if 
	bad ($ soloname .. " est vraiment gene par le fait que la bonbonne doit etre partagee aux amis, quels qu'ils soient imaginaires. " .. soloer xHe .. " il s'en prend a la bonbonne et la ecrase avec colere. maintenant, personne ne peut en avoir !" $) cyoa-text! candysharesolo
else 
	good ($ "Chaque personne apprend peut-etre une lecon precieuse sur le partage en essayant de diviser deja un bonbon minuscule. peut-etre. a peu pres." $) cyoa-text! ' candyshareroll foreach-team-char
then
;

cyoa-choice: candyresult
	"C'est vraiment un miniaturise" cyoa-title!
	cdog? if
		($ "Vous avez donne une barre de chocolat au chien ! cela ne devrait pas etre fait car les chiens ne doivent pas manger du chocolat !" $) text!
		bitefx ouch-- morale2+
	then;
	($ cname .. " mange cette bonbon, a grand renfort. sa saveur sucree est enorme. " .. chim .. " se sentir un peu moins mal " .. chis .. " la vie actuelle n'est pas au canada." $) cyoa-text! cyoa-text-cr goodfx
	solo if 
		cyoa-text-cr ($ cHe .. " se sent assez satisfait de ne pas devoir partager." $) +text attitude++ attitudeREV
	then
	morale2+ cyoa-text-cr 
;

cyoa-bridge: candyresult-pick 
	someicon
	"Quelqu'un devrait manger la petite tablettes de chocolat ?" cyoa-text!
	' candyresult cyoa-team-choice
;

cyoa: card6-cyoa
	"Barre de chocolat heureuse" cyoa-title! 
	0 to cyoaicon cyoaicon
	($ Thegroup .. " quelqu'un trouve une tablete de chocolat sur le bord de la route ! c'est une taille 'divertissante' ... 	qui en mange ?" $) cyoa-text! basic-tone
	team-count 3 > if
		' candyresult 2 #cyoa-team-choice
		choice( "Choisissez quelqu'un d'autre." )choice: candyresult-pick 
	else
		' candyresult 3 #cyoa-team-choice
	then
	choice( "Partagez-le avec tout le monde" )choice: candyshare 
;

// END Candy finding

// Parkouuur - A Spotlight CYOA. Also has an extra TRUMP option if you have revealed stats.
// Pretty standard layout for stat checks that we should reuse frequently. Result if over 4, result if under 2, if average do a stat roll which amounts to a weighted coin toss.

: card7-pcheck 
	cpet? if
		($ cname .. " une manoeuvre douce de parkour consistant simplement a passer entre les jambes des zombies. quelles que soient !" $) cyoa-text! goodfx morale++
	then;
	
	cfitness+ if
		($ cname .. " fait-il une instruction ?" .. RED .. " SAUVAGEMENT, SAUT DE PARKOUR" .. WHITE .. " ecarter quelques dechets et les jeter au-dela de la cloture. pas de gros souci. c'est trop insignifiant pour faire une grande affaire. " .. cname .. "." $) cyoa-text! goodfx morale++ 
	then;
	
	cfitness- if
		($ cname .. " se fait maladroitement a gravir le passage de cloture, mais est bien trop eloigne de la forme physique necessaire. les zombies attaquent." $) cyoa-text! ouch-- bitefx
	then;
	
	fitnessroll if
		($ cname .. " parvient a gravir le passage au temps, echappant pour le moment aux dechirures. merci dieu du parcours urbain." $) cyoa-text! goodfx
		fitness++ 
	then;
	
		($ cname .. " lutte pour monter au-dessus du clotis, un zombie parvient a mordre ma jambe." $) cyoa-text! bitefx sting--
;
cyoa-choice: card7-parkour card7-pcheck fitnessREV ;

// Doesn't check Strength for high/low, just does a roll no matter what. Advantage: Can use it to go from 5 to 6 str. Disadvantage: Pointless and dangerous at 6.
: card7-scheck 
	cpet? if
		($ cname .. " comprend le concept de jeter des choses, mais n'est reellement pas performant a cause du manque de doigts-pouce." $) cyoa-text! bitefx ouch--
	then;
	strengthroll if
		($ cname .. " jete plusieurs zombies au sol en les renversant avec des poubelles. " .. cHe .. " echappements et se fait une belle exercise en meme temps." $) cyoa-text! goodfx
		strength++ morale++
	then;
		($ cname .. " essaye de se defendre contre les zombies en jetant des poubelles sur eux, mais ne parvient pas a jeter assez pour les arreter dans leurs agressions." $) cyoa-text! bitefx ouch--
;
cyoa-choice: card7-throw card7-scheck strengthREV ;

cyoa-choice: card7-flip
($ cname .. " Se fait completement perdue et jette des choses contre les zombies, suivi d'une ecrasement sur eux. cela est tres efficace et bien sur tres therapeutique. " .. cname .. "." $)
cyoa-text! goodfx
	morale++ temper++ strength++ temperREV strengthREV
;
: card7-tryflip 
	cchar angrystrong-revealed? not if then;
	cchar angrystrong? if
		choice( "SE DECHAINER COMPLETEMENT, SANS RESERVE." )choice: card7-flip
	then
;

cyoa: card7-cyoa
	"Lutter ou renverser" cyoa-title!
	choosehuman cicon
	($ cname .. " se fait tromper lors de l'exploration d'une ville et se trouve encercle par un grand groupe de zombies, arme de rien. . ." .. cHe .. " est bloque dans une impasse. a la fin de l'impasse se trouve une grande quantite de dechets lourds et une grande cloture." $) cyoa-text! basic-tone
		card7-tryflip
		choice( "Utilisez vos competences de parkour." )choice: card7-parkour
		choice( "Jeter les dechets" )choice: card7-throw
;

// END Parkouuur

} }

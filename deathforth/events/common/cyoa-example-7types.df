uses base.df
uses charfilt.df
uses charstats.df
uses road.df

(* Basic Template
: EFFECT cyoa-good-dialog ($ cname .. " 'pupst auf "  .. someone .colourname .. "." $) cyoa-text!
: OLDERSTYLE "'BEISPIEL EREIGNIS KARTE 1A" $cyoa-ok-dialog 0 cyoa-image-icon! basic-tone ;
: card1-cyoa
	cyoa-start
	" " cyoa-title!
	0 to cyoaicon cyoaicon
	" " cyoa-text! basic-tone
	choice( "'BESCHREIBUNG" )choice: EFFECT
	// NOTE that only 4 choices are shown max, with choices on top/first taking priority.
	; 
*)

(* CYOA standard types:
1. INSTANT RESULT - Example: Muscle Fan
No choice! Something happens instantly. Can do simple or complex rolls to modify the result. The most board-game like option. Keep these rare.
2. PERSON PICK - Example: Candy Finding
You're given an action and then have to pick a person to perform that action. The check/roll can be simple or complex. It should usually be a choice of up to 3 people, then a choice for an alternate group option. This makes things more interesting if you're currently solo.
3. ACTION PICK - Example: Traffic Jam, Quiet Town, Abandoned Gun Range
You're given a situation and provided 2-3 choices as to what to do. The choices then pick a person to carry out that action based on stats, or use the whole group. Gun Range has one of its options change to PERSON PICK.
4. SPOTLIGHT - Example: Parkouuur
A situation is presented and then a person is chosen from your group at random. You're given two, rarely three options for that person to perform. They can be pretty complex rolls or checks. The stats used for the rolls are then revealed, whether the check was good or bad.

REVEALED STATS
It's up to the CYOA writer as to whether or not the result should reveal stats to the group. Generally, loyalty is really rare to reveal, where something like making a really great shot in CYOA reveals your shooting stat outright.

REVEALED STAT SPECIAL OPTIONS - Examples: Parkouuur
All CYOA types have the option of checking your party for combinations of high, or really low, revealed stats. If the checks are successful, additional options can be added to the list of choices. Generally these should use combinations of stats instead of single stats, to make them rarer to trigger. If you can think of one or two of these to include in a CYOA, do so, but overall it should be hard for a single character to get a lot of these in succession.

See charstats.df and charfilt.df for details on what the words do

*)


cyoa{ road{

// Abandoned Gun Range
// Example of a GRAB BAG style CYOA. Give someone a point of shooting, get some ammo instead, or gamble health.
 
 // ammosearch1 gives ammo. See the bottom of chartstats.df for loot words.
: card1-search ($ Thegroup .. " 'findet eine kleine Menge Munition." $) $cyoa-ok-dialog ammosearch1 goodfx cyoaicon ;

// One selected person shoots
cyoa-choice: card1-picked
	"'Zielubungen" cyoa-title!
	
	chumany? if
		($ cname .. " 'kann etwas Zeit zum Uben mit dem Schießen verbringen. Selbst die Ziele waren zombieartig!" $) cyoa-text! +cr goodfx
		shootingREV shooting++
	else
		($ cname .. " 'kaut eine Weile auf einer alten rostigen Waffe herum. Sie wird schleimig." $) cyoa-text! morale2+ goodfx
	then
;

cyoa-choice: card1-one 
"'Mit wem sollte man ein paar Stunden mit Schießubungen verbringen?" cyoa-text! 
	' card1-picked cyoa-team-choice
;
// For entire group, individually rolls a 40% chance of gaining a point in shooting skill.
: card1-spray 
	40 %chance if 
		dup humany? if 
			.shooting++ 
		else 
			.morale-- 
		then
	else 
		.ouch-- 
	then 
;

cyoa-choice: card1-group "'Alle beschließen, alle Ziele gleichzeitig im Dunkeln des Schießstandes abzuschießen. Es wird keine Uberlegung daruber angestellt, ob das eine gute Idee ist. WAS AUCH IMMER." cyoa-text! badfx
	' card1-spray foreach-team-char 
	// Note syntax: ' <single word, make shortcut if more> foreach-team-char
	someone .ouch--
;

cyoa: card1-cyoa
	"'Verlassener Schießstand" cyoa-title!
	0 to cyoaicon cyoaicon // picks a CYOA image, displays it using "'cyoaicon"
	($ Thegroup .. " 'findet ein Gewehrschießstand. Er ist leer und hauptsachlich von Versorgungsmitteln bereitgestellt worden. Allerdings befinden sich noch Schießscheiben und eine funktionstuchtige Pistole da. " .. gthey .. " 'findet" .. g1s .. " 'Nach Munition suchen" $) cyoa-text! basic-tone
	1 WEAPON_PISTOL trunk.weapon+
	choice( "'Gruppe ungeordnet trainieren" )choice: card1-search
	// SOLO CHOICE, cancels group choices after it if solo
	solo if
		soloer charchoice( " 'schießt ausbilden" )choice: card1-picked
	then;
	choice( "'Gruppe haphazardisch ausbilden" )choice: card1-group
	choice( "'Eine Person vorsichtig ausbilden" )choice: card1-one
;

// END Abandoned Gun Range

// Muscle Fan
// Example of an INSTANT RESULT CYOA with no choices.

: card2-charm
	cpet? if
		cdog? if
			($ cname .. " 'macht Hundaugen bei der Frau." $)
		else
			($ cname .. " 'rollt sich auf und verhalt sich sehr nett." $)
		then +crtext ($ "'sie gibt " .. cname .. " 'eine Menge Essen!" $) +crtext	7 10 rnd trunk-food+
	else
		($ cname .. " ' uberredet die Frau, weitere Details zu geben. Stattdessen gibt sie jedoch hervorragende Trainingsanweisungen." $) +crtext
		witsREV attitudeREV morale++ strength++ strengthREV
	then
;
: card2-jerk 
	cpet? if
		($ cname .. " " .. cbarkmeow .. "sie sieht die Frau an." $) +crtext	
	else
		($ cname .. " 'Muskelnordchen'" $) +crtext
	then
	aholeREV morale2+
;
: card2-pumped ($ cname .. " fuhlt sich aufgepumpt wegen dieser Geschichte und setzt sich auf einen nahegelegenen Baumstamm." $) +text cyoa-text-cr
	strengthREV	strength++ morale++
;
cyoa: card2-cyoa ($ someone .colourname .. " 'Er hat die Zombies in zwei Halften gerissen, nachdem er sie zuerst auf der Bank gedruckt hat. Er war zu stark, um ihn zu kontrollieren.'" $) cyoa-text! +cr 1 cyoa-image-icon! 4 500 tone 

	strongest-pass
	if strongest choosechar card2-pumped then;

	most-charming
	if choosechar card2-charm then;
	
	most-ahole
	if choosechar card2-jerk then;

	solo if ($ soloer .colourname .. " wundert sich, was das alles sollte." $) +text then;

	"'Jeder in Ihrer Gruppe schaut sich gegenseitig verdachtig an." +text
;

// END Muscle Fan

// Quiet Town
// Rest up if you have food, find some ammo for no penalty, or get more supplies in exchange for health if you don't have a paranoid character.

: card3-3rest 60 %chance if .morale2+ else .morale3+ then ;
cyoa-choice: card3-3day ($ Thegroup .. " 'wahlt eines der Hauser aus, um es fur die Barrikade zu nutzen, und ruht dort einige Tage. Es ist ein willkommener Ruhepaus, obwohl es am Ende etwas langweilig wurde." $) cyoa-text! basic-tone 
	sleep team-count -6 * LOOT_FOOD trunk.loot+ 3 calendar-day+
	' card3-3rest foreach-team-char heal-event
;
cyoa-choice: card3-1day ($ Thegroup .. " 'wahlt eines der Hauser aus, um es fur die Barrikade zu nutzen und ruht darin." $) cyoa-text! basic-tone 
	sleep team-count -2 *  LOOT_FOOD trunk.loot+ 1 calendar-day+
	' .morale++ foreach-team-char heal-event
;
cyoa-choice: card3-qloot ($ Thegroup .. " 'sucht nach offensichtlichen Platzen, um Vorrate zu verstecken. Man findet Munition." $) cyoa-text! basic-tone ammosearch1 ;

: card3-zomb 
	most-paranoid
	if choosechar
		($ cname .. "', der stets paranoid ist, wird von dem Zombie nicht uberrascht und kann es leicht umgehen." $) +text 50 %chance if witsREV else attitudeREV then goodfx
	else
		someone choosechar ($ "'leidet jedoch darunter, dass das Zombie " .. cname .. " 'erst findet." $)
		+text ouch2- bitefx
	then
	cicon // forces the icon to be the chosen character
;
cyoa-choice: card3-search ($ Thegroup .. " 'sucht die Stadt grundlich durch und wird von einem einzelnen Zombie uberrascht. " $) cyoa-text! card3-zomb ammosearch1 homesearch1 ;

cyoa: card3-cyoa
	"'Stille Stadt" cyoa-title!
	"'Es ist so still hier. Es gibt keine Zeichen von Leben oder Unleben. Es scheint, als ob jeder das, was er nehmen konnte, aus dieser Stadt geflohen ist." cyoa-text! 0 to cyoaicon cyoaicon basic-tone
		choice( "'Diese Stadt grundlich durchsuchen" )choice: card3-search
		choice( "'Schnell rauben und fliehen" )choice: card3-qloot
		trunk-food team-count 2 * 1 - > if 
			choice( "'Ruhe fur einen Tag" )choice: card3-1day
		then
		trunk-food team-count 6 * 1 - > if 
			choice( "'Ruhe fur drei Tage" )choice: card3-3day 
		then
;

// END Quiet Town

// Lost Stronghold
// Ultra simple DO A TOUGH CITY EVENT OR DON'T choice.

cyoa: card4-cyoa
	"'Verlorenes Fort" cyoa-title!

	"'Dieses Apartmentgebaude wurde in eine befestigte Festung verwandelt. Es wurde vollstandig von einer Horde Zombies uberrannt. Auf der Plusseite scheinen sie im Moment recht ruhig zu sein." cyoa-text!
	1.3 1.7 frnd 9 15 frnd 0 1 rnd zforecast
	"events/inner/region/hellhotel.df" $load-region
		choice( "'Die Festung angreifen" )choice: premission
		gas-draw
;

// END Lost Stronghold

// Traffic Jam
// Examples of manual stat checks in this one. Mostly we should use the shortcuts like cwits+

: card5-wacheck cchar .attitude 4 < cchar .wits 3 > and ;

cyoa-choice: card5-walk good
	($ Thegroup .. " 'verlasst das alte Auto und geht zu Fuß weiter, sucht unterwegs in Autos nach Versorgung." $) cyoa-text! car-abandon ammosearch1
;

: card5-detadd
	card5-wacheck if
		good "'Der Umweg verlauft uberraschend gut!"
	then;

	""

	cchar .attitude 3 > if 
		bad "'Diese Route war zu optimistisch, sie dauerte Tage! " $cat
	then 

	cchar .wits 4 < if 
		bad "'Der Weg erwies sich als TOTALER SCHrott, und das Auto wurde beschadigt." $cat
	then
;

: card5-detadd2
	card5-wacheck if
		morale++
	then;

	cchar .wits 4 < if 
		 morale-- -2 car-chassis+
	then

	cchar .attitude 3 > if 
		+cr fullcamp 
		trunk-gas 69 > if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			car-mpg 0 > if bandit-gas then
		then heal-event
	then;

;

cyoa-choice: card5-detour

	chumany? if 
		($ cname .. " ' entscheidet sich fur einen Umweg. " .. card5-detadd $) 
	else
		($ cname .. " 'bellt aus dem Fenster zu einem verborgenen, uberwucherten Pfad! " .. card5-detadd $)
	then cyoa-text!

	witsREV	attitudeREV	card5-detadd2
;

cyoa-choice: card5-bench
	($ cname .. " 'druckt eine ruinierte Autotur. Das klart kaum das Wrack, und dein Auto muss immer noch verlassen werden." $) cyoa-text! goodfx
	car-abandon
	morale++ strengthREV strength++ 
;

: card5-mudadd2 cwits+ if morale++ witsREV then;
	morale-- ouch--all -3 car-chassis+ ;

: card5-mudadd cwits+ if
		good "'Mit einer Kombination aus Wut und Wahrnehmung sturzt das Auto nicht ab."
	then;

	bad "'Es lauft gut, bis das Auto gegen einen Felsen kracht."
;

cyoa-choice: card5-mudding
	($ cname .. "  pflugt durch die Schulter in der Nahe der Straße. " $) card5-mudadd $cat cyoa-text!
	temperREV
	card5-mudadd2
;

cyoa: card5-cyoa
	"Stau" cyoa-title!
	"Ein Meer von Autos blockiert die Straße vor uns. Der Auffahrunfall ist so schlimm, dass es keinen offensichtlichen Weg hindurch gibt." cyoa-text!
		vehicle.which@ 0 = if 
			($ "'It's not a problem to squeeze through on foot without a car!' " .. Thegroup .. " ' pruft die Autos auf Vorrate wahrend des Weges." $) +crtext ammosearch1 goodfx
		then;
		angriest-pass if
			angriest choosechar chumany? if cchar charchoice( "' Offroad aggressiv!" )choice: card5-mudding then
		then

		strongest-pass if
			strongest charchoice( "': Auto banken" )choice: card5-bench
		then

		smartest charchoice( "': Neue Route suchen" )choice: card5-detour

		choice( "'Auto verlassen und gehen" )choice: card5-walk
;

// END Traffic Jam

// Candy finding
// Pretty similar GRAB BAG to Abandoned Gun Shop, but with interesting solo options.

: candysharesolo soloer .morale-- soloer .strength++ soloer .temper-- soloer .attitude-- ;
: candyshareroll args( @ )
	<- @ .loyaltyREV 
	<- @ .loyalty 4 < if
		<- @ .morale--
	else
		<- @ .morale++
	then
;

cyoa-choice: candyshare
solo if 
	bad ($ soloname .. " ' ist wirklich erbost daruber, dass die Schokolade mit Freunden geteilt werden muss, obwohl diese imaginar sind. " .. soloer xHe .. " ' jascht und tritt auf das Schokoladenstuck. Nun kann niemand es haben!" $) cyoa-text! candysharesolo
else 
	good ($ "'Jeder lernt eine wertvolle Lektion uber das Teilen, indem er versucht, ein schon kleines Schokoladentafelchen zu teilen. Vielleicht. Irgendwie." $) cyoa-text! ' candyshareroll foreach-team-char
then
;

cyoa-choice: candyresult
	"'Es ist wirklich Fun-Size" cyoa-title!
	cdog? if
		($ "'Das Schokoladenstuck war Schokolade! Hunde sollten keine Schokolade essen!	Was hast du getan?" $) text!
		bitefx ouch-- morale2+
	then;
	($ cname .. " 'frißt das Schokoladenstuck, großartig. Die sußen Zuckerstoffe machen " .. chim .. " 'etwas besser uber " .. chis .. " current life in Canada." $) cyoa-text! cyoa-text-cr goodfx
	solo if 
		cyoa-text-cr ($ cHe .. " 'fuhlt sich ziemlich eingebildet, weil er nicht teilen muss.'" $) +text attitude++ attitudeREV
	then
	morale2+ cyoa-text-cr 
;

cyoa-bridge: candyresult-pick 
	someicon
	"'Wer soll die kleine Schokoladentafel essen?" cyoa-text!
	' candyresult cyoa-team-choice
;

cyoa: card6-cyoa
	"'Glucksschokoladentafel" cyoa-title! 
	0 to cyoaicon cyoaicon
	($ Thegroup .. " 'findet eine Schokoladentafel auf der Straße! Sie ist fun size. Wer darf sie essen?" $) cyoa-text! basic-tone
	team-count 3 > if
		' candyresult 2 #cyoa-team-choice
		choice( "'Wahle jemand anderen" )choice: candyresult-pick 
	else
		' candyresult 3 #cyoa-team-choice
	then
	choice( "'Teile es mit jedem" )choice: candyshare 
;

// END Candy finding

// Parkouuur - A Spotlight CYOA. Also has an extra TRUMP option if you have revealed stats.
// Pretty standard layout for stat checks that we should reuse frequently. Result if over 4, result if under 2, if average do a stat roll which amounts to a weighted coin toss.

: card7-pcheck 
	cpet? if
		($ cname .. " 'fuhrt einen sußen Parkour-Trick vor, bei dem er/sie einfach zwischen den Beinen der Zombies hindurchgeht. Was auch immer!" $) cyoa-text! goodfx morale++
	then;
	
	cfitness+ if
		($ cname .. " 'fuhrt einen" .. RED .. " 'SAHNEPARKOUR- SALTO" .. WHITE .. " 'von ein paar Mulltonnen und uber den Zaun. Kein großes Ding. Das ist alles viel zu einfach fur " .. cname .. "." $) cyoa-text! goodfx morale++ 
	then;
	
	cfitness- if
		($ cname .. " 'versucht, uber den Zaun zu klettern, ist dafur aber viel zu aus der Form geraten. Die Zombies greifen an." $) cyoa-text! ouch-- bitefx
	then;
	
	fitnessroll if
		($ cname .. " 'schafft es, sich rechtzeitig uber den Zaun zu schwingen und entkommt so den Zombies furs Erste. Gott segne das Parkouring." $) cyoa-text! goodfx
		fitness++ 
	then;
	
		($ cname .. " 'kampft sich uber den Zaun. Ein Zombie beißt ihm/ihr in die Beine." $) cyoa-text! bitefx sting--
;
cyoa-choice: card7-parkour card7-pcheck fitnessREV ;

// Doesn't check Strength for high/low, just does a roll no matter what. Advantage: Can use it to go from 5 to 6 str. Disadvantage: Pointless and dangerous at 6.
: card7-scheck 
	cpet? if
		($ cname .. " 'versteht das Prinzip des Werfens, ist aber nicht besonders gut darin, weil er/sie keine Daumen hat." $) cyoa-text! bitefx ouch--
	then;
	strengthroll if
		($ cname .. " 'knallt mehrere Zombies um, indem er/sie Mulltonnen wirft. " .. cHe .. " 'entkommt und bekommt gleichzeitig ein tolles Workout. Perfekt!" $) cyoa-text! goodfx
		strength++ morale++
	then;
		($ cname .. " 0" $) cyoa-text! bitefx ouch--
;
cyoa-choice: card7-throw card7-scheck strengthREV ;

cyoa-choice: card7-flip
($ cname .. " 1d20+5 =  25  " .. cname .. "." $)
cyoa-text! goodfx
	morale++ temper++ strength++ temperREV strengthREV
;
: card7-tryflip 
	cchar angrystrong-revealed? not if then;
	cchar angrystrong? if
		choice( "'GANZ AUFGERISSEN WERDEN" )choice: card7-flip
	then
;

cyoa: card7-cyoa
	"Kampfen oder Kippen" cyoa-title!
	choosehuman cicon
	($ cname .. " 	Er macht Fehler beim Ausspahen einer Stadt und wird von einer großen Gruppe von Zombies ohne Waffen in die Enge getrieben." .. cHe .. "  ist in einer Gasse gefangen. Am Ende der Gasse ist schwerer Mull und ein großer Zaun." $) cyoa-text! basic-tone
		card7-tryflip
		choice( "'Benutze Parkour-Fahigkeiten" )choice: card7-parkour
		choice( "'Werfen Sie Mull weg" )choice: card7-throw
;

// END Parkouuur

} }

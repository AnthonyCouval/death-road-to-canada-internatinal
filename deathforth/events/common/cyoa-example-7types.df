uses base.df
uses charfilt.df
uses charstats.df
uses road.df

(* Basic Template
: EFFECT cyoa-good-dialog ($ cname .. "  scoreggia su "  .. someone .colourname .. "." $) cyoa-text!
: OLDERSTYLE "ESEMPIO EVENTO CARTA 1A" $cyoa-ok-dialog 0 cyoa-image-icon! basic-tone ;
: card1-cyoa
	cyoa-start
	" " cyoa-title!
	0 to cyoaicon cyoaicon
	" " cyoa-text! basic-tone
	choice( "Italiano" )choice: EFFECT
	// NOTE that only 4 choices are shown max, with choices on top/first taking priority.
	; 
*)

(* CYOA standard types:
1. INSTANT RESULT - Example: Muscle Fan
No choice! Something happens instantly. Can do simple or complex rolls to modify the result. The most board-game like option. Keep these rare.
2. PERSON PICK - Example: Candy Finding
You're given an action and then have to pick a person to perform that action. The check/roll can be simple or complex. It should usually be a choice of up to 3 people, then a choice for an alternate group option. This makes things more interesting if you're currently solo.
3. ACTION PICK - Example: Traffic Jam, Quiet Town, Abandoned Gun Range
You're given a situation and provided 2-3 choices as to what to do. The choices then pick a person to carry out that action based on stats, or use the whole group. Gun Range has one of its options change to PERSON PICK.
4. SPOTLIGHT - Example: Parkouuur
A situation is presented and then a person is chosen from your group at random. You're given two, rarely three options for that person to perform. They can be pretty complex rolls or checks. The stats used for the rolls are then revealed, whether the check was good or bad.

REVEALED STATS
It's up to the CYOA writer as to whether or not the result should reveal stats to the group. Generally, loyalty is really rare to reveal, where something like making a really great shot in CYOA reveals your shooting stat outright.

REVEALED STAT SPECIAL OPTIONS - Examples: Parkouuur
All CYOA types have the option of checking your party for combinations of high, or really low, revealed stats. If the checks are successful, additional options can be added to the list of choices. Generally these should use combinations of stats instead of single stats, to make them rarer to trigger. If you can think of one or two of these to include in a CYOA, do so, but overall it should be hard for a single character to get a lot of these in succession.

See charstats.df and charfilt.df for details on what the words do

*)


cyoa{ road{

// Abandoned Gun Range
// Example of a GRAB BAG style CYOA. Give someone a point of shooting, get some ammo instead, or gamble health.
 
 // ammosearch1 gives ammo. See the bottom of chartstats.df for loot words.
: card1-search ($ Thegroup .. " trova una piccola quantita di munizioni." $) $cyoa-ok-dialog ammosearch1 goodfx cyoaicon ;

// One selected person shoots
cyoa-choice: card1-picked
	"Esercitazioni di tiro" cyoa-title!
	
	chumany? if
		($ cname .. " ha un po di tempo per esercitarsi nel tiro. 		I bersagli erano addirittura a tema zombie!" $) cyoa-text! +cr goodfx
		shootingREV shooting++
	else
		($ cname .. " rosicchia una vecchia pistola arrugginita per un po. 		Gli cola la bava." $) cyoa-text! morale2+ goodfx
	then
;

cyoa-choice: card1-one 
"Chi dovrebbe dedicare un po di tempo allesercitazione di tiro?" cyoa-text! 
	' card1-picked cyoa-team-choice
;
// For entire group, individually rolls a 40% chance of gaining a point in shooting skill.
: card1-spray 
	40 %chance if 
		dup humany? if 
			.shooting++ 
		else 
			.morale-- 
		then
	else 
		.ouch-- 
	then 
;

cyoa-choice: card1-group "Tutti decidono di sparare tutti insieme ai bersagli, nel poligono di tiro buio. Nessuno pensa se sia una buona idea o meno. QUALUNQUE COSA." cyoa-text! badfx
	' card1-spray foreach-team-char 
	// Note syntax: ' <single word, make shortcut if more> foreach-team-char
	someone .ouch--
;

cyoa: card1-cyoa
	"Poligono di tiro abbandonato" cyoa-title!
	0 to cyoaicon cyoaicon // picks a CYOA image, displays it using "cyoaicon"
	($ Thegroup .. " 'trova un poligono di tiro. E vuoto e per lo piu ripulito dalle scorte. Tuttavia, " .. gthey .. "  trova" .. g1s .. " alcuni bersagli di tiro e una pistola funzionante." $) cyoa-text! basic-tone
	1 WEAPON_PISTOL trunk.weapon+
	choice( "Cerca munizioni" )choice: card1-search
	// SOLO CHOICE, cancels group choices after it if solo
	solo if
		soloer charchoice( " si allena a sparare" )choice: card1-picked
	then;
	choice( "Addestra il gruppo alla rinfusa" )choice: card1-group
	choice( "Addestra con attenzione una persona" )choice: card1-one
;

// END Abandoned Gun Range

// Muscle Fan
// Example of an INSTANT RESULT CYOA with no choices.

: card2-charm
	cpet? if
		cdog? if
			($ cname .. " fa gli occhi dolci alla donna." $)
		else
			($ cname .. " si rotola e fa la carina." $)
		then +crtext ($ "Lei da " .. cname .. " una manciata di cibo!" $) +crtext	7 10 rnd trunk-food+
	else
		($ cname .. "  convince la donna a fornire maggiori informazioni. Lei invece offre qualche buona consiglio sui esercizi." $) +crtext
		witsREV attitudeREV morale++ strength++ strengthREV
	then
;
: card2-jerk 
	cpet? if
		($ cname .. " agitandosi " .. cbarkmeow .. "si guarda freneticamente verso la donna." $) +crtext	
	else
		($ cname .. " grida cruelmente tu sei una NERD MUSCOLARE." $) +crtext
	then
	aholeREV morale2+
;
: card2-pumped ($ cname .. " sentirsi POMPOSO su questa storia e si tuffa in un tronco vicino per fare dei pressioni." $) +text cyoa-text-cr
	strengthREV	strength++ morale++
;
cyoa: card2-cyoa ($ someone .colourname .. " ' incontra un altro sopravvissuto che grida di aver visto un corpo oleoso, che era troppo SWOLE per controllare, spezzando zombie in due dopo averli premuti a forza." $) cyoa-text! +cr 1 cyoa-image-icon! 4 500 tone 

	strongest-pass
	if strongest choosechar card2-pumped then;

	most-charming
	if choosechar card2-charm then;
	
	most-ahole
	if choosechar card2-jerk then;

	solo if ($ soloer .colourname .. " si chiede cosa fosse tutto quello." $) +text then;

	"Tutti nel tuo gruppo guardano luno laltro nervosamente." +text
;

// END Muscle Fan

// Quiet Town
// Rest up if you have food, find some ammo for no penalty, or get more supplies in exchange for health if you don't have a paranoid character.

: card3-3rest 60 %chance if .morale2+ else .morale3+ then ;
cyoa-choice: card3-3day ($ Thegroup .. " 'scelge una delle case per barricare, poi riposa li per qualche giorno. E un bel pausa, anche se stava diventando noiosa alla fine." $) cyoa-text! basic-tone 
	sleep team-count -6 * LOOT_FOOD trunk.loot+ 3 calendar-day+
	' card3-3rest foreach-team-char heal-event
;
cyoa-choice: card3-1day ($ Thegroup .. " barricata una casa e prende un po di riposo." $) cyoa-text! basic-tone 
	sleep team-count -2 *  LOOT_FOOD trunk.loot+ 1 calendar-day+
	' .morale++ foreach-team-char heal-event
;
cyoa-choice: card3-qloot ($ Thegroup .. " cerca velocemente nei posti piu ovvi per nascondere le provviste. Trova un po di munizioni." $) cyoa-text! basic-tone ammosearch1 ;

: card3-zomb 
	most-paranoid
	if choosechar
		($ cname .. ", sempre paranoico, non e sorpreso dallo zombi e lo evita facilmente." $) +text 50 %chance if witsREV else attitudeREV then goodfx
	else
		someone choosechar ($ "Sfortunatamente lo zombi ha trovato " .. cname .. "  per primo." $)
		+text ouch2- bitefx
	then
	cicon // forces the icon to be the chosen character
;
cyoa-choice: card3-search ($ Thegroup .. "  perlustra la citta alla ricerca di rifornimenti rimasti indietro, ma viene attaccato da un singolo zombi. " $) cyoa-text! card3-zomb ammosearch1 homesearch1 ;

cyoa: card3-cyoa
	"Citta Tranquilla" cyoa-title!
	"'E cosi tranquillo qui. Non ci sono segni di vita o non vita. Sembra che tutti abbiano preso quello che potevano e abbiano evacuato immediatamente questa citta." cyoa-text! 0 to cyoaicon cyoaicon basic-tone
		choice( "Saccheggia accuratamente la citta" )choice: card3-search
		choice( "Saccheggia rapidamente e scappa" )choice: card3-qloot
		trunk-food team-count 2 * 1 - > if 
			choice( "Riposati per un giorno" )choice: card3-1day
		then
		trunk-food team-count 6 * 1 - > if 
			choice( "Riposati per tre giorni" )choice: card3-3day 
		then
;

// END Quiet Town

// Lost Stronghold
// Ultra simple DO A TOUGH CITY EVENT OR DON'T choice.

cyoa: card4-cyoa
	"Roccaforte perduta" cyoa-title!

	"Questo condominio e stato trasformato in una fortezza barricata. E stato completamente invaso da unorda di zombi. Il lato positivo e che sembrano abbastanza docili per ora." cyoa-text!
	1.3 1.7 frnd 9 15 frnd 0 1 rnd zforecast
	"events/inner/region/hellhotel.df" $load-region
		choice( "Attacca la Fortezza" )choice: premission
		gas-draw
;

// END Lost Stronghold

// Traffic Jam
// Examples of manual stat checks in this one. Mostly we should use the shortcuts like cwits+

: card5-wacheck cchar .attitude 4 < cchar .wits 3 > and ;

cyoa-choice: card5-walk good
	($ Thegroup .. "  abbandona la vecchia macchina e inizia a camminare, cercando rifornimenti nelle macchine lungo la strada." $) cyoa-text! car-abandon ammosearch1
;

: card5-detadd
	card5-wacheck if
		good "La deviazione va inaspettatamente bene!"
	then;

	""

	cchar .attitude 3 > if 
		bad "Prendere questa strada e stato troppo ottimista, e durata per giorni! " $cat
	then 

	cchar .wits 4 < if 
		bad "La strada si e rivelata una VERA SCHIFEZZA, e la macchina e rimasta danneggiata." $cat
	then
;

: card5-detadd2
	card5-wacheck if
		morale++
	then;

	cchar .wits 4 < if 
		 morale-- -2 car-chassis+
	then

	cchar .attitude 3 > if 
		+cr fullcamp 
		trunk-gas 69 > if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			car-mpg 0 > if bandit-gas then
		then heal-event
	then;

;

cyoa-choice: card5-detour

	chumany? if 
		($ cname .. " decide di fare un giro di perlustrazione. " .. card5-detadd $) 
	else
		($ cname .. " abbaia alla finestra di un sentiero nascosto e invaso! " .. card5-detadd $)
	then cyoa-text!

	witsREV	attitudeREV	card5-detadd2
;

cyoa-choice: card5-bench
	($ cname .. " " $) cyoa-text! goodfx
	car-abandon
	morale++ strengthREV strength++ 
;

: card5-mudadd2 cwits+ if morale++ witsREV then;
	morale-- ouch--all -3 car-chassis+ ;

: card5-mudadd cwits+ if
		good "Con una combinazione di essere incazzato e perspicace, la macchina non si schianta."
	then;

	bad "Va tutto bene finche l'auto non sbatte contro una roccia."
;

cyoa-choice: card5-mudding
	($ cname .. " attraversa larcata vicino alla strada " $) card5-mudadd $cat cyoa-text!
	temperREV
	card5-mudadd2
;

cyoa: card5-cyoa
	"Ingorgo" cyoa-title!
	"Un mare di auto blocca la strada davanti. Laccumulo e cosi grave che non ce una via duscita ovvia." cyoa-text!
		vehicle.which@ 0 = if 
			($ "Senza macchina, non e un problema passare a piedi " .. Thegroup .. " controlla le auto per rifornimenti lungo la strada." $) +crtext ammosearch1 goodfx
		then;
		angriest-pass if
			angriest choosechar chumany? if cchar charchoice( ": Offroading aggressivo!" )choice: card5-mudding then
		then

		strongest-pass if
			strongest charchoice( ": Panca una macchina" )choice: card5-bench
		then

		smartest charchoice( ": Trova una nuova strada" )choice: card5-detour

		choice( "Abbandona la macchina e cammina" )choice: card5-walk
;

// END Traffic Jam

// Candy finding
// Pretty similar GRAB BAG to Abandoned Gun Shop, but with interesting solo options.

: candysharesolo soloer .morale-- soloer .strength++ soloer .temper-- soloer .attitude-- ;
: candyshareroll args( @ )
	<- @ .loyaltyREV 
	<- @ .loyalty 4 < if
		<- @ .morale--
	else
		<- @ .morale++
	then
;

cyoa-choice: candyshare
solo if 
	bad ($ soloname .. " e davvero infastidito dal fatto che la caramella debba essere condivisa con gli amici, anche se sono immaginari " .. soloer xHe .. " infuria e calpesta la caramella. Adesso nessuno puo averla!" $) cyoa-text! candysharesolo
else 
	good ($ "Tutti imparano una preziosa lezione sulla condivisione cercando di dividere una barretta di caramelle gia minuscola. Forse. In un certo senso." $) cyoa-text! ' candyshareroll foreach-team-char
then
;

cyoa-choice: candyresult
	"E davvero divertente" cyoa-title!
	cdog? if
		($ "'La barretta di cioccolato era al cioccolato! I cani non dovrebbero mangiare cioccolato! Che cosa hai fatto?" $) text!
		bitefx ouch-- morale2+
	then;
	($ cname .. " mangia quella caramella, alla grande. La dolcezza zuccherina fa " .. chim .. " sentirsi un po meglio riguardo " .. chis .. " attuale vita nel non Canada." $) cyoa-text! cyoa-text-cr goodfx
	solo if 
		cyoa-text-cr ($ cHe .. " si sente piuttosto compiaciuto di non dover condividere." $) +text attitude++ attitudeREV
	then
	morale2+ cyoa-text-cr 
;

cyoa-bridge: candyresult-pick 
	someicon
	"Chi dovrebbe mangiare la minuscola barretta di cioccolato?" cyoa-text!
	' candyresult cyoa-team-choice
;

cyoa: card6-cyoa
	"Fortunata barretta di cioccolato" cyoa-title! 
	0 to cyoaicon cyoaicon
	($ Thegroup .. " 'trova una barretta di cioccolato sul ciglio della strada! E di dimensioni divertenti. Chi puo mangiarla?" $) cyoa-text! basic-tone
	team-count 3 > if
		' candyresult 2 #cyoa-team-choice
		choice( "Scegli qualcun altro" )choice: candyresult-pick 
	else
		' candyresult 3 #cyoa-team-choice
	then
	choice( "Condividilo con tutti" )choice: candyshare 
;

// END Candy finding

// Parkouuur - A Spotlight CYOA. Also has an extra TRUMP option if you have revealed stats.
// Pretty standard layout for stat checks that we should reuse frequently. Result if over 4, result if under 2, if average do a stat roll which amounts to a weighted coin toss.

: card7-pcheck 
	cpet? if
		($ cname .. " 'fa una mossa di parkour dolce che coinvolge solo una sorta di camminata attraverso le gambe degli zombi. Qualunque cosa!" $) cyoa-text! goodfx morale++
	then;
	
	cfitness+ if
		($ cname .. " fa un" .. RED .. " MALATA FINTA DI PARKOUR" .. WHITE .. " 'da un po' di spazzatura e oltre il recinto. Non e un grosso problema. E una mossa troppo banale per " .. cname .. "." $) cyoa-text! goodfx morale++ 
	then;
	
	cfitness- if
		($ cname .. " cerca goffamente di scavalcare la recinzione ma e troppo fuori forma per farlo. Gli zombi attaccano." $) cyoa-text! ouch-- bitefx
	then;
	
	fitnessroll if
		($ cname .. " riesce a salire sulla recinzione in tempo, evitando di essere fatto a pezzi per ora. Dio benedica il parkour." $) cyoa-text! goodfx
		fitness++ 
	then;
	
		($ cname .. "  lotta per scavalcare la recinzione. Uno zombie riesce a mordergli una gamba." $) cyoa-text! bitefx sting--
;
cyoa-choice: card7-parkour card7-pcheck fitnessREV ;

// Doesn't check Strength for high/low, just does a roll no matter what. Advantage: Can use it to go from 5 to 6 str. Disadvantage: Pointless and dangerous at 6.
: card7-scheck 
	cpet? if
		($ cname .. " ' capisce il concetto di lanciare oggetti, ma non e molto bravo a farlo perche non ha i pollici." $) cyoa-text! bitefx ouch--
	then;
	strengthroll if
		($ cname .. "  travolge diversi zombi lanciando bidoni della spazzatura. " .. cHe .. "  scappa e fa anche un ottimo allenamento." $) cyoa-text! goodfx
		strength++ morale++
	then;
		($ cname .. " ' cerca di combattere gli zombi lanciandogli bidoni della spazzatura, ma non riesce a lanciarne abbastanza per fermare il loro attacco." $) cyoa-text! bitefx ouch--
;
cyoa-choice: card7-throw card7-scheck strengthREV ;

cyoa-choice: card7-flip
($ cname .. " ' impazzisce COMPLETAMENTE e inizia a lanciare roba agli zombi e poi a calpestarli. E sia efficace che molto terapeutico per " .. cname .. "." $)
cyoa-text! goodfx
	morale++ temper++ strength++ temperREV strengthREV
;
: card7-tryflip 
	cchar angrystrong-revealed? not if then;
	cchar angrystrong? if
		choice( " IMPAZZISCE COMPLETAMENTE" )choice: card7-flip
	then
;

cyoa: card7-cyoa
	" Combatti o fuggi" cyoa-title!
	choosehuman cicon
	($ cname .. "  commette errori durante lesplorazione di una citta e viene accerchiato da un folto gruppo di zombi mentre e disarmato. 	" .. cHe .. "  e intrappolato in un vicolo. Alla fine del vicolo ci sono rifiuti pesanti e una grande recinzione." $) cyoa-text! basic-tone
		card7-tryflip
		choice( " Usa le abilita di parkour" )choice: card7-parkour
		choice( " Lancia spazzatura" )choice: card7-throw
;

// END Parkouuur

} }


cyoa{ road{ 

: camp-pic
	50 %chance if
		image-camp-1
	else
		image-camp-2
	then	
;

// Rares

cyoa-choice: common2-rare-tp-share
	someicon
	solo if
		($ cname .. "  realizes that this roll of toilet paper is a treasure that must not be given away." $) text! morale4+ goodfx 1 WEAPON_CARDBOARD trunk.weapon+
		zombop++
	then;
	"The team decides to try to share the roll of toilet paper." text!
	loyalty-average 2 < if
		"'It doesn't take them long to be erupt into a huge fight! The situation descends into an all out brawl. The toilet paper ends up getting torn apart by accident!" +crtext morale3-all badfx
	else
		loyalty-average 4 < if
			"'There's some inevitable fighting from having to share the TP, but the group manages to overcome their differences." +crtext morale2+all goodfx
		else
			"'The group joins forces around the toilet paper, mutually protecting it. Even after the TP is gone, the cardboard tube becomes a symbol for the group's unity." +crtext morale4+all goodfx
		then
	then
	' .loyaltyREV foreach-team-char 1 WEAPON_CARDBOARD trunk.weapon+
	zombop++
;

cyoa-choice: common2-rare-tp-trade
	someicon
	($ Thegroup .. " ' decides to sell the roll of toilet paper as soon as possible. It's not worth carrying something THAT valuable around!" $) text!
		50 %chance if
			"A settlement buys the roll of TP for 20 food! Not bad!" +crtext 20 trunk-food+
		else
			50 %chance if
				"'A shrewd merchant buys the TP for 12 food. Can't complain!" +crtext 12 trunk-food+
			else
				($ "A " .. "VERY" rbold .. "  parte interessada compra o TP por " .. "30 comida!" fbold $) +crtext 30 trunk-food+
			then
		then goodfx morale++all
;

cyoa: common2-rare-tp
	"Mais Valioso Que Ouro" cyoa-title!
	($ Thegroup .. "  encontra um " .. "ROLO DE PAPEL HIGIENICO INTOCADO" fbold .. " ' enquanto vasculhavam. O papel higienico foi a primeira coisa a acabar depois do apocalipse. Ainda ha toneladas de comida enlatada e municao deixadas na America. Papel higienico decente foi usado rapidamente. Este e o recurso mais raro possivel. ESCOLHA COM SABEDORIA:" $) text!
		solo if
			choice( "Use este presente precioso" )choice: common2-rare-tp-share
		else
			choice( "Compartilhe este presente precioso" )choice: common2-rare-tp-share
		then
		choice( "Negocie o o mais rapido possivel" )choice: common2-rare-tp-trade
;

cyoa-choice: common2-rare-zombmaster-run
	image-creepy-girl
	($ "O " .. "MESTRE DOS ZUMBIS" rbold .. "  monta uma pequena pista de obstaculos e ensina " .. thegroup .. "  como correr de uma forma mais eficiente e menos embaracosa!" $) text! goodfx
	' .dexterity++ foreach-team-char
;

: common2-rare-zomb-workout args( @ )
	<- @ .strengthREV <- @ .fitnessREV <- @ .strength++ <- @ .fitness++
;

cyoa-choice: common2-rare-zombmaster-fight
	image-creepy-girl
	($ "O " .. "MESTRE DOS ZUMBIS" rbold .. "  ilumina " .. thegroup .. "  sobre armas brancas com algumas dicas e alguns " .. "TREINAMENTO MUITO PERIGOSO." rbold $) text! goodfx bitefx
	' common2-rare-zomb-workout foreach-team-char sting--all
;

cyoa-choice: common2-rare-zombmaster-shoot
	image-creepy-girl
	($ "O " .. "MESTRE DOS ZUMBIS" rbold .. "  da " .. thegroup .. "  uma otima licao sobre tiro apropriado! Ela da um dever de casa para fazer uma caca de zumbi real e fornece um monte de municao e um rifle!" $) text! goodfx ' .shootingREV foreach-team-char ' .shooting++ foreach-team-char
	60 trunk-bullet+ 40 trunk-rifle-shell+ 30 trunk-shotgun-shell+ 1 WEAPON_RIFLE trunk.weapon+
;

cyoa-choice: common2-rare-zombmaster-tough
	image-creepy-girl
	($ "O " .. "MESTRE DOS ZUMBIS" rbold .. "  grunhe." .. thegroup .. "  como endurecer " .. ghis .. "  a pele contra mordidas de zumbis! Este processo envolve espanca la " .. ghim .. "  para cima." $) text! goodfx bitefx
	' .vitality++ foreach-team-char sting--all
;

cyoa: common2-rare-zombmaster
	image-creepy-girl "O Mestre Zumbi" cyoa-title!
	($ Thegroup .. " ' encontra uma mulher estranha usando oculos de protecao, um lenco e uma capa. Ela diz que e a " .. "MESTRE DOS ZUMBIS" rbold .. "  e que ensinara uma de suas dicas secretas de zumbis como um servico a humanidade." $) text! goodfx
		choice( "Aula de tiro zumbi" )choice: common2-rare-zombmaster-shoot
		choice( "Basta fugir constantemente" )choice: common2-rare-zombmaster-run
		choice( "Como lutar com corpo a corpo" )choice: common2-rare-zombmaster-fight
		choice( "Endureca contra mordidas" )choice: common2-rare-zombmaster-tough
;

// END rares

// Man trapped under cardboard thievery

0 value common2-cboardtype

: cboardprize
	common2-cboardtype case
		1 of "CASO" endof
		2 of "ESPINGARDA" endof
		3 of "FALCAO" endof
		4 of "REVOLVER DE TIROLHO " endof
		5 of "MACANA DE BATE" endof
		6 of "REVOLVER DE CALADAO" endof
	endcase
;

: cboardget
	common2-cboardtype case
		1 of 1 WEAPON_NIGHTSTICK endof
		2 of 8 trunk-shotgun-shell+ 1 WEAPON_SHOTGUN endof
		3 of 12 trunk-rifle-shell+ 1 WEAPON_RIFLE endof
		4 of 6 trunk-bullet+ 1 WEAPON_DOLT endof
		5 of 1 WEAPON_FIRE endof
		6 of 6 trunk-bullet+ 1 WEAPON_SNUBNOSE endof
	endcase
;

cyoa-choice: common2-cboard-steal
	($ cname .. " 'rouba o homem indefeso de seu " .. cboardprize rbold .. "." $) text! badfx
	cloyalty- if
		($ cHe .. " se sente muito satisfeito com isso." $) +crtext morale++
	else
		cloyalty+ if
			($ cHe .. " se sente incrivelmente culpado por isso!" $) +crtext morale3-
		else
			($ cHe .. " tem um" $) +crtext morale--
		then
	then loyaltyREV
	($ "Pegou um " rbold .. cboardprize rbold $) +crtext cboardget trunk.weapon+
;

: cboard-help-check args( @ )
	<- @ .cloyalty- if <- @ .loyaltyREV <- @ .morale-- then; <- @ .morale++
;

cyoa-choice: common2-cboard-help
	($ Thegroup .. " 'salva o homem dos caixas de papelao que o amarravam. Ele nao pode pagar uma recompensa, mas e realmente grato!" $) text! ' cboard-help-check foreach-team-char goodfx
;

cyoa-choice: common2-cboard-bandit
	($ cname .. " 'rouba o homem indefeso de seu " .. cboardprize rbold .. " e depois leva o resto de suas provisoes!" $) text! badfx
	rob-guilt	
	($ "Pegou um " rbold .. cboardprize rbold $) +crtext cboardget trunk.weapon+ 
	10 20 rnd trunk-bullet+ 5 15 rnd trunk-rifle-shell+ 3 12 rnd trunk-shotgun-shell+
	2 4 rnd trunk-medical+ 1 6 rnd trunk-food+
	aholeREV
;

cyoa-bridge: common2-cboard-steal4
	someicon
	"Quem deve roubar sua arma?" cyoa-text!
	' common2-cboard-steal cyoa-team-choice
;

cyoa: common2-cboard
	"Ajudando alguem em necessidade!" cyoa-title!
	1 6 rnd to common2-cboardtype
	($ "Enquanto vasculhava, " .. thegroup .. " ' encontra um homem coberto de caixas de papelao. Ele diz que esta preso la ha dias! Ele tem um " .. cboardprize rbold .. "  caido no chao fora de seu alcance." $) text!
	choice( "Ajude o a levantar" )choice: common2-cboard-help
	most-ahole if
		charchoice( ": Roube todos os seus suprimentos" )choice: common2-cboard-bandit
		team-count 2 > if
			choice( "Escolha um ladrao" )choice: common2-cboard-steal4
		then;
	then	
		
	solo if
		soloer choosechar choice( "Roube sua arma" )choice: common2-cboard-steal
	else
		"Alguem deveria roubar sua arma?" +crtext
		team-count 3 > if
			choice( "Escolha um ladrao" )choice: common2-cboard-steal4
		then 
		' common2-cboard-steal 3 #cyoa-team-choice
	then
;

// Quick template for the usual "escolha outra pessoa" choice
(*

cyoa-bridge: 4
	someicon
	"Quem deveria ?" cyoa-text!
	'  cyoa-team-choice
;

		team-count 3 > if
			'  2 #cyoa-team-choice
			choice( "Escolha outra pessoa" )choice: 4
		else

		then

*)

// Farm help

: common2-farm-reward
	8 trunk-food+
	most-charming if
		choosechar ($ cname .. "  convence os fazendeiros a darem " .. chim .. "  um forcado!" $) +crtext
		1 WEAPON_PITCHFORK trunk.weapon+
	then
;

cyoa-choice: common2-farm-mechanic
	($ cname .. "  oferece " .. chis .. "  servicos consertando coisas ao redor da fazenda. " $) text!
	cmechanic+ if	
		($ cHe .. " ' e tao habilidoso que a grande quantidade de trabalho nao e um grande problema." $) +text
	else
		($ "'Ha tantas coisas quebradas que " .. che .. "  fica exausto." $) +text cchar tired
	then
	mechanicREV mechanic++
	cchar .mechanical 3 < if
		($ "Os fazendeiros se recusam a dar uma recompensa depois que descobrem que tudo ainda esta quebrado." $) +crtext badfx
	else
		($ "Os fazendeiros sao gratos pelo trabalho!" $) +crtext goodfx common2-farm-reward
	then
;

cyoa-choice: common2-farm-bandit
	($ cname .. "  rouba os pacificos fazendeiros. Eles foram completamente pegos de surpresa contra uma ameaca nao zumbi." $) text! badfx
	14 trunk-food+ rob-guilt2 aholeREV
;

cyoa-choice: common2-farm-haul
	someicon
	($ Thegroup .. " ' transporta toneladas de coisas ao redor. Sempre ha coisas para transportar em uma fazenda. E um trabalho exaustivo e " .. gthey .. "  pegue" .. g1s .. "  muito cansado!" $) text! goodfx 
	' .strengthREV foreach-team-char ' .strength++ foreach-team-char common2-farm-reward
	// added STR check for scythe
	strongest-pass if strongest choosechar
		($ "Os fazendeiros notaram " .. cname .. "''s forca incomum e deram " .. chim .. "  uma foice de presente!" $) +crtext
		1 WEAPON_SCYTHE trunk.weapon+
	then
	nosleep
;

cyoa-choice: common2-farm-till
	someicon
	($ Thegroup .. " ' ajuda a arar os campos e plantar novas colheitas. E um trabalho exaustivo e " .. gthey .. "  pegue" .. g1s .. "  muito cansado!" $) text! goodfx 
	' .fitnessREV foreach-team-char ' .fitness++ foreach-team-char common2-farm-reward
	// added FIT check for scythe
	fittest-pass if fittest choosechar
		($ "Os fazendeiros notaram " .. cname .. "''s nivel de condicionamento fisico e deu " .. chim .. "  uma foice de presente!" $) +crtext
		1 WEAPON_SCYTHE trunk.weapon+
	then
	nosleep
;

cyoa: common2-farm
	"Uma Viagem a Fazenda" cyoa-title!
	($ Thegroup .. " 8 food" $) text!
		choice( "Lavoura e plantio" )choice: common2-farm-till
		choice( "Puxando e puxando" )choice: common2-farm-haul
		most-ahole if
			charchoice( "roube a fazenda" )choice: common2-farm-bandit
		then
		mechanicest	charchoice( ": Reparar trabalho" )choice: common2-farm-mechanic
		choice( "Nao tem tempo para ajudar" )choice
;

// Annoying diplomat

: common2-detoured
	vehicle.which@ 0 > if choice( "Fazer um desvio" )choice: car-detour else
		// ($ Thegroup .. "  pega um longo desvio a pe." $) +crtext 
		1 calendar-day+ +cr fullcamp
	then
;

cyoa-bridge: common2-meeting-talk
	($ cname .. "  fala com o representante chato." $) text!
	cpet? if
		($ cHe .. " ' nao e suposto ser capaz de falar, entao " .. che .. "  principalmente so lambe " .. chim .. "eu. O idiota e realmente insultado por ser feito para falar com um " .. cdogcat .. ", mas os guardas se divertem. Eles deixam " .. thegroup .. "  passar!" $) +crtext goodfx morale++all
	then;
	cchar grating? if
		($ cHe .. "  realmente se da bem! Eles principalmente trocam " .. "CALMA AI" pbold .. "  piadas entre si. Por uma hora. " .. Thegroup .. "  e deixado passar." $) +crtext witsattREV goodfx cool-it++ cool-it++
	then;
	cchar oblivious? if
		($ cHe .. " ' de alguma forma nao percebe todas as vezes que o idiota e irritante ou insultuoso. A reuniao vai bem e " .. thegroup .. " is let go." $) +crtext witsattREV goodfx
	then;
	cattitude+ if
		($ cHe .. " consegue ser muito educado. Depois de questionar o suficiente sem sentido " .. thegroup .. " e deixado passar pela barricada." $) +crtext attitudeREV goodfx
	then;
	cchar angrystrong? if
		($ cHe .. " perde " .. chis .. " pass. " .. thegroup .. "  atraves, de qualquer forma." $) +crtext attitudeREV strengthREV goodfx morale++all
	then;
	ctemper- if
		($ cHe .. " perde " .. chis .. "  tempera de imediato e comeca a gritar. Isso realmente assusta os guardas, e eles comecam a atirar. " .. Thegroup .. "  tem que fugir!" $) +crtext temperREV bitefx sting--all
		common2-detoured	
	then;
	ctemper+ temperroll or if
		($ cHe .. "  consegue controlar " .. chis .. "  temperamento! " .. Thegroup .. "  e deixado passar pela barricada apos algum aborrecimento adicional." $) +crtext goodfx
	else
		($ cHe .. "  tenta controlar " .. chis .. "  temperamento, mas o cara e muito chato. Ele corre e diz aos guardas para nao abrirem a porta. " .. Thegroup .. "  tem que encontrar um caminho alternativo!" $) +crtext badfx
		common2-detoured
	then temperREV
;

cyoa-bridge: common2-meeting-talk4
	someicon
	"Quem deve tentar lidar com a pessoa irritante?" cyoa-text!
	' common2-meeting-talk cyoa-team-choice
;

cyoa: common2-meeting
	"Diplomacia Calma" cyoa-title!
	($ Thegroup .. "  e bloqueado por outro grupo de sobreviventes que montou uma barricada para se proteger. Eles sao desconfiados e fortemente armados. A pessoa que eles enviam para falar e " .. "EXTREMAMENTE IRRITANTE" rbold .. "." $) text!
		solo if
			($ "  Ele continua dizendo coisas como " .. "CALMA AI" pbold .. "  e " .. "'Diga nao borrife." pbold $) +text
			soloer choosechar choice( "Tente nao perder a paciencia" )choice: common2-meeting-talk
		else
			"  Quem deveria tentar lidar com ele?" +text
			team-count 3 > if
				' common2-meeting-talk 2 #cyoa-team-choice
				choice( "Escolha outra pessoa" )choice: common2-meeting-talk4
			else
				' common2-meeting-talk 3 #cyoa-team-choice
			then
		then
		vehicle.which@ 0 > if smartest charchoice( ": Faca um desvio" )choice: car-detour then
;

// Park Chatting

: common2-parkchat-tipme args( @ )
	<- @ 40 %chance if 
		.shooting++ 
	else
		50 %chance if 
			.mechanic++
		else
			.medical++
		then
	then
;

cyoa-choice: common2-parkchat-tips
	cpet? if 
		($ cname .. " " .. cbarkmeow .. "s de uma forma muito sabia e experiente." $) text!
	else
		($ cname .. "  trocam conselhos sobre diferentes topicos." $) text!
	then
	cwits- if
		"O sobrevivente amigavel fica confuso com o conselho inutil." +crtext badfx witsREV
	then;
	cwits+ if
		"O sobrevivente amigavel fica iluminado pelo conselho e da otimas dicas proprias!" +crtext
		goodfx ' common2-parkchat-tipme foreach-team-char witsREV
	then;
		($ "O sobrevivente amigavel agradece e da um pequeno conselho a " .. cname .. "  em troca." $) +crtext
		cchar common2-parkchat-tipme goodfx witsREV
;

cyoa-choice: common2-parkchat-pep
	cpet? if 
		($ cname .. " " .. cbarkmeow .. "s de uma forma reconfortante." $) text!
	else
		($ cname .. "  tenta dar um discurso motivacional." $) text!
	then
	cattitude- if
		"O sobrevivente amigavel na verdade se sente pior agora." +crtext badfx attitudeREV
	then;
	cattitude+ if
		"O sobrevivente amigavel se sente muito otimista apos a conversa e faz um discurso inspirador!" +crtext
		morale2+all goodfx attitudeREV
	then;
		($ "O sobrevivente amigavel da algumas palavras tranquilizadoras em troca." $) +crtext
		morale++all goodfx attitudeREV
;

cyoa-choice: common2-parkchat-bandit
	($ cname .. " 'rouba facilmente a pessoa amigavel. Ele nao tinha muito, mas isso lhe ensinara uma licao importante sobre confiar nos outros." $) text! badfx
		2 4 rnd trunk-food+ 50 %chance if 1 3 rnd trunk-medical+ then rob-guilt
		aholeREV
;

cyoa-bridge: common2-parkchat-coolit
	cpet? if
		($ cname .. " comeca " .. "PEIDANDO" rbold .. " para a pessoa, e de alguma forma continua a faze lo por varios minutos sem parar por um momento." $)
	else
		($ cname .. " interrompe o sobrevivente amigavel no meio da frase, para lhe dizer para " .. "SE ACALMAR." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Cara Amigavel" recruitee .name!
		"'Ele fica pasmo com isso. Ele afirma que tudo isso esta de acordo com as profecias e que ele esta segurando um presente para o Escolhido!" +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Ele olha para " .. cname .. ", sem falar, ate " .. thegroup .. " vai embora." $) +crtext
		morale--all badfx
	then witsattREV 
;

cyoa: common2-parkchat
	"Como um dia normal" cyoa-title!
	($ Thegroup .. " encontra outra pessoa apenas relaxando em um banco de parque. Ele parece amigavel. Eles tem uma conversa, assim como antes do fim do mundo. Qual foi o assunto principal?" $) text!
		smartest charchoice( ": Troca de conselhos" )choice: common2-parkchat-tips
		gallant charchoice( ": Conversa estimulante" )choice: common2-parkchat-pep
		most-ahole if
			charchoice( ": Dando suas coisas para mim" )choice: common2-parkchat-bandit
		then
		most-grating if
			charchoice( ": Acalmando" fcoolit )choice: common2-parkchat-coolit
		then
;

// Busted leg

cyoa-choice: common2-busted-med
	($ cname .. " ' tenta tratar a perna quebrada da mulher." $) text!
	cmedical+ if
		($ cHe .. "  usa " .. chis .. "  conhecimento medico para consertar a perna dela rapidamente! Ela aprecia a ajuda e da o que pode!" $) +crtext morale++ medicalREV medical++ goodfx -1 trunk-medical+ 2 trunk-food+
	then;
	cmedical- if
		($ cHe .. "  nao faz ideia do que " .. che .. "  esta fazendo, desperdica um dia e talvez piore um pouco as coisas." $) +crtext medicalREV cloyalty- not if morale-- then loyaltyREV +cr fullcamp badfx -1 trunk-medical+ 1 calendar-day+
	then;
	medicalroll if
		($ cHe .. " ' leva um dia para ajudar a mulher a tratar sua perna. Ela e grata, mas nao pode poupar muito!" $) +crtext morale++ medicalREV medical++ goodfx 2 trunk-food+
	else
		($ cHe .. " ' tenta ajudar ao longo de um dia, mas nao consegue fazer muito. Ainda assim, a pratica leva a perfeicao." $) +crtext medicalREV medical++ cloyalty- not if morale-- then loyaltyREV badfx
	then +cr fullcamp -1 trunk-medical+ 1 calendar-day+ heal-event
;

cyoa-bridge: common2-busted-coolit
	cpet? if
		($ cname .. "  comeca a rolar no chao frio." $)
	else
		($ cname .. "  diz a mulher para " .. "SE ACALMAR." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Mulher de muleta" recruitee .name!
		($ "Ela entende isso como um conselho de que deve aplicar compressas frias na perna. Depois que " .. thegroup .. "  a ajuda com isso, ela oferece uma recompensa!" $) +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Ela pergunta " .. "O QUE HA DE ERRADO COM VOCE, EXATAMENTE?" pbold .. "  antes de mancar para longe em desgosto." $) +crtext
		morale--all badfx
	then witsattREV
;

cyoa-choice: common2-busted-bandit
	($ cname .. "  facilmente rouba da mulher ferida os poucos suprimentos que ela tinha deixado. Bom trabalho!" $) text! badfx
		2 4 rnd trunk-food+ rob-guilt aholeREV
;

cyoa: common2-busted
	"Perna Quebrada" cyoa-title!
	($ Thegroup .. "  encontra uma mulher mancando com uma muleta. Uma de suas pernas foi ferida em uma queda recente enquanto ela fugia de zumbis." $) text!
		trunk-medical 0 > if
			medicalest charchoice( ": Ajude sua perna" )choice: common2-busted-med
			choice( "Nao tem tempo para ajudar" )choice
		else
			($ Thegroup .. "  nao tem nenhum suprimento medico para ajuda la." $) +crtext
			choice( "Deixe a sozinha" )choice
		then
		most-grating if
			charchoice( ": Diga a ela para se ACALMAR" fcoolit )choice: common2-busted-coolit
		then
		most-ahole if
			charchoice( ": Roube os suprimentos dela" )choice: common2-busted-bandit
		then
;

// Car trouble

: common2-carhelp-win
	homesearch1 ammosearch1
;

cyoa-choice: common2-carhelp-mech
	($ cname .. " ' tenta consertar o carro quebrado do homem." $) text!
	cmechanic+ if
		($ cHe .. "  usa " .. chis .. "  conhecimento de carros para repara lo rapidamente! Ele agradece a ajuda e da alguns suprimentos!" $) +crtext morale++ mechanicREV mechanic++ goodfx ammosearch1
	then;
	cmechanic- if
		($ cHe .. "  tambem nao tem ideia do que " .. che .. " esta a fazer, e desperdicia um bom dia brincando com isso." $) +crtext mechanicREV cloyalty- not if morale-- then loyaltyREV +cr fullcamp badfx 1 calendar-day+ heal-event
	then;
	mechanicroll if
		($ cHe .. " 'ajuda a ajudar durante um dia para ajudar a reparar o carro do homem. Ele lhe da algumas provisoes como recompensa!" $) +crtext morale++ mechanicREV mechanic++ goodfx ammosearch1
	else
		($ cHe .. " 'tenta ajudar ao longo de um dia, mas nao e bem sucedido. Ainda assim, e bom treinar." $) +crtext mechanicREV mechanic++ cloyalty- not if morale-- then loyaltyREV badfx
	then +cr fullcamp 1 calendar-day+ heal-event
;

cyoa-bridge: common2-carhelp-coolit
	cpet? if
		($ cname .. " ignora o homem estranho e comeca a comer formigas que caminham no chao frio." $)
	else
		($ cname .. " diz ao homem estranho para " .. "SE ACALMAR." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Homem Solitario" recruitee .name!
		($ "'ESTA CERTO! O CARRO SOZINHO NECESITA MAIS AGUA A PARTE DE SE PARA FUNCIONAR NOVAMENTE!" pbold .. " Lhe oferece uma recompensa" $) +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Comeca a gritar em " .. thegroup .. " ate que " .. gthey .. " saiam" .. g1s .. "." $) +crtext
		morale--all badfx
	then witsattREV
;

cyoa-choice: common2-carhelp-bandit
	($ cname .. " rouba facilmente os suprimentos do homem encalhado, ate mesmo sua chave inglesa." $) text! badfx
		common2-carhelp-win 1 WEAPON_WRENCH trunk.weapon+ rob-guilt aholeREV
;

cyoa: common2-carhelp
	"Ajudando com problemas de carro" cyoa-title!
	char-gen to recruitee 
	0 recruitee .mechanical!
	0 recruitee .female! ricon
	WEAPON_WRENCH recruitee .weapona!
	($ Thegroup .. " 'encontra um homem tentando consertar seu carro quebrado. Ele claramente nao tem ideia do que esta fazendo." $) text!
		mechanicest charchoice( ": Ajude a consertar o carro" )choice: common2-carhelp-mech
		most-charming if
			charchoice( ": Recrute o" )choice: recruit-yes
		then
		most-grating if
			charchoice( ": Diga a ele para ESFRIAR" fcoolit )choice: common2-carhelp-coolit
		then
		most-ahole if
			charchoice( ": Roube seus suprimentos" )choice: common2-carhelp-bandit
		then
		choice( "Nao tem tempo para ajudar" )choice
;

// Bandit raid

: common2-raid-prize
	1 WEAPON_RIFLE trunk.weapon+ 20 trunk-rifle-shell+ 15 trunk-food+ morale++all goodfx
;

cyoa-choice: common2-raid-calmshot
	($ cname .. " pega um rifle de cowboy dos colonos e se esconde em um lugar alto. Quando os bandidos aparecem " .. che .. " faz varios tiros incriveis para estourar seus pneus. Os bandidos ficam assustados demais para continuar o ataque! A luta acaba antes de comecar. Os colonos ficam felizes em dar a recompensa!" $) text! common2-raid-prize
;

: common2-raid-shoot+
	dup humany? 50 %chance and if .shooting++ then
;

: common2-raid-fightprize
	choosehuman alive? if
		"Os colonos permanecem fieis a sua palavra e dao uma grande recompensa!" +crtext
		common2-raid-prize
		most-calmshot if
			choosechar ($ cname .. " faz alguns tiros malucos que inspiram os outros." $) +crtext
			' .shooting++ foreach-team-char
		else
			' common2-raid-shoot+ foreach-team-char
		then
	then
;

cyoa-choice: common2-raid-fight
	someicon ($ Thegroup .. " decide ajudar os colonos a lutar contra os bandidos, diretamente." $) text!
	shootingREVall
	shooting-total 14 > if
		"A habilidade de tiro do grupo ajuda os colonos a sobrecarregar os bandidos. Ninguem se machuca!" +crtext
		common2-raid-fightprize
	then;
	shooting-total 10 > if
		"'A luta e dura, mas as habilidades de tiro do grupo salvam o assentamento." +crtext
		ouch--all bitefx common2-raid-fightprize
	then;
	shooting-total 6 > if
		"'A luta e brutal! As habilidades de tiro do grupo mal ajudam a afastar os bandidos." +crtext
		ouch12rnd--all bitefx common2-raid-fightprize
	then;
		"'A ajuda fornecida nao foi suficiente no tiroteio. Os bandidos expulsam todos do assentamento." +crtext ouch2-all bitefx
;

cyoa-choice: common2-raid-mech
	($ cname .. " ' tenta construir as defesas do assentamento." $) text!
	cmechanic+ if
		($ cHe .. "  rapidamente faz planos e consegue pessoas para ajudar " .. chim .. "  transformar o lugar em uma fortaleza! Os bandidos ficam sobrecarregados com as novas defesas quando aparecem e fogem sem machucar ninguem." $) +crtext mechanicREV mechanic++ common2-raid-prize
	then;
	mechanicroll if
		($ cHe .. "  conta aos colonos como construir fortificacoes em algumas areas fracas. As defesas extras parecem ajudar muito no ataque dos bandidos! Os colonos sao gratos pela ajuda!" $) +crtext mechanicREV mechanic++ common2-raid-prize
	else
		($ cHe .. " ' tenta fortificar algumas coisas, mas os esforcos nao parecem ajudar muito contra o ataque vicioso dos bandidos. Os colonos feridos acusam " .. cname .. "  de desperdicar seu tempo e se recusam a dar a recompensa." $) +crtext mechanicREV morale--all sting--all bitefx
	then
;

cyoa: common2-raid
	"Defesa contra invasao de bandidos" cyoa-title!
	($ Thegroup .. "  e solicitado por um assentamento. Eles dizem que serao invadidos por bandidos em breve. Se " .. thegroup .. "  ajuda com a defesa, " .. gthey .. "  recebera uma recompensa de 15 alimentos, alguma municao e um rifle de cowboy! Este poderia ser " .. "very dangerous..." rbold $) text!
		vehicle.which@ 0 > if 
			choice( "'Nao ajude, va embora" )choice else choice( "Portugues" )choice
		then
		choice( "Ajude os no tiroteio" )choice: common2-raid-fight
		mechanicest charchoice( ": Construa defesas" )choice: common2-raid-mech
		most-calmshot if
			charchoice( "': Eu cuido disso" )choice: common2-raid-calmshot
		then
;

// Rooftop relaxing

cyoa-choice: common2-relax-morale
	($ Thegroup .. "  passa algum tempo " .. "' so relaxando" fbold .. "." $) text!
	morale2+all cool-it++ goodfx
;

cyoa-choice: common2-relax-strength
	($ Thegroup .. "  faz alguns exercicios de forca improvisados. O movel mais pesado que pode ser levantado e pego e depois largado. Repita ate " .. "MEGABUFF" fbold .. "." $) text!
	' .strength++ foreach-team-char goodfx
;

cyoa-choice: common2-relax-fitness
	($ Thegroup .. "  faz alguns exercicios cardiovasculares improvisados. Principalmente burpees, alguns polichinelos. Repita ate " .. "ULTRA FIT" fbold .. "." $) text!
	' .fitness++ foreach-team-char goodfx
;

cyoa-choice: common2-relax-med
	($ cname .. "  ensina " .. everyone .. "  o que " .. che .. "  sabe sobre atendimento medico de emergencia e odontologia semi indolor." $) text!
		cmedical- if 
			($ cHe .. " ' nao sabe muito sobre isso! E uma licao completamente inutil." $) +crtext badfx
		else
			cchar ' .medical++ foreach-team-char-except goodfx
		then medicalREV morale++
;

cyoa-choice: common2-relax-mech
	($ cname .. "  ensina " .. everyone .. "  o que " .. che .. "  sabe sobre conserto de automoveis e outras maneiras de baguncar acidentalmente as maquinas." $) text!
		cmechanic- if 
			($ cHe .. " ' nao sabe muito sobre isso! E uma licao completamente inutil." $) +crtext badfx
		else
			cchar ' .mechanic++ foreach-team-char-except goodfx
		then mechanicREV morale++
;

cyoa-choice: common2-roofrelax-reveal
	($ cname .. "  revela " .. chis .. "  VERDADEIRA ESSENCIA." $) text!
		allREV goodfx
;

cyoa: common2-roofrelax
	image-tired-survivor 63 to cyoaicon
	($ Thegroup .. "  tem um raro momento de paz para relaxar no telhado de um apartamento." $) text!
	solo if
		soloer choosechar
		($ "Sem ninguem para incomodar " .. chim .. ", " .. che .. "  decide:" $) +crtext
		choice( "CALMA AI" )choice: common2-relax-morale
		choice( "Exercicio para forca" )choice: common2-relax-strength
		choice( "Exercicio para condicionamento fisico" )choice: common2-relax-fitness
	else
		"'Ha tempo para conversar e alguem revela muito sobre si mesmo:" +crtext
		' common2-roofrelax-reveal cyoa-team-choice
	then
;

// Safehouse relaxing

cyoa: common2-saferelax
	image-door-on-brick 27 to cyoaicon
	($ Thegroup .. "  tem um momento de paz para descansar em um esconderijo, nenhum zumbi a vista." $) text!
	solo if
		soloer choosechar
		($ "Sem ninguem para incomodar " .. chim .. ", " .. che .. "  decide:" $) +crtext
		choice( "CALMA AI" )choice: common2-relax-morale
		choice( "Exercicio para forca" )choice: common2-relax-strength
		choice( "Exercicio para condicionamento fisico" )choice: common2-relax-fitness
	else
		"Com algum tempo livre, eles decidem:" +crtext
			50 %chance if 
				choice( "CALMA AI" )choice: common2-relax-morale
			else
				50 %chance if
					choice( "Exercicio para forca" )choice: common2-relax-strength
				else
					choice( "Exercicio para condicionamento fisico" )choice: common2-relax-fitness
				then
			then
			mechanicest charchoice( ": Ensinar habilidades mecanicas" )choice: common2-relax-mech
			medicalest charchoice( ": Ensinar habilidades medicas" )choice: common2-relax-med
	then
;

} }

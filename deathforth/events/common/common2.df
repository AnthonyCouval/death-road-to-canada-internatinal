
cyoa{ road{ 

: camp-pic
	50 %chance if
		image-camp-1
	else
		image-camp-2
	then	
;

// Rares

cyoa-choice: common2-rare-tp-share
	someicon
	solo if
		($ cname .. " realise que ce rouleau de papier toilette est un tresor qui ne doit pas etre donne." $) text! morale4+ goodfx 1 WEAPON_CARDBOARD trunk.weapon+
		zombop++
	then;
	"Lequipe decide dessayer de partager le rouleau de papier toilette." text!
	loyalty-average 2 < if
		"'Il ne leur faut pas longtemps pour eclater en une enorme bagarre ! La situation degenere en une bagarre generale. Le papier toilette finit par etre dechire par accident !" +crtext morale3-all badfx
	else
		loyalty-average 4 < if
			"'Il y a quelques combats inevitables pour partager le papier toilette, mais le groupe parvient a surmonter ses differences." +crtext morale2+all goodfx
		else
			"'Le groupe s'unit autour du papier toilette, le protegeant mutuellement. Meme apres la disparition du papier toilette, le tube en carton devient un symbole de l'unite du groupe." +crtext morale4+all goodfx
		then
	then
	' .loyaltyREV foreach-team-char 1 WEAPON_CARDBOARD trunk.weapon+
	zombop++
;

cyoa-choice: common2-rare-tp-trade
	someicon
	($ Thegroup .. " ' decide de vendre le rouleau de papier toilette des que possible. Ca ne vaut pas la peine de transporter quelque chose d'aussi precieux !" $) text!
		50 %chance if
			"Une colonie achete le rouleau de papier toilette pour 20 vivres ! Pas mal !" +crtext 20 trunk-food+
		else
			50 %chance if
				"'Un marchand astucieux achete le papier toilette pour 12 vivres. On ne peut pas se plaindre !" +crtext 12 trunk-food+
			else
				($ "Un " .. "TRES" rbold .. "  acheteur interesse achete le papier toilette pour " .. "30 vivres !" fbold $) +crtext 30 trunk-food+
			then
		then goodfx morale++all
;

cyoa: common2-rare-tp
	"Plus precieux que lor" cyoa-title!
	($ Thegroup .. "  trouve un " .. "ROULEAU DE PAPIER TOILETTE IMMACULE" fbold .. " roll of toilet paper." $) text!
		solo if
			choice( "Utilisez ce precieux cadeau" )choice: common2-rare-tp-share
		else
			choice( "Partagez ce precieux cadeau" )choice: common2-rare-tp-share
		then
		choice( "Echangez le des que possible" )choice: common2-rare-tp-trade
;

cyoa-choice: common2-rare-zombmaster-run
	image-creepy-girl
	($ "Le " .. "MAITRE DES ZOMBIES" rbold .. "  met en place un petit parcours dobstacles et apprend " .. thegroup .. "  comment courir de maniere plus efficace et moins embarrassante !" $) text! goodfx
	' .dexterity++ foreach-team-char
;

: common2-rare-zomb-workout args( @ )
	<- @ .strengthREV <- @ .fitnessREV <- @ .strength++ <- @ .fitness++
;

cyoa-choice: common2-rare-zombmaster-fight
	image-creepy-girl
	($ "Le " .. "MAITRE DES ZOMBIES" rbold .. "  eclaire " .. thegroup .. "  sur les armes de melee avec quelques conseils et quelques " .. "ENTRAINEMENT TRES DANGEREUX." rbold $) text! goodfx bitefx
	' common2-rare-zomb-workout foreach-team-char sting--all
;

cyoa-choice: common2-rare-zombmaster-shoot
	image-creepy-girl
	($ "Le " .. "MAITRE DES ZOMBIES" rbold .. "  donne " .. thegroup .. "  une excellente lecon sur le tir correct ! Elle donne un devoir a faire pour une vraie chasse aux zombies, et fournit un tas de munitions et un fusil !" $) text! goodfx ' .shootingREV foreach-team-char ' .shooting++ foreach-team-char
	60 trunk-bullet+ 40 trunk-rifle-shell+ 30 trunk-shotgun-shell+ 1 WEAPON_RIFLE trunk.weapon+
;

cyoa-choice: common2-rare-zombmaster-tough
	image-creepy-girl
	($ "Le " .. "MAITRE DES ZOMBIES" rbold .. "  grogne." .. thegroup .. "  comment endurcir " .. ghis .. "  la peau contre les morsures de zombies ! Ce processus implique de la battre " .. ghim .. " " $) text! goodfx bitefx
	' .vitality++ foreach-team-char sting--all
;

cyoa: common2-rare-zombmaster
	image-creepy-girl "Le Maitre des Zombies" cyoa-title!
	($ Thegroup .. " 'rencontre une femme etrange portant des lunettes, un foulard et un capot. Elle dit etre la " .. "MAITRE DES ZOMBIES" rbold .. "  et quelle enseignera lun de ses secrets zombie en tant que service a lhumanite." $) text! goodfx
		choice( "Lecon de tir sur les zombies" )choice: common2-rare-zombmaster-shoot
		choice( "Seulement sen echapper continuellement" )choice: common2-rare-zombmaster-run
		choice( "Combat a mains armees" )choice: common2-rare-zombmaster-fight
		choice( "Resister aux morsures" )choice: common2-rare-zombmaster-tough
;

// END rares

// Man trapped under cardboard thievery

0 value common2-cboardtype

: cboardprize
	common2-cboardtype case
		1 of "Baton de nuit" endof
		2 of "FUSIL A CANON ROTATIF" endof
		3 of "FUSIL" endof
		4 of "REVOLVER DE 45 DOLT" endof
		5 of "PIQUE POIRE" endof
		6 of "REVOVLER A CANON COURT" endof
	endcase
;

: cboardget
	common2-cboardtype case
		1 of 1 WEAPON_NIGHTSTICK endof
		2 of 8 trunk-shotgun-shell+ 1 WEAPON_SHOTGUN endof
		3 of 12 trunk-rifle-shell+ 1 WEAPON_RIFLE endof
		4 of 6 trunk-bullet+ 1 WEAPON_DOLT endof
		5 of 1 WEAPON_FIRE endof
		6 of 6 trunk-bullet+ 1 WEAPON_SNUBNOSE endof
	endcase
;

cyoa-choice: common2-cboard-steal
	($ cname .. " ' vole l'homme sans defense " .. cboardprize rbold .. "." $) text! badfx
	cloyalty- if
		($ cHe .. "  se sent plutot suffisant a ce sujet." $) +crtext morale++
	else
		cloyalty+ if
			($ cHe .. "  se sent incroyablement coupable a ce sujet !" $) +crtext morale3-
		else
			($ cHe .. "  se sent coupable a ce sujet." $) +crtext morale--
		then
	then loyaltyREV
	($ "Jai un " rbold .. cboardprize rbold $) +crtext cboardget trunk.weapon+
;

: cboard-help-check args( @ )
	<- @ .cloyalty- if <- @ .loyaltyREV <- @ .morale-- then; <- @ .morale++
;

cyoa-choice: common2-cboard-help
	($ Thegroup .. " ' sauve l'homme de tous les cartons qui l'ecrasent. Il ne peut pas epargner une recompense, mais est vraiment reconnaissant !" $) text! ' cboard-help-check foreach-team-char goodfx
;

cyoa-choice: common2-cboard-bandit
	($ cname .. " ' vole l'homme sans defense " .. cboardprize rbold .. "  puis prend le reste de ses fournitures !" $) text! badfx
	rob-guilt	
	($ "Jai un " rbold .. cboardprize rbold $) +crtext cboardget trunk.weapon+ 
	10 20 rnd trunk-bullet+ 5 15 rnd trunk-rifle-shell+ 3 12 rnd trunk-shotgun-shell+
	2 4 rnd trunk-medical+ 1 6 rnd trunk-food+
	aholeREV
;

cyoa-bridge: common2-cboard-steal4
	someicon
	"Qui devrait voler son arme ?" cyoa-text!
	' common2-cboard-steal cyoa-team-choice
;

cyoa: common2-cboard
	"Aider quelquun dans le besoin !" cyoa-title!
	1 6 rnd to common2-cboardtype
	($ "En fouillant, " .. thegroup .. " ' rencontre un homme couvert de cartons. Il dit qu'il est coince la depuis des jours ! Il a un " .. cboardprize rbold .. "  gisant sur le sol hors de sa portee." $) text!
	choice( "Aidez le a se relever" )choice: common2-cboard-help
	most-ahole if
		charchoice( "voler tous ses fournitures" )choice: common2-cboard-bandit
		team-count 2 > if
			choice( "Choisir un voleur" )choice: common2-cboard-steal4
		then;
	then	
		
	solo if
		soloer choosechar choice( "Voler son arme" )choice: common2-cboard-steal
	else
		"Si quelquun lui volait son arme?" +crtext
		team-count 3 > if
			choice( "Choisir un voleur" )choice: common2-cboard-steal4
		then 
		' common2-cboard-steal 3 #cyoa-team-choice
	then
;

// Quick template for the usual "choisis quelquun dautre" choice
(*

cyoa-bridge: 4
	someicon
	"Qui devrait ?" cyoa-text!
	'  cyoa-team-choice
;

		team-count 3 > if
			'  2 #cyoa-team-choice
			choice( "Choisissez quelquun dautre" )choice: 4
		else

		then

*)

// Farm help

: common2-farm-reward
	8 trunk-food+
	most-charming if
		choosechar ($ cname .. "  convainc les fermiers de donner " .. chim .. "  une fourche !" $) +crtext
		1 WEAPON_PITCHFORK trunk.weapon+
	then
;

cyoa-choice: common2-farm-mechanic
	($ cname .. "  offre " .. chis .. "  des services pour reparer des choses dans la ferme. " $) text!
	cmechanic+ if	
		($ cHe .. " ' est si doue que la grande quantite de travail n'est pas un gros probleme." $) +text
	else
		($ "'Il y a tellement de choses cassees que " .. che .. "  devient epuise." $) +text cchar tired
	then
	mechanicREV mechanic++
	cchar .mechanical 3 < if
		($ "Les fermiers refusent de donner une recompense apres avoir decouvert que tout est toujours casse." $) +crtext badfx
	else
		($ "Les fermiers sont reconnaissants pour le travail!" $) +crtext goodfx common2-farm-reward
	then
;

cyoa-choice: common2-farm-bandit
	($ cname .. " " $) text! badfx
	14 trunk-food+ rob-guilt2 aholeREV
;

cyoa-choice: common2-farm-haul
	someicon
	($ Thegroup .. " it's hard to keep up with the work. " .. gthey .. "  obtenir" .. g1s .. " tres fatigue!" $) text! goodfx 
	' .strengthREV foreach-team-char ' .strength++ foreach-team-char common2-farm-reward
	// added STR check for scythe
	strongest-pass if strongest choosechar
		($ "Les fermiers ont remarque " .. cname .. "'s unusual strength and gave " .. chim .. " une faux en cadeau!" $) +crtext
		1 WEAPON_SCYTHE trunk.weapon+
	then
	nosleep
;

cyoa-choice: common2-farm-till
	someicon
	($ Thegroup .. " worker can only work on  field at a time. " .. gthey .. "  obtenir" .. g1s .. " tres fatigue!" $) text! goodfx 
	' .fitnessREV foreach-team-char ' .fitness++ foreach-team-char common2-farm-reward
	// added FIT check for scythe
	fittest-pass if fittest choosechar
		($ "Les fermiers ont remarque " .. cname .. "''s niveau de fitness et donna " .. chim .. " une faux en cadeau!" $) +crtext
		1 WEAPON_SCYTHE trunk.weapon+
	then
	nosleep
;

cyoa: common2-farm
	"Un voyage a la ferme" cyoa-title!
	($ Thegroup .. " rencontre des gens qui essaient de gerer une petite exploitation agricole. Ils offrent 8 aliments en echange de quelques aides." $) text!
		choice( "Semer et planter" )choice: common2-farm-till
		choice( "Chargement et chargement" )choice: common2-farm-haul
		most-ahole if
			charchoice( ": Voler la ferme" )choice: common2-farm-bandit
		then
		mechanicest	charchoice( ": Travaux de reparation" )choice: common2-farm-mechanic
		choice( "Pas assez de temps pour les aider" )choice
;

// Annoying diplomat

: common2-detoured
	vehicle.which@ 0 > if choice( "Faire un detour" )choice: car-detour else
		// ($ Thegroup .. " emprunte une longue detour a pied." $) +crtext 
		1 calendar-day+ +cr fullcamp
	then
;

cyoa-bridge: common2-meeting-talk
	($ cname .. " parle avec le representant genant." $) text!
	cpet? if
		($ cHe .. " 'n'est pas suppose etre capable de parler, donc " .. che .. " se contente surtout de lecher " .. chim .. "lui meme. Le dingus est vraiment blesse par avoir a parler a un " .. cdogcat .. "pass. " .. thegroup .. " a travers!" $) +crtext goodfx morale++all
	then;
	cchar grating? if
		($ cHe .. "  sentendent vraiment bien ! Ils echangent surtout des " .. "COOL IT" pbold .. "  piques entre eux. Pendant une heure. " .. Thegroup .. "  est laisse passer." $) +crtext witsattREV goodfx cool-it++ cool-it++
	then;
	cchar oblivious? if
		($ cHe .. " ' ne remarque pas du tout a quel point le cretin est agacant ou insultant. La reunion se passe bien et " .. thegroup .. "  est laisse passer." $) +crtext witsattREV goodfx
	then;
	cattitude+ if
		($ cHe .. "  parvient a etre tres poli. Apres avoir repondu a suffisamment de questions inutiles, " .. thegroup .. "  est laisse passer la barricade." $) +crtext attitudeREV goodfx
	then;
	cchar angrystrong? if
		($ cHe .. " perd " .. chis .. "  temperament tout de suite et le met KO dun seul coup de poing. Les gardes de la barricade sont impressionnes par ca ! Lun deux leve le pouce. Ils decident de laisser " .. thegroup .. "  passer, quand meme." $) +crtext attitudeREV strengthREV goodfx morale++all
	then;
	ctemper- if
		($ cHe .. " perd " .. chis .. "  temperament tout de suite et se met a crier. Cela effraie vraiment les gardes, qui se mettent a tirer. " .. Thegroup .. "  doit fuir !" $) +crtext temperREV bitefx sting--all
		common2-detoured	
	then;
	ctemper+ temperroll or if
		($ cHe .. "  parvient a controler " .. chis .. "  temperament ! " .. Thegroup .. "  est laisse passer la barricade apres une nouvelle contrariete." $) +crtext goodfx
	else
		($ cHe .. " tente de controler " .. chis .. " il est trop chiant. Il court et dit aux gardes de ne pas ouvrir la porte. " .. Thegroup .. " doit trouver une solution!" $) +crtext badfx
		common2-detoured
	then temperREV
;

cyoa-bridge: common2-meeting-talk4
	someicon
	"Qui devrait essayer de traiter avec la personne ennuyeuse?" cyoa-text!
	' common2-meeting-talk cyoa-team-choice
;

cyoa: common2-meeting
	"Diplomatie du refroidissement" cyoa-title!
	($ Thegroup .. " " .. "EXTREMEMENT ENERVANT" rbold .. "." $) text!
		solo if
			($ " Il narrete pas de dire des choses comme " .. "COOL IT" pbold .. "  et " .. "'DIRE PAS LA LOI REJETTE LA LOI." pbold $) +text
			soloer choosechar choice( "Essayez de ne pas perdre la calme" )choice: common2-meeting-talk
		else
			" Qui devrait essayer de le gerer?" +text
			team-count 3 > if
				' common2-meeting-talk 2 #cyoa-team-choice
				choice( "Choisissez quelquun dautre" )choice: common2-meeting-talk4
			else
				' common2-meeting-talk 3 #cyoa-team-choice
			then
		then
		vehicle.which@ 0 > if smartest charchoice( ": Prenez un detour" )choice: car-detour then
;

// Park Chatting

: common2-parkchat-tipme args( @ )
	<- @ 40 %chance if 
		.shooting++ 
	else
		50 %chance if 
			.mechanic++
		else
			.medical++
		then
	then
;

cyoa-choice: common2-parkchat-tips
	cpet? if 
		($ cname .. " " .. cbarkmeow .. "il est tres sage et experimente a sa maniere." $) text!
	else
		($ cname .. " ils echangent des conseils sur divers sujets." $) text!
	then
	cwits- if
		"Le survivant amical est confus par lavis inutile." +crtext badfx witsREV
	then;
	cwits+ if
		"Le survivant amical est eclairci par lavis, et donne des bons conseils a son tour!" +crtext
		goodfx ' common2-parkchat-tipme foreach-team-char witsREV
	then;
		($ "Le survivant amical apprecie cela, et donne un peu davis en retour " .. cname .. " en retour." $) +crtext
		cchar common2-parkchat-tipme goodfx witsREV
;

cyoa-choice: common2-parkchat-pep
	cpet? if 
		($ cname .. " " .. cbarkmeow .. "de maniere rassurante" $) text!
	else
		($ cname .. " essaye de donner un discours dencouragement." $) text!
	then
	cattitude- if
		"Le survivant amical se sent en fait pire maintenant." +crtext badfx attitudeREV
	then;
	cattitude+ if
		"Le survivant amical se sent tres optimiste apres la discussion, et donne un discours inspirant!" +crtext
		morale2+all goodfx attitudeREV
	then;
		($ "Le survivant amical donne des paroles rassurantes en retour." $) +crtext
		morale++all goodfx attitudeREV
;

cyoa-choice: common2-parkchat-bandit
	($ cname .. " ' vole facilement la personne amicale. Il n'avait pas grand chose, mais cela lui apprendra une lecon importante sur la confiance envers les autres." $) text! badfx
		2 4 rnd trunk-food+ 50 %chance if 1 3 rnd trunk-medical+ then rob-guilt
		aholeREV
;

cyoa-bridge: common2-parkchat-coolit
	cpet? if
		($ cname .. "  commence " .. "PETE" rbold .. "  sur la personne, et continue en quelque sorte a le faire pendant plusieurs minutes sans sarreter un seul instant." $)
	else
		($ cname .. "  interrompt le survivant amical en plein milieu de sa phrase, pour lui dire de " .. "CALME TOI." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Gars sympa" recruitee .name!
		"'Il est stupefait par ca. Il pretend que tout cela est conforme aux propheties, et qu'il a conserve un cadeau pour l'Elu !" +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Il regarde fixement " .. cname .. ", sans parler, jusqua ce que " .. thegroup .. "  parte." $) +crtext
		morale--all badfx
	then witsattREV 
;

cyoa: common2-parkchat
	"Comme un jour normal" cyoa-title!
	($ Thegroup .. "  rencontre une autre personne en train de se detendre sur un banc de parc. Il a lair amical. Ils ont une conversation, comme avant la fin du monde. Quel etait le sujet principal ?" $) text!
		smartest charchoice( ": Echange de conseils" )choice: common2-parkchat-tips
		gallant charchoice( ": Discours dencouragement" )choice: common2-parkchat-pep
		most-ahole if
			charchoice( ": Me donner vos affaires" )choice: common2-parkchat-bandit
		then
		most-grating if
			charchoice( ": Se calmer" fcoolit )choice: common2-parkchat-coolit
		then
;

// Busted leg

cyoa-choice: common2-busted-med
	($ cname .. " essaye de traiter la jambe cassee de la femme." $) text!
	cmedical+ if
		($ cHe .. " utilise " .. chis .. " x st Aid Kit" $) +crtext morale++ medicalREV medical++ goodfx -1 trunk-medical+ 2 trunk-food+
	then;
	cmedical- if
		($ cHe .. " na aucune idee de ce que " .. che .. " fait, perd une journee, et peut etre aggrave les choses un peu." $) +crtext medicalREV cloyalty- not if morale-- then loyaltyREV +cr fullcamp badfx -1 trunk-medical+ 1 calendar-day+
	then;
	medicalroll if
		($ cHe .. " prend un jour pour aider la femme a soigner sa jambe. Elle est reconnaissante, mais ne peut pas donner grand chose!" $) +crtext morale++ medicalREV medical++ goodfx 2 trunk-food+
	else
		($ cHe .. " tente de venir en aide tout au long de la journee, mais n'arrive pas a faire grand chose. Cependant, c'est en forgeant qu'on devient forgeron." $) +crtext medicalREV medical++ cloyalty- not if morale-- then loyaltyREV badfx
	then +cr fullcamp -1 trunk-medical+ 1 calendar-day+ heal-event
;

cyoa-bridge: common2-busted-coolit
	cpet? if
		($ cname .. " commence a rouler sur le sol froid." $)
	else
		($ cname .. "  dit a la femme de " .. "CALME TOI." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Femme Bequille" recruitee .name!
		($ "Elle prend cela pour conseil de sappliquer des compresses froids a sa jambe. Apres ca elle offre recompense " .. thegroup .. " qui laide avec ca, lui offre une recompense!" $) +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Elle demande a " .. "QUOI DE MECK FAUT IL EN BREF? EXACTEMENT?" pbold .. " avant de sen aller en degout." $) +crtext
		morale--all badfx
	then witsattREV
;

cyoa-choice: common2-busted-bandit
	($ cname .. " qui lui vole facilement les fournitures restantes de la femme blessee. Bravo!" $) text! badfx
		2 4 rnd trunk-food+ rob-guilt aholeREV
;

cyoa: common2-busted
	"Jambe cassee" cyoa-title!
	($ Thegroup .. " rencontre une femme qui se balance avec un baton lune de ses jambes etant blessee recemment lorsquelle sen etait echappee des zombies." $) text!
		trunk-medical 0 > if
			medicalest charchoice( ": Aidez lui a son genou" )choice: common2-busted-med
			choice( "Pas assez de temps pour les aider" )choice
		else
			($ Thegroup .. " manque tout equipement medical pour laider avec." $) +crtext
			choice( "Laissez la seule" )choice
		then
		most-grating if
			charchoice( ": Dites lui de ralentir un peu" fcoolit )choice: common2-busted-coolit
		then
		most-ahole if
			charchoice( "voler ses fournitures" )choice: common2-busted-bandit
		then
;

// Car trouble

: common2-carhelp-win
	homesearch1 ammosearch1
;

cyoa-choice: common2-carhelp-mech
	($ cname .. " tente de reparer la voiture cassee de l'homme." $) text!
	cmechanic+ if
		($ cHe .. " utilise " .. chis .. " car knowledge to quickly repair it! He appreciates the help and gives some supplies!" $) +crtext morale++ mechanicREV mechanic++ goodfx ammosearch1
	then;
	cmechanic- if
		($ cHe .. " na aucune idee de ce que  means " .. che .. " fait des choses inutiles, perdant ainsi une bonne journee a ne rien faire." $) +crtext mechanicREV cloyalty- not if morale-- then loyaltyREV +cr fullcamp badfx 1 calendar-day+ heal-event
	then;
	mechanicroll if
		($ cHe .. " prend un jour pour aider a reparer la voiture de l'homme. Il donne quelques fournitures en guise de recompense!" $) +crtext morale++ mechanicREV mechanic++ goodfx ammosearch1
	else
		($ cHe .. " ' essaie d'aider au fil de la journee, mais n'y parvient pas. C'est quand meme bon pour l'entrainement." $) +crtext mechanicREV mechanic++ cloyalty- not if morale-- then loyaltyREV badfx
	then +cr fullcamp 1 calendar-day+ heal-event
;

cyoa-bridge: common2-carhelp-coolit
	cpet? if
		($ cname .. "  ignore lhomme bloque et se met a manger des fourmis qui rampent sur le sol froid." $)
	else
		($ cname .. "  dit a lhomme bloque de " .. "CALME TOI." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Homme bloque" recruitee .name!
		($ "'TU AS RAISON ! LA VOITURE A JUSTE BESOIN DE PLUS DE LIQUIDE DE REFROIDISSEMENT POUR REDEMARRER !" pbold .. "  Il offre une recompense !" $) +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Il se met a crier sur " .. thegroup .. "  jusqua ce que " .. gthey .. "  sen aillent" .. g1s .. "." $) +crtext
		morale--all badfx
	then witsattREV
;

cyoa-choice: common2-carhelp-bandit
	($ cname .. "  vole facilement les affaires de lhomme bloque, meme sa cle a molette." $) text! badfx
		common2-carhelp-win 1 WEAPON_WRENCH trunk.weapon+ rob-guilt aholeREV
;

cyoa: common2-carhelp
	"Aide avec un probleme de voiture" cyoa-title!
	char-gen to recruitee 
	0 recruitee .mechanical!
	0 recruitee .female! ricon
	WEAPON_WRENCH recruitee .weapona!
	($ Thegroup .. " ' rencontre un homme qui essaie de reparer sa voiture en panne. Il est clair qu'il n'a aucune idee de ce qu'il fait." $) text!
		mechanicest charchoice( ": Aider a reparer la voiture" )choice: common2-carhelp-mech
		most-charming if
			charchoice( ": Le recruter" )choice: recruit-yes
		then
		most-grating if
			charchoice( "dis lui de se CALMER" fcoolit )choice: common2-carhelp-coolit
		then
		most-ahole if
			charchoice( "voler ses fournitures" )choice: common2-carhelp-bandit
		then
		choice( "Pas assez de temps pour les aider" )choice
;

// Bandit raid

: common2-raid-prize
	1 WEAPON_RIFLE trunk.weapon+ 20 trunk-rifle-shell+ 15 trunk-food+ morale++all goodfx
;

cyoa-choice: common2-raid-calmshot
	($ cname .. " prend un fus " .. che .. " " $) text! common2-raid-prize
;

: common2-raid-shoot+
	dup humany? 50 %chance and if .shooting++ then
;

: common2-raid-fightprize
	choosehuman alive? if
		"Les colons restent fideles a leur parole et donnent une grosse recompense !" +crtext
		common2-raid-prize
		most-calmshot if
			choosechar ($ cname .. " fait des tirs de fou qui inspirent les autres." $) +crtext
			' .shooting++ foreach-team-char
		else
			' common2-raid-shoot+ foreach-team-char
		then
	then
;

cyoa-choice: common2-raid-fight
	someicon ($ Thegroup .. " " $) text!
	shootingREVall
	shooting-total 14 > if
		"La competence de tir du groupe aide les colons a submerger les bandits. Personne nest blesse !" +crtext
		common2-raid-fightprize
	then;
	shooting-total 10 > if
		"Le combat est rude, mais les competences de tir du groupe sauvent la colonie." +crtext
		ouch--all bitefx common2-raid-fightprize
	then;
	shooting-total 6 > if
		"Le combat est brutal! Les competences de tir du groupe aident a peine a repousser les bandits." +crtext
		ouch12rnd--all bitefx common2-raid-fightprize
	then;
		"XA" +crtext ouch2-all bitefx
;

cyoa-choice: common2-raid-mech
	($ cname .. " 'construit les defenses de l'etablissement." $) text!
	cmechanic+ if
		($ cHe .. "  planifie rapidement et trouve des gens pour aider " .. chim .. " " $) +crtext mechanicREV mechanic++ common2-raid-prize
	then;
	mechanicroll if
		($ cHe .. "  indique aux colons comment construire des fortifications dans certaines zones faibles. Les defenses supplementaires semblent beaucoup aider lors de lattaque des bandits ! Les colons sont reconnaissants pour laide !" $) +crtext mechanicREV mechanic++ common2-raid-prize
	else
		($ cHe .. " ' essaie de fortifier certaines choses, mais les efforts ne semblent pas avoir beaucoup aide contre la violente attaque des bandits. Les colons blesses accusent " .. cname .. "  de perdre leur temps et refusent de donner la recompense." $) +crtext mechanicREV morale--all sting--all bitefx
	then
;

cyoa: common2-raid
	" Defense contre les raids de bandits" cyoa-title!
	($ Thegroup .. "  est sollicite par une colonie. Ils disent quils seront bientot attaques par des bandits. Si " .. thegroup .. "  aide a la defense, " .. gthey .. "  obtiendra une recompense de 15 unites de nourriture, des munitions et un fusil de cow boy ! Cela pourrait etre " .. "very dangerous..." rbold $) text!
		vehicle.which@ 0 > if 
			choice( "' Ne pas aider, partir" )choice else choice( "'Ne les aidez pas" )choice
		then
		choice( "Les aidez dans laffrontement" )choice: common2-raid-fight
		mechanicest charchoice( ": Construisons des defenses" )choice: common2-raid-mech
		most-calmshot if
			charchoice( "': Je le gererai" )choice: common2-raid-calmshot
		then
;

// Rooftop relaxing

cyoa-choice: common2-relax-morale
	($ Thegroup .. " passe quelque temps " .. "'se reposant simplement" fbold .. "." $) text!
	morale2+all cool-it++ goodfx
;

cyoa-choice: common2-relax-strength
	($ Thegroup .. " fait des exercices de force improvises. Le mobilier le plus lourd qui peut etre souleve est releve, puis mis a terre. Repeter jusqua " .. "MEGABUFF" fbold .. "." $) text!
	' .strength++ foreach-team-char goodfx
;

cyoa-choice: common2-relax-fitness
	($ Thegroup .. " fait des exercices cardio improvises. Principalement des burpees, quelques sauts en place. Repeter jusqua " .. "ULTRA FIT" fbold .. "." $) text!
	' .fitness++ foreach-team-char goodfx
;

cyoa-choice: common2-relax-med
	($ cname .. " enseigne " .. everyone .. " quoi " .. che .. " sait comment donner les premiers soins et pratiquer la dentisterie sans trop faire mal." $) text!
		cmedical- if 
			($ cHe .. " il n'y connait pas grand chose! C'est un cours completement inutile." $) +crtext badfx
		else
			cchar ' .medical++ foreach-team-char-except goodfx
		then medicalREV morale++
;

cyoa-choice: common2-relax-mech
	($ cname .. " enseigne " .. everyone .. " quoi " .. che .. " connait la reparation de voiture et dautres facons de gacher accidentellement des machines." $) text!
		cmechanic- if 
			($ cHe .. " il n'y connait pas grand chose! C'est un cours completement inutile." $) +crtext badfx
		else
			cchar ' .mechanic++ foreach-team-char-except goodfx
		then mechanicREV morale++
;

cyoa-choice: common2-roofrelax-reveal
	($ cname .. " revele " .. chis .. "  VRAIE ESSENCE." $) text!
		allREV goodfx
;

cyoa: common2-roofrelax
	image-tired-survivor 63 to cyoaicon
	($ Thegroup .. "  obtient un rare moment de paix pour se detendre sur le toit dun appartement." $) text!
	solo if
		soloer choosechar
		($ "Sans personne pour deranger " .. chim .. ", " .. che .. "  decide de:" $) +crtext
		choice( "COOL IT" )choice: common2-relax-morale
		choice( "Exercice pour la force" )choice: common2-relax-strength
		choice( "Exercice pour la forme" )choice: common2-relax-fitness
	else
		"'Il est temps de parler, et quelqu'un revele beaucoup de choses sur lui meme:" +crtext
		' common2-roofrelax-reveal cyoa-team-choice
	then
;

// Safehouse relaxing

cyoa: common2-saferelax
	image-door-on-brick 27 to cyoaicon
	($ Thegroup .. "  obtient un moment de paix pour se reposer dans une planque, pas de zombies en vue." $) text!
	solo if
		soloer choosechar
		($ "Sans personne pour deranger " .. chim .. ", " .. che .. "  decide de:" $) +crtext
		choice( "COOL IT" )choice: common2-relax-morale
		choice( "Exercice pour la force" )choice: common2-relax-strength
		choice( "Exercice pour la forme" )choice: common2-relax-fitness
	else
		"Avec un peu de temps libre, ils decident de:" +crtext
			50 %chance if 
				choice( "COOL IT" )choice: common2-relax-morale
			else
				50 %chance if
					choice( "Exercice pour la force" )choice: common2-relax-strength
				else
					choice( "Exercice pour la forme" )choice: common2-relax-fitness
				then
			then
			mechanicest charchoice( ": Enseigner les competences de mecanicien" )choice: common2-relax-mech
			medicalest charchoice( ": Enseigner les competences medicales" )choice: common2-relax-med
	then
;

} }

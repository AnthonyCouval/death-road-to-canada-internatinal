
cyoa{ road{ 

: camp-pic
	50 %chance if
		image-camp-1
	else
		image-camp-2
	then	
;

// Rares

cyoa-choice: common2-rare-tp-share
	someicon
	solo if
		($ cname .. " cette rouleau de papier hygienique est une tresor qui ne doit pas etre donne." $) text! morale4+ goodfx 1 WEAPON_CARDBOARD trunk.weapon+
		zombop++
	then;
	"L'equipe decide de tenter de partager le role du rouleau de papier hygienique." text!
	loyalty-average 2 < if
		"Il ne leur prend pas longtemps pour se jeter dans une lutte a grande echelle ! la situation bascule dans une bagarre generale. le papier hygienique finit par etre dechire par inadvertence !" +crtext morale3-all badfx
	else
		loyalty-average 4 < if
			"Bien qu'il y ait quelques brouillards inevitables a cause du partage du wc, le groupe parvient finalement a surmonter leurs differences." +crtext morale2+all goodfx
		else
			"Le groupe se rassemble autour du rouleau de papier hygienique, se protegeant mutuellement. malgre le depart du papier toilette, la boite en carton devient un symbole de l'unite du groupe." +crtext morale4+all goodfx
		then
	then
	' .loyaltyREV foreach-team-char 1 WEAPON_CARDBOARD trunk.weapon+
	zombop++
;

cyoa-choice: common2-rare-tp-trade
	someicon
	($ Thegroup .. " il decide de vendre le rouleau de papier hygienique des que possible. il n'est pas a porter autant quelque chose de valeur autour!" $) text!
		50 %chance if
			"Il achete une rouleau de papier a toilette pour 20 aliments." +crtext 20 trunk-food+
		else
			50 %chance if
				"Un marchand astucieux achete du riz a 12 denrees alimentaires. pas de plainte !" +crtext 12 trunk-food+
			else
				($ "A : " .. "TRES" rbold .. " interesse achete le terrain parcelle pour " .. "trente aliments !" fbold $) +crtext 30 trunk-food+
			then
		then goodfx morale++all
;

cyoa: common2-rare-tp
	"Plus precieux que de l'or" cyoa-title!
	($ Thegroup .. " trouve une " .. "ROULEAU PRESENT DE TOILETTES SANITAIRES A NEUF" fbold .. " dans la recherche de fournitures apres l'apocalypse, le papier hygienique a ete le premier a disparaitre. il reste encore des quantites importantes de nourriture en boite et de munitions aux etats-unis. le bon papier pour le derriere etait rapidement utilise. c'est la ressource la plus rare possible. chercher sagement :" $) text!
		solo if
			choice( "Utilisez cette precieuse don" )choice: common2-rare-tp-share
		else
			choice( "Partagez ce precieux cadeau" )choice: common2-rare-tp-share
		then
		choice( "Defavorisez le commerce immediatement" )choice: common2-rare-tp-trade
;

cyoa-choice: common2-rare-zombmaster-run
	image-creepy-girl
	($ "Le " .. "MAITRE DE ZOMBIES." rbold .. " installe une petite parcours d'obstacles et enseigne. " .. thegroup .. " ### pour fonctionner de maniere plus efficace et moins genante :" $) text! goodfx
	' .dexterity++ foreach-team-char
;

: common2-rare-zomb-workout args( @ )
	<- @ .strengthREV <- @ .fitnessREV <- @ .strength++ <- @ .fitness++
;

cyoa-choice: common2-rare-zombmaster-fight
	image-creepy-girl
	($ "Le " .. "MAITRE DE ZOMBIES." rbold .. " eclaire, eclaircis " .. thegroup .. " utilisez efficacement vos armes de melee et suivez ces conseils : " .. "ENTRAINEMENT TRES DANGEREUX." rbold $) text! goodfx bitefx
	' common2-rare-zomb-workout foreach-team-char sting--all
;

cyoa-choice: common2-rare-zombmaster-shoot
	image-creepy-girl
	($ "Le " .. "MAITRE DE ZOMBIES." rbold .. " donne " .. thegroup .. " une excellente lecon sur le tir correct ! elle donne un devoir pour faire de la chasse reelle aux zombies, et fournit une quantite importante de munitions et d'un fusil !" $) text! goodfx ' .shootingREV foreach-team-char ' .shooting++ foreach-team-char
	60 trunk-bullet+ 40 trunk-rifle-shell+ 30 trunk-shotgun-shell+ 1 WEAPON_RIFLE trunk.weapon+
;

cyoa-choice: common2-rare-zombmaster-tough
	image-creepy-girl
	($ "Le " .. "MAITRE DE ZOMBIES." rbold .. " affiche " .. thegroup .. " comment reduire la fragilite ? " .. ghis .. " se faire remuer la peau contre les morsures de zombies ! cela implique qu'elle battage. " .. ghim .. " haut." $) text! goodfx bitefx
	' .vitality++ foreach-team-char sting--all
;

cyoa: common2-rare-zombmaster
	image-creepy-girl "Maitre des zombies" cyoa-title!
	($ Thegroup .. " elle rencontre une femme etrange portant des lunettes de soleil, un foulard et un capuchon. elle affirme etre : " .. "MAITRE DE ZOMBIES." rbold .. " enseignez egalement l'un de vos secrets zombies en tant que service a l'humanite." $) text! goodfx
		choice( "Legon: tirez sur les zombies !" )choice: common2-rare-zombmaster-shoot
		choice( "Courir continuellement dans tous les sens" )choice: common2-rare-zombmaster-run
		choice( "Les frais de passage du bandit sont reduits." )choice: common2-rare-zombmaster-fight
		choice( "Repetez-vous : resistez aux morsures." )choice: common2-rare-zombmaster-tough
;

// END rares

// Man trapped under cardboard thievery

0 value common2-cboardtype

: cboardprize
	common2-cboardtype case
		1 of "BATON DE NUIT" endof
		2 of "VOLEUR" endof
		3 of "FUSIL" endof
		4 of "REVOLVER DOLT45" endof
		5 of "HACHE-FENETRE A FEU" endof
		6 of "MOUCHOIR ARRIERE DE PISTOLET A COUBURE" endof
	endcase
;

: cboardget
	common2-cboardtype case
		1 of 1 WEAPON_NIGHTSTICK endof
		2 of 8 trunk-shotgun-shell+ 1 WEAPON_SHOTGUN endof
		3 of 12 trunk-rifle-shell+ 1 WEAPON_RIFLE endof
		4 of 6 trunk-bullet+ 1 WEAPON_DOLT endof
		5 of 1 WEAPON_FIRE endof
		6 of 6 trunk-bullet+ 1 WEAPON_SNUBNOSE endof
	endcase
;

cyoa-choice: common2-cboard-steal
	($ cname .. " comment bien ils peuvent utiliser une arme a feu. " .. cboardprize rbold .. "." $) text! badfx
	cloyalty- if
		($ cHe .. " ressent beaucoup de satisfaction concernant cela." $) +crtext morale++
	else
		cloyalty+ if
			($ cHe .. " se sent terriblement coupable de cela !" $) +crtext morale3-
		else
			($ cHe .. " se sent coupable de cela." $) +crtext morale--
		then
	then loyaltyREV
	($ "Avez un " rbold .. cboardprize rbold $) +crtext cboardget trunk.weapon+
;

: cboard-help-check args( @ )
	<- @ .cloyalty- if <- @ .loyaltyREV <- @ .morale-- then; <- @ .morale++
;

cyoa-choice: common2-cboard-help
	($ Thegroup .. " libere l'homme des boites de carton qui le retenait. il n'a pas a ta disposition de recompense, mais est vraiment reconnaissant !" $) text! ' cboard-help-check foreach-team-char goodfx
;

cyoa-choice: common2-cboard-bandit
	($ cname .. " comment bien ils peuvent utiliser une arme a feu. " .. cboardprize rbold .. " et ensuite prend le reste de ses approvisionnements !" $) text! badfx
	rob-guilt	
	($ "Avez un " rbold .. cboardprize rbold $) +crtext cboardget trunk.weapon+ 
	10 20 rnd trunk-bullet+ 5 15 rnd trunk-rifle-shell+ 3 12 rnd trunk-shotgun-shell+
	2 4 rnd trunk-medical+ 1 6 rnd trunk-food+
	aholeREV
;

cyoa-bridge: common2-cboard-steal4
	someicon
	"Qui devrait voler son arme ?" cyoa-text!
	' common2-cboard-steal cyoa-team-choice
;

cyoa: common2-cboard
	"Aide a quelqu'un qui en a besoin !" cyoa-title!
	1 6 rnd to common2-cboardtype
	($ "Alors que vous fouillez. " .. thegroup .. " 'il tombe sur un homme recouvert de boites en carton. il affirme avoir ete bloque la pendant des jours!' " .. cboardprize rbold .. " legerement etendu sur le sol, hors de portee de ses mains." $) text!
	choice( "Aidez-le a se lever." )choice: common2-cboard-help
	most-ahole if
		charchoice( ": voler tous ses approvisionnements" )choice: common2-cboard-bandit
		team-count 2 > if
			choice( "Choisissez un voleur" )choice: common2-cboard-steal4
		then;
	then	
		
	solo if
		soloer choosechar choice( "Voler son arme" )choice: common2-cboard-steal
	else
		"Si quelqu'un volait son arme, il serait important de se mettre a l'abri immediatement et de signaler la police." +crtext
		team-count 3 > if
			choice( "Choisissez un voleur" )choice: common2-cboard-steal4
		then 
		' common2-cboard-steal 3 #cyoa-team-choice
	then
;

// Quick template for the usual "magasin de grande surface alimentaire" choice
(*

cyoa-bridge: 4
	someicon
	"Quelqu'un devrait." cyoa-text!
	'  cyoa-team-choice
;

		team-count 3 > if
			'  2 #cyoa-team-choice
			choice( "Choisissez quelqu'un d'autre." )choice: 4
		else

		then

*)

// Farm help

: common2-farm-reward
	8 trunk-food+
	most-charming if
		choosechar ($ cname .. " convincez les agriculteurs a donner. " .. chim .. " une fourche a fumier !" $) +crtext
		1 WEAPON_PITCHFORK trunk.weapon+
	then
;

cyoa-choice: common2-farm-mechanic
	($ cname .. " offre " .. chis .. " reparations de services autour de la ferme. " $) text!
	cmechanic+ if	
		($ cHe .. " sa maitrise est telle qu'un grand volume de travail n'est pas un probleme importante." $) +text
	else
		($ "Il y a tant de choses cassees. " .. che .. " s'epuise." $) +text cchar tired
	then
	mechanicREV mechanic++
	cchar .mechanical 3 < if
		($ "Les fermiers refusent de recompenser apres qu'ils ont decouvert que tout etait encore casse." $) +crtext badfx
	else
		($ "Les fermiers sont reconnaissants pour le travail effectue !" $) +crtext goodfx common2-farm-reward
	then
;

cyoa-choice: common2-farm-bandit
	($ cname .. " les fermiers paisibles sont depouilles. ils ont ete pris par surprise face a une menace non-zombie." $) text! badfx
	14 trunk-food+ rob-guilt2 aholeREV
;

cyoa-choice: common2-farm-haul
	someicon
	($ Thegroup .. " traine des tonnes de choses autour. il y a toujours quelque chose a trainer sur une ferme. c'est du travail eprouvant. " .. gthey .. " obtenir" .. g1s .. " tres fatigue" $) text! goodfx 
	' .strengthREV foreach-team-char ' .strength++ foreach-team-char common2-farm-reward
	// added STR check for scythe
	strongest-pass if strongest choosechar
		($ "Les agriculteurs ont remarque. " .. cname .. "sa force inhabituelle et lui donna " .. chim .. " c'est un couteau a fauche, offert en cadeau !" $) +crtext
		1 WEAPON_SCYTHE trunk.weapon+
	then
	nosleep
;

cyoa-choice: common2-farm-till
	someicon
	($ Thegroup .. " le labourage aide a la culture des champs et a la plantation de nouvelles recoltes. c'est du travail epuisant. " .. gthey .. " obtenir" .. g1s .. " tres fatigue" $) text! goodfx 
	' .fitnessREV foreach-team-char ' .fitness++ foreach-team-char common2-farm-reward
	// added FIT check for scythe
	fittest-pass if fittest choosechar
		($ "Les agriculteurs ont remarque. " .. cname .. "niveau de condition physique de lui. " .. chim .. " c'est un couteau a fauche, offert en cadeau !" $) +crtext
		1 WEAPON_SCYTHE trunk.weapon+
	then
	nosleep
;

cyoa: common2-farm
	"Un voyage a la ferme" cyoa-title!
	($ Thegroup .. " vous rencontrez des gens qui essaient de gerer une petite exploitation agricole. ils offrent 8 aliments en echange de votre aide." $) text!
		choice( "Semer et labourer" )choice: common2-farm-till
		choice( "Tirage et deport" )choice: common2-farm-haul
		most-ahole if
			charchoice( ": voler la ferme" )choice: common2-farm-bandit
		then
		mechanicest	charchoice( ": travaux de reparation" )choice: common2-farm-mechanic
		choice( "Reserve de nourriture comprenant quinze aliments." )choice
;

// Annoying diplomat

: common2-detoured
	vehicle.which@ 0 > if choice( "Prendre un detour" )choice: car-detour else
		// ($ Thegroup .. " il fait une longue detour a pied." $) +crtext 
		1 calendar-day+ +cr fullcamp
	then
;

cyoa-bridge: common2-meeting-talk
	($ cname .. " parle avec le representant geneur." $) text!
	cpet? if
		($ cHe .. " ne doit pas etre capable de parler. " .. che .. " baisements : surtout des lechets. " .. chim .. "soi-meme. le dingue est vraiment offense par etre contraint de parler a quelqu'un. " .. cdogcat .. "les gardes ne s'amusent qu'en la laissant passer. " .. thegroup .. " a travers !" $) +crtext goodfx morale++all
	then;
	cchar grating? if
		($ cHe .. " elles se entendent tres bien ! elles echangent surtout. " .. "REFRIGERER" pbold .. " se disputent l'un contre l'autre pendant une heure. " .. Thegroup .. " a un endroit sur ..." $) +crtext witsattREV goodfx cool-it++ cool-it++
	then;
	cchar oblivious? if
		($ cHe .. " malgre quelquefois qu'il ne remarque pas toutes les fois l'agitation ou les injures du gadget, la reunion progresse bien. " .. thegroup .. " c'est passe." $) +crtext witsattREV goodfx
	then;
	cattitude+ if
		($ cHe .. " manage a etre tres poli. apres assez de questions inutiles, " .. thegroup .. " passe par la barricade." $) +crtext attitudeREV goodfx
	then;
	cchar angrystrong? if
		($ cHe .. " perd " .. chis .. " laissez-passer immediatement et le frappe a l'unisson, mettant ko. les gardes de barricade sont impressionnes par cela ! un d'eux donne un pouce en haut. ils decident de la laisser passer. " .. thegroup .. " a travers, quand meme." $) +crtext attitudeREV strengthREV goodfx morale++all
	then;
	ctemper- if
		($ cHe .. " perd " .. chis .. " faites chaleureusement ceder immediatement et commencez a hurler. cela fait reellement peur aux gardes, et ils commencent a tirer. " .. Thegroup .. " il doit fuir !" $) +crtext temperREV bitefx sting--all
		common2-detoured	
	then;
	ctemper+ temperroll or if
		($ cHe .. " parvient a maitriser " .. chis .. " calme-toi ! " .. Thegroup .. " est laisse passer au-dela de l'obstacle apres quelques genes supplementaires." $) +crtext goodfx
	else
		($ cHe .. " essaie de controler " .. chis .. " arrete de s'emouvoir, mais ce garcon est simplement trop genant. il court et demande aux gardes de ne pas ouvrir la porte. " .. Thegroup .. " il faut trouver un chemin autour !" $) +crtext badfx
		common2-detoured
	then temperREV
;

cyoa-bridge: common2-meeting-talk4
	someicon
	"Il ne faut pas forcement que quelqu'un tente de gerer une personne agacante. il est possible qu'elle se calme seule ou que d'autres personnes puissent la gerer sans qu'il soit necessaire votre intervention. cependant, si vous sentez que vous pouvez l'apaiser et resoudre le probleme en utilisant vos competences de communication" cyoa-text!
	' common2-meeting-talk cyoa-team-choice
;

cyoa: common2-meeting
	"Diplomatie du refroidissement de cool-it" cyoa-title!
	($ Thegroup .. " nous sommes bloques par un autre groupe de survivants qui ont dresse une barricade pour se proteger. ils sont mefiants et tres armes... le leur qu'ils envoient parler est : " .. "TRES ENNUYANT" rbold .. "." $) text!
		solo if
			($ " Il repete constamment des choses telles que... " .. "REFRIGERER" pbold .. " et " .. "DIRE PAS L'ARROSEZ." pbold $) +text
			soloer choosechar choice( "Ne perdez pas la calme." )choice: common2-meeting-talk
		else
			" Il faut que quelqu'un tente de le gerer." +text
			team-count 3 > if
				' common2-meeting-talk 2 #cyoa-team-choice
				choice( "Choisissez quelqu'un d'autre." )choice: common2-meeting-talk4
			else
				' common2-meeting-talk 3 #cyoa-team-choice
			then
		then
		vehicle.which@ 0 > if smartest charchoice( ": faites un detour par ici." )choice: car-detour then
;

// Park Chatting

: common2-parkchat-tipme args( @ )
	<- @ 40 %chance if 
		.shooting++ 
	else
		50 %chance if 
			.mechanic++
		else
			.medical++
		then
	then
;

cyoa-choice: common2-parkchat-tips
	cpet? if 
		($ cname .. " " .. cbarkmeow .. "s est tres sage et tres experimente." $) text!
	else
		($ cname .. " echange des conseils sur divers sujets." $) text!
	then
	cwits- if
		"Le survivant amical est perplexe face a l'avis inutile." +crtext badfx witsREV
	then;
	cwits+ if
		"Le survivant amical offre des conseils precieux et partage egalement quelques-uns de siens !" +crtext
		goodfx ' common2-parkchat-tipme foreach-team-char witsREV
	then;
		($ "Le survivant amical apporte son aide et donne quelques conseils. " .. cname .. " a la place de cela, dans le sens oppose." $) +crtext
		cchar common2-parkchat-tipme goodfx witsREV
;

cyoa-choice: common2-parkchat-pep
	cpet? if 
		($ cname .. " " .. cbarkmeow .. "assurez-le en jouant sur ton langue." $) text!
	else
		($ cname .. " encouragez-vous les uns les autres, restez confiantes et fuyez les doutes. vous etes plus fortes que cela ne parait, n'oubliez pas de votre talent" $) text!
	then
	cattitude- if
		"Le survivant amical se sent maintenant encore plus mal." +crtext badfx attitudeREV
	then;
	cattitude+ if
		"Le survivant amical se sent tres optimiste apres la discussion, et tient un discours inspirant !" +crtext
		morale2+all goodfx attitudeREV
	then;
		($ "Tu seras a l'abri bientot, ne t'inquiete pas." $) +crtext
		morale++all goodfx attitudeREV
;

cyoa-choice: common2-parkchat-bandit
	($ cname .. " facilement depouille une personne amicale. il n'avait pas beaucoup, mais cela lui en apprendra une importante lecon sur la confiance envers les autres." $) text! badfx
		2 4 rnd trunk-food+ 50 %chance if 1 3 rnd trunk-medical+ then rob-guilt
		aholeREV
;

cyoa-bridge: common2-parkchat-coolit
	cpet? if
		($ cname .. " commence " .. "FARTERE : DEPASSER DES GAZ INTESTINAUX" rbold .. " il regarde continuellement la personne et cela dure plusieurs minutes sans interruption." $)
	else
		($ cname .. " arrete, mon ami. je dois te dire quelque chose important. " .. "FAITES LE REFROIDIR." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Ami agreable" recruitee .name!
		"Il est ebahi par cela. il affirme que c'est conforme aux predictions et qu'il gardait une recompense pour le choisi !" +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Il regarde fixement vers. " .. cname .. "jusqu'a ce que tu parle. " .. thegroup .. " feuilles." $) +crtext
		morale--all badfx
	then witsattREV 
;

cyoa: common2-parkchat
	"Comme une journee normale" cyoa-title!
	($ Thegroup .. " le sujet principal de leur conversation etait la vie quotidienne et les evenements locaux avant l'apocalypse." $) text!
		smartest charchoice( ": conseil d'echange" )choice: common2-parkchat-tips
		gallant charchoice( ": discours d'encouragement" )choice: common2-parkchat-pep
		most-ahole if
			charchoice( ": vous m'offrez vos choses ?" )choice: common2-parkchat-bandit
		then
		most-grating if
			charchoice( ": refroidissement" fcoolit )choice: common2-parkchat-coolit
		then
;

// Busted leg

cyoa-choice: common2-busted-med
	($ cname .. " essaye de traiter la jambe casse de la femme." $) text!
	cmedical+ if
		($ cHe .. " biais. " .. chis .. " assurez-vous de fournir une expertise medicale rapide pour reparer sa jambe ! elle apprecie votre aide et vous offre ce que elle peut sparer !" $) +crtext morale++ medicalREV medical++ goodfx -1 trunk-medical+ 2 trunk-food+
	then;
	cmedical- if
		($ cHe .. " aucune idee de quoi " .. che .. " il gaspille un jour et peut aggraver les choses un peu." $) +crtext medicalREV cloyalty- not if morale-- then loyaltyREV +cr fullcamp badfx -1 trunk-medical+ 1 calendar-day+
	then;
	medicalroll if
		($ cHe .. " une journee pour l'aider a traiter sa jambe. elle est remerciante, mais ne peut pas depensier beaucoup !" $) +crtext morale++ medicalREV medical++ goodfx 2 trunk-food+
	else
		($ cHe .. " prepare a faire tout son possible pendant une journee, mais ne parvient pas a realiser beaucoup. malgre cela, la pratique fait parfait." $) +crtext medicalREV medical++ cloyalty- not if morale-- then loyaltyREV badfx
	then +cr fullcamp -1 trunk-medical+ 1 calendar-day+ heal-event
;

cyoa-bridge: common2-busted-coolit
	cpet? if
		($ cname .. " commencent a rouler sur le sol froid." $)
	else
		($ cname .. " 'assurez-vous de faire ceci pour la femme :' " .. "FAITES LE REFROIDIR." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Je deteste lundis" recruitee .name!
		($ "Elle prend cela pour conseil de mettre des compresses froids sur sa jambe. " .. thegroup .. " aide-la dans cela, elle offre recompense!" $) +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Elle demande " .. "QU'EST CE QUI TE FAIT MAL, EXACTEMENT ?" pbold .. " avant de s'en aller avec degout en boitant." $) +crtext
		morale--all badfx
	then witsattREV
;

cyoa-choice: common2-busted-bandit
	($ cname .. " facilement depouille la femme blessee de ce peu de fournitures qu'elle en restait. bravo !" $) text! badfx
		2 4 rnd trunk-food+ rob-guilt aholeREV
;

cyoa: common2-busted
	"* jamb brisee" cyoa-title!
	($ Thegroup .. " rencontre une femme qui se balance avec un baton de soutien. l'une de ses jambes etait blessee recemment lorsqu'elle s'enfuait des zombies." $) text!
		trunk-medical 0 > if
			medicalest charchoice( "aidez-lui a son jambier." )choice: common2-busted-med
			choice( "Reserve de nourriture comprenant quinze aliments." )choice
		else
			($ Thegroup .. " manque de fourniture medicale pour l'aider." $) +crtext
			choice( "Laisser-la tranquille." )choice
		then
		most-grating if
			charchoice( "calme-toi." fcoolit )choice: common2-busted-coolit
		then
		most-ahole if
			charchoice( "explorer applique sur char : l'explorateur est monte sur le char." )choice: common2-busted-bandit
		then
;

// Car trouble

: common2-carhelp-win
	homesearch1 ammosearch1
;

cyoa-choice: common2-carhelp-mech
	($ cname .. " essaie de reparer la voiture casse du homme." $) text!
	cmechanic+ if
		($ cHe .. " biais. " .. chis .. " aidez-le rapidement a reparer sa voiture ! il apprecie votre aide et vous offre quelques fournitures en remerciement." $) +crtext morale++ mechanicREV mechanic++ goodfx ammosearch1
	then;
	cmechanic- if
		($ cHe .. " je n'ai aucune idee de quoi " .. che .. " il passe son temps a faire ca et perdit une bonne journee a se gaspiller autour de caquettes." $) +crtext mechanicREV cloyalty- not if morale-- then loyaltyREV +cr fullcamp badfx 1 calendar-day+ heal-event
	then;
	mechanicroll if
		($ cHe .. " il faut un jour pour l'aider a reparer la voiture de l'homme. il nous remet des fournitures en guise de recompense!" $) +crtext morale++ mechanicREV mechanic++ goodfx ammosearch1
	else
		($ cHe .. " essaie d'aider au fil du jour, mais n'y reussit pas completement. malgre tout, cela represente une bonne pratique." $) +crtext mechanicREV mechanic++ cloyalty- not if morale-- then loyaltyREV badfx
	then +cr fullcamp 1 calendar-day+ heal-event
;

cyoa-bridge: common2-carhelp-coolit
	cpet? if
		($ cname .. " ignore le homme etranghe et commence a manger des fourmis qui creusent sur le sol froid." $)
	else
		($ cname .. " laissez-le s'aider par nous. " .. "FAITES LE REFROIDIR." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Mobilier d'art ou magasin de meubles." recruitee .name!
		($ "VOUS AVEZ RAISON ! LA VOITURE SIMPLEMENT A BESOIN DE PLUS D'EAU DE REFROIDISSEMENT POUR FONCTIONNER A NOUVEAU !" pbold .. " Il offre une recompense !" $) +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Il commence a hurler. " .. thegroup .. " jusque a " .. gthey .. " quitter" .. g1s .. "." $) +crtext
		morale--all badfx
	then witsattREV
;

cyoa-choice: common2-carhelp-bandit
	($ cname .. " facilement vole aux vivants echoues leurs approvisionnements, y compris leur cle de caisse." $) text! badfx
		common2-carhelp-win 1 WEAPON_WRENCH trunk.weapon+ rob-guilt aholeREV
;

cyoa: common2-carhelp
	"Aidez a une panne automobile" cyoa-title!
	char-gen to recruitee 
	0 recruitee .mechanical!
	0 recruitee .female! ricon
	WEAPON_WRENCH recruitee .weapona!
	($ Thegroup .. " un homme rencontre un homme essayant de reparer sa voiture cassee. il est clair qu'il n'a aucune idee de ce qu'il fait." $) text!
		mechanicest charchoice( ": aidez-moi a reparer la voiture." )choice: common2-carhelp-mech
		most-charming if
			charchoice( "armee de la mort !" )choice: recruit-yes
		then
		most-grating if
			charchoice( "faites-lui attendre un peu." fcoolit )choice: common2-carhelp-coolit
		then
		most-ahole if
			charchoice( ": voler ses approvisionnements" )choice: common2-carhelp-bandit
		then
		choice( "Reserve de nourriture comprenant quinze aliments." )choice
;

// Bandit raid

: common2-raid-prize
	1 WEAPON_RIFLE trunk.weapon+ 20 trunk-rifle-shell+ 15 trunk-food+ morale++all goodfx
;

cyoa-choice: common2-raid-calmshot
	($ cname .. " prend une carabine de cow-boy aux colons et se cache dans un endroit eleve. quand les voleurs apparaissent, " .. che .. " 'faites plusieurs tirs impressionnants pour deflater leurs pneus... les voleurs deviennent trop effrayes pour continuer l'attaque ! la bagarre est terminee avant le debut. les colons sont heureux de vous verser la recompense!'" $) text! common2-raid-prize
;

: common2-raid-shoot+
	dup humany? 50 %chance and if .shooting++ then
;

: common2-raid-fightprize
	choosehuman alive? if
		"Le colonisateur reste fidele a sa parole et accorde une recompense genereuse !" +crtext
		common2-raid-prize
		most-calmshot if
			choosechar ($ cname .. " fait des tirs folle inspirant les autres." $) +crtext
			' .shooting++ foreach-team-char
		else
			' common2-raid-shoot+ foreach-team-char
		then
	then
;

cyoa-choice: common2-raid-fight
	someicon ($ Thegroup .. " decide de se joindre directement aux colons pour lutter contre les bandits." $) text!
	shootingREVall
	shooting-total 14 > if
		"L'aptitude au tir du groupe permet aux colons de surmonter les brigands. personne n'est blesse !" +crtext
		common2-raid-fightprize
	then;
	shooting-total 10 > if
		"Lutte rude, mais la capacite de tir du groupe sauve le village." +crtext
		ouch--all bitefx common2-raid-fightprize
	then;
	shooting-total 6 > if
		"La bagarre est meurtriere ! les tirs de groupe ne sont qu'un faible soutien pour chasser les voleurs." +crtext
		ouch12rnd--all bitefx common2-raid-fightprize
	then;
		"L'aide apportee n'etait pas suffisante lors du tirefesses. les voleurs chassent tous les habitants du village." +crtext ouch2-all bitefx
;

cyoa-choice: common2-raid-mech
	($ cname .. " construit des defenses pour le reglement." $) text!
	cmechanic+ if
		($ cHe .. " rapidement elabore des plans et rallie des gens a l'aide. " .. chim .. " transformez ce lieu en forteresse ! les voleurs sont debordes par les nouvelles defenses quand ils apparaissent, et s'enfuient sans blesser personne." $) +crtext mechanicREV mechanic++ common2-raid-prize
	then;
	mechanicroll if
		($ cHe .. " instructions pour les colons :" $) +crtext mechanicREV mechanic++ common2-raid-prize
	else
		($ cHe .. " essaye de renforcer certaines choses, mais ses efforts ne semblent pas avoir d'effet contre l'attaque des brigands cruellement violents. les colons blesses accusent : " .. cname .. " n'economisez pas votre temps et ne donnez pas le prix recompense." $) +crtext mechanicREV morale--all sting--all bitefx
	then
;

cyoa: common2-raid
	"Defense contre un raid de voleur" cyoa-title!
	($ Thegroup .. " nous sommes desoles d'apprendre cela, mais nous ne pouvons pas vous envoyer des renforts immediatement. cependant, nous pouvons vous fournir quelques conseils pour preparer votre installation face a l'attaque de bandits : " .. thegroup .. " aide a la defense. " .. gthey .. " vous recevrez une recompense de 15 nourriture, quelque munition et un fusil a pompe cow-boy !. " .. "very dangerous..." rbold $) text!
		vehicle.which@ 0 > if 
			choice( "Ne l'aidez pas, accelerer !" )choice else choice( "Ne les aidez pas." )choice
		then
		choice( "Aidez-les dans le tireau" )choice: common2-raid-fight
		mechanicest charchoice( "construire des defenses." )choice: common2-raid-mech
		most-calmshot if
			charchoice( "je le ferai." )choice: common2-raid-calmshot
		then
;

// Rooftop relaxing

cyoa-choice: common2-relax-morale
	($ Thegroup .. " passe quelque temps " .. "seulement passer du temps agreablement" fbold .. "." $) text!
	morale2+all cool-it++ goodfx
;

cyoa-choice: common2-relax-strength
	($ Thegroup .. " effectue des exercices de force au hasard avec des efforts supplementaires. le meuble le plus lourd a soulever est releve puis mis a terre. repeter cela jusqu'a ce qu'il soit termine. " .. "MEGABUFF : TERME UTILISE POUR DECRIRE UNE CRE" fbold .. "." $) text!
	' .strength++ foreach-team-char goodfx
;

cyoa-choice: common2-relax-fitness
	($ Thegroup .. " effectue des exercices cardiovasculaires improvises. principalement des burpees, quelques sauts en place. repete cela jusqu'a : " .. "ULTRA FIT : ULTRA FORME, TRES EN FORME." fbold .. "." $) text!
	' .fitness++ foreach-team-char goodfx
;

cyoa-choice: common2-relax-med
	($ cname .. " un fan de l'anime. " .. everyone .. " qu'est-ce que ? " .. che .. " sa connait en soins medicaux d'urgence et en dentisterie demi-douleur." $) text!
		cmedical- if 
			($ cHe .. " c'est une lecon totalement inutile ! je ne sais pas beaucoup en ce qui concerne cela." $) +crtext badfx
		else
			cchar ' .medical++ foreach-team-char-except goodfx
		then medicalREV morale++
;

cyoa-choice: common2-relax-mech
	($ cname .. " un fan de l'anime. " .. everyone .. " qu'est-ce que ? " .. che .. " connait la reparation de voitures et d'autres facons d'accidentellement deranger les machines." $) text!
		cmechanic- if 
			($ cHe .. " c'est une lecon totalement inutile ! je ne sais pas beaucoup en ce qui concerne cela." $) +crtext badfx
		else
			cchar ' .mechanic++ foreach-team-char-except goodfx
		then mechanicREV morale++
;

cyoa-choice: common2-roofrelax-reveal
	($ cname .. " revele " .. chis .. " VRAI-ESSENCE." $) text!
		allREV goodfx
;

cyoa: common2-roofrelax
	image-tired-survivor 63 to cyoaicon
	($ Thegroup .. " se trouve une rare tranquille occasion de reposer sur le toit d'un immeuble." $) text!
	solo if
		soloer choosechar
		($ "Sans personne a perturber : " .. chim .. ", " .. che .. " decide de :" $) +crtext
		choice( "REFRIGERER" )choice: common2-relax-morale
		choice( "Entrainement pour la force" )choice: common2-relax-strength
		choice( "Entrainement pour le fitness" )choice: common2-relax-fitness
	else
		"Il y a still temps de parler, et quelqu'un revele beaucoup a propos de lui-meme." +crtext
		' common2-roofrelax-reveal cyoa-team-choice
	then
;

// Safehouse relaxing

cyoa: common2-saferelax
	image-door-on-brick 27 to cyoaicon
	($ Thegroup .. " une interruption calme pour reposer dans un refuge sur, pas de zombies en vue." $) text!
	solo if
		soloer choosechar
		($ "Sans personne a perturber : " .. chim .. ", " .. che .. " decide de :" $) +crtext
		choice( "REFRIGERER" )choice: common2-relax-morale
		choice( "Entrainement pour la force" )choice: common2-relax-strength
		choice( "Entrainement pour le fitness" )choice: common2-relax-fitness
	else
		"Avec un peu de temps a repos, ils decident de :" +crtext
			50 %chance if 
				choice( "REFRIGERER" )choice: common2-relax-morale
			else
				50 %chance if
					choice( "Entrainement pour la force" )choice: common2-relax-strength
				else
					choice( "Entrainement pour le fitness" )choice: common2-relax-fitness
				then
			then
			mechanicest charchoice( "enseigner des competences mecaniques." )choice: common2-relax-mech
			medicalest charchoice( "enseignement de competences medicales." )choice: common2-relax-med
	then
;

} }

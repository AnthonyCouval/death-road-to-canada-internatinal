
cyoa{ road{ 

: camp-pic
	50 %chance if
		image-camp-1
	else
		image-camp-2
	then	
;

// Rares

cyoa-choice: common2-rare-tp-share
	someicon
	solo if
		($ cname .. "  si rende conto che questo rotolo di carta igienica e un tesoro che non deve essere regalato." $) text! morale4+ goodfx 1 WEAPON_CARDBOARD trunk.weapon+
		zombop++
	then;
	"Il team decide di provare a condividere il rotolo di carta igienica." text!
	loyalty-average 2 < if
		"'Non ci vuole molto perche scoppi una grossa lite! La situazione degenera in una rissa totale. La carta igienica finisce per essere strappata accidentalmente!" +crtext morale3-all badfx
	else
		loyalty-average 4 < if
			"'Ci sono inevitabili lotte dovute alla condivisione della carta igienica, ma il gruppo riesce a superare le proprie divergenze." +crtext morale2+all goodfx
		else
			"'Il gruppo unisce le forze intorno alla carta igienica, proteggendola reciprocamente. Anche dopo che la carta igienica e finita, il tubo di cartone diventa un simbolo dell'unita del gruppo." +crtext morale4+all goodfx
		then
	then
	' .loyaltyREV foreach-team-char 1 WEAPON_CARDBOARD trunk.weapon+
	zombop++
;

cyoa-choice: common2-rare-tp-trade
	someicon
	($ Thegroup .. " ' decide di vendere il rotolo di carta igienica il prima possibile. Non vale la pena portare in giro qualcosa di COSI prezioso!" $) text!
		50 %chance if
			"Un insediamento acquista il rotolo di carta igienica per 20 cibo! Niente male!" +crtext 20 trunk-food+
		else
			50 %chance if
				"'Un astuto mercante acquista la carta igienica per 12 cibi. Non posso lamentarmi!" +crtext 12 trunk-food+
			else
				($ "Un " .. "MOLTO" rbold .. "  parte interessata acquista il TP per " .. "30 cibo!" fbold $) +crtext 30 trunk-food+
			then
		then goodfx morale++all
;

cyoa: common2-rare-tp
	"Piu prezioso delloro" cyoa-title!
	($ Thegroup .. "  trova un " .. "ROTOLO INTATTO DI CARTA IGIENICA" fbold .. " ' durante il saccheggio. La carta igienica e stata la prima cosa a finire dopo l'apocalisse. Ci sono ancora tonnellate di cibo in scatola e munizioni rimaste in America. La carta igienica decente e finita in fretta. Questa e la risorsa piu rara possibile. SCEGLI SAGGIAMENTE:" $) text!
		solo if
			choice( "Usa questo prezioso dono" )choice: common2-rare-tp-share
		else
			choice( "Condividi questo prezioso dono" )choice: common2-rare-tp-share
		then
		choice( "Scambialo il prima possibile" )choice: common2-rare-tp-trade
;

cyoa-choice: common2-rare-zombmaster-run
	image-creepy-girl
	($ "Il " .. "PADRONI DEGLI ZOMBIE" rbold .. "  crea un piccolo percorso a ostacoli e insegna " .. thegroup .. "  a correre in modo piu efficiente e meno imbarazzante!" $) text! goodfx
	' .dexterity++ foreach-team-char
;

: common2-rare-zomb-workout args( @ )
	<- @ .strengthREV <- @ .fitnessREV <- @ .strength++ <- @ .fitness++
;

cyoa-choice: common2-rare-zombmaster-fight
	image-creepy-girl
	($ "Il " .. "PADRONI DEGLI ZOMBIE" rbold .. "  illumina " .. thegroup .. "  sulle armi da mischia con alcuni consigli e alcune " .. "ADDESTRAMENTO MOLTO PERICOLOSO." rbold $) text! goodfx bitefx
	' common2-rare-zomb-workout foreach-team-char sting--all
;

cyoa-choice: common2-rare-zombmaster-shoot
	image-creepy-girl
	($ "Il " .. "PADRONI DEGLI ZOMBIE" rbold .. "  da " .. thegroup .. "  una grande lezione sul tiro corretto! Assegna un compito per casa per fare una vera caccia agli zombi e fornisce un mucchio di munizioni e un fucile!" $) text! goodfx ' .shootingREV foreach-team-char ' .shooting++ foreach-team-char
	60 trunk-bullet+ 40 trunk-rifle-shell+ 30 trunk-shotgun-shell+ 1 WEAPON_RIFLE trunk.weapon+
;

cyoa-choice: common2-rare-zombmaster-tough
	image-creepy-girl
	($ "Il " .. "PADRONI DEGLI ZOMBIE" rbold .. "  grugnisce." .. thegroup .. "  come indurire " .. ghis .. "  la pelle contro i morsi di zombi! Questo processo comporta il suo pestaggio " .. ghim .. "  su." $) text! goodfx bitefx
	' .vitality++ foreach-team-char sting--all
;

cyoa: common2-rare-zombmaster
	image-creepy-girl "Il Maestro Zombi" cyoa-title!
	($ Thegroup .. " ' incontra una strana donna che indossa occhiali, una sciarpa e un mantello. Dice di essere la " .. "PADRONI DEGLI ZOMBIE" rbold .. "  e che insegnera uno dei suoi segreti sugli zombi come servizio allumanita." $) text! goodfx
		choice( "Lezione di tiro agli zombi" )choice: common2-rare-zombmaster-shoot
		choice( "Corri via costantemente" )choice: common2-rare-zombmaster-run
		choice( "Come combattere con la mischia" )choice: common2-rare-zombmaster-fight
		choice( "Indurisci contro i morsi" )choice: common2-rare-zombmaster-tough
;

// END rares

// Man trapped under cardboard thievery

0 value common2-cboardtype

: cboardprize
	common2-cboardtype case
		1 of "MANGANELLO" endof
		2 of "FUCILE A POMPA" endof
		3 of "FUCILE" endof
		4 of "REVOLVER DOLT" endof
		5 of "ASCIA ANTINCENDIO" endof
		6 of "REVOLVER A CANNA CORTA" endof
	endcase
;

: cboardget
	common2-cboardtype case
		1 of 1 WEAPON_NIGHTSTICK endof
		2 of 8 trunk-shotgun-shell+ 1 WEAPON_SHOTGUN endof
		3 of 12 trunk-rifle-shell+ 1 WEAPON_RIFLE endof
		4 of 6 trunk-bullet+ 1 WEAPON_DOLT endof
		5 of 1 WEAPON_FIRE endof
		6 of 6 trunk-bullet+ 1 WEAPON_SNUBNOSE endof
	endcase
;

cyoa-choice: common2-cboard-steal
	($ cname .. " ruba l'uomo indifeso " .. cboardprize rbold .. "." $) text! badfx
	cloyalty- if
		($ cHe .. "  si sente piuttosto compiaciuto di questo." $) +crtext morale++
	else
		cloyalty+ if
			($ cHe .. "  si sente incredibilmente in colpa per questo!" $) +crtext morale3-
		else
			($ cHe .. "  si sente in colpa per questo." $) +crtext morale--
		then
	then loyaltyREV
	($ "Ha un " rbold .. cboardprize rbold $) +crtext cboardget trunk.weapon+
;

: cboard-help-check args( @ )
	<- @ .cloyalty- if <- @ .loyaltyREV <- @ .morale-- then; <- @ .morale++
;

cyoa-choice: common2-cboard-help
	($ Thegroup .. " ' salva l'uomo da tutte le scatole di cartone che lo bloccano. Non puo risparmiare una ricompensa, ma e davvero grato!" $) text! ' cboard-help-check foreach-team-char goodfx
;

cyoa-choice: common2-cboard-bandit
	($ cname .. " ruba l'uomo indifeso " .. cboardprize rbold .. "  e poi prende il resto delle sue provviste!" $) text! badfx
	rob-guilt	
	($ "Ha un " rbold .. cboardprize rbold $) +crtext cboardget trunk.weapon+ 
	10 20 rnd trunk-bullet+ 5 15 rnd trunk-rifle-shell+ 3 12 rnd trunk-shotgun-shell+
	2 4 rnd trunk-medical+ 1 6 rnd trunk-food+
	aholeREV
;

cyoa-bridge: common2-cboard-steal4
	someicon
	"Chi dovrebbe rubare la sua arma?" cyoa-text!
	' common2-cboard-steal cyoa-team-choice
;

cyoa: common2-cboard
	"Aiutare qualcuno in difficolta!" cyoa-title!
	1 6 rnd to common2-cboardtype
	($ "Mentre cercava cibo, " .. thegroup .. " ' incontra un uomo coperto di scatole di cartone. Dice di essere rimasto bloccato li per giorni! Ha un " .. cboardprize rbold .. "  sdraiato per terra fuori dalla sua portata." $) text!
	choice( "Aiutalo ad alzarsi" )choice: common2-cboard-help
	most-ahole if
		charchoice( ": Ruba tutte le sue provviste" )choice: common2-cboard-bandit
		team-count 2 > if
			choice( "Italiano" )choice: common2-cboard-steal4
		then;
	then	
		
	solo if
		soloer choosechar choice( "Rubargli larma" )choice: common2-cboard-steal
	else
		"Qualcuno dovrebbe rubargli larma?" +crtext
		team-count 3 > if
			choice( "Italiano" )choice: common2-cboard-steal4
		then 
		' common2-cboard-steal 3 #cyoa-team-choice
	then
;

// Quick template for the usual "scegli qualcun altro" choice
(*

cyoa-bridge: 4
	someicon
	"Chi dovrebbe?" cyoa-text!
	'  cyoa-team-choice
;

		team-count 3 > if
			'  2 #cyoa-team-choice
			choice( "Scegli qualcun altro" )choice: 4
		else

		then

*)

// Farm help

: common2-farm-reward
	8 trunk-food+
	most-charming if
		choosechar ($ cname .. "  convince i contadini a dargli " .. chim .. "  un forcone!" $) +crtext
		1 WEAPON_PITCHFORK trunk.weapon+
	then
;

cyoa-choice: common2-farm-mechanic
	($ cname .. "  offre i " .. chis .. "  suoi servizi per aggiustare le cose nella fattoria " $) text!
	cmechanic+ if	
		($ cHe .. " ' e cosi abile che la grande quantita di lavoro non e un grosso problema." $) +text
	else
		($ "'Ci sono cosi tante cose rotte che " .. che .. "  diventa esausto." $) +text cchar tired
	then
	mechanicREV mechanic++
	cchar .mechanical 3 < if
		($ "I contadini si rifiutano di dare una ricompensa dopo aver scoperto che tutto e ancora rotto." $) +crtext badfx
	else
		($ "I contadini sono grati per il lavoro!" $) +crtext goodfx common2-farm-reward
	then
;

cyoa-choice: common2-farm-bandit
	($ cname .. "  deruba i pacifici contadini. Sono stati completamente colti di sorpresa contro una minaccia non zombie." $) text! badfx
	14 trunk-food+ rob-guilt2 aholeREV
;

cyoa-choice: common2-farm-haul
	someicon
	($ Thegroup .. " it's a lot of work. " .. gthey .. "  ottenere" .. g1s .. " molto stanco!" $) text! goodfx 
	' .strengthREV foreach-team-char ' .strength++ foreach-team-char common2-farm-reward
	// added STR check for scythe
	strongest-pass if strongest choosechar
		($ "I contadini notarono " .. cname .. "'s unusual strength and gave " .. chim .. "  una falce in regalo!" $) +crtext
		1 WEAPON_SCYTHE trunk.weapon+
	then
	nosleep
;

cyoa-choice: common2-farm-till
	someicon
	($ Thegroup .. " ' aiuta con la lavorazione dei campi e la semina di nuove colture. E un lavoro estenuante, e " .. gthey .. "  ottenere" .. g1s .. " molto stanco!" $) text! goodfx 
	' .fitnessREV foreach-team-char ' .fitness++ foreach-team-char common2-farm-reward
	// added FIT check for scythe
	fittest-pass if fittest choosechar
		($ "I contadini notarono " .. cname .. "' livello di forma fisica e ha dato " .. chim .. "  una falce in regalo!" $) +crtext
		1 WEAPON_SCYTHE trunk.weapon+
	then
	nosleep
;

cyoa: common2-farm
	"Un viaggio alla fattoria" cyoa-title!
	($ Thegroup .. "  incontra alcune persone che stanno cercando di gestire una piccola fattoria. Offrono 8 cibo in cambio di un po di aiuto." $) text!
		choice( "Lavorare e piantare" )choice: common2-farm-till
		choice( "Trasportare e trasportare" )choice: common2-farm-haul
		most-ahole if
			charchoice( ": Robare la fattoria" )choice: common2-farm-bandit
		then
		mechanicest	charchoice( ": Lavori di riparazione" )choice: common2-farm-mechanic
		choice( "Non ce tempo per aiutare" )choice
;

// Annoying diplomat

: common2-detoured
	vehicle.which@ 0 > if choice( "Fare una deviazione" )choice: car-detour else
		// ($ Thegroup .. "  prende una lunga deviazione a piedi." $) +crtext 
		1 calendar-day+ +cr fullcamp
	then
;

cyoa-bridge: common2-meeting-talk
	($ cname .. "  parla con il rappresentante fastidioso." $) text!
	cpet? if
		($ cHe .. " ' non dovrebbe essere in grado di parlare, quindi " .. che .. "  lecca principalmente " .. chim .. "se stesso. Il cretino e davvero insultato dal dover parlare a un " .. cdogcat .. "pass. " .. thegroup .. "  through!" $) +crtext goodfx morale++all
	then;
	cchar grating? if
		($ cHe .. "  davvero va daccordo! Scambiano soprattutto " .. "CALMA" pbold .. "  battute tra di loro. Per unora. " .. Thegroup .. "  viene lasciato passare." $) +crtext witsattREV goodfx cool-it++ cool-it++
	then;
	cchar oblivious? if
		($ cHe .. " ' in qualche modo non nota tutte le volte che quel cretino e fastidioso o insultante. L'incontro va bene e " .. thegroup .. "  viene lasciato passare." $) +crtext witsattREV goodfx
	then;
	cattitude+ if
		($ cHe .. "  riesce a essere molto educato. Dopo abbastanza domande inutili, " .. thegroup .. "  viene lasciato passare attraverso la barricata." $) +crtext attitudeREV goodfx
	then;
	cchar angrystrong? if
		($ cHe .. "  perde " .. chis .. "  subito il carattere e lo mette KO con un pugno. Le guardie della barricata sono impressionate da questo! Una fa un pollice in su. Decidono di lasciar passare " .. thegroup .. "  comunque." $) +crtext attitudeREV strengthREV goodfx morale++all
	then;
	ctemper- if
		($ cHe .. "  perde " .. chis .. "  subito il carattere e inizia a urlare. Questo spaventa davvero le guardie, e iniziano a sparare. " .. Thegroup .. "  deve fuggire!" $) +crtext temperREV bitefx sting--all
		common2-detoured	
	then;
	ctemper+ temperroll or if
		($ cHe .. "  riesce a controllare " .. chis .. "  carattere! " .. Thegroup .. " viene lasciato passare attraverso la barricata dopo un ulteriore fastidio." $) +crtext goodfx
	else
		($ cHe .. " cerca di controllare " .. chis .. " temper, ma il tizio e troppo fastidioso. Corre e dice alle guardie di non aprire la porta. " .. Thegroup .. " deve trovare un modo per aggirare!" $) +crtext badfx
		common2-detoured
	then temperREV
;

cyoa-bridge: common2-meeting-talk4
	someicon
	"Chi dovrebbe cercare di affrontare la persona fastidiosa?" cyoa-text!
	' common2-meeting-talk cyoa-team-choice
;

cyoa: common2-meeting
	"Diplomazia del raffreddamento" cyoa-title!
	($ Thegroup .. " e bloccato da un altro gruppo di sopravvissuti che ha eretto una barricata per proteggersi. Sono diffidenti e pesantemente armati.	La persona che mandano a parlare e " .. "ESTREMAmente FASTIDIOSO" rbold .. "." $) text!
		solo if
			($ "  Continua a dire cose come " .. "CALMA" pbold .. "  e " .. "'DILLO NON SPRUZZARLO." pbold $) +text
			soloer choosechar choice( "Cerca di non perdere la calma" )choice: common2-meeting-talk
		else
			"  Chi dovrebbe provare ad affrontarlo?" +text
			team-count 3 > if
				' common2-meeting-talk 2 #cyoa-team-choice
				choice( "Scegli qualcun altro" )choice: common2-meeting-talk4
			else
				' common2-meeting-talk 3 #cyoa-team-choice
			then
		then
		vehicle.which@ 0 > if smartest charchoice( ": fai una deviazione" )choice: car-detour then
;

// Park Chatting

: common2-parkchat-tipme args( @ )
	<- @ 40 %chance if 
		.shooting++ 
	else
		50 %chance if 
			.mechanic++
		else
			.medical++
		then
	then
;

cyoa-choice: common2-parkchat-tips
	cpet? if 
		($ cname .. " " .. cbarkmeow .. "s in un modo molto saggio ed esperto." $) text!
	else
		($ cname .. "  scambia consigli su diversi argomenti." $) text!
	then
	cwits- if
		"Il sopravvissuto amichevole e confuso dal consiglio senza valore." +crtext badfx witsREV
	then;
	cwits+ if
		"Il sopravvissuto amichevole e illuminato dal consiglio e fornisce alcuni ottimi suggerimenti suoi!" +crtext
		goodfx ' common2-parkchat-tipme foreach-team-char witsREV
	then;
		($ "Il sopravvissuto amichevole lo apprezza e da un piccolo consiglio a " .. cname .. "  in cambio." $) +crtext
		cchar common2-parkchat-tipme goodfx witsREV
;

cyoa-choice: common2-parkchat-pep
	cpet? if 
		($ cname .. " " .. cbarkmeow .. "s in modo rassicurante." $) text!
	else
		($ cname .. "  cerca di fare un discorso di incoraggiamento." $) text!
	then
	cattitude- if
		"Il sopravvissuto amichevole in realta ora si sente peggio." +crtext badfx attitudeREV
	then;
	cattitude+ if
		"Il sopravvissuto amichevole si sente molto ottimista dopo il discorso e tiene un discorso stimolante!" +crtext
		morale2+all goodfx attitudeREV
	then;
		($ "Il sopravvissuto amichevole ricambia con alcune parole rassicuranti." $) +crtext
		morale++all goodfx attitudeREV
;

cyoa-choice: common2-parkchat-bandit
	($ cname .. " ' deruba facilmente la persona amichevole. Non aveva molto, ma questo gli insegnera un'importante lezione sulla fiducia negli altri." $) text! badfx
		2 4 rnd trunk-food+ 50 %chance if 1 3 rnd trunk-medical+ then rob-guilt
		aholeREV
;

cyoa-bridge: common2-parkchat-coolit
	cpet? if
		($ cname .. "  inizia " .. "SCOPREGGIARE" rbold .. "  alla persona, e in qualche modo continua a farlo per diversi minuti senza fermarsi neanche per un momento." $)
	else
		($ cname .. "  interrompe il sopravvissuto amichevole a meta frase, per dirgli di " .. "CALMARSI." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Ragazzo amichevole" recruitee .name!
		"'Ne e sbalordito. Afferma che tutto cio e secondo le profezie e che ha tenuto un dono per il Prescelto!" +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Fissa " .. cname .. ", senza parlare, fino a quando " .. thegroup .. " foglie." $) +crtext
		morale--all badfx
	then witsattREV 
;

cyoa: common2-parkchat
	"Come un giorno normale" cyoa-title!
	($ Thegroup .. "  incontro un altro che si riposa tranquillamente su una panchina del parco. Sembra amichevole. Hanno una conversazione, come prima della fine del mondo.	Cosa era il tema principale?" $) text!
		smartest charchoice( ": Scambio di consigli" )choice: common2-parkchat-tips
		gallant charchoice( ": Discorso di incoraggiamento" )choice: common2-parkchat-pep
		most-ahole if
			charchoice( ": Ti danno le tue cose" )choice: common2-parkchat-bandit
		then
		most-grating if
			charchoice( ": Calma" fcoolit )choice: common2-parkchat-coolit
		then
;

// Busted leg

cyoa-choice: common2-busted-med
	($ cname .. " ' prova a trattare la gamba rotta della donna." $) text!
	cmedical+ if
		($ cHe .. "  usa " .. chis .. "  utilizza le sue conoscenze mediche per riparare rapidamente la gamba di lei! Lei apprezza laiuto e da quanto puo sparire" $) +crtext morale++ medicalREV medical++ goodfx -1 trunk-medical+ 2 trunk-food+
	then;
	cmedical- if
		($ cHe .. " non sa cosa fare a " .. che .. "  fa, perdono un giorno, e forse rende le cose un po peggiori." $) +crtext medicalREV cloyalty- not if morale-- then loyaltyREV +cr fullcamp badfx -1 trunk-medical+ 1 calendar-day+
	then;
	medicalroll if
		($ cHe .. " ' trascorre un giorno per aiutarla a trattare la gamba. Lei e gratissima, ma non puo sparire molto." $) +crtext morale++ medicalREV medical++ goodfx 2 trunk-food+
	else
		($ cHe .. " ' prova ad aiutare in un giorno, ma non riesce a fare molto. Tuttavia, la pratica rende perfetto." $) +crtext medicalREV medical++ cloyalty- not if morale-- then loyaltyREV badfx
	then +cr fullcamp -1 trunk-medical+ 1 calendar-day+ heal-event
;

cyoa-bridge: common2-busted-coolit
	cpet? if
		($ cname .. "  comincia a ruotare intorno al freddo terreno." $)
	else
		($ cname .. "  dice alla donna di " .. "CALMARSI." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Donna con le stampelle" recruitee .name!
		($ "Lo prende come un consiglio di applicarsi degli impacchi freddi alla gamba. Dopo che " .. thegroup .. "  lha aiutata in questo, offre una ricompensa!" $) +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Lei chiede " .. "COSA CE CHE NON VA IN TE, ESATTAMENTE?" pbold .. "  prima di andarsene zoppicando disgustata." $) +crtext
		morale--all badfx
	then witsattREV
;

cyoa-choice: common2-busted-bandit
	($ cname .. "  deruba facilmente la donna ferita dei pochi rifornimenti che le erano rimasti. Ottimo lavoro!" $) text! badfx
		2 4 rnd trunk-food+ rob-guilt aholeREV
;

cyoa: common2-busted
	"Gamba rotta" cyoa-title!
	($ Thegroup .. "  incontra una donna che zoppica con una stampella. Una delle sue gambe era ferita in una caduta recente mentre stava scappando dagli zombie." $) text!
		trunk-medical 0 > if
			medicalest charchoice( ": Aiutala con la gamba" )choice: common2-busted-med
			choice( "Non ce tempo per aiutare" )choice
		else
			($ Thegroup .. "  non ha forniture mediche per aiutarla." $) +crtext
			choice( "Lasciala stare" )choice
		then
		most-grating if
			charchoice( ": Dille di STARE CALMA" fcoolit )choice: common2-busted-coolit
		then
		most-ahole if
			charchoice( ": Ruba i suoi rifornimenti" )choice: common2-busted-bandit
		then
;

// Car trouble

: common2-carhelp-win
	homesearch1 ammosearch1
;

cyoa-choice: common2-carhelp-mech
	($ cname .. " ' cerca di riparare la macchina rotta dell'uomo." $) text!
	cmechanic+ if
		($ cHe .. "  usa " .. chis .. "  conoscenze di auto per ripararla velocemente! Apprezza laiuto e da alcune provviste!" $) +crtext morale++ mechanicREV mechanic++ goodfx ammosearch1
	then;
	cmechanic- if
		($ cHe .. "  non ha idea di cosa " .. che .. "  stia facendo, e spreca un bel giorno a scorreggiare in giro." $) +crtext mechanicREV cloyalty- not if morale-- then loyaltyREV +cr fullcamp badfx 1 calendar-day+ heal-event
	then;
	mechanicroll if
		($ cHe .. " ' impiega un giorno per aiutare a riparare la macchina dell'uomo. Da alcune provviste come ricompensa!" $) +crtext morale++ mechanicREV mechanic++ goodfx ammosearch1
	else
		($ cHe .. " ' cerca di aiutare nel corso di un giorno, ma non ha successo. Tuttavia, e una buona pratica." $) +crtext mechanicREV mechanic++ cloyalty- not if morale-- then loyaltyREV badfx
	then +cr fullcamp 1 calendar-day+ heal-event
;

cyoa-bridge: common2-carhelp-coolit
	cpet? if
		($ cname .. "  ignora luomo abbandonato e inizia a mangiare le formiche che strisciano sul terreno freddo." $)
	else
		($ cname .. "  dice alluomo abbandonato di " .. "CALMARSI." pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Uomo Abbandonato" recruitee .name!
		($ "'HAI RAGIONE! LA MACCHINA HA SOLO BISOGNO DI PIU REFRIGERANTE PER RICOMINCIARE A FUNZIONARE!" pbold .. "  Offre una ricompensa!" $) +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Inizia a urlare contro " .. thegroup .. "  finche " .. gthey .. "  non se ne va" .. g1s .. "." $) +crtext
		morale--all badfx
	then witsattREV
;

cyoa-choice: common2-carhelp-bandit
	($ cname .. " deruba facilmente luomo bloccato delle sue provviste, persino della sua chiave inglese." $) text! badfx
		common2-carhelp-win 1 WEAPON_WRENCH trunk.weapon+ rob-guilt aholeREV
;

cyoa: common2-carhelp
	"Aiutare con i problemi con lauto" cyoa-title!
	char-gen to recruitee 
	0 recruitee .mechanical!
	0 recruitee .female! ricon
	WEAPON_WRENCH recruitee .weapona!
	($ Thegroup .. " 'incontra un uomo che sta cercando di riparare la sua auto rotta. Chiaramente non ha idea di cosa stia facendo." $) text!
		mechanicest charchoice( ": Aiuto a riparare lauto" )choice: common2-carhelp-mech
		most-charming if
			charchoice( ": Reclutalo" )choice: recruit-yes
		then
		most-grating if
			charchoice( ": Digli di STARE CALMO" fcoolit )choice: common2-carhelp-coolit
		then
		most-ahole if
			charchoice( ": Ruba le sue provviste" )choice: common2-carhelp-bandit
		then
		choice( "Non ce tempo per aiutare" )choice
;

// Bandit raid

: common2-raid-prize
	1 WEAPON_RIFLE trunk.weapon+ 20 trunk-rifle-shell+ 15 trunk-food+ morale++all goodfx
;

cyoa-choice: common2-raid-calmshot
	($ cname .. " prende un fucile da cowboy dagli abitanti e si nasconde in un posto alto. Quando arrivano i banditi, " .. che .. " fa piu tiri incredibili per far scoppiare le loro gomme. I banditi si spaventano troppo per continuare lattacco! La lotta finisce prima di iniziare. Gli abitanti sono felici di dare la ricompensa!" $) text! common2-raid-prize
;

: common2-raid-shoot+
	dup humany? 50 %chance and if .shooting++ then
;

: common2-raid-fightprize
	choosehuman alive? if
		"Gli abitanti rimangono fedeli alla loro parola e danno una grossa ricompensa!" +crtext
		common2-raid-prize
		most-calmshot if
			choosechar ($ cname .. " fa dei tiri pazzeschi che ispirano gli altri." $) +crtext
			' .shooting++ foreach-team-char
		else
			' common2-raid-shoot+ foreach-team-char
		then
	then
;

cyoa-choice: common2-raid-fight
	someicon ($ Thegroup .. " decide di aiutare gli abitanti a combattere i banditi, direttamente." $) text!
	shootingREVall
	shooting-total 14 > if
		"Labilita nel tiro del gruppo aiuta gli abitanti a sopraffare i banditi. Nessuno si fa male!" +crtext
		common2-raid-fightprize
	then;
	shooting-total 10 > if
		"'La lotta e dura, ma le abilita nel tiro del gruppo salvano l'insediamento." +crtext
		ouch--all bitefx common2-raid-fightprize
	then;
	shooting-total 6 > if
		"'La lotta e brutale! Le abilita di tiro del gruppo aiutano a malapena a respingere i banditi." +crtext
		ouch12rnd--all bitefx common2-raid-fightprize
	then;
		"'L'aiuto fornito non e stato sufficiente nella sparatoria. I banditi allontanano tutti dall'insediamento." +crtext ouch2-all bitefx
;

cyoa-choice: common2-raid-mech
	($ cname .. " ' prova a rafforzare le difese dell'insediamento." $) text!
	cmechanic+ if
		($ cHe .. "  elabora rapidamente dei piani e convince le persone ad aiutare " .. chim .. "  trasforma il luogo in una fortezza! I banditi sono sopraffatti dalle nuove difese quando si presentano e fuggono senza ferire nessuno." $) +crtext mechanicREV mechanic++ common2-raid-prize
	then;
	mechanicroll if
		($ cHe .. "  spiega agli abitanti come costruire delle fortificazioni in alcune aree deboli. Le difese aggiuntive sembrano aiutare molto nellattacco dei banditi! Gli abitanti sono grati per laiuto!" $) +crtext mechanicREV mechanic++ common2-raid-prize
	else
		($ cHe .. " ' cerca di fortificare alcune cose, ma gli sforzi non sembrano aver aiutato molto contro il feroce attacco dei banditi. Gli abitanti feriti accusano " .. cname .. "  di aver perso tempo e si rifiutano di dare la ricompensa." $) +crtext mechanicREV morale--all sting--all bitefx
	then
;

cyoa: common2-raid
	"Difesa dal raid dei banditi" cyoa-title!
	($ Thegroup .. "  viene chiesto aiuto da un insediamento. Dicono che presto saranno attaccati dai banditi. Se " .. thegroup .. "  aiuta con la difesa, " .. gthey .. "  ricevera una ricompensa di 15 cibo, alcune munizioni e un fucile da cowboy! Questo potrebbe essere " .. "very dangerous..." rbold $) text!
		vehicle.which@ 0 > if 
			choice( "'Non aiutare, allontanarsi" )choice else choice( "'Non aiutarli" )choice
		then
		choice( "Aiutali nella sparatoria" )choice: common2-raid-fight
		mechanicest charchoice( ": Rafforza le difese" )choice: common2-raid-mech
		most-calmshot if
			charchoice( "': Me ne occupero io" )choice: common2-raid-calmshot
		then
;

// Rooftop relaxing

cyoa-choice: common2-relax-morale
	($ Thegroup .. "  trascorre un po di tempo " .. "' semplicemente a rilassarsi" fbold .. "." $) text!
	morale2+all cool-it++ goodfx
;

cyoa-choice: common2-relax-strength
	($ Thegroup .. "  esegue alcuni esercizi di forza improvvisati. Vengono sollevati i mobili piu pesanti che si riescono a sollevare, quindi rimessi giu. Ripeti fino a quando " .. "MEGABUFF" fbold .. "." $) text!
	' .strength++ foreach-team-char goodfx
;

cyoa-choice: common2-relax-fitness
	($ Thegroup .. "  esegue alcuni esercizi cardiovascolari improvvisati. Principalmente burpee, alcuni jumping jack. Ripeti fino a quando " .. "ULTRA FIT" fbold .. "." $) text!
	' .fitness++ foreach-team-char goodfx
;

cyoa-choice: common2-relax-med
	($ cname .. "  insegna a " .. everyone .. "  cosa " .. che .. "  sa dellassistenza medica di emergenza e dellodontoiatria semi indolore." $) text!
		cmedical- if 
			($ cHe .. " ' non ne sa molto! E una lezione completamente inutile." $) +crtext badfx
		else
			cchar ' .medical++ foreach-team-char-except goodfx
		then medicalREV morale++
;

cyoa-choice: common2-relax-mech
	($ cname .. "  insegna a " .. everyone .. "  cosa " .. che .. "  conosce la riparazione delle auto e altri modi per rovinare accidentalmente i macchinari." $) text!
		cmechanic- if 
			($ cHe .. " ' non ne sa molto! E una lezione completamente inutile." $) +crtext badfx
		else
			cchar ' .mechanic++ foreach-team-char-except goodfx
		then mechanicREV morale++
;

cyoa-choice: common2-roofrelax-reveal
	($ cname .. "  rivela " .. chis .. "  VERA ESSENZA." $) text!
		allREV goodfx
;

cyoa: common2-roofrelax
	image-tired-survivor 63 to cyoaicon
	($ Thegroup .. "  si concede un raro momento di pace per rilassarsi sul tetto di un appartamento." $) text!
	solo if
		soloer choosechar
		($ "Senza nessuno a infastidire " .. chim .. ", " .. che .. " decide di:" $) +crtext
		choice( "CALMA" )choice: common2-relax-morale
		choice( "Esercizio per la forza" )choice: common2-relax-strength
		choice( "Esercizio per la forma fisica" )choice: common2-relax-fitness
	else
		"'C'e tempo per parlare, e qualcuno rivela molto di se:" +crtext
		' common2-roofrelax-reveal cyoa-team-choice
	then
;

// Safehouse relaxing

cyoa: common2-saferelax
	image-door-on-brick 27 to cyoaicon
	($ Thegroup .. "  si concede un momento di pace per riposare in un rifugio, nessun zombie in vista." $) text!
	solo if
		soloer choosechar
		($ "Senza nessuno a infastidire " .. chim .. ", " .. che .. " decide di:" $) +crtext
		choice( "CALMA" )choice: common2-relax-morale
		choice( "Esercizio per la forza" )choice: common2-relax-strength
		choice( "Esercizio per la forma fisica" )choice: common2-relax-fitness
	else
		"Con un po di tempo libero, decidono di:" +crtext
			50 %chance if 
				choice( "CALMA" )choice: common2-relax-morale
			else
				50 %chance if
					choice( "Esercizio per la forza" )choice: common2-relax-strength
				else
					choice( "Esercizio per la forma fisica" )choice: common2-relax-fitness
				then
			then
			mechanicest charchoice( ": Insegna abilita meccaniche" )choice: common2-relax-mech
			medicalest charchoice( ": Insegna abilita mediche" )choice: common2-relax-med
	then
;

} }


cyoa{ road{ 

: camp-pic
	50 %chance if
		image-camp-1
	else
		image-camp-2
	then	
;

// Rares

cyoa-choice: common2-rare-tp-share
	someicon
	solo if
		($ cname .. " percebe que este rolo de papel higienico e um tesouro que nao deve ser dado" $) text! morale4+ goodfx 1 WEAPON_CARDBOARD trunk.weapon+
		zombop++
	then;
	"O time decide tentar compartilhar o rolo de papel higienico." text!
	loyalty-average 2 < if
		"'Nao leva muito tempo para eles se transformarem em uma briga enorme! A situacao desce em uma briga total. O papel higienico acaba sendo arrancado acidentalmente!" +crtext morale3-all badfx
	else
		loyalty-average 4 < if
			"'Ha algumas brigas inevitaveis por causa do compartilhar o TP, mas o grupo consegue superar as diferencas." +crtext morale2+all goodfx
		else
			"'O grupo se junta em torno do papel higienico, protegendoo mutuamente. Mesmo depois que o TP acaba, a tubulacao de cartao se torna um simbolo para a unidade do grupo." +crtext morale4+all goodfx
		then
	then
	' .loyaltyREV foreach-team-char 1 WEAPON_CARDBOARD trunk.weapon+
	zombop++
;

cyoa-choice: common2-rare-tp-trade
	someicon
	($ Thegroup .. " 'decide vender o rolo de papel higienico o mais rapido possivel. Nao vale levar algo TA valioso por ai!" $) text!
		50 %chance if
			"Uma assentamento compra o rolo de TP por 20 alimentos! Nao e ruim!" +crtext 20 trunk-food+
		else
			50 %chance if
				"'Um mercador astuto compra o TP por 12 alimentos. Nao posso queixarme!" +crtext 12 trunk-food+
			else
				($ "Uma " .. "Muito" rbold .. " parte interessada compra o TP por " .. "30 alimentos!" fbold $) +crtext 30 trunk-food+
			then
		then goodfx morale++all
;

cyoa: common2-rare-tp
	"Mais valioso que ouro" cyoa-title!
	($ Thegroup .. " " .. "ROLO DE PAPEL HIGIENICO INTOCAVEL" fbold .. " ' enquanto catava. Papel higienico foi a primeira coisa a acabar depois do apocalipse. Ainda ha toneladas de comida enlatada e municao restantes nos EUA. Papel higienico decente acabou rapido. Este e o recurso mais raro possivel. ESCOLHA COM SABEDORIA:" $) text!
		solo if
			choice( "Use este presente precioso" )choice: common2-rare-tp-share
		else
			choice( "Compartilhe este presente precioso" )choice: common2-rare-tp-share
		then
		choice( "Negocieo o mais rapido possivel" )choice: common2-rare-tp-trade
;

cyoa-choice: common2-rare-zombmaster-run
	image-creepy-girl
	($ "O " .. "MESTRE DOS ZUMBIS" rbold .. " monta uma pequena pista de obstaculos e ensina " .. thegroup .. "  a correr de forma mais eficiente e menos embaracosa!" $) text! goodfx
	' .dexterity++ foreach-team-char
;

: common2-rare-zomb-workout args( @ )
	<- @ .strengthREV <- @ .fitnessREV <- @ .strength++ <- @ .fitness++
;

cyoa-choice: common2-rare-zombmaster-fight
	image-creepy-girl
	($ "O " .. "MESTRE DOS ZUMBIS" rbold .. " ilumina " .. thegroup .. " em armas brancas com algumas dicas e " .. "TREINAMENTO MUITO PERIGOSO." rbold $) text! goodfx bitefx
	' common2-rare-zomb-workout foreach-team-char sting--all
;

cyoa-choice: common2-rare-zombmaster-shoot
	image-creepy-girl
	($ "O " .. "MESTRE DOS ZUMBIS" rbold .. "  da " .. thegroup .. " 3" $) text! goodfx ' .shootingREV foreach-team-char ' .shooting++ foreach-team-char
	60 trunk-bullet+ 40 trunk-rifle-shell+ 30 trunk-shotgun-shell+ 1 WEAPON_RIFLE trunk.weapon+
;

cyoa-choice: common2-rare-zombmaster-tough
	image-creepy-girl
	($ "O " .. "MESTRE DOS ZUMBIS" rbold .. "  grunhe." .. thegroup .. "  como endurecer " .. ghis .. "  bater  the skin of the animal with a wooden stick to make it more tender. " .. ghim .. " " $) text! goodfx bitefx
	' .vitality++ foreach-team-char sting--all
;

cyoa: common2-rare-zombmaster
	image-creepy-girl "O Mestre dos Zumbis" cyoa-title!
	($ Thegroup .. " " .. "MESTRE DOS ZUMBIS" rbold .. " e que ela ensinara uma de suas dicas secretas sobre zumbis como um servico a humanidade." $) text! goodfx
		choice( "Aula de tiro a zumbis" )choice: common2-rare-zombmaster-shoot
		choice( "So correr constantemente" )choice: common2-rare-zombmaster-run
		choice( "Como lutar com melee" )choice: common2-rare-zombmaster-fight
		choice( "Fique mais forte contra mordidas" )choice: common2-rare-zombmaster-tough
;

// END rares

// Man trapped under cardboard thievery

0 value common2-cboardtype

: cboardprize
	common2-cboardtype case
		1 of "BATO DE NOITE" endof
		2 of "ESpingarda" endof
		3 of "FUSIL" endof
		4 of "REVOLVER DOLT45" endof
		5 of "MACA DE FIRE" endof
		6 of "REVOLVER DE CANO CURTO" endof
	endcase
;

: cboardget
	common2-cboardtype case
		1 of 1 WEAPON_NIGHTSTICK endof
		2 of 8 trunk-shotgun-shell+ 1 WEAPON_SHOTGUN endof
		3 of 12 trunk-rifle-shell+ 1 WEAPON_RIFLE endof
		4 of 6 trunk-bullet+ 1 WEAPON_DOLT endof
		5 of 1 WEAPON_FIRE endof
		6 of 6 trunk-bullet+ 1 WEAPON_SNUBNOSE endof
	endcase
;

cyoa-choice: common2-cboard-steal
	($ cname .. "  rouba o homem indefeso " .. cboardprize rbold .. "." $) text! badfx
	cloyalty- if
		($ cHe .. " 'se sente muito satisfeito com isso.'" $) +crtext morale++
	else
		cloyalty+ if
			($ cHe .. " 'sentese incrivelmente culpado por isso!'" $) +crtext morale3-
		else
			($ cHe .. " 'se sente culpado por isso.'" $) +crtext morale--
		then
	then loyaltyREV
	($ "0.0 " rbold .. cboardprize rbold $) +crtext cboardget trunk.weapon+
;

: cboard-help-check args( @ )
	<- @ .cloyalty- if <- @ .loyaltyREV <- @ .morale-- then; <- @ .morale++
;

cyoa-choice: common2-cboard-help
	($ Thegroup .. " ' salva o homem de todas as caixas de papelao que o prendiam. Ele nao pode poupar uma recompensa, mas e muito grato!" $) text! ' cboard-help-check foreach-team-char goodfx
;

cyoa-choice: common2-cboard-bandit
	($ cname .. "  rouba o homem indefeso " .. cboardprize rbold .. "  e depois pega o resto de seus suprimentos!" $) text! badfx
	rob-guilt	
	($ "0.0 " rbold .. cboardprize rbold $) +crtext cboardget trunk.weapon+ 
	10 20 rnd trunk-bullet+ 5 15 rnd trunk-rifle-shell+ 3 12 rnd trunk-shotgun-shell+
	2 4 rnd trunk-medical+ 1 6 rnd trunk-food+
	aholeREV
;

cyoa-bridge: common2-cboard-steal4
	someicon
	"Quem deve roubar sua arma?" cyoa-text!
	' common2-cboard-steal cyoa-team-choice
;

cyoa: common2-cboard
	"Ajudando alguem necessitado!" cyoa-title!
	1 6 rnd to common2-cboardtype
	($ "Enquanto catava lixo, " .. thegroup .. " ' encontra um homem coberto de caixas de papelao. Ele diz que esta preso la ha dias! Ele tem um " .. cboardprize rbold .. "  deitado no chao fora de seu alcance." $) text!
	choice( "Ajudeo a se levantar" )choice: common2-cboard-help
	most-ahole if
		charchoice( ": Roube todos os seus suprimentos" )choice: common2-cboard-bandit
		team-count 2 > if
			choice( "Escolha um ladrao" )choice: common2-cboard-steal4
		then;
	then	
		
	solo if
		soloer choosechar choice( "Roube sua arma" )choice: common2-cboard-steal
	else
		"Alguem deveria roubar sua arma?" +crtext
		team-count 3 > if
			choice( "Escolha um ladrao" )choice: common2-cboard-steal4
		then 
		' common2-cboard-steal 3 #cyoa-team-choice
	then
;

// Quick template for the usual "escolha outra pessoa" choice
(*

cyoa-bridge: 4
	someicon
	"Quem deveria ?" cyoa-text!
	'  cyoa-team-choice
;

		team-count 3 > if
			'  2 #cyoa-team-choice
			choice( "Escolha alguem outro" )choice: 4
		else

		then

*)

// Farm help

: common2-farm-reward
	8 trunk-food+
	most-charming if
		choosechar ($ cname .. " convence os agricultores a darem " .. chim .. " um tridente!" $) +crtext
		1 WEAPON_PITCHFORK trunk.weapon+
	then
;

cyoa-choice: common2-farm-mechanic
	($ cname .. " ofertas " .. chis .. "  Servicos em consertar coisas na fazenda. " $) text!
	cmechanic+ if	
		($ cHe .. " ' e tao habilidoso que a grande quantidade de trabalho nao e um grande problema." $) +text
	else
		($ "'Ha tantas coisas quebradas que " .. che .. "  fica exausto." $) +text cchar tired
	then
	mechanicREV mechanic++
	cchar .mechanical 3 < if
		($ "Os fazendeiros se recusam a dar uma recompensa depois que descobrem que tudo ainda esta quebrado." $) +crtext badfx
	else
		($ "Os fazendeiros estao gratos pelo trabalho!" $) +crtext goodfx common2-farm-reward
	then
;

cyoa-choice: common2-farm-bandit
	($ cname .. "  rouba os pacificos fazendeiros. Eles foram completamente pegos de surpresa contra uma ameaca nao-zumbi." $) text! badfx
	14 trunk-food+ rob-guilt2 aholeREV
;

cyoa-choice: common2-farm-haul
	someicon
	($ Thegroup .. " ' carrega toneladas de coisas por ai. Sempre ha coisas para carregar em uma fazenda. E um trabalho exaustivo, e " .. gthey .. "  pega" .. g1s .. "  muito cansado!" $) text! goodfx 
	' .strengthREV foreach-team-char ' .strength++ foreach-team-char common2-farm-reward
	// added STR check for scythe
	strongest-pass if strongest choosechar
		($ "Os fazendeiros notaram " .. cname .. "''s forca incomum e deram " .. chim .. "  uma foice de presente!" $) +crtext
		1 WEAPON_SCYTHE trunk.weapon+
	then
	nosleep
;

cyoa-choice: common2-farm-till
	someicon
	($ Thegroup .. " ' ajuda a arar os campos e plantar novas colheitas. E um trabalho exaustivo, e " .. gthey .. "  pega" .. g1s .. "  muito cansado!" $) text! goodfx 
	' .fitnessREV foreach-team-char ' .fitness++ foreach-team-char common2-farm-reward
	// added FIT check for scythe
	fittest-pass if fittest choosechar
		($ "Os fazendeiros notaram " .. cname .. "''s nivel de condicionamento fisico e deram " .. chim .. "  uma foice de presente!" $) +crtext
		1 WEAPON_SCYTHE trunk.weapon+
	then
	nosleep
;

cyoa: common2-farm
	"Portugues" cyoa-title!
	($ Thegroup .. " correde contra algumas pessoas que estao tentando gerenciar uma pequena fazenda. Elas oferecem 8 comida em troca de alguma ajuda." $) text!
		choice( "Trabalhando e plantando" )choice: common2-farm-till
		choice( "Transportando e transportando" )choice: common2-farm-haul
		most-ahole if
			charchoice( ": Rouba a fazenda" )choice: common2-farm-bandit
		then
		mechanicest	charchoice( ": Trabalhos de reparacao" )choice: common2-farm-mechanic
		choice( "Nao tem tempo para ajudar" )choice
;

// Annoying diplomat

: common2-detoured
	vehicle.which@ 0 > if choice( "Fazer um desvio" )choice: car-detour else
		// ($ Thegroup .. " toma uma longa desvio a pe." $) +crtext 
		1 calendar-day+ +cr fullcamp
	then
;

cyoa-bridge: common2-meeting-talk
	($ cname .. " fala com o representante irritante." $) text!
	cpet? if
		($ cHe .. " 'nao deveria ser capaz de falar, portanto " .. che .. " seu principal ato e lamber " .. chim .. "ele mesmo. O dingus se ofende muito por ter que falar com um " .. cdogcat .. ", mas os guardas estao divertidos. Eles deixam " .. thegroup .. " 'vai!'" $) +crtext goodfx morale++all
	then;
	cchar grating? if
		($ cHe .. " " .. "ESFRIE" pbold .. "  trocadilhos entre si. Por uma hora. " .. Thegroup .. " e permitido passar." $) +crtext witsattREV goodfx cool-it++ cool-it++
	then;
	cchar oblivious? if
		($ cHe .. " 1 " .. thegroup .. " is let go." $) +crtext witsattREV goodfx
	then;
	cattitude+ if
		($ cHe .. " 'consegue ser muito educado. Depois de questionar o suficiente sem sentido, ' " .. thegroup .. " e deixado passar pela barricada." $) +crtext attitudeREV goodfx
	then;
	cchar angrystrong? if
		($ cHe .. " perde " .. chis .. "  pass. " .. thegroup .. " 'de qualquer forma'" $) +crtext attitudeREV strengthREV goodfx morale++all
	then;
	ctemper- if
		($ cHe .. " perde " .. chis .. "  tem que se calmar de imediato e comeca a gritar. Isso realmente espanta os guardas, e eles comecam a disparar. " .. Thegroup .. " precisa fugir!" $) +crtext temperREV bitefx sting--all
		common2-detoured	
	then;
	ctemper+ temperroll or if
		($ cHe .. " consegue controlar " .. chis .. " temperamento! " .. Thegroup .. " e deixado passar pela barricada apos alguma mais anedota." $) +crtext goodfx
	else
		($ cHe .. " tenta controlar " .. chis .. " temperamento, mas a pessoa e simplesmente muito irritante. Ele corre e conta para os guardas para nao abrem a porta. " .. Thegroup .. " tem que encontrar uma maneira de se desviar!" $) +crtext badfx
		common2-detoured
	then temperREV
;

cyoa-bridge: common2-meeting-talk4
	someicon
	"Quem deve tentar lidar com a pessoa irritante?" cyoa-text!
	' common2-meeting-talk cyoa-team-choice
;

cyoa: common2-meeting
	"Diplomacia de calma" cyoa-title!
	($ Thegroup .. " esta bloqueado por outro grupo de sobreviventes que instalou uma barricada para se protegerem. Eles sao suspeitosos e armados a ponto. A pessoa que e enviada a falar e " .. "EXTREMEMENTE IRRITANTE" rbold .. "." $) text!
		solo if
			($ " Ele diz coisas como " .. "ESFRIE" pbold .. "  e " .. "'DIGA NaO AO SPRAY." pbold $) +text
			soloer choosechar choice( "Tente nao perder a paciencia" )choice: common2-meeting-talk
		else
			"  Quem deveria tentar lidar com ele?" +text
			team-count 3 > if
				' common2-meeting-talk 2 #cyoa-team-choice
				choice( "Escolha alguem outro" )choice: common2-meeting-talk4
			else
				' common2-meeting-talk 3 #cyoa-team-choice
			then
		then
		vehicle.which@ 0 > if smartest charchoice( ": Faca um desvio" )choice: car-detour then
;

// Park Chatting

: common2-parkchat-tipme args( @ )
	<- @ 40 %chance if 
		.shooting++ 
	else
		50 %chance if 
			.mechanic++
		else
			.medical++
		then
	then
;

cyoa-choice: common2-parkchat-tips
	cpet? if 
		($ cname .. " " .. cbarkmeow .. "s de uma forma muito sabia e experiente." $) text!
	else
		($ cname .. "  trocam conselhos sobre diferentes topicos." $) text!
	then
	cwits- if
		"O sobrevivente amigavel fica confuso com o conselho inutil." +crtext badfx witsREV
	then;
	cwits+ if
		"O sobrevivente amigavel fica iluminado pelo conselho e da algumas otimas dicas proprias!" +crtext
		goodfx ' common2-parkchat-tipme foreach-team-char witsREV
	then;
		($ "O sobrevivente amigavel aprecia e da um pequeno conselho a " .. cname .. "  em troca." $) +crtext
		cchar common2-parkchat-tipme goodfx witsREV
;

cyoa-choice: common2-parkchat-pep
	cpet? if 
		($ cname .. " " .. cbarkmeow .. "s de forma reconfortante." $) text!
	else
		($ cname .. "  tenta dar um discurso motivacional." $) text!
	then
	cattitude- if
		"O sobrevivente amigavel na verdade se sente pior agora." +crtext badfx attitudeREV
	then;
	cattitude+ if
		"O sobrevivente amigavel se sente muito otimista apos a palestra e faz um discurso inspirador!" +crtext
		morale2+all goodfx attitudeREV
	then;
		($ "O sobrevivente amigavel da algumas palavras tranquilizadoras em troca." $) +crtext
		morale++all goodfx attitudeREV
;

cyoa-choice: common2-parkchat-bandit
	($ cname .. " 'Rouba facilmente a pessoa amigavel. Ele nao tinha muito, mas isso lhe ensinara uma licao importante em confiar nos outros.'" $) text! badfx
		2 4 rnd trunk-food+ 50 %chance if 1 3 rnd trunk-medical+ then rob-guilt
		aholeREV
;

cyoa-bridge: common2-parkchat-coolit
	cpet? if
		($ cname .. " comeca " .. "PEIDANDO" rbold .. " 'na pessoa, e de alguma forma continua a fazelo por varios minutos sem parar por um momento sequer.'" $)
	else
		($ cname .. "  'Shut up, Im trying to concentrate!' " .. "'CALMA AI.'" pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Cara Legal" recruitee .name!
		"'Ele se envergonha deste. Afirma que isto esta de acordo com as profecias, e que tem guardado um presente para o Escolhido!" +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Ele olha para " .. cname .. ", sem falar, ate ele sair. " .. thegroup .. " folhas." $) +crtext
		morale--all badfx
	then witsattREV 
;

cyoa: common2-parkchat
	"Como um dia normal" cyoa-title!
	($ Thegroup .. " encontra outra pessoa sentada em banco de parque. Ele parece amigavel. Eles tem uma conversa, igual antes do mundo acabar.	O que foi o assunto principal?" $) text!
		smartest charchoice( ": Troca de conselhos" )choice: common2-parkchat-tips
		gallant charchoice( ": Palavra de coragem" )choice: common2-parkchat-pep
		most-ahole if
			charchoice( ": Meu presente para voce" )choice: common2-parkchat-bandit
		then
		most-grating if
			charchoice( ": Calate" fcoolit )choice: common2-parkchat-coolit
		then
;

// Busted leg

cyoa-choice: common2-busted-med
	($ cname .. " 'tenta tratar da perna quebrada da mulher." $) text!
	cmedical+ if
		($ cHe .. " usando " .. chis .. " 1000 gold" $) +crtext morale++ medicalREV medical++ goodfx -1 trunk-medical+ 2 trunk-food+
	then;
	cmedical- if
		($ cHe .. "  nao tem ideia do que " .. che .. " 'esta fazendo, perde um dia, e talvez piora um pouco as coisas.'" $) +crtext medicalREV cloyalty- not if morale-- then loyaltyREV +cr fullcamp badfx -1 trunk-medical+ 1 calendar-day+
	then;
	medicalroll if
		($ cHe .. " 'Leva um dia para ajudar a mulher a tratar a perna. Ela agradece, mas nao pode dar muito!'" $) +crtext morale++ medicalREV medical++ goodfx 2 trunk-food+
	else
		($ cHe .. " tenta ajudar ao longo do dia, mas nao consegue fazer muito. Ainda assim, a pratica leva a perfeicao." $) +crtext medicalREV medical++ cloyalty- not if morale-- then loyaltyREV badfx
	then +cr fullcamp -1 trunk-medical+ 1 calendar-day+ heal-event
;

cyoa-bridge: common2-busted-coolit
	cpet? if
		($ cname .. " comeca a rolar no chao frio." $)
	else
		($ cname .. "  diz a mulher para " .. "'CALMA AI.'" pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Mulher com muleta" recruitee .name!
		($ "Ela interpreta isso como um conselho para aplicar compressas frias na perna. Depois de " .. thegroup .. "  a ajuda com isso, ela oferece uma recompensa!" $) +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Ela pergunta " .. "O QUE HA DE ERRADO COM VOCE, EXATAMENTE?" pbold .. "  antes de mancar para longe com desgosto." $) +crtext
		morale--all badfx
	then witsattREV
;

cyoa-choice: common2-busted-bandit
	($ cname .. "  rouba facilmente os poucos suprimentos que restavam da mulher ferida. Muito bem!" $) text! badfx
		2 4 rnd trunk-food+ rob-guilt aholeREV
;

cyoa: common2-busted
	"Perna quebrada" cyoa-title!
	($ Thegroup .. "  encontra uma mulher mancando com uma muleta. Uma de suas pernas foi ferida em uma queda recente enquanto ela fugia de zumbis." $) text!
		trunk-medical 0 > if
			medicalest charchoice( ": Ajude sua perna" )choice: common2-busted-med
			choice( "Nao tem tempo para ajudar" )choice
		else
			($ Thegroup .. "  nao tem nenhum material medico para ajudala." $) +crtext
			choice( "Deixea em paz" )choice
		then
		most-grating if
			charchoice( ": Diga a ela para RELAXAR" fcoolit )choice: common2-busted-coolit
		then
		most-ahole if
			charchoice( "roube suas suprimentos" )choice: common2-busted-bandit
		then
;

// Car trouble

: common2-carhelp-win
	homesearch1 ammosearch1
;

cyoa-choice: common2-carhelp-mech
	($ cname .. " tenta consertar o carro quebrado do homem." $) text!
	cmechanic+ if
		($ cHe .. " usando " .. chis .. "  Carro conhecimento para reparalo rapidamente! Ele agradece a ajuda e da alguns suprimentos!" $) +crtext morale++ mechanicREV mechanic++ goodfx ammosearch1
	then;
	cmechanic- if
		($ cHe .. "  means " .. che .. " 'esta fazendo, e desperdica um bom dia dando voltas'" $) +crtext mechanicREV cloyalty- not if morale-- then loyaltyREV +cr fullcamp badfx 1 calendar-day+ heal-event
	then;
	mechanicroll if
		($ cHe .. " 'Leva um dia para ajudar a consertar o carro do homem. Ele da algumas suprimentos como recompensa!'" $) +crtext morale++ mechanicREV mechanic++ goodfx ammosearch1
	else
		($ cHe .. " tentou ajudar ao longo do dia, mas nao foi bem sucedido. Ainda assim, e um bom treino." $) +crtext mechanicREV mechanic++ cloyalty- not if morale-- then loyaltyREV badfx
	then +cr fullcamp 1 calendar-day+ heal-event
;

cyoa-bridge: common2-carhelp-coolit
	cpet? if
		($ cname .. " 'ignora o homem encalhado e comeca a comer formigas rastejando no chao frio.'" $)
	else
		($ cname .. " 'Im going to get help.  Ill be back soon.' " .. "'CALMA AI.'" pbold $)
	then text!
	cool-it-7 if
		char-gen to recruitee "Homem Encalhado" recruitee .name!
		($ "'VOCE ESTA CERTO! O CARRO SO PRECISA DE MAIS LIQUIDO DE ARREFECIMENTO PARA FUNCIONAR DE NOVO!'" pbold .. " Ele oferece uma recompensa!" $) +crtext
		cooled-it goodfx rescue-trainitem morale3+all
	else cool-it++
		($ "Ele comeca a gritar em " .. thegroup .. "  ate que " .. gthey .. "  saia" .. g1s .. "." $) +crtext
		morale--all badfx
	then witsattREV
;

cyoa-choice: common2-carhelp-bandit
	($ cname .. " consegue furtar facilmente as provisoes do homem solitario, inclusive seu chave de ferramenta.," $) text! badfx
		common2-carhelp-win 1 WEAPON_WRENCH trunk.weapon+ rob-guilt aholeREV
;

cyoa: common2-carhelp
	"Ajudando com problemas de carro" cyoa-title!
	char-gen to recruitee 
	0 recruitee .mechanical!
	0 recruitee .female! ricon
	WEAPON_WRENCH recruitee .weapona!
	($ Thegroup .. " 'encontra um homem tentando reparar seu carro quebrado. Ele claramente nao tem ideia do que esta fazendo.'" $) text!
		mechanicest charchoice( ": Ajudeme a corrigir o carro" )choice: common2-carhelp-mech
		most-charming if
			charchoice( ": Recruteo" )choice: recruit-yes
		then
		most-grating if
			charchoice( ": Calase cala!" fcoolit )choice: common2-carhelp-coolit
		then
		most-ahole if
			charchoice( ": Rouba suas provisoes" )choice: common2-carhelp-bandit
		then
		choice( "Nao tem tempo para ajudar" )choice
;

// Bandit raid

: common2-raid-prize
	1 WEAPON_RIFLE trunk.weapon+ 20 trunk-rifle-shell+ 15 trunk-food+ morale++all goodfx
;

cyoa-choice: common2-raid-calmshot
	($ cname .. " pega uma espingarda de cowboy dos assentados e se esconde em um lugar alto. Quando os bandidos aparecem, , " .. che .. " faz multiplas impressionantes disparos para quebrar as pneus deles. Os bandidos ficam demasiadamente assustados para continuar o ataque! A luta acaba antes mesmo de comecar. Os assentados estao felizes em pagar a recompensa!" $) text! common2-raid-prize
;

: common2-raid-shoot+
	dup humany? 50 %chance and if .shooting++ then
;

: common2-raid-fightprize
	choosehuman alive? if
		"Os assentados se mantem fieis a sua palavra e pagam uma grande recompensa!" +crtext
		common2-raid-prize
		most-calmshot if
			choosechar ($ cname .. " 'faz umas jogadas malucas que inspiram os outros.'" $) +crtext
			' .shooting++ foreach-team-char
		else
			' common2-raid-shoot+ foreach-team-char
		then
	then
;

cyoa-choice: common2-raid-fight
	someicon ($ Thegroup .. " 1" $) text!
	shootingREVall
	shooting-total 14 > if
		"A habilidade de tiro do grupo ajuda os colonos a superar os bandidos. Ninguem se machuca!" +crtext
		common2-raid-fightprize
	then;
	shooting-total 10 > if
		"A luta e dura, mas a habilidade de tiro do grupo salva o assentamento." +crtext
		ouch--all bitefx common2-raid-fightprize
	then;
	shooting-total 6 > if
		"'A luta e brutal! As habilidades de tiro do grupo mal ajudam a afastar os bandidos." +crtext
		ouch12rnd--all bitefx common2-raid-fightprize
	then;
		"'A ajuda fornecida nao foi suficiente no tiroteio. Os bandidos expulsam todos do assentamento." +crtext ouch2-all bitefx
;

cyoa-choice: common2-raid-mech
	($ cname .. " ' tenta construir as defesas do assentamento." $) text!
	cmechanic+ if
		($ cHe .. "  rapidamente faz planos e consegue pessoas para ajudar " .. chim .. "  transformam o local em uma fortaleza! Os bandidos ficam sobrecarregados com as novas defesas quando aparecem e fogem sem machucar ninguem." $) +crtext mechanicREV mechanic++ common2-raid-prize
	then;
	mechanicroll if
		($ cHe .. "  diz aos colonos como construir fortificacoes em algumas areas fracas. As defesas extras parecem ajudar muito no ataque dos bandidos! Os colonos sao gratos pela ajuda!" $) +crtext mechanicREV mechanic++ common2-raid-prize
	else
		($ cHe .. " ' tenta fortalecer algumas coisas, mas os esforcos nao parecem ajudar muito contra o ataque cruel dos bandidos. Os colonos feridos acusam " .. cname .. "  de desperdicar seu tempo e se recusam a dar a recompensa." $) +crtext mechanicREV morale--all sting--all bitefx
	then
;

cyoa: common2-raid
	"Defesa contra invasoes de bandidos" cyoa-title!
	($ Thegroup .. "  e pedido ajuda por um assentamento. Eles dizem que serao invadidos por bandidos em breve. Se " .. thegroup .. "  ajuda na defesa, " .. gthey .. "  recebera uma recompensa de 15 alimentos, alguma municao e um rifle de cauboi! Isto poderia ser " .. "very dangerous..." rbold $) text!
		vehicle.which@ 0 > if 
			choice( "'Nao ajude, va embora" )choice else choice( "Portugues" )choice
		then
		choice( "Ajudeos no tiroteio" )choice: common2-raid-fight
		mechanicest charchoice( "construir defesas" )choice: common2-raid-mech
		most-calmshot if
			charchoice( "eu vou resolver isso" )choice: common2-raid-calmshot
		then
;

// Rooftop relaxing

cyoa-choice: common2-relax-morale
	($ Thegroup .. "  passa algum tempo " .. "so na tranquilidade" fbold .. "." $) text!
	morale2+all cool-it++ goodfx
;

cyoa-choice: common2-relax-strength
	($ Thegroup .. " 10. " .. "MEGABUFF" fbold .. "." $) text!
	' .strength++ foreach-team-char goodfx
;

cyoa-choice: common2-relax-fitness
	($ Thegroup .. " 0. " .. "ULTRA FIT" fbold .. "." $) text!
	' .fitness++ foreach-team-char goodfx
;

cyoa-choice: common2-relax-med
	($ cname .. "  ensina " .. everyone .. "  ensina que " .. che .. "  sabe sobre atendimento medico de emergencia e odontologia sem dor." $) text!
		cmedical- if 
			($ cHe .. " ' nao sabe muito sobre isso! E uma licao completamente inutil.'" $) +crtext badfx
		else
			cchar ' .medical++ foreach-team-char-except goodfx
		then medicalREV morale++
;

cyoa-choice: common2-relax-mech
	($ cname .. "  ensina " .. everyone .. "  ensina que " .. che .. "  sabe sobre reparos de carros e outras maneiras de acidentalmente engalgar maquinas." $) text!
		cmechanic- if 
			($ cHe .. " ' nao sabe muito sobre isso! E uma licao completamente inutil.'" $) +crtext badfx
		else
			cchar ' .mechanic++ foreach-team-char-except goodfx
		then mechanicREV morale++
;

cyoa-choice: common2-roofrelax-reveal
	($ cname .. " revela " .. chis .. "  REVELA A SUA VERDADEIRA ESSENCIA." $) text!
		allREV goodfx
;

cyoa: common2-roofrelax
	image-tired-survivor 63 to cyoaicon
	($ Thegroup .. "  revela" $) text!
	solo if
		soloer choosechar
		($ " Sem ninguem para molestar " .. chim .. ", " .. che .. "  decide:" $) +crtext
		choice( "ESFRIE" )choice: common2-relax-morale
		choice( " Exercicio para forca" )choice: common2-relax-strength
		choice( " Exercicio para aptidao fisica" )choice: common2-relax-fitness
	else
		"' Ha tempo para falar, e alguem revela muito sobre si mesmo:'." +crtext
		' common2-roofrelax-reveal cyoa-team-choice
	then
;

// Safehouse relaxing

cyoa: common2-saferelax
	image-door-on-brick 27 to cyoaicon
	($ Thegroup .. " 'Consegue um momento de paz para descansar em um esconderijo, sem zumbis a vista.'" $) text!
	solo if
		soloer choosechar
		($ " Sem ninguem para molestar " .. chim .. ", " .. che .. "  decide:" $) +crtext
		choice( "ESFRIE" )choice: common2-relax-morale
		choice( " Exercicio para forca" )choice: common2-relax-strength
		choice( " Exercicio para aptidao fisica" )choice: common2-relax-fitness
	else
		"'Com algum tempo livre, eles decidem:'" +crtext
			50 %chance if 
				choice( "ESFRIE" )choice: common2-relax-morale
			else
				50 %chance if
					choice( " Exercicio para forca" )choice: common2-relax-strength
				else
					choice( " Exercicio para aptidao fisica" )choice: common2-relax-fitness
				then
			then
			mechanicest charchoice( "ensinar habilidades mecanicas" )choice: common2-relax-mech
			medicalest charchoice( "ensinar habilidades medicas" )choice: common2-relax-med
	then
;

} }

// this is called after carstats.df
events{ road{

: car-lost-old
	// 0 to car-working
	0 vehicle.which!
	walk-music
	// temporary wear thingy
	car-max-engine to car-engine
	car-update-display
	badfx
	walk-day-pick
	// OLD walk-day-chance
;

: car-boom
	($ "AUTO DISTRUTTA!		Armi del bagagliaio perse!" rbold $) +cr +text
	car-lost trunk.killer
;

: car-break
	($ "AUTO DISTRUTTA!" rbold $) +cr +text
	car-lost
;

: car-stolen
	($ "AUTO RUBATA!" rbold $) +cr +text
	car-lost
;

: car-abandon
	($ "AUTO ABBANDONATA!" rbold $) +cr +text
	car-lost
;

: car-fullhealth
	car-max-chassis to car-chassis
;

: car-fulltune
	car-max-engine to car-engine
;

: car-gas+
	trunk-gas+
	trunk-gas 0 < if
		0 LOOT_GAS trunk.loot!
	then
;

: car-chassis+
	' car-chassis +!
	car-chassis car-max-chassis > if
		car-fullhealth
	then;
	car-update-display
	car-chassis 0 <= if
		car-break
	then
;

: car-armor+
	dup ' car-max-chassis +!
	dup car-chassis+
	1 ' car-armor +!
;

: car-tune+
	' car-engine +!
	car-engine car-max-engine > if
		car-fulltune
	then
;

: car-megatune+
	dup ' car-max-engine +!
	dup car-tune+
;

: car-guzzle 10 car-mpg * car-armor + ;
: guzzle-mpg 
	trunk-gas car-guzzle < if
		trunk-gas
	else
		car-guzzle
	then
	"INGURGITA: " type dup print
	-1 * trunk-gas+ 
;

// Logic for garage-choices

: car-work-randnum
	cchar .mechanical 7 < if 2 else 1.4 then // For over-max mechanical, get a tighter repair variance
;

: car-engine-rnd car-max-engine car-work-randnum / car-max-engine rnd ;
: car-tune-rnd car-engine-rnd car-tune+ ;
: car-tune-rnd-half car-engine-rnd 2 / car-tune+ ;

: car-chassis-rnd car-max-chassis car-work-randnum / car-max-chassis rnd ;
: car-weld-rnd car-chassis-rnd car-chassis+ ;
: car-weld-rnd-half car-chassis-rnd 2 / car-chassis+ ;

: mechanic-diff
	cchar .mechanical car-repair - 
;
: mechanic-silentrepair 
	mechanic-diff -1 > if car-tune-rnd car-weld-rnd else car-tune-rnd-half car-weld-rnd-half then
;

: mechanic-weldup // Combine with tuneup
	mechanic-diff 1 > if
		car-weld-rnd car-tune-rnd goodfx
		($ cHe .. "  ripara lauto in pochissimo tempo!" $)
	then;
	mechanic-diff -1 > if
		car-weld-rnd car-tune-rnd
		"Il riparo occupa una buona parte della giornata."
	then;
	mechanic-diff -2 > if
		car-weld-rnd-half car-tune-rnd-half
		"Il riparo occupa una buona parte della giornata, e sono molto difficili."
	then;
	($ cHe .. " non sa cosa fare a " .. che .. "'sta facendo nulla, e spende un giorno intero fingendosi a lavorare sulla macchina da auto." $)
;
: mechanic-day?
	mechanic-diff 2 < if
		fullcamp
	then
;

// Used to repair a car that's fully broken down
: mechanic-repair? cchar .mechanical car-repair ;
: mechanic-repair
	car-engine-rnd to car-engine
	1 ' car-repair +!
;
: car-tunecheck
	mechanic-repair? > if
		($ cname .. " facilmente ripara la macchina in pochi minuti!" $) +cr +text goodfx
		mechanic-repair mechanicREV
	then; 
	mechanic-repair? = if
		mechanicroll if
			($ cHe .. " alcuna maniera riesce a far partire la macchina, ma ci vuole un intero giorno." $) +cr +text goodfx
			cchar .mechanical@ 6 > if
				($  "La prossima volta che questa macchina si spezza " .. cname .. " 'non potra ripararla assolutamente." rbold $) +crtext
			else
				($ "La prossima volta che questa macchina si spezza " .. cname .. " 'non potra ripararla se " rbold .. chis rbold .. " il livello di abilita meccanica e superiore!" rbold $) +crtext
			then
			mechanic-repair
		else
			($ cHe .. " spende un giorno a riparare la macchina, ma essa rifiuta di partire." $) +cr +text car-abandon	
		then
	else
		($ cHe .. " fallisce completamente a riparare la macchina dopo aver speso un giorno su di essa." $) +cr +text car-abandon
	then mechanicREV +cr fullcamp
;

: car-strengthcheck
	strengthroll if
		($ cHe .. " e tanto sorpreso quanto chiunque altro quando il motore ruggisce in vita!" $) +cr +text
		mechanic-repair
		car-chassis cchar .strength - 1 < if
			"'La macchina non fa molta strada prima che qualche parte che e stata colpita prenda fuoco." +cr +text
		then
		cchar .strength -1 * car-chassis+ strengthREV
	then;
		($ cHe .. " riesce appena a rompere qualcosa nella macchina e qualche osso nella " .. chis .. " mano." $) +cr +text car-abandon morale-- strengthREV strength--
;

// Road block encounter common stuff
	
: detour-roll
	car-mpg 0 = if
		"Abbastanza divertente, questa macchina non prende benzina." +cr +text
		goodfx
	then;
	50 %chance if
		($ gThey .. " si perdono per un giorno intero, fino a quando alla fine trovano una strada alternativa." $)
		+cr +text
		trunk-gas 70 car-mpg * >= if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			bandit-gas
		then
		+cr fullcamp heal-event
	else
		"Ci vuole un po piu di tempo e benzina, ma non troppo!" +cr +text
		trunk-gas 30 car-mpg * >= if
			-25 -30 rnd car-mpg * car-gas+
		else
			bandit-gas
		then
	then
;

: car-plowthrough
	car-armor 0 > if
		"La macchina sfonda lostacolo, anche se un po della sua armatura improvvisata finisce per cadere." +cr +text goodfx
		-1 ' car-armor +!
	else
		-2 -3 rnd car-chassis+ badfx
		car-chassis 0 > if
			"La macchina passa, ma subisce qualche danno." +crtext
		else
			"La macchina e passata, ma ha subito troppi danni. E andata in pezzi poco piu avanti." +crtext
		then 
	then
;

: car-jump
	car-speed 2 < if
		"La macchina lenta si ribalta immediatamente e prende fuoco, senza nemmeno superare lostacolo." +cr +text badfx
		ouch2-all car-break
	then;
	car-speed 2 = if
		"La macchina ce la fa a malapena, ma affonda come unincudine e si schianta a terra. Pezzi della macchina volano in varie direzioni." +cr +text badfx
		-2 -3 rnd car-chassis+
		car-chassis 0 > if "'Ha preso una botta, ma sta ancora andando!" +crtext then
	then;
	car-speed 3 > if
		"'La macchina prende un'incredibile velocita, colpisce il salto e inizia a volare per un po' grazie ai razzi! E esaltante!" +cr +text goodfx morale2+all
	then;
	car-speed 2 > if
		"La macchina prende grande velocita, colpisce il salto, e plana proprio sopra lostacolo!" +cr +text goodfx
	then;
;
	
// Option for shooting stuff in a frenzy, needs work later

: car-shoot-ammo? trunk-bullet 0 > trunk-rifle-shell 0 > trunk-shotgun-shell 0 > or or ;
: car-shoot-check trunk-bullet 29 > trunk-rifle-shell 20 > trunk-shotgun-shell 10 > and and ;
: car-shoot // improve this later
	trunk-bullet 0 > if -30 trunk-bullet+ then
	trunk-rifle-shell 0 > if -20 trunk-rifle-shell+ then
	trunk-shotgun-shell 0 > if -10 trunk-shotgun-shell+ then
	trunk-bullet 0 < if
		0 LOOT_BULLET trunk.loot!
	then
	trunk-shotgun-shell 0 < if
		0 LOOT_SHOTGUN_SHELL trunk.loot!
	then
	trunk-rifle-shell 0 < if
		0 LOOT_RIFLE_SHELL trunk.loot!
	then
;

} }

cyoa{ road{ 

// Common used elements for road block encounters

cyoa-choice: car-detour
	($ Thegroup .. "  decide di fare una deviazione! Di solito e una cattiva idea." $) cyoa-text!
	detour-roll
;

// Used to tune up a car in a garage, before it's broken
: car-tuneup // partially combined with weldup now
	car-engine car-max-engine >= if
		"La macchina corre come nuova! " 
	else
		car-engine 14 > if
			"'La macchina corre come se fosse in buona forma. "
		then
		car-engine 15 < car-engine 7 > and if
			"La macchina ha visto giorni migliori. "
		then
		car-engine 8 < if
			"La macchina potrebbe ancora rompersi da un momento allaltro. "
		then
	then
	+crtext
;
cyoa-choice: car-weldup
	($ cname .. "  prova a riparare la macchina nella speranza che vada un po piu avanti prima di rompersi. " $) cyoa-text!
	mechanic-weldup +text
	car-chassis car-max-chassis = if
		"La macchina sembra intatta!" 
	else
		car-chassis 3 < if
			"La macchina sembra ancora tutta distrutta."
		else
			"La macchina sembra tutta malconcia, ma ancora robusta."
		then
	then
	car-tuneup
	+text mechanicREV mechanic-diff -2 > if mechanic++ then +cr mechanic-day?
;

// Standard choices in every garage
: garage-choices
	mechanicest choosechar
	choice( "Riparazione e messa a punto dellauto" )choice: car-weldup
	// choice( "Messa a punto completa" )choice: car-tuneup 
	choice( "Guidare via" )choice 
;

cyoa: car-nogas
	"FUORI GAS" cyoa-title!
	($ "Lauto e completamente a corto di benzina e si ferma.	" .. "AUTO ABBANDONATA!" rbold $) cyoa-text! badfx car-lost
;

cyoa-choice: car-abandoned
	($ "Senza perdere tempo, " .. thegroup .. "  decide di abbandonare lauto e camminare finche non trova un nuovo veicolo." $) cyoa-text!
	car-abandon
;
cyoa-choice: car-breakdown-punch
	($ cname .. " ' apre il cofano dell'auto e inizia a TIRARE PUGNI." $) cyoa-text! car-strengthcheck strengthREV
;
cyoa-choice: car-breakdown-fix
	($ cname .. "  apre il cofano dellauto." $) cyoa-text! car-tunecheck mechanicREV
;
cyoa: car-breakdown
	"GUASTO AUTO" cyoa-title!
	($ "'L'auto si e guastata a causa dell'uso e degli abusi. Non si riavviera piu." $) cyoa-text!
		choice( "Abbandona lauto" )choice: car-abandoned
		mechanicest charchoice( "  tenta di ripararla" )choice: car-breakdown-fix
		someone charchoice( "  prende a pugni lauto" )choice: car-breakdown-punch
		
;

} }

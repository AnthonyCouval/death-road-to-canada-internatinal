// this is called after carstats.df
events{ road{

: car-lost-old
	// 0 to car-working
	0 vehicle.which!
	walk-music
	// temporary wear thingy
	car-max-engine to car-engine
	car-update-display
	badfx
	walk-day-pick
	// OLD walk-day-chance
;

: car-boom
	($ "CARRO OBLITERADO! Armas do porta malas perdidas!" rbold $) +cr +text
	car-lost trunk.killer
;

: car-break
	($ "CARRO DESTRUIDO!" rbold $) +cr +text
	car-lost
;

: car-stolen
	($ "CARRO ROUBADO!" rbold $) +cr +text
	car-lost
;

: car-abandon
	($ "CARRO ABANDONADO!" rbold $) +cr +text
	car-lost
;

: car-fullhealth
	car-max-chassis to car-chassis
;

: car-fulltune
	car-max-engine to car-engine
;

: car-gas+
	trunk-gas+
	trunk-gas 0 < if
		0 LOOT_GAS trunk.loot!
	then
;

: car-chassis+
	' car-chassis +!
	car-chassis car-max-chassis > if
		car-fullhealth
	then;
	car-update-display
	car-chassis 0 <= if
		car-break
	then
;

: car-armor+
	dup ' car-max-chassis +!
	dup car-chassis+
	1 ' car-armor +!
;

: car-tune+
	' car-engine +!
	car-engine car-max-engine > if
		car-fulltune
	then
;

: car-megatune+
	dup ' car-max-engine +!
	dup car-tune+
;

: car-guzzle 10 car-mpg * car-armor + ;
: guzzle-mpg 
	trunk-gas car-guzzle < if
		trunk-gas
	else
		car-guzzle
	then
	"GUZZLE: " type dup print
	-1 * trunk-gas+ 
;

// Logic for garage-choices

: car-work-randnum
	cchar .mechanical 7 < if 2 else 1.4 then // For over-max mechanical, get a tighter repair variance
;

: car-engine-rnd car-max-engine car-work-randnum / car-max-engine rnd ;
: car-tune-rnd car-engine-rnd car-tune+ ;
: car-tune-rnd-half car-engine-rnd 2 / car-tune+ ;

: car-chassis-rnd car-max-chassis car-work-randnum / car-max-chassis rnd ;
: car-weld-rnd car-chassis-rnd car-chassis+ ;
: car-weld-rnd-half car-chassis-rnd 2 / car-chassis+ ;

: mechanic-diff
	cchar .mechanical car-repair - 
;
: mechanic-silentrepair 
	mechanic-diff -1 > if car-tune-rnd car-weld-rnd else car-tune-rnd-half car-weld-rnd-half then
;

: mechanic-weldup // Combine with tuneup
	mechanic-diff 1 > if
		car-weld-rnd car-tune-rnd goodfx
		($ cHe .. "  conserta o carro com maestria em pouco tempo!" $)
	then;
	mechanic-diff -1 > if
		car-weld-rnd car-tune-rnd
		"O conserto leva uma boa parte do dia."
	then;
	mechanic-diff -2 > if
		car-weld-rnd-half car-tune-rnd-half
		"O conserto leva uma boa parte do dia e e muito dificil."
	then;
	($ cHe .. "  nao faz ideia do que " .. che .. "' esta fazendo e perde um dia fingindo trabalhar no carro." $)
;
: mechanic-day?
	mechanic-diff 2 < if
		fullcamp
	then
;

// Used to repair a car that's fully broken down
: mechanic-repair? cchar .mechanical car-repair ;
: mechanic-repair
	car-engine-rnd to car-engine
	1 ' car-repair +!
;
: car-tunecheck
	mechanic-repair? > if
		($ cname .. "  conserta o carro facilmente e rapidamente!" $) +cr +text goodfx
		mechanic-repair mechanicREV
	then; 
	mechanic-repair? = if
		mechanicroll if
			($ cHe .. "  de alguma forma consegue fazer o carro funcionar, mas leva um dia inteiro." $) +cr +text goodfx
			cchar .mechanical@ 6 > if
				($  "Na proxima vez que este carro quebrar, " .. cname .. " ' nao conseguira conserta lo de jeito nenhum." rbold $) +crtext
			else
				($ "Na proxima vez que este carro quebrar, " .. cname .. " ' nao conseguira conserta lo a menos que " rbold .. chis rbold .. "  a habilidade de mecanico seja maior!" rbold $) +crtext
			then
			mechanic-repair
		else
			($ cHe .. "  perde um dia tentando consertar o carro, mas ele se recusa a ligar." $) +cr +text car-abandon	
		then
	else
		($ cHe .. "  falha completamente em consertar o carro depois de perder um dia com isso." $) +cr +text car-abandon
	then mechanicREV +cr fullcamp
;

: car-strengthcheck
	strengthroll if
		($ cHe .. "  fica tao surpreso quanto qualquer um quando o motor ruge e volta a vida!" $) +cr +text
		mechanic-repair
		car-chassis cchar .strength - 1 < if
			"'O carro nao vai muito longe na estrada antes que alguma peca que foi perfurada pegue fogo." +cr +text
		then
		cchar .strength -1 * car-chassis+ strengthREV
	then;
		($ cHe .. " consegue quebrar alguma coisa no carro e alguns ossos " .. chis .. " mao" $) +cr +text car-abandon morale-- strengthREV strength--
;

// Road block encounter common stuff
	
: detour-roll
	car-mpg 0 = if
		"Engracado, esse carro nao consome gasolina." +cr +text
		goodfx
	then;
	50 %chance if
		($ gThey .. " " $)
		+cr +text
		trunk-gas 70 car-mpg * >= if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			bandit-gas
		then
		+cr fullcamp heal-event
	else
		"Demora um pouco mais de tempo e gasolina, mas nao muito!" +cr +text
		trunk-gas 30 car-mpg * >= if
			-25 -30 rnd car-mpg * car-gas+
		else
			bandit-gas
		then
	then
;

: car-plowthrough
	car-armor 0 > if
		"O carro arrebenta o obstaculo, embora parte de sua armadura improvisada acabe caindo." +cr +text goodfx
		-1 ' car-armor +!
	else
		-2 -3 rnd car-chassis+ badfx
		car-chassis 0 > if
			"O carro passa, mas sofre alguns danos." +crtext
		else
			"O carro passou, mas sofreu muitos danos. Ele se desfez a uma curta distancia na estrada." +crtext
		then 
	then
;

: car-jump
	car-speed 2 < if
		"O carro lento capota imediatamente e explode em chamas, nem mesmo passando pelo obstaculo." +cr +text badfx
		ouch2-all car-break
	then;
	car-speed 2 = if
		"O carro mal consegue passar, mas afunda como uma bigorna e se esmaga no chao. Pedacos do carro voam em varias direcoes." +cr +text badfx
		-2 -3 rnd car-chassis+
		car-chassis 0 > if "Levou uma surra, mas ainda esta funcionando!" +crtext then
	then;
	car-speed 3 > if
		"O carro acumula uma velocidade incrivel, atinge o salto e comeca a voar por um tempo devido aos propulsores de foguete! E emocionante!" +cr +text goodfx morale2+all
	then;
	car-speed 2 > if
		"O carro acumula grande velocidade, acerta o pulo e navega bem sobre o obstaculo!" +cr +text goodfx
	then;
;
	
// Option for shooting stuff in a frenzy, needs work later

: car-shoot-ammo? trunk-bullet 0 > trunk-rifle-shell 0 > trunk-shotgun-shell 0 > or or ;
: car-shoot-check trunk-bullet 29 > trunk-rifle-shell 20 > trunk-shotgun-shell 10 > and and ;
: car-shoot // improve this later
	trunk-bullet 0 > if -30 trunk-bullet+ then
	trunk-rifle-shell 0 > if -20 trunk-rifle-shell+ then
	trunk-shotgun-shell 0 > if -10 trunk-shotgun-shell+ then
	trunk-bullet 0 < if
		0 LOOT_BULLET trunk.loot!
	then
	trunk-shotgun-shell 0 < if
		0 LOOT_SHOTGUN_SHELL trunk.loot!
	then
	trunk-rifle-shell 0 < if
		0 LOOT_RIFLE_SHELL trunk.loot!
	then
;

} }

cyoa{ road{ 

// Common used elements for road block encounters

cyoa-choice: car-detour
	($ Thegroup .. "  decide pegar um desvio! Isso geralmente e uma ma ideia." $) cyoa-text!
	detour-roll
;

// Used to tune up a car in a garage, before it's broken
: car-tuneup // partially combined with weldup now
	car-engine car-max-engine >= if
		"O carro roda como novo " 
	else
		car-engine 14 > if
			"'O carro roda como se estivesse em boa forma "
		then
		car-engine 15 < car-engine 7 > and if
			"O carro ja viu dias melhores "
		then
		car-engine 8 < if
			"O carro ainda pode quebrar a qualquer momento "
		then
	then
	+crtext
;
cyoa-choice: car-weldup
	($ cname .. "  tenta consertar o carro na esperanca de que va um pouco mais longe antes de quebrar " $) cyoa-text!
	mechanic-weldup +text
	car-chassis car-max-chassis = if
		"O carro parece sem danos!" 
	else
		car-chassis 3 < if
			"O carro ainda parece todo destruido."
		else
			"O carro parece todo batido, mas ainda resistente."
		then
	then
	car-tuneup
	+text mechanicREV mechanic-diff -2 > if mechanic++ then +cr mechanic-day?
;

// Standard choices in every garage
: garage-choices
	mechanicest choosechar
	choice( "Conserto e ajuste de carro" )choice: car-weldup
	// choice( "Ajuste completo" )choice: car-tuneup 
	choice( "Va embora" )choice 
;

cyoa: car-nogas
	"SEM GASOLINA" cyoa-title!
	($ "O carro ficou completamente sem gasolina e parou." .. "CARRO ABANDONADO!" rbold $) cyoa-text! badfx car-lost
;

cyoa-choice: car-abandoned
	($ "Sem perder tempo, " .. thegroup .. "  decide abandonar o carro e caminhar ate encontrar um novo veiculo." $) cyoa-text!
	car-abandon
;
cyoa-choice: car-breakdown-punch
	($ cname .. " ' abre o capo do carro e comeca a SOCAR." $) cyoa-text! car-strengthcheck strengthREV
;
cyoa-choice: car-breakdown-fix
	($ cname .. "  levanta o capo do carro." $) cyoa-text! car-tunecheck mechanicREV
;
cyoa: car-breakdown
	"QUEBRA DO CARRO" cyoa-title!
	($ "'O carro quebrou de tanto uso e abuso. Nao liga mais." $) cyoa-text!
		choice( "Abandonar o carro" )choice: car-abandoned
		mechanicest charchoice( "  tenta conserta lo" )choice: car-breakdown-fix
		someone charchoice( "  soca o carro" )choice: car-breakdown-punch
		
;

} }

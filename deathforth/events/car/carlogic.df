// this is called after carstats.df
events{ road{

: car-lost-old
	// 0 to car-working
	0 vehicle.which!
	walk-music
	// temporary wear thingy
	car-max-engine to car-engine
	car-update-display
	badfx
	walk-day-pick
	// OLD walk-day-chance
;

: car-boom
	($ "AUTO DESTRUIDO!		Armas del maletero perdidas!" rbold $) +cr +text
	car-lost trunk.killer
;

: car-break
	($ "AUTO DESTRUIDO!" rbold $) +cr +text
	car-lost
;

: car-stolen
	($ "AUTO ROBADO!" rbold $) +cr +text
	car-lost
;

: car-abandon
	($ "AUTO ABANDONADO!" rbold $) +cr +text
	car-lost
;

: car-fullhealth
	car-max-chassis to car-chassis
;

: car-fulltune
	car-max-engine to car-engine
;

: car-gas+
	trunk-gas+
	trunk-gas 0 < if
		0 LOOT_GAS trunk.loot!
	then
;

: car-chassis+
	' car-chassis +!
	car-chassis car-max-chassis > if
		car-fullhealth
	then;
	car-update-display
	car-chassis 0 <= if
		car-break
	then
;

: car-armor+
	dup ' car-max-chassis +!
	dup car-chassis+
	1 ' car-armor +!
;

: car-tune+
	' car-engine +!
	car-engine car-max-engine > if
		car-fulltune
	then
;

: car-megatune+
	dup ' car-max-engine +!
	dup car-tune+
;

: car-guzzle 10 car-mpg * car-armor + ;
: guzzle-mpg 
	trunk-gas car-guzzle < if
		trunk-gas
	else
		car-guzzle
	then
	"GUZZLE: " type dup print
	-1 * trunk-gas+ 
;

// Logic for garage-choices

: car-work-randnum
	cchar .mechanical 7 < if 2 else 1.4 then // For over-max mechanical, get a tighter repair variance
;

: car-engine-rnd car-max-engine car-work-randnum / car-max-engine rnd ;
: car-tune-rnd car-engine-rnd car-tune+ ;
: car-tune-rnd-half car-engine-rnd 2 / car-tune+ ;

: car-chassis-rnd car-max-chassis car-work-randnum / car-max-chassis rnd ;
: car-weld-rnd car-chassis-rnd car-chassis+ ;
: car-weld-rnd-half car-chassis-rnd 2 / car-chassis+ ;

: mechanic-diff
	cchar .mechanical car-repair - 
;
: mechanic-silentrepair 
	mechanic-diff -1 > if car-tune-rnd car-weld-rnd else car-tune-rnd-half car-weld-rnd-half then
;

: mechanic-weldup // Combine with tuneup
	mechanic-diff 1 > if
		car-weld-rnd car-tune-rnd goodfx
		($ cHe .. "  repara el coche de forma experta en un santiamen!" $)
	then;
	mechanic-diff -1 > if
		car-weld-rnd car-tune-rnd
		"La reparacion lleva gran parte del dia."
	then;
	mechanic-diff -2 > if
		car-weld-rnd-half car-tune-rnd-half
		"La reparacion lleva gran parte del dia y es muy dificil."
	then;
	($ cHe .. "  no tiene ni idea de lo que " .. che .. "' esta haciendo y pierde el tiempo de un dia fingiendo trabajar en el coche." $)
;
: mechanic-day?
	mechanic-diff 2 < if
		fullcamp
	then
;

// Used to repair a car that's fully broken down
: mechanic-repair? cchar .mechanical car-repair ;
: mechanic-repair
	car-engine-rnd to car-engine
	1 ' car-repair +!
;
: car-tunecheck
	mechanic-repair? > if
		($ cname .. "  repara facilmente el coche en un santiamen!" $) +cr +text goodfx
		mechanic-repair mechanicREV
	then; 
	mechanic-repair? = if
		mechanicroll if
			($ cHe .. "  se las arregla para que el coche funcione, pero tarda un dia entero." $) +cr +text goodfx
			cchar .mechanical@ 6 > if
				($  "La proxima vez que este coche se estropee, " .. cname .. " ' no podra repararlo en absoluto." rbold $) +crtext
			else
				($ "La proxima vez que este coche se estropee, " .. cname .. " ' no podra repararlo a menos que " rbold .. chis rbold .. "  la habilidad del mecanico sea mayor!" rbold $) +crtext
			then
			mechanic-repair
		else
			($ cHe .. "  pierde un dia intentando reparar el coche, pero este se niega a arrancar." $) +cr +text car-abandon	
		then
	else
		($ cHe .. "  fracasa completamente en la reparacion del coche despues de perder un dia en ello." $) +cr +text car-abandon
	then mechanicREV +cr fullcamp
;

: car-strengthcheck
	strengthroll if
		($ cHe .. "  esta tan sorprendido como cualquiera cuando el motor ruge para arrancar!" $) +cr +text
		mechanic-repair
		car-chassis cchar .strength - 1 < if
			"'El auto no llega muy lejos por la carretera antes de que alguna parte que fue golpeada se incendie." +cr +text
		then
		cchar .strength -1 * car-chassis+ strengthREV
	then;
		($ cHe .. "  apenas logra romper algo en el auto y algunos huesos en " .. chis .. "  mano." $) +cr +text car-abandon morale-- strengthREV strength--
;

// Road block encounter common stuff
	
: detour-roll
	car-mpg 0 = if
		"Curiosamente, este auto no necesita gasolina." +cr +text
		goodfx
	then;
	50 %chance if
		($ gThey .. "  se pierde durante un dia completo, hasta que finalmente encuentra una ruta alternativa." $)
		+cr +text
		trunk-gas 70 car-mpg * >= if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			bandit-gas
		then
		+cr fullcamp heal-event
	else
		"Se necesita algo de tiempo y gasolina extra, pero no demasiado!" +cr +text
		trunk-gas 30 car-mpg * >= if
			-25 -30 rnd car-mpg * car-gas+
		else
			bandit-gas
		then
	then
;

: car-plowthrough
	car-armor 0 > if
		"El auto embiste el obstaculo, aunque parte de su armadura improvisada termina cayendo." +cr +text goodfx
		-1 ' car-armor +!
	else
		-2 -3 rnd car-chassis+ badfx
		car-chassis 0 > if
			"El auto pasa, pero sufre algunos danos." +crtext
		else
			"El auto paso, pero sufrio demasiado dano. Se desarmo a poca distancia por el camino." +crtext
		then 
	then
;

: car-jump
	car-speed 2 < if
		"El auto lento se voltea inmediatamente y estalla en llamas, ni siquiera despeja el obstaculo." +cr +text badfx
		ouch2-all car-break
	then;
	car-speed 2 = if
		"El auto apenas lo logra, pero se hunde como un yunque y se estrella contra el suelo. Trozos del auto vuelan en varias direcciones." +cr +text badfx
		-2 -3 rnd car-chassis+
		car-chassis 0 > if "'Recibio una paliza, pero sigue funcionando!" +crtext then
	then;
	car-speed 3 > if
		"'El auto acumula una velocidad increible, golpea el salto y comienza a volar por un tiempo debido a los propulsores de cohetes Es emocionante!" +cr +text goodfx morale2+all
	then;
	car-speed 2 > if
		"El auto toma gran velocidad, pega el salto y vuela por sobre el obstaculo!" +cr +text goodfx
	then;
;
	
// Option for shooting stuff in a frenzy, needs work later

: car-shoot-ammo? trunk-bullet 0 > trunk-rifle-shell 0 > trunk-shotgun-shell 0 > or or ;
: car-shoot-check trunk-bullet 29 > trunk-rifle-shell 20 > trunk-shotgun-shell 10 > and and ;
: car-shoot // improve this later
	trunk-bullet 0 > if -30 trunk-bullet+ then
	trunk-rifle-shell 0 > if -20 trunk-rifle-shell+ then
	trunk-shotgun-shell 0 > if -10 trunk-shotgun-shell+ then
	trunk-bullet 0 < if
		0 LOOT_BULLET trunk.loot!
	then
	trunk-shotgun-shell 0 < if
		0 LOOT_SHOTGUN_SHELL trunk.loot!
	then
	trunk-rifle-shell 0 < if
		0 LOOT_RIFLE_SHELL trunk.loot!
	then
;

} }

cyoa{ road{ 

// Common used elements for road block encounters

cyoa-choice: car-detour
	($ Thegroup .. "  decide tomar un desvio! Esto suele ser una mala idea." $) cyoa-text!
	detour-roll
;

// Used to tune up a car in a garage, before it's broken
: car-tuneup // partially combined with weldup now
	car-engine car-max-engine >= if
		"El auto funciona como nuevo! " 
	else
		car-engine 14 > if
			"'El auto funciona como si estuviera en buen estado. "
		then
		car-engine 15 < car-engine 7 > and if
			"El auto ha visto dias mejores. "
		then
		car-engine 8 < if
			"El auto aun puede descomponerse en cualquier momento. "
		then
	then
	+crtext
;
cyoa-choice: car-weldup
	($ cname .. "  intenta reparar el auto con la esperanza de que avance un poco mas antes de descomponerse. " $) cyoa-text!
	mechanic-weldup +text
	car-chassis car-max-chassis = if
		"El auto luce sin danos!" 
	else
		car-chassis 3 < if
			"El auto aun luce todo abollado."
		else
			"El auto luce todo golpeado, pero aun resistente."
		then
	then
	car-tuneup
	+text mechanicREV mechanic-diff -2 > if mechanic++ then +cr mechanic-day?
;

// Standard choices in every garage
: garage-choices
	mechanicest choosechar
	choice( "Reparacion y afinacion de autos" )choice: car-weldup
	// choice( "Afinacion completa" )choice: car-tuneup 
	choice( "Conducir lejos" )choice 
;

cyoa: car-nogas
	"SIN GASOLINA" cyoa-title!
	($ "	El coche se ha quedado completamente sin gasolina y se detiene." .. "AUTO ABANDONADO!" rbold $) cyoa-text! badfx car-lost
;

cyoa-choice: car-abandoned
	($ "Sin perder tiempo " .. thegroup .. "  decide abandonar el coche y caminar hasta que encuentre un vehiculo nuevo." $) cyoa-text!
	car-abandon
;
cyoa-choice: car-breakdown-punch
	($ cname .. " ' abre el capo del coche y empieza a GOLPEAR." $) cyoa-text! car-strengthcheck strengthREV
;
cyoa-choice: car-breakdown-fix
	($ cname .. "  abre el capo del coche." $) cyoa-text! car-tunecheck mechanicREV
;
cyoa: car-breakdown
	"AVERIA DEL COCHE" cyoa-title!
	($ "'El coche se ha averiado por el uso y el abuso. No volvera a arrancar." $) cyoa-text!
		choice( "Abandonar el coche" )choice: car-abandoned
		mechanicest charchoice( "  intenta arreglarlo" )choice: car-breakdown-fix
		someone charchoice( "  golpea el coche" )choice: car-breakdown-punch
		
;

} }

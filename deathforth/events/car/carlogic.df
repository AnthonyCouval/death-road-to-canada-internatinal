// this is called after carstats.df
events{ road{

: car-lost-old
	// 0 to car-working
	0 vehicle.which!
	walk-music
	// temporary wear thingy
	car-max-engine to car-engine
	car-update-display
	badfx
	walk-day-pick
	// OLD walk-day-chance
;

: car-boom
	($ "AUTO ZERSTORT!		Waffen im Kofferraum verloren!" rbold $) +cr +text
	car-lost trunk.killer
;

: car-break
	($ "AUTO ZERSTORT!" rbold $) +cr +text
	car-lost
;

: car-stolen
	($ "AUTO GESTOHLEN!" rbold $) +cr +text
	car-lost
;

: car-abandon
	($ "AUTO ABGESTELLT!" rbold $) +cr +text
	car-lost
;

: car-fullhealth
	car-max-chassis to car-chassis
;

: car-fulltune
	car-max-engine to car-engine
;

: car-gas+
	trunk-gas+
	trunk-gas 0 < if
		0 LOOT_GAS trunk.loot!
	then
;

: car-chassis+
	' car-chassis +!
	car-chassis car-max-chassis > if
		car-fullhealth
	then;
	car-update-display
	car-chassis 0 <= if
		car-break
	then
;

: car-armor+
	dup ' car-max-chassis +!
	dup car-chassis+
	1 ' car-armor +!
;

: car-tune+
	' car-engine +!
	car-engine car-max-engine > if
		car-fulltune
	then
;

: car-megatune+
	dup ' car-max-engine +!
	dup car-tune+
;

: car-guzzle 10 car-mpg * car-armor + ;
: guzzle-mpg 
	trunk-gas car-guzzle < if
		trunk-gas
	else
		car-guzzle
	then
	"TANKEN: " type dup print
	-1 * trunk-gas+ 
;

// Logic for garage-choices

: car-work-randnum
	cchar .mechanical 7 < if 2 else 1.4 then // For over-max mechanical, get a tighter repair variance
;

: car-engine-rnd car-max-engine car-work-randnum / car-max-engine rnd ;
: car-tune-rnd car-engine-rnd car-tune+ ;
: car-tune-rnd-half car-engine-rnd 2 / car-tune+ ;

: car-chassis-rnd car-max-chassis car-work-randnum / car-max-chassis rnd ;
: car-weld-rnd car-chassis-rnd car-chassis+ ;
: car-weld-rnd-half car-chassis-rnd 2 / car-chassis+ ;

: mechanic-diff
	cchar .mechanical car-repair - 
;
: mechanic-silentrepair 
	mechanic-diff -1 > if car-tune-rnd car-weld-rnd else car-tune-rnd-half car-weld-rnd-half then
;

: mechanic-weldup // Combine with tuneup
	mechanic-diff 1 > if
		car-weld-rnd car-tune-rnd goodfx
		($ cHe .. "  repariert das Auto im Handumdrehen fachmannisch!" $)
	then;
	mechanic-diff -1 > if
		car-weld-rnd car-tune-rnd
		"Die Reparaturen nehmen einen Groteil des Tages in Anspruch."
	then;
	mechanic-diff -2 > if
		car-weld-rnd-half car-tune-rnd-half
		"Die Reparaturen nehmen einen Groteil des Tages in Anspruch und sind sehr schwierig."
	then;
	($ cHe .. "  hat keine Ahnung was " .. che .. "' tut, und verschwendet einen Tag damit, so zu tun, als wurde er am Auto arbeiten." $)
;
: mechanic-day?
	mechanic-diff 2 < if
		fullcamp
	then
;

// Used to repair a car that's fully broken down
: mechanic-repair? cchar .mechanical car-repair ;
: mechanic-repair
	car-engine-rnd to car-engine
	1 ' car-repair +!
;
: car-tunecheck
	mechanic-repair? > if
		($ cname .. "  repariert das Auto im Handumdrehen!" $) +cr +text goodfx
		mechanic-repair mechanicREV
	then; 
	mechanic-repair? = if
		mechanicroll if
			($ cHe .. "  schafft es irgendwie, das Auto zum Laufen zu bringen, aber es dauert einen ganzen Tag." $) +cr +text goodfx
			cchar .mechanical@ 6 > if
				($  "Wenn dieses Auto das nachste Mal kaputt geht " .. cname .. " absolut nicht reparieren konnen" rbold $) +crtext
			else
				($ "Wenn dieses Auto das nachste Mal kaputt geht " .. cname .. " wird es nicht reparieren konnen es sei denn dass " rbold .. chis rbold .. "  mechaniker Fahigkeit ist hoher!" rbold $) +crtext
			then
			mechanic-repair
		else
			($ cHe .. "  verschwendet einen Tag damit, das Auto zu reparieren, aber es springt nicht an." $) +cr +text car-abandon	
		then
	else
		($ cHe .. "  kann das Auto nach einem Tag Arbeit nicht reparieren." $) +cr +text car-abandon
	then mechanicREV +cr fullcamp
;

: car-strengthcheck
	strengthroll if
		($ cHe .. "  ist genauso uberrascht wie alle anderen als der Motor zum Leben erwacht!" $) +cr +text
		mechanic-repair
		car-chassis cchar .strength - 1 < if
			"'Das Auto kommt nicht weit bevor ein Teil das geschlagen wurde Feuer fangt." +cr +text
		then
		cchar .strength -1 * car-chassis+ strengthREV
	then;
		($ cHe .. "  schafft es gerade noch etwas im Auto und einige Knochen in " .. chis .. "  hand zu brechen." $) +cr +text car-abandon morale-- strengthREV strength--
;

// Road block encounter common stuff
	
: detour-roll
	car-mpg 0 = if
		"Komischerweise braucht dieses Auto kein Benzin." +cr +text
		goodfx
	then;
	50 %chance if
		($ gThey .. "  verirren sich einen ganzen Tag lang, bis sie endlich eine alternative Route finden." $)
		+cr +text
		trunk-gas 70 car-mpg * >= if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			bandit-gas
		then
		+cr fullcamp heal-event
	else
		"Es braucht etwas mehr Zeit und Benzin, aber nicht zu viel!" +cr +text
		trunk-gas 30 car-mpg * >= if
			-25 -30 rnd car-mpg * car-gas+
		else
			bandit-gas
		then
	then
;

: car-plowthrough
	car-armor 0 > if
		"Das Auto rast direkt durch das Hindernis, obwohl ein Teil seiner provisorischen Panzerung abfallt." +cr +text goodfx
		-1 ' car-armor +!
	else
		-2 -3 rnd car-chassis+ badfx
		car-chassis 0 > if
			"Das Auto kommt durch, nimmt aber Schaden." +crtext
		else
			"Das Auto kam durch, nahm aber zu viel Schaden. Es fiel ein kurzes Stuck weiter die Strae hinunter auseinander." +crtext
		then 
	then
;

: car-jump
	car-speed 2 < if
		"Das langsame Auto uberschlagt sich sofort und geht in Flammen auf, ohne das Hindernis zu uberwinden." +cr +text badfx
		ouch2-all car-break
	then;
	car-speed 2 = if
		"Das Auto schafft es kaum, sinkt aber wie ein Amboss und zerschellt auf dem Boden. Teile des Autos fliegen in verschiedene Richtungen." +cr +text badfx
		-2 -3 rnd car-chassis+
		car-chassis 0 > if "'Es hat eine Tracht Prugel bekommen, lauft aber noch!" +crtext then
	then;
	car-speed 3 > if
		"'Das Auto baut eine unglaubliche Geschwindigkeit auf, springt uber die Rampe und fliegt aufgrund der Raketenbooster eine Weile! Es ist berauschend!" +cr +text goodfx morale2+all
	then;
	car-speed 2 > if
		"Das Auto baut eine groe Geschwindigkeit auf, springt uber die Rampe und segelt direkt uber das Hindernis!" +cr +text goodfx
	then;
;
	
// Option for shooting stuff in a frenzy, needs work later

: car-shoot-ammo? trunk-bullet 0 > trunk-rifle-shell 0 > trunk-shotgun-shell 0 > or or ;
: car-shoot-check trunk-bullet 29 > trunk-rifle-shell 20 > trunk-shotgun-shell 10 > and and ;
: car-shoot // improve this later
	trunk-bullet 0 > if -30 trunk-bullet+ then
	trunk-rifle-shell 0 > if -20 trunk-rifle-shell+ then
	trunk-shotgun-shell 0 > if -10 trunk-shotgun-shell+ then
	trunk-bullet 0 < if
		0 LOOT_BULLET trunk.loot!
	then
	trunk-shotgun-shell 0 < if
		0 LOOT_SHOTGUN_SHELL trunk.loot!
	then
	trunk-rifle-shell 0 < if
		0 LOOT_RIFLE_SHELL trunk.loot!
	then
;

} }

cyoa{ road{ 

// Common used elements for road block encounters

cyoa-choice: car-detour
	($ Thegroup .. "  beschliet einen Umweg zu nehmen! Das ist normalerweise eine schlechte Idee." $) cyoa-text!
	detour-roll
;

// Used to tune up a car in a garage, before it's broken
: car-tuneup // partially combined with weldup now
	car-engine car-max-engine >= if
		"Das Auto lauft wie neu! " 
	else
		car-engine 14 > if
			"'Das Auto lauft als ware es in gutem Zustand. "
		then
		car-engine 15 < car-engine 7 > and if
			"Das Auto hat schon bessere Tage gesehen. "
		then
		car-engine 8 < if
			"Das Auto kann jederzeit noch kaputt gehen. "
		then
	then
	+crtext
;
cyoa-choice: car-weldup
	($ cname .. "  versucht das Auto zu reparieren in der Hoffnung dass es noch ein Stuck weiter fahrt bevor es kaputt geht. " $) cyoa-text!
	mechanic-weldup +text
	car-chassis car-max-chassis = if
		"Das Auto sieht unbeschadigt aus!" 
	else
		car-chassis 3 < if
			"Das Auto sieht immer noch total kaputt aus."
		else
			"Das Auto sieht total verbeult aus, ist aber immer noch robust."
		then
	then
	car-tuneup
	+text mechanicREV mechanic-diff -2 > if mechanic++ then +cr mechanic-day?
;

// Standard choices in every garage
: garage-choices
	mechanicest choosechar
	choice( "Auto Reparatur und Tuning" )choice: car-weldup
	// choice( "Kompletter Check" )choice: car-tuneup 
	choice( "Wegfahren" )choice 
;

cyoa: car-nogas
	"KEIN BENZIN" cyoa-title!
	($ "Dem Auto ist komplett das Benzin ausgegangen und es rollt zum Stehen." .. "AUTO ABGESTELLT!" rbold $) cyoa-text! badfx car-lost
;

cyoa-choice: car-abandoned
	($ "Ohne Zeit zu verschwenden, " .. thegroup .. "  beschliet das Auto stehen zu lassen und zu Fu weiterzugehen bis sie ein neues Fahrzeug finden." $) cyoa-text!
	car-abandon
;
cyoa-choice: car-breakdown-punch
	($ cname .. " ' offnet die Motorhaube des Autos und fangt an zu SCHLAGEN." $) cyoa-text! car-strengthcheck strengthREV
;
cyoa-choice: car-breakdown-fix
	($ cname .. "  offnet die Motorhaube des Autos." $) cyoa-text! car-tunecheck mechanicREV
;
cyoa: car-breakdown
	"AUTO PANNE" cyoa-title!
	($ "'Das Auto ist durch ubermaigen Gebrauch kaputt gegangen. Es startet nicht mehr." $) cyoa-text!
		choice( "Auto stehen lassen" )choice: car-abandoned
		mechanicest charchoice( "  versucht es zu reparieren" )choice: car-breakdown-fix
		someone charchoice( "  schlagt auf das Auto ein" )choice: car-breakdown-punch
		
;

} }

// this is called after carstats.df
events{ road{

: car-lost-old
	// 0 to car-working
	0 vehicle.which!
	walk-music
	// temporary wear thingy
	car-max-engine to car-engine
	car-update-display
	badfx
	walk-day-pick
	// OLD walk-day-chance
;

: car-boom
	($ "L'automobile a ete effacee !. . 	les armes du coffre ont disparu !" rbold $) +cr +text
	car-lost trunk.killer
;

: car-break
	($ "VOITURE DETRUITE !" rbold $) +cr +text
	car-lost
;

: car-stolen
	($ "VOITURE VOLEE !" rbold $) +cr +text
	car-lost
;

: car-abandon
	($ "VOITURE ABANDONNEE !" rbold $) +cr +text
	car-lost
;

: car-fullhealth
	car-max-chassis to car-chassis
;

: car-fulltune
	car-max-engine to car-engine
;

: car-gas+
	trunk-gas+
	trunk-gas 0 < if
		0 LOOT_GAS trunk.loot!
	then
;

: car-chassis+
	' car-chassis +!
	car-chassis car-max-chassis > if
		car-fullhealth
	then;
	car-update-display
	car-chassis 0 <= if
		car-break
	then
;

: car-armor+
	dup ' car-max-chassis +!
	dup car-chassis+
	1 ' car-armor +!
;

: car-tune+
	' car-engine +!
	car-engine car-max-engine > if
		car-fulltune
	then
;

: car-megatune+
	dup ' car-max-engine +!
	dup car-tune+
;

: car-guzzle 10 car-mpg * car-armor + ;
: guzzle-mpg 
	trunk-gas car-guzzle < if
		trunk-gas
	else
		car-guzzle
	then
	"OH DANG ! " type dup print
	-1 * trunk-gas+ 
;

// Logic for garage-choices

: car-work-randnum
	cchar .mechanical 7 < if 2 else 1.4 then // For over-max mechanical, get a tighter repair variance
;

: car-engine-rnd car-max-engine car-work-randnum / car-max-engine rnd ;
: car-tune-rnd car-engine-rnd car-tune+ ;
: car-tune-rnd-half car-engine-rnd 2 / car-tune+ ;

: car-chassis-rnd car-max-chassis car-work-randnum / car-max-chassis rnd ;
: car-weld-rnd car-chassis-rnd car-chassis+ ;
: car-weld-rnd-half car-chassis-rnd 2 / car-chassis+ ;

: mechanic-diff
	cchar .mechanical car-repair - 
;
: mechanic-silentrepair 
	mechanic-diff -1 > if car-tune-rnd car-weld-rnd else car-tune-rnd-half car-weld-rnd-half then
;

: mechanic-weldup // Combine with tuneup
	mechanic-diff 1 > if
		car-weld-rnd car-tune-rnd goodfx
		($ cHe .. " expertement repare la voiture a temps perdu !" $)
	then;
	mechanic-diff -1 > if
		car-weld-rnd car-tune-rnd
		"Les reparations occupent une bonne partie de la journee."
	then;
	mechanic-diff -2 > if
		car-weld-rnd-half car-tune-rnd-half
		"Les reparations prennent une bonne partie de la journee et sont tres difficiles."
	then;
	($ cHe .. " aucune idee de quoi " .. che .. "travaille sur la voiture et gaspille un jour en semblant travailler dessus." $)
;
: mechanic-day?
	mechanic-diff 2 < if
		fullcamp
	then
;

// Used to repair a car that's fully broken down
: mechanic-repair? cchar .mechanical car-repair ;
: mechanic-repair
	car-engine-rnd to car-engine
	1 ' car-repair +!
;
: car-tunecheck
	mechanic-repair? > if
		($ cname .. " facilement repare la voiture a temps perdu !" $) +cr +text goodfx
		mechanic-repair mechanicREV
	then; 
	mechanic-repair? = if
		mechanicroll if
			($ cHe .. " quelque maniere qu'il le fasse, il met un jour pour faire fonctionner la voiture." $) +cr +text goodfx
			cchar .mechanical@ 6 > if
				($  "La prochaine fois que cette voiture s'arretera, " .. cname .. " cela ne sera pas possible de le reparer absolument." rbold $) +crtext
			else
				($ "La prochaine fois que cette voiture s'arretera, " .. cname .. " nous ne serons pas en mesure de le reparer sauf si... " rbold .. chis rbold .. " l'expertise mecanique est superieure !" rbold $) +crtext
			then
			mechanic-repair
		else
			($ cHe .. " il perd une journee a essayer de reparer la voiture, mais elle refuse de demarrer." $) +cr +text car-abandon	
		then
	else
		($ cHe .. " la reparation de la voiture echoue totalement apres que le jour soit passe a essayer de la reparer." $) +cr +text car-abandon
	then mechanicREV +cr fullcamp
;

: car-strengthcheck
	strengthroll if
		($ cHe .. " comme tout le monde, il est etonne quand l'engine rumeur a la vie !" $) +cr +text
		mechanic-repair
		car-chassis cchar .strength - 1 < if
			"La voiture ne parcourt pas loin sur la route avant que quelque partie qui a subi un choc prenne feu." +cr +text
		then
		cchar .strength -1 * car-chassis+ strengthREV
	then;
		($ cHe .. " il parvient a casser quelque chose dans la voiture et a briser certains os. " .. chis .. " > local est une cle de" $) +cr +text car-abandon morale-- strengthREV strength--
;

// Road block encounter common stuff
	
: detour-roll
	car-mpg 0 = if
		"Ce voiture ne consomme pas de gaz essence." +cr +text
		goodfx
	then;
	50 %chance if
		($ gThey .. " perdre la route pendant une journee complete, jusqu'a en trouver une alternative." $)
		+cr +text
		trunk-gas 70 car-mpg * >= if
			-55 -70 rnd car-mpg * car-gas+ 
		else
			bandit-gas
		then
		+cr fullcamp heal-event
	else
		"Cela prend un peu plus de temps et de gaz, mais pas beaucoup !" +cr +text
		trunk-gas 30 car-mpg * >= if
			-25 -30 rnd car-mpg * car-gas+
		else
			bandit-gas
		then
	then
;

: car-plowthrough
	car-armor 0 > if
		"La voiture percute directement l'obstacle, meme si certains morceaux de son blindage improvise tombent en desordre." +cr +text goodfx
		-1 ' car-armor +!
	else
		-2 -3 rnd car-chassis+ badfx
		car-chassis 0 > if
			"La voiture passe, mais subit des degats." +crtext
		else
			"La voiture est passee, mais a subi des dommages excessifs. elle s'est decomposee a une courte distance." +crtext
		then 
	then
;

: car-jump
	car-speed 2 < if
		"La voiture lente se renverse brusquement et explosent en feu, ne parvenant pas a franchir l'obstacle." +cr +text badfx
		ouch2-all car-break
	then;
	car-speed 2 = if
		"La voiture peine a avancer, puis s'enfonce comme un marteau et heurte terre brutalement. des morceaux de voiture volent dans toutes les directions." +cr +text badfx
		-2 -3 rnd car-chassis+
		car-chassis 0 > if "Malgre les coups qu'il a recus, il fonctionne encore !" +crtext then
	then;
	car-speed 3 > if
		"La voiture accelere a une vitesse incroyable, atteint la rampante et vole pendant quelque temps grace aux propulseurs-fusees ! c'est excitant !" +cr +text goodfx morale2+all
	then;
	car-speed 2 > if
		"La voiture accelere considerablement, frappe le saut et decolle au-dessus de l'obstacle !" +cr +text goodfx
	then;
;
	
// Option for shooting stuff in a frenzy, needs work later

: car-shoot-ammo? trunk-bullet 0 > trunk-rifle-shell 0 > trunk-shotgun-shell 0 > or or ;
: car-shoot-check trunk-bullet 29 > trunk-rifle-shell 20 > trunk-shotgun-shell 10 > and and ;
: car-shoot // improve this later
	trunk-bullet 0 > if -30 trunk-bullet+ then
	trunk-rifle-shell 0 > if -20 trunk-rifle-shell+ then
	trunk-shotgun-shell 0 > if -10 trunk-shotgun-shell+ then
	trunk-bullet 0 < if
		0 LOOT_BULLET trunk.loot!
	then
	trunk-shotgun-shell 0 < if
		0 LOOT_SHOTGUN_SHELL trunk.loot!
	then
	trunk-rifle-shell 0 < if
		0 LOOT_RIFLE_SHELL trunk.loot!
	then
;

} }

cyoa{ road{ 

// Common used elements for road block encounters

cyoa-choice: car-detour
	($ Thegroup .. " il decide de prendre un detour ! cela est generalement une mauvaise idee." $) cyoa-text!
	detour-roll
;

// Used to tune up a car in a garage, before it's broken
: car-tuneup // partially combined with weldup now
	car-engine car-max-engine >= if
		"La voiture fonctionne comme une nouvelle ! " 
	else
		car-engine 14 > if
			"La voiture fonctionne comme si elle etait en bon etat. "
		then
		car-engine 15 < car-engine 7 > and if
			"La voiture a vu des jours meilleurs. "
		then
		car-engine 8 < if
			"La voiture pourrait encore s'arreter a tout moment. "
		then
	then
	+crtext
;
cyoa-choice: car-weldup
	($ cname .. " essaye de reparer la voiture esperant qu'elle puisse aller un peu plus loin avant se casser a nouveau. " $) cyoa-text!
	mechanic-weldup +text
	car-chassis car-max-chassis = if
		"L'automobile semble intacte !" 
	else
		car-chassis 3 < if
			"La voiture encore parait completement endommagee."
		else
			"La voiture a l'aspect battue et usee, mais elle reste solide."
		then
	then
	car-tuneup
	+text mechanicREV mechanic-diff -2 > if mechanic++ then +cr mechanic-day?
;

// Standard choices in every garage
: garage-choices
	mechanicest choosechar
	choice( "Reparation et reglage de voiture" )choice: car-weldup
	// choice( "Reglage complet" )choice: car-tuneup 
	choice( "Fuyez loin de la" )choice 
;

cyoa: car-nogas
	"DESOLE, VOTRE CARBURANT EST TERMINE." cyoa-title!
	($ "La voiture est a l'arret, apres avoir totalement epuise son reserve de carburant." .. "VOITURE ABANDONNEE !" rbold $) cyoa-text! badfx car-lost
;

cyoa-choice: car-abandoned
	($ "Choix du sophiste. " .. thegroup .. " il decide d'abandonner la voiture et de marcher jusqu'a ce qu'il trouve une nouvelle voiture." $) cyoa-text!
	car-abandon
;
cyoa-choice: car-breakdown-punch
	($ cname .. " ouvre le capot de la voiture et commence a frapper." $) cyoa-text! car-strengthcheck strengthREV
;
cyoa-choice: car-breakdown-fix
	($ cname .. " ouvre le capot de la voiture." $) cyoa-text! car-tunecheck mechanicREV
;
cyoa: car-breakdown
	"RUPTURE DE VOITURE" cyoa-title!
	($ "La voiture a cesse de fonctionner en raison d'une utilisation excessive et abusive. elle ne redemarre plus." $) cyoa-text!
		choice( "Laisser la voiture" )choice: car-abandoned
		mechanicest charchoice( " essayez de le corriger" )choice: car-breakdown-fix
		someone charchoice( " frappe la voiture" )choice: car-breakdown-punch
		
;

} }

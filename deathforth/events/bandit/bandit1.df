
cyoa{ road{

: bandithurt
	ezbandit? if 
		ouch--all
	else
		ouch12rnd--all
	then
;

: haggler-morale 
	haggler? if else morale--all then
;

: bandit-give-in
	($ Thegroup .. " 'decide jugarlo seguro " .. gThey .. " 'da" .. g1s .. " 'en la demanda de los bandidos." $) cyoa-text! badfx
;
cyoa-choice: bandit-pay-foodtoll
	someicon bandit-give-in bandit-toll-pay haggler-morale 
;
cyoa-choice: bandit-pay-lowtoll
	someicon bandit-give-in eat-all bandit-ammomed2 haggler-morale
;
cyoa-choice: bandit-pay-badtoll
	someicon bandit-give-in bandit-ammomed 
		// check if in car
		vehicle.which@ 0 if
			bandit-gas
		else
			bandit-gastank
		then morale2-all
;
cyoa-choice: bandit-start-fight
	($ Thegroup .. " 'sabe que cada uno de " .. ghis .. " 'los suministros son esenciales para " .. ghis .. " 'las posibilidades de alcanzar Canada. Asi que " .. gthey .. " 'lucha" .. g1s .. "." $) cyoa-text! someicon bitefx
		ezbandit? if 
			ouch--all
		else
			70 %chance if
				ouch12rnd--all
			then;
			ouch2-all
		then
;

: bandit-demand
	bandit-toll? if
		($ bandit-toll .. " 'comida " .. "para pasar safely." rbold $) +text
		choice( "Paga peaje de " .. bandit-toll .. " 'comida" )choice: bandit-pay-foodtoll
	else
		trunk-food 4 > if
			ezbandit? if 
				($ "el resto de tu comida, y un cuarto de tu municion y suministros medicos." rbold $) 
			else
				($ "1/2 of your ammo and medical supplies" rbold $) 
			then +text
			choice( "Ceder a las demandas del bandido" )choice: bandit-pay-lowtoll
		else
			ezbandit? if
				($ "todo tu gas extra, y la mitad de tus municiones y suministros medicos!" rbold $)
			else
				($ "todo tu municion, suministros medicos y tanques de gas extra!" rbold $)
			then +text
			choice( "Ceder a las duras demandas de los bandidos" )choice: bandit-pay-badtoll
		then
	then
;
: bandit-fight
	choice( "Rechazar y luchar!" )choice: bandit-start-fight
;
: bandit-demand-fight
	bandit-demand bandit-fight
;

cyoa-choice: bandit-charm-sucker
	($ cname .. "  engana a los bandidos. Mientras esperan que se les deje caer suministros, " .. ghe .. " 'corre a la carro y sale a fullas.	Los bandidos logran dispararle al coche mientras se aleja! Solo tienen tiempo para herirlo." $) cyoa-text! -1 car-chassis+ bitefx
	car-chassis 0 > if
		"Recibe un par de agujeros adicionales, y esta en buen estado." +crtext
	then loyaltyREV
;

cyoa-choice: bandit-charm-tp
	($ Thegroup .. ", no dispuesto ni capaz de sparear otros suministros, decide tirar " .. ghis .. " 'armas extra desde el maletero.	" .. "ARMAS EXTRA PERDIDAS!" rbold $) cyoa-text! trunk.clear badfx
;

cyoa-choice: bandit-charm-morale
	($ Thegroup .. ", no dispuesto ni capaz de sparear otros suministros, decide perder algunos articulos personales queridos a los banditos.	" .. "QUE PENA!" rbold $) cyoa-text! badfx morale3-all
;

cyoa-bridge: bandit-start-charm
	($ cname .. " 'ofrece un compromiso a los banditos. " .. gHe .. " 'dejara algo que quieran los bandidos en la carretera, luego se ira. Ninguna herida para ninguno de las partes." $) cyoa-text! witsattREV 
	"Los suministros seran:" +crtext
		trunk-food bandit-toll2 1 - > if
			choice( bandit-toll2 .. " 'comida" )choice: bandit-toll-pay2
		then
		cloyalty- if
			choice( "CHAU, GEEKS!!" )choice: bandit-charm-sucker
		then
		trunk-bullet 1 > trunk-rifle-shell 1 > trunk-shotgun-shell 1 > or or if
			choice( "La mitad de tu municion" )choice: bandit-ammo2
		then
		trunk-gas 100 > if
			choice( "Tus tanques de gas extra" )choice: bandit-gastank
		then
		trunk-medical 1 > if
			choice( "La mitad de tus suministros medicos" )choice: bandit-med2
		then
		choice( "Tesoros personales" )choice: bandit-charm-morale
		trunk.count 0 > if
			choice( "Todas las armas del maletero" )choice: bandit-charm-tp
		then
;
: bandit-charm
	most-charming if 
		charchoice( " 'ofrece un compromiso" )choice: bandit-start-charm
	then
;

// Toll road

cyoa-bridge: bandit-tollroad-coolit
	cpet? if
		($ cname .. " 'hace un pedo en el puente toll." $)
	else
		($ cname .. " 'le dice a los bandidos del puente que" .. " 'TRanquILIDAD!" pbold $)
	then cyoa-text!
	cool-it-7 if
		char-gen to recruitee "Bandido del puente" recruitee .name!
		($ "Esto realmente hace pensar a los bandidos.		Han sido simplemente codiciosos?		Ofrecen un regalo para empezar a enmendarse!" $)
		+crtext morale3+all rescue-trainitem cooled-it goodfx
	else cool-it++
		($ "Los bandidos se niegan a bajar el puente hasta que reciban la mayor parte de " .. thegroup .. "suministros deÂ '" $) +crtext badfx bandit-mostgas morale2-all
	then witsattREV 
;

cyoa-choice: bandit-tollroad-shoot
	($ Thegroup .. " 'dibuja " .. ghis .. " 'arma" .. gs .. " 'en los bandidos." $) cyoa-text!
	($ gThey .. " " .. gare .. " 'tan rapido que los banditos estan desbordados, y obligados a levantar el puente.	" .. Thegroup .. " 'pasa por el puente sin dano." $) +crtext goodfx
;

cyoa-choice: bandit-tollroad-jump
	($ cname .. " 'esta realmente enojado por tener que pagar peajes de puentes, incluso despues de la civilizacion. " .. cHe .. " 'se estrella directamente en el puente retraido, enviando el coche volando a traves." $) cyoa-text! temperREV car-jump
;

cyoa: bandit-tollroad

	($ "Los bandidos estan guardando un puente retirado sobre un rio. Exigen que cualquiera que pasa pague un peaje.	" .. "Quieren " rbold $) cyoa-text!
		bandit-demand-fight
		shooting-total 12 > if
			choice( "Armas" )choice: bandit-tollroad-shoot
		then
		most-grating if
			charchoice( " 'le dice a los Banditos: 'Tranquilos" fcoolit )choice: bandit-tollroad-coolit
		then
		smartest-pass if
			smartest charchoice( ": Haz un desvio en su lugar" )choice: car-detour
		then
		angriest-pass if 
			angriest choosechar chumany? if
				cchar charchoice( ": SALTARLOS" )choice: bandit-tollroad-jump
			then
		then
;

// Quiet Ambush

cyoa-choice: bandit-quiet-paranoid
	($ cname .. "  les llama la atencion a los bandidos por su farol, inmediatamente. Antes de que los bandidos puedan gritar, " .. cname .. " 1 " .. thegroup .. "  se aleja por un camino lateral. Siempre es bueno planear para lo peor!" $) cyoa-text! goodfx morale++ morale++all witsREV attitudeREV
;

cyoa-choice: bandit-quiet-angry
	($ "Sin previo aviso y sin decir una sola palabra, " .. cname .. " 'se acerca al bandido mas cercano y le da un punetazo en la cara, noqueandolo. Luego " .. cname .. " 	Everyone's stunned. The remaining three bandits decide they should quietly pick up their friends and leave." $) cyoa-text! goodfx morale++ morale++all temperREV strengthREV
;

cyoa-choice: bandit-quiet-pay
	($ Thegroup .. " paga a los bandidos su cuota de extorsion, en lugar de arriesgarse a la carniceria." $) cyoa-text!
	-10 trunk-food+ badfx
;

: bandit-quiet-roll
	50 %chance if
		"Una de los bandidos grita, y el caos surge al salir a relucir las zombis. La huida fue turbia." +crtext ouch--all badfx
	else
		($ "Los bandidos hablaban mucho, y no hacen un ruido alguno cuando se les llaman mentira. " .. Thegroup .. " ' vuelve al coche mientras los banditos se retiran." $) +crtext goodfx
	then
;

cyoa-choice: bandit-quiet-nofood
	choosehuman cicon
	($ cname .. " 'explica que " .. ghe .. " ' no tiene suficiente comida." $) cyoa-text! 
	trunk-food 4 > if
		"Los bandidos no quieren arriesgarse a la carniceria, asi que aceptan tomando menos comida." +crtext eat-all
	else
		"Los banditos creen que esto es una mentira!" +crtext
		bandit-quiet-roll
	then
;

cyoa-choice: bandit-quiet-yell
	choosehuman cicon
	($ cname .. " 'desafia la mentira de los bandits." $) cyoa-text! 
	bandit-quiet-roll
;

cyoa-bridge: bandit-quiet-coolit
	cpet? if
		($ cname .. " 'farta muy debilmente a los bandits." .. " Silent... but deadly." pbold $)
	else	
		($ "A silenciosas palabras, " .. cname .. " 'dice a los banditos que" .. " 'TRanquILIDAD!" pbold $) 
	then cyoa-text!
	cool-it-7 if
		char-gen to recruitee "Bandido callado" recruitee .name!
		($ "Los bandidos se congelan en su lugar, sin saber que hacer. Uno ofrece un soborno si " .. cname .. " 'simplemente se va y promete no regresar. Los bandidos pueden dar sus suministros, o ensenar silenciosamente secretos de zombies a traves del lenguaje de senas." $)
		+crtext 
		rescue-trainitem 
		cooled-it goodfx
	else cool-it++
		"Los bandidos quedan, durante un tiempo que parece largo, pasmados por esto." +crtext 
		bandit-quiet-roll
	then witsattREV 
;

cyoa: bandit-quiet
	// NEED IMAGE 
	someicon
	($ "Cinco bandidos emboscan a " .. thegroup .. " 'mientras " .. gtheyare .. " 'esta a punto de colarse en una casa infestada de zombies. Los bandidos hablan en un susurro, asustados de atraer la atencion de los muertos vivientes. Dicen que hay que entregarles algunos suministros, o ellos gritaran." $) cyoa-text!
		trunk-food 9 > if
			choice( "Entrega 10 de comida" )choice: bandit-quiet-pay
		else
			choice( "No tengo la comida!" )choice: bandit-quiet-nofood
		then		
		most-angrystrong if 
			charchoice( " 'entra en una furia" )choice: bandit-quiet-angry
		then
		most-paranoid if 
			charchoice( " 'ya tenia un plan" )choice: bandit-quiet-paranoid
		then
		most-grating if 
			charchoice( " 'le dice a los Banditos: 'Tranquilos" fcoolit )choice: bandit-quiet-coolit
		then
		choice( "Llama la atencion" )choice: bandit-quiet-yell
;

// Buff Bandits

cyoa-choice: bandit-buff-pose
	($ cname .. " 'retar a los bandidos a un duelo de poses." $) cyoa-text!
	cstrength+ if
		($ cHe .. " ' realiza una rutina de flexion increiblemente impresionante, forjada por el entrenamiento. Los ladrones pierden claramente y tienen que dejarlo ir " .. thegroup .. " ' no esta cerca de la masa muscular de los ladrones." $) +crtext goodfx morale2+all strengthREV
	then;
	
	cstrength- if
		90 %chance if
			($ cHe .. " ' es dejado ir. " .. cHe .. " ' pierde rapidamente la postura. Los ladrones toman muchos suministros adicionales, incluso algunos que no necesitan, para ensenarle " .. thegroup .. " 'una leccion sobre los debiles desafian a los musculosos. " $) +crtext morale--all bandit-most bandit-gastank badfx
		else
			($ "Por una extrana coincidencia, " .. che .. " ' gana la postura-off gracias a un pequeno lapsus en el reglamento de los jueces de bodybuilding. Los ladrones estan bastante enojados por esto, pero sus manos estan atadas por sus reglas." $) +crtext strength++ goodfx morale++all
		then strengthREV
	then;
	
	strengthroll if
		($ cHe .. " ' logra derrotar a los ladrones en una competencia de bodybuilding, tanto en los segmentos de flexion como en el segmento de talento! " .. Thegroup .. " ' es dejado ir." $)
		+crtext goodfx strength++ morale++all strengthREV
	then;
	($ cname .. " ' pierde la postura-off. Es humillante, y los ladrones toman muchos suministros como castigo por la presuncion de desafiarles." $) +crtext
		morale-- badfx bandit-ammomed2 bandit-food
;

cyoa-bridge: bandit-buff-coolit
	cpet? if
		($ cname .. " ' farta a los banditos, y tambien a la idea de bodybuilding." $)
	else	
		($ cname .. " ' les pide a los bandidos musculosos que" .. " 'TRanquILIDAD!" pbold $)
	then cyoa-text!
	cool-it-7 if
		char-gen to recruitee "Bandido de la Banda" recruitee .name!
		($ "They are impressed by this bold attitude!		They declare " .. chim .. " 1,000 gold pieces.		They also offer to join your army.		Do you accept?		Yes		No" $)
		+crtext morale3+all rescskill1 rescue-reward-shuffle recruit-choice cooled-it goodfx
	else cool-it++
		($ "Los bandidos se enojan de esta actitud negativa y toman sus iras en " .. thegroup .. "el coche de el. Todos lo golpean a mano desnuda, haciendo dano serio." $) +crtext -2 car-chassis+ badfx	
		morale--all bitefx
		"Los banditos musculosos se van, flexionando. Incluso el perro." +crtext	
	then witsattREV
;

cyoa-choice: bandit-buff-obliv
	($ cname .. " 'pregunta a los banditos sobre sus " .. "BANDAS DE SUOR" pbold .. " y, or = o " .. "SIEMPRE LEVANTAN BARRAS DE PESAS Mientras HABLAN.'	" pbold .. "Los bandidos hablan sobre una tradicion perdida increible de " .. "ENTRENAMIENTO FISICO" rbold .. " 'que se perdio cuando la civilizacion cayo.	Mostraron lo que significaban en una inspiradora exposicion muscular. En el proceso, los banditos se olvidan de robar " .. thegroup .. "." $) cyoa-text!
	morale2+all goodfx witsattREV
;

cyoa: bandit-buff
	image-buff-bandit
	"Estan todos usando bandas de suor" cyoa-title!
	($ "Aparecen bandidos, todos con bandas de suor y todos super musculosos, incluso su perro. Exigen proteina en polvo!'	" .. "Tambien aceptaran " rbold $) cyoa-text!
		bandit-demand
		strongest charchoice( " 'les desafia a un duelo de poses" )choice: bandit-buff-pose
		most-grating if
			charchoice( " 'les dice a los bandidos que se 'Calmen" fcoolit )choice: bandit-buff-coolit
		then
		most-oblivious if
			charchoice( " 'pregunta que pasa con los atuendos" )choice: bandit-buff-obliv
		then
		bandit-fight
		
;

// Greedy bandits that demand all of your stuff

: bandit-allofit-ez
	ezbandit? if "la mitad de" else "todo" then
;

cyoa-choice: bandit-allofit-pay
	($ "Los bandidos revisan " .. thegroup .. "'las pertenencias y toman toda la comida y " .. bandit-allofit-ez .. " 'los suministros.	" .. Thegroup .. " 'no esta contento con esto, pero al menos los bandidos no robaron el coche ni siquiera drenaron la gasolina. A cambio de la cooperacion, tambien le devolvieron las armas." $) cyoa-text! badfx
	bandit-most
	morale2-all
;
: bandit-allofit-check args( chara ) 
	<- chara .cfitness+ if
		<- chara .sting--
	else
		<- chara .cfitness- if
			<- chara .ouch2-
		else
			<- chara .ouch-- 50 %chance if <- chara .sting-- then
		then
	then <- chara .fitnessREV
;
cyoa-choice: bandit-allofit-run
	($ Thegroup .. " 'decide huir, pensando que los suministros valen la pena el riesgo.	Los bandidos comienzan a disparar!" $) cyoa-text!
	solo if
		cfitness+ if
			($ cname .. " spanish " .. cHe .. " 'solo recibe un roce" $) +crtext sting--
		else
			cfitness- if
				($ cname .. " runs a few feet before getting winded. 
				" .. cHe .. "  recibe un golpe fuerte." $) +crtext ouch2-
			else
				($ cname .. " corre tan lejos como " .. che .. " 'se puede: " .. cHe .. " ' se le da un golpe." $) +crtext ouch-- 50 %chance if sting-- then
			then
		then
	else
		' bandit-allofit-check foreach-team-char
	then bitefx
;
cyoa-bridge: bandit-allofit-coolit
	cpet? if
		($ cname .. " ' farta a los bandidos, les da todo ', " .. chis .. " ' gas de flatulencia." $)
	else	
		($ cname .. " 'dice a los banditos que" .. " 'TRanquILIDAD!" pbold $) 
	then cyoa-text!
	cool-it-7 if
		char-gen to recruitee "Exbandido" recruitee .name!
		($ "Esto detiene a los banditos en sus huellas. Que hubiera sucedido si han estado viviendo mal toda esta vida? Hubiera ido mejor si simplemente se habian calmado? Deciden hacer solo cosas buenas desde ahora y ofrecer un regalo!" .. Thegroup .. " ' esta invulnerable, con ', " .. ghis .. " ' espiritus enaltecidos." $)
		+crtext morale3+all rescue-trainitem cooled-it goodfx
	else cool-it++
		($ "Los banditos no estan muy impresionados por esto. Abren fuego mientras " .. thegroup .. " ' intenta huir." $) +crtext 
		notsolo if 
			($ "Por lo menos, the majority of the bullets hit " .. cname .. "." $) +crtext
		then
		ouch2- sting--all bitefx
	then witsattREV 
;

cyoa-choice: bandit-allofit-paranoid
	($ "Siempre prudente, " .. cname .. " ' estaba de guardia antes del emboscada. " .. cHe .. " 'se lanza hacia una cobertura cercana pero facil de pasar por alto " $) cyoa-text!
	notsolo if ($ Everyone .. " 'sigue a " .. chim .. "." $) +text then
	"Los bandidos pierden su abrumadora ventaja y se alejan despues de una breve pelea." +crtext bitefx sting--all witsattREV
;

cyoa: bandit-allofit
	"Dinero vs Vida" cyoa-title! someicon
	($ "Alcanzan a " .. thegroup .. " 'por sorpresa, en un lugar realmente terrible para " .. ghim .. " 'entregar todas las comidas y " .. bandit-allofit-ez .. " 'suministros!	" .. "DINERO O VIDA!" rbold $) cyoa-text!
		most-charming if 
			charchoice( " 'ofrece un compromiso" )choice: bandit-start-charm
		else
			choice( "Entregar suministros" )choice: bandit-allofit-pay
		then
		most-grating if 
			charchoice( " 'le dice a los Banditos: 'Tranquilos" fcoolit )choice: bandit-allofit-coolit
		then
		most-paranoid if 
			charchoice( ": Podemos luchar contra ellos!" )choice: bandit-allofit-paranoid
		then
		choice( "Corran!" )choice: bandit-allofit-run
;		

} }



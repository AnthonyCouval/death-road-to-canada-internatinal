cyoa{ road{ regiondef{

cyoa: info-dingus
	"wugh" cyoa-title!
	"'Ciao, sono l'Info Dingus" text!
;

uses events/unlock/perktrait.df
uses events/unlock/perk1.df
uses events/unlock/perk2.df
uses events/unlock/perk3.df
uses events/unlock/trait1.df
uses events/unlock/gnome.df
uses events/unlock/twitter.df

: challenge-gamemodecheck

	// Increment this 0 by 1 for each game mode with wins
	0 gstats{
		wins-normal		if 1+ then
		wins-long		if 1+ then
		wins-special	if 1+ then
		wins-family		if 1+ then
		wins-specialEX	if 1+ then
		wins-familyEX	if 1+ then
		wins-kepa		if 1+ then
		wins-short		if 1+ then
		wins-marathon	if 1+ then
		wins-deadlier	if 1+ then
		wins-endless	if 1+ then
		wins-opp		if 1+ then
		wins-quickdeath	if 1+ then
		wins-4jerks		if 1+ then
		wins-4jerksEX	if 1+ then
	}
;

cyoa: challenge-statue1
	gstats{ challenge-statue1 } 1 < if
		"SFIDA DELLE MODALITA I" cyoa-title!
		challenge-gamemodecheck 6 < if
			($ "Sconfiggi 6 diverse modalita di gioco per " .. "sbloccare" rainbold .. "  un assortimento di " .. "9 NUOVI CAPPELLI" rainbold .. "  per il Custom Character Maker!" $) text!
			($ "Modalita di gioco completate: " bbold .. challenge-gamemodecheck $) +crtext
		else
			($ "You've beaten 6 different game modes!
			" .. "SFIDA COMPLETATA!" rainbold $) text!
			goodfx gstats{ 1 to challenge-statue1 } filesave{ save-global-stats }
			"I nuovi cappelli sono sbloccati permanentemente. Dai unocchiata nel Custom Character Maker!" +crtext
			extrahatcheck
		then
	else
		"SFIDA DELLE MODALITA I   COMPLETATA!" cyoa-title!
		($ "Dopo aver completato 6 diverse modalita di gioco, hai guadagnato un lotto di 9 nuovi cappelli per il Custom Character Maker! Questi includono cappelli molto ricercati come:		" .. "CASSETTA DEL WATER CON COPERCHIO		CAPPUCCIO CON TESTA DI CAVALLO		CORONA DORO" rbold .. "		E ALTRO ANCORA!		" fbold $) text!
		extrahatcheck
	then
	choice( "INDIETRO" )choice
;

cyoa: challenge-statue2
	gstats{ challenge-statue1 } 2 < if
		"SFIDA DELLE MODALITA II" cyoa-title!
		challenge-gamemodecheck 10 < if
			($ "Sconfiggi 10 diverse modalita di gioco per " .. "sbloccare" rainbold .. "  un assortimento di " .. "6 steli nuovi" rainbold .. "  e " .. "8 nuove ombrelloni" rainbold .. "  per il Custom Character Maker!" $) text!
			($ "Modalita di gioco completate: " bbold .. challenge-gamemodecheck $) +crtext
		else
			($ "You've beaten 10 different game modes!
			" .. "SFIDA COMPLETATA!" rainbold $) text!
			goodfx gstats{ 2 to challenge-statue1 } filesave{ save-global-stats }
			"Gli steli nuovi e le ombrelloni sono sbloccati permanentemente. Guarda loro nel Creatore Personale!" +crtext
			extrahatcheck extraglassescheck
		then
	else
		"Sfida dei Modi II   COMPLETATO!" cyoa-title!
		($ "Dopo aver battuto 10 modalita diverse, hai guadagnato una scorta di 6 nuovi steli e 8 nuove ombrelloni per il Creatore Personale! Questo includeva anche accessori molto ricercati come:		" .. "Feltro da principessa" rbold .. "		E ALTRO ANCORA!		" fbold $) text!
		extrahatcheck extraglassescheck
	then
	choice( "INDIETRO" )choice
;

cyoa: challenge-statue3
	gstats{ challenge-statue1 } 3 < if
		"CHALLENGE OF MODES III" cyoa-title!
		challenge-gamemodecheck 14 < if
			($ "Battere 14 modalita diverse per " .. "sbloccare" rainbold .. "  un assortimento di " .. "12 nuovi steli" rainbold .. "  per il Custom Character Maker!" $) text!
			($ "Modalita di gioco completate: " bbold .. challenge-gamemodecheck $) +crtext
		else
			($ "You've beaten 14 different game modes!
			" .. "SFIDA COMPLETATA!" rainbold $) text!
			goodfx gstats{ 3 to challenge-statue1 } filesave{ save-global-stats }
			"I nuovi cappelli sono sbloccati permanentemente. Dai unocchiata nel Custom Character Maker!" +crtext
			extrahatcheck
		then
	else
		"CHALLENGE OF MODES III - COMPLETE!" cyoa-title!
		($ "Dopo aver battuto 14 modalita diverse, hai guadagnato una scorta di 12 nuovi steli per il Creatore Personale! Questo includeva anche steli molto ricercati come:		" .. "'Grande pesce		Cappello di Tam O Shanter		Calzarella		Cappello da cucina" rbold .. "		E ALTRO ANCORA!		" fbold $) text!
		extrahatcheck
	then
	choice( "INDIETRO" )choice
;

cyoa: challenge-statue4
	gstats{ challenge-statue1 } 4 < if
		"Sfida dei Modi IV" cyoa-title!
		gstats{ best-gamemode-streak 4 } > challenge-gamemodecheck 13 > and if
			($ "You've streaked 5 different game modes!
			" .. "SFIDA COMPLETATA!" rainbold $) text!
			goodfx gstats{ 4 to challenge-statue1 } filesave{ save-global-stats }
			"I nuovi cappelli sono sbloccati permanentemente. Dai unocchiata nel Custom Character Maker!" +crtext
			extrahatcheck
		else
			($ "Dopo aver superato le sfide precedenti, ottenere una serie vincente di 5 modalita diverse per " .. "sbloccare" rainbold .. "  un assortimento di " .. "18 NUOVI CAPPELLI" rainbold .. "  per il Custom Character Maker!" $) text!
			($ "Modalita di gioco completate: " bbold .. challenge-gamemodecheck $) +crtext
			($ "Miglior Sequenza di Diverse Modalita di Gioco: " bbold .. gstats{ best-gamemode-streak } $) +crtext
		then
	else
		"SFIDA DELLE MODALITA IV   COMPLETATA!" cyoa-title!
		($ "Dopo una sequenza di 5 diverse modalita di gioco, hai guadagnato un lotto di 18 nuovi cappelli per il Creatore di Personaggi Personalizzati! Cio include cappelli molto ricercati come:		" .. "CAPPELLO DA GNOMO		CAPPELLO DI CARTA STAGNOLA		MASCHERA DI CARTONE		CAPPELLO DA STREGA" rbold .. "		E ALTRO ANCORA!		" fbold $) text!
		extrahatcheck
	then
	choice( "INDIETRO" )choice
;

cyoa: jerk-dingus
	"Il Negozio dei Cretini ha Chiamato" cyoa-title!
	"'SEI UN CRETINO	OK, SARANNO CINQUE DOLLARI!		GRAZIE PER AVER FATTO ACQUISTI	AL NEGOZIO DEI CRETINI" text!
	choice( "'SONO TUTTI FINITI" )choice
;

cyoa: roaming-capyrun
	"Il Roditore Vagabondo" cyoa-title!
	($ "La cosa del roditore " .. "capibara" pbold .. "  fugge quando ti avvicini.	Corre verso il " .. "NORD EST!" rainbold $) text!
	clear-cyoa deleteme
;

cyoa: ugnome-note
	"Dalla Scrivania di Unomey" cyoa-title!
	($ "Il biglietto recita quanto segue:" $) cyoa-text! 
	"Italian" pbold +crtext
;

// : tutorial-repeat 0 saveslot! MAIN_EVENT_NEW main-post-event "events/tutorial.df" $load-region to-mission ;
: tutorial-repeat 0 saveslot! gstats{ 0 to tutorial-done save } sys_reset ;

cyoa: tutorial
	"Tutorial" $upper cyoa-title!
	someicon
	($ "Puoi ripetere il tutorial qui!	Questo e utile se ti sei perso informazioni su cose come far cadere armi o lanciare mobili.	Questa opzione ti riportera alla schermata del titolo, basta premere START per accedere nuovamente al tutorial." $) cyoa-text! basic-tone
	
		choice( "USCITA" )choice
		choice( "Ripeti tutorial" )choice: tutorial-repeat
;

: dingus-deleted-dialog
	FLASHY "CANCELLATO!" .. $cyoa-ok-dialog "Dingus" $upper cyoa-title!
;

: deletedata-gstats gstats{ gstats-clear } sys_reset 5 0.95 2dup rumble-camera rumble-gui sfx{ rumble } dingus-deleted-dialog ;
: deletedata-custchars filesave{ custchars-clear save-custchars load-custchars } dingus-deleted-dialog sfx{ creepy } ;
: deletedata-both deletedata-custchars deletedata-gstats ;

cyoa-blank: deletedata-gstats-confirm
	"Sei sicuro di voler eliminare le tue statistiche, i vantaggi e le caratteristiche? Cio eliminera anche cose come i progressi della modalita di gioco." text!
	"NOTA: selezionando questa opzione si uscira alla schermata del titolo." pbold +crtext
	choice( "NO" )choice
	choice( "SI, ELIMINALE" )choice: deletedata-gstats
;

cyoa-blank: deletedata-custchars-confirm
	"Sei sicuro di voler eliminare tutti i tuoi personaggi personalizzati?" text!
	choice( "NO" )choice
	choice( "SI, ELIMINALE" )choice: deletedata-custchars
;

cyoa-blank: deletedata-both-confirm
	"Sei sicuro di voler eliminare statistiche, vantaggi, caratteristiche e personaggi personalizzati? Cio eliminera anche cose come i progressi della modalita di gioco." text!
	"NOTA: selezionando questa opzione si uscira alla schermata del titolo." pbold +crtext
	choice( "NO" )choice
	choice( "SI, ELIMINA TUTTO" )choice: deletedata-both
;

// Do ARE YOU SURE? choices too
cyoa: deletedata
	"Cancellazione dati" $upper cyoa-title!
	"'SONO IL DINGUS DI CANCELLAZIONE DATI	CANCELLERO TUTTI I TUOI DATI!" rbold text!
	"Lo scheletro emette rumori di sonagli.	Puoi eliminare i tuoi dati di gioco qui! Anche se probabilmente non dovresti farlo." +crtext
	choice( "USCITA" )choice
	choice( "Personaggi personalizzati" )choice: deletedata-custchars-confirm
	choice( "Statistiche e vantaggi" )choice: deletedata-gstats-confirm
	choice( "Cancella entrambi" )choice: deletedata-both-confirm
; 

cyoa-blank: freeZP-give-go
	iicon
	"I PUNTI ZOMBO sono aumentati a 99!" rainbold text!
	goodfx gstats{ 99 to zombo-point save }
	
;

cyoa: freeZP-give
	"Regalo di punti Zombo!" cyoa-title! iicon
	"'BENE E IL RAMO DI TEST QUINDI POSSO DARTI QUALCHE PUNTO ZOMBO" bbold text!
	gstats{ zombo-point } 99 < if 
		choice( "Dammi quei punti Zombo" )choice: freeZP-give-go
	else
		"TORNA QUANDO HAI MENO DI 99 PZ!" bbold
	then +crtext
	choice( "Lascia per ora" )choice
	
;

} } }

map{ regiondef{

: info-dingus
	nomove-npc
	40 lt .character@ .specialtype!
	' infostats lt .cyoa!
;

: tutorial-dingus
	nomove-npc
	10 lt .character@ .specialtype!
	' tutorial lt .cyoa!
;

: delete-dingus
	nomove-npc
	78 lt .character@ .specialtype!
	' deletedata lt .cyoa!
;

: perk-event args( me eventid perk ) THING_EVENT_TIMER <- eventid <> if then;

	<- me .cyoa@ 0= if then; // Just time out, don't display icon anymore

	15 <- me .repeating_timer

	<- me .nearest? if "" <- me .report then;

	// <- me .loot@ dup 0= if drop 1 GREEN "$" .. else looticon then
	// <- me GREEN "11" .. looticon - a ?
	
	<- perk 1 < if
		<- me "?" pbold
	else
		<- me <- perk bbold
	then 1 swap <- me .reportdurnow
;


(*
	EXAMPLE USAGE: Create a new event handler for the specified value

	gstats{ toilets-opened } pe: perk-event-toilet
*)

: pe: token $create last >body ! does> @ execute perk-event ;

: gstat: postpone gstats{ postpone ' postpone } ;

gstats{ ' perk1-stockleft } pe: perk1-stockleft-event
gstat: perk2-stockleft pe: perk2-stockleft-event
gstat: perk3-stockleft pe: perk3-stockleft-event
gstat: trait1-stockleft pe: trait1-stockleft-event
gstat: gnome-stockleft pe: gnome-stockleft-event
gstat: twitter-twitsleft pe: twitter-twitsleft-event

: total-unlocktally gstats{ perk1-unlocktally perk2-unlocktally perk3-unlocktally trait1-unlocktally + + + } ;
// 14 right now without any unlocked, has to be increased if new perks are added that are pre-unlocked

: total-upgradetally gstats{ perk1-upgradetally perk2-upgradetally perk3-upgradetally + + } ;
// 23 with nothing unlocked currently.
// Need to upgrade zombotown.df if either upgradetally or unlocktally changes.

: unlocker-shared
	// new-camper
	nomove-npc
	// 0.01 lt .character@ .ai.wander_chance!
	1 lt .timer!
;

: perk-dingus unlocker-shared
		
	57 lt .character@ .specialtype!
	' perk1-stockleft-event lt .events!
	' perk-dingus lt .cyoa!
;

: perk1-dingus unlocker-shared
		
	12 lt .character@ .specialtype!
	' perk1-stockleft-event lt .events!
	' perk1-dingus lt .cyoa!
;

: perk2-dingus unlocker-shared
		
	25 lt .character@ .specialtype!
	' perk2-stockleft-event lt .events!
	' perk2-dingus lt .cyoa!
;

: perk3-dingus unlocker-shared
		
	46 lt .character@ .specialtype!
	' perk3-stockleft-event lt .events!
	' perk3-dingus lt .cyoa!
;

: trait1-dingus unlocker-shared
		
	24 lt .character@ .specialtype!
	' trait1-stockleft-event lt .events!
	' trait1-dingus lt .cyoa!
;

: jerk-dingus
	// new-camper
	nomove-npc
	21 lt .character@ .specialtype!
	
	' trader-event lt .events!
	1 lt .timer!

	' jerk-dingus lt .cyoa!
;

: challenge-statue1 
	spawn special_objects48_base_id 0x 24 + 2 + lt .spriteid! bbox immovable
	' challenge-statue1 lt .cyoa!
;

: challenge-statue2
	spawn special_objects48_base_id 0x 24 + 1 + lt .spriteid! bbox immovable
	' challenge-statue2 lt .cyoa!
;

: challenge-statue3
	spawn special_objects48_base_id 0x 24 + 3 + lt .spriteid! bbox immovable
	' challenge-statue3 lt .cyoa!
;

: challenge-statue4
	spawn special_objects48_base_id 0x 24 + 0 + lt .spriteid! bbox immovable
	' challenge-statue4 lt .cyoa!
;

: generalinfo-duder
	nomove-npc
	' gameinfo lt .cyoa!
;

: unlockgnome unlocker-shared
	lt .character@ .gnomify2
	' gnome-stockleft-event lt .events!
	' unlockgnome lt .cyoa!	
;

: ugnome-note wrotenote cyoa{ ' ugnome-note cyoa! } ;

: unlockgnome-hut gstats{ ugnome-found } 1 < if unlockgnome else ugnome-note then ;
: capy-gnort args( me )
	<- me ($ "*GNORT*" rbold $) <- me
	.sprayit
	// statmod{ .report } clear-cyoa
	sfx{ sing 500 fx .d! 2.25 fx .pitch! 0.9 fx .q! <- me .adjust-vol } 
;

: capy-chat args( me )
	gstats{ ugnome-found } 0 > if
		<- me capy-gnort
	else
		cyoa{ roaming-capyrun }
	then
	; 

: roaming-capybara
	new-camper lt .character@ .gerbilfy
	// 0.06 lt .character@ .ai.wander_chance!
	' capy-chat lt .cyoa!
;

: hut-capybara
	new-npc lt .character@ .gerbilfy
	' capy-gnort lt .cyoa!
;

: freeZP-dingus
	nomove-npc 
	40 lt .character@ .specialtype!
	
	' trader-event lt .events!
	1 lt .timer!
	' freeZP-give lt .cyoa!
;

: twitter-chicken
	nomove-npc
	lt .character@ .chickenify
	300 angle-colour 2 rgb* lt .clr!

	' twitter-twitsleft-event lt .events!
	1 lt .timer!
	' twitterbird lt .cyoa!
;

} }

// MANDIBLE

cyoa{ regiondef{

cyoa: sycamore-dingus
	"Professor Sycamore" cyoa-title!
	"'Non dire legname, mi fa venire brutti ricordi." pbold text!
	"--- Professor Sycamore
	Creator of: Death Road to Sycamore" +crtext
	"Contributi per laggiornamento MANDIBOLA: " rbold +crtext
	"Punch Bot e Speed Bot nello Special Mechanical Trading Camp" bbold +crtext
	choice( "Robot molto belli" )choice
;

cyoa: modguard-dingus
	"Capanna dentro una capanna" cyoa-title!
	"Io sono il guardiano della Capanna del Contributore! Passato me ci sono creatori che hanno contribuito con nuovi contenuti a Death Road! Sono creatori di mod di Death Road to Canada, dal nostro Discord " text! "#mostra mod" pbold +text "  e " +text "#deathroad modding" pbold +text 
	
	"Entra per leggere le loro parole di ispirazione e scopri cosa hanno aggiunto in ogni aggiornamento!	E una capanna allinterno della capanna informativa piu grande!" +crtext
	choice( "Controlliamo questa capanna" )choice
;

} }

map{ regiondef{

: sycamore-dingus
	// new-camper
	nomove-npc
	121 lt .character@ .specialtype!

	' sycamore-dingus lt .cyoa!
;

: modder-dingus
	// new-camper
	nomove-npc
	120 lt .character@ .specialtype!

	' modguard-dingus lt .cyoa!
;


} }

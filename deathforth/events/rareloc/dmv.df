// Change one $add-rooms to $load-rooms if you want to edit them in the room editor directly, but change it back to $add-rooms when done

"../deathforth/rooms/eyeball.txt" $add-rooms
"events/inner/officeloc.df" $load // Pretty much just for the office counter

// Uncomment below for easy testing with "events/rareloc/dmv.df" $load-region to-mission in hack.df
// Also see final lines for turning regiondef on for testing

// "../deathforth/rooms/restroom.txt" $add-rooms

cyoa{

: dmv-cool-namer args( chara )
	"Genial " <- chara .name@ $cat <- chara .name!
;

: dmv-text-1
	($ cname .. "''s nombre ahora es " $) text!
;

: dmv-text-2
	($ cname .. "'! La licencia de conducir lo dice! Es legalmente vinculante!" $) +text clear-cyoa
;

: dmv-text-badname
	cchar dsyp? if
		($ cname .. " ' realmente no le importa demasiado el nombre. Es algo apropiado!" $) +crtext goodfx
		morale++ vitality++ statmod{ -5 to dsyp-farted }
	then;
	($ cname .. "  instantaneamente forma una gran astilla en " .. chis .. "  hombro. " .. cHe .. "  entra en un estado permanente de ira latente!" $) +crtext strength++ fitness++ morale-- temper-- badfx
;

cyoa-choice: dmv-machine-1
	dmv-text-1 "Smellbert" pchar .name! dmv-text-2
	"Que nombre nuevo tan completamente horrible!" +crtext dmv-text-badname
;

cyoa-choice: dmv-machine-2
	dmv-text-1 "Fartfart" pchar .name! dmv-text-2
	($ "Esto todavia es mejor que " .. chis .. "  nombre anterior." $) +crtext dmv-text-badname
;

cyoa-choice: dmv-machine-3
	dmv-text-1 "Harry Ham" pchar .name! dmv-text-2
	$( cname .. "  fue el nombre mas popular durante siglos en la era de la Baja Edad Media, tanto para ninas como para ninos. Se pensaba que proporcionaba una bendicion de buena salud." $) +crtext
	vitality++ goodfx
;

cyoa-choice: dmv-machine-4
	dmv-text-1 pchar dmv-cool-namer dmv-text-2
	($ cname .. "  se siente muy bien con " .. chis .. "  nuevo nombre!" $) +crtext
	morale3+ goodfx
;

cyoa-bridge: dmv-machine-go
	"'Cual debe ser el nombre en la licencia de conducir?" text!
	choice( "Smellbert" )choice: dmv-machine-1
	choice( "Fartfart" )choice: dmv-machine-2
	choice( "Harry Ham" )choice: dmv-machine-3
	choice( "Genial " .. pchar .name@ )choice: dmv-machine-4
;

cyoa: dmv-machine
	"A License... to Drive" cyoa-title! picon!
	"'No puedo creer que te hayas saltado la fila! Muy grosero! Esta es una maquina que se utiliza para imprimir licencias de conducir. Puede imprimir una licencia final antes de quedarse sin suministros." text!
	($ pname .. "  podria usar uno nuevo, " .. phis .. "  la licencia esta a punto de vencer!" $) +crtext
		choice( "Dejalo por ahora" )choice
		choice( "Imprimir nueva licencia" )choice: dmv-machine-go
;

}

map{ roomgen{ : dmv-machine hospitalmachine swolebolt cyoa{ ' dmv-machine cyoa! } ; } }

locdef{
		
location: dmv
		max-rooms: 4
		entry-tags: dmvstart
		pre-script: set-theme-office
		script: set-theme-office
				
		: dmvdoors
			random-door "dmvend" only-with-door
			random-door "restroom" only-with-door
			random-door "restroom" only-with-door
		;
		
		room-choice: dmvstart
			room-weight: 80
			room-count: 1
			room-script: nad
			room-post-script: dmvdoors 5diff %zombs
			
		room-choice: dmvend
			room-weight: 1
			room-count: 1
			room-script: nad
			room-post-script: 10diff %zombs
			filter-only
			
		room-choice: restroom
			room-weight: 1
			room-count: 2
			room-script: nad
			room-post-script: 20diff %zombs
			filter-only
			
}

// Comment the (* and *) lines out for easy testing, see line 6
// Remove the comments you add before actually putting them in the city generator
(*
regiondef{			

	region: dmv
		script: timeofday o'clock

		inside
					
		loc-choice: dmv 
		loc-weight: 99 
		loc-count: 1

}
*)

cyoa{ road{

// Fire escape

cyoa-choice: camp3-fire-paranoid
	($ cname .. " ALWAYS has a fire safety plan prepared. It doesn't matter where " .. che .. " is, " .. cname .. " constantly thinks up new fire safety plans.
	All supplies were saved! " $) text! notsolo if "No one was injured." +text then goodfx morale++ witsattREV
	+cr fullcamp
;

: isfireproof? statmod{ .fireproof? }
	// old way - .trait@ "Fireproof" $=
;

cyoa-choice: camp3-fire-pick
	cchar isfireproof? if
		($ cname .. " is completely fireproof.
		 " .. cHe .. " saves the supplies at a leisurely pace. No hurry!" $) text!
		 notsolo if ($ Theothers .. " escape" .. g2s .. ", no one is harmed." $) +crtext then
		 +cr fullcamp
	then;

	($ cname .. " essaie de rester calme et observateur." $) text!

	ctemper- if
		($ cHe .. " n'arrive pas du tout à garder son calme !" .. Thegroup .. " panics!" $) +crtext badfx morale2-all
	else
		temper+roll if
			($ cHe .. " reste très calme !" $) +crtext goodfx
		else
			($ cHe .. " panique, ce qui lui fait perdre un temps preieux" $) +crtext morale--all badfx
		then temperREV
	then

	wits+roll if
		($ cHe .. " planifie une sortie rapide. " .. gThey .. " s'echappent sans se blesser !" $) +crtext
	else
		($ cHe .. " va dans le mauvais sens. " .. Thegroup .. " se fait bruler !" $) +crtext ouch--all bitefx
	then witsREV

	choosehuman alive? if
		($ Thegroup .. " s'echappe ! " $) +crtext fullcamp
	then
;

cyoa-bridge: camp3-fire-choose
	solo if
		soloer choosechar camp3-fire-pick
	then;
		picon
		"Qui planifie et dirige l'evasion ?" text!
		' camp3-fire-pick cyoa-team-choice
;

cyoa-choice: camp3-fire-ditch
	($ Thegroup .. " s'en sort. Malheureusement, beaucoup de fournitures ont ete laissees derriere, toutes celles qui n'etaient pas dans la voiture." $) text! bandit-ammomed2 bandit-food2 badfx +cr fullcamp
;

cyoa: camp3-fire
	image-dog-on-fire
	"Fire!" cyoa-title!
	($ Thegroup .. " va a l'encontre de ses principes et campe dans un appartement en ville, parce qu'il n'y a pas de zombies aux alentours." $) text!
	($ "Quand " .. gthey .. " se reveillent, l'immeuble est en feu. " $) +crtext 1 600 tone
		most-paranoid if
			charchoice( " n'a pas froid aux yeux en matiere de securite incendie" )choice: camp3-fire-paranoid
		else
			choice( "Planifier l'evasion" )choice: camp3-fire-choose
		then
		choice( "Courez MAINTENANT" )choice: camp3-fire-ditch
;

// Grocery store trust exercise

cyoa-choice: camp3-grocery-pick
	($ Theothers .. " monte la garde a l'exterieur pour eviter les pillards pendant que " .. cname .. " fouille l'epicerie." $) text!
	cloyalty- if cchar to bully
		cchar .morale 3 < if
			($ cname .. " s'empare de la nourriture qui s'y trouve et s'en va en douce pour ne plus jamais revenir. Quelle fouine !" $) +crtext
			loyaltyREV decruit-bad
		else
			($ cname .. " dit qu'il n'y avait pas de nourriture la-dedans." $) +crtext cchar morale--all-except badfx
			most-paranoid if choosechar
				cloyalty- not if
				($ cname .. " soupconne qu'il s'agit d'un mensonge !" .. cHe .. " commence à garder un oeil sur " .. bname .. "." $) +crtext bully .loyaltyREV
				then
			then
		then +cr fullcamp
	then;
	($ cname .. " ramene de la nourriture !" $) +crtext
	morale++ cloyalty+ if 6 9 else 50 %chance if 3 6 else 2 9 then then trunk-food+ goodfx +cr fullcamp
;

cyoa-choice: camp3-grocery-solo
	someicon
	($ cname .. " s'empare de quelques boites de conserve qui se trouvaient sur les etageres, evitant ainsi un petit nombre de zombies. Un autre groupe de pillards arrive.
	Les pillards commencent a tirer ! Ils veulent vraiment ces boites de conserve !
	" .. cname .. " retourne au camp en courant pour se battre. Ils ont l'air d'etre parti, mais " .. cname .. " reste eveille toute la nuit." $) text! 3 4 rnd trunk-food+ badfx +cr tiredcamp
;

cyoa-choice: camp3-grocery-no
	($ Thegroup .. " choisit sagement de ne pas se battre pour quelques boites de conserve. Il y aura d'autres epiceries, avec moins d'humains a craindre." $) text! +cr fullcamp
;

cyoa-bridge: camp3-grocery-pick4
	someicon
	"Qui devrait aller piller l'epicerie ?" cyoa-text!
	' camp3-grocery-pick cyoa-team-choice
;

cyoa: camp3-grocery
	image-mob-at-store
	($ Thegroup .. " etablit un campement devant une epicerie. " .. gThey .. " remarquent qu'un autre groupe les suit et va probablement tenter de piller l'endroit." $) cyoa-text!
		solo if
			"Camping sur un Parking" cyoa-title!
			choice( "Essayer de piller l'epicerie" )choice: camp3-grocery-solo
		else
			"Exercice de Confiance a l'Epicerie" cyoa-title!
			($ "Envoyer une personne pour piller ?" $) +crtext
			// The other" .. g3s .. " will stand guard."
			team-count 3 > if
				' camp3-grocery-pick 2 #cyoa-team-choice
				choice( "Choisir quelqu'un d'autre" )choice: camp3-grocery-pick4
			else
				' camp3-grocery-pick 3 #cyoa-team-choice
			then
		then
		choice( "Laissez-les faire, evitez les ennuis" )choice: camp3-grocery-no
;

// Sharing camp with other survivor group

cyoa-choice: camp3-crowd-paranoid
	($ cname .. " ne fait confiance a personne dans le camp. " .. cHe .. " reste eveille toute la nuit, a l'affut de tout signe de danger. Rien ne se passe, et " .. che .. " est epuise le lendemain matin." $) cyoa-text! +cr chowtime sleep-except witsattREV
;

cyoa-choice: camp3-crowd-rob
	($ cname .. " n'a aucun mal a cambrioler le campement pacifique. " .. Thegroup .. " vole rapidement de la nourriture et s'enfuit a toute vitesse. " .. gThey .. " conduisent toute la nuit et " .. gare .. " fatigues le lendemain." $) cyoa-text!
	8 15 rnd trunk-food+
	aholeREV
	// notsolo if ($ Theothersare .. " ont ete entraines dans le mouvement." $) +crtext then
	rob-guilt +cr tiredcamp
;

cyoa-choice: camp3-crowd-coolit
	cpet? if
		($ cname .. " va au milieu du camp bonde et commence a " .. "PETER COMME UNE TORNADE." rbold $)
	else
		($ cname .. " monte sur un carton et dit a tout le camp de se " .. "CALMER." pbold $)
	then text!
	cool-it-7 if
		"Les autres groupes dans le camp prennent cela comme un signe qu'ils devraient SE DETENDRE et SE LACHER davantage du coup ils organisent un grand festin !" +crtext
		cooled-it goodfx sleep 15 trunk-food+ morale3+all
	else cool-it++
		($ "Tous les membres du camp se retournent presque immediatement contre " .. thegroup .. ". " .. gTheyare .. " ejectes, et etablissent leur campement dans un endroit beaucoup moins confortable." $) +crtext
		morale--all +cr fullcamp
	then witsattREV
;

cyoa-choice: camp3-crowd-camp
	($ Thegroup .. " prend un risque et va dormir dans le camp surpeuple." $) cyoa-text!
	60 %chance if "Rien d'inhabituel ne se produit." +crtext
	else
		50 %chance if
			"L'un des autres groupes apprend que vous voyagez ver le Canada. Ils vous donnent quelques fournitures, car ils disent que vous en aurez plus besoin qu'eux." +crtext ammosearch1 5 8 rnd trunk-food+ 2 6 rnd trunk-medical+ morale++all
		else
			50 %chance if
				someicon
				($ "Quelqu'un, et nous ne saurons jamais qui, a fait la farce du doigt dans l'eau a " .. cname .. "." $) +crtext badfx morale2-
			else
				"Au matin, les autres groupes sont partis et les fournitures ont disparues !" +crtext badfx bandit-ammomed2
			then
		then
	then +cr fullcamp
;

cyoa-choice: camp3-crowd-tired
	($ Thegroup .. "  ne peut se resoudre a faire confiance a autant d'etrangers. " .. gThey .. " decident qu'il est plus prudent de partir en voiture des qu'" .. gthey .. " peuvent." $) cyoa-text! +cr tiredcamp
;

cyoa: camp3-crowd
	image-camp-1
	"Camping bonde" cyoa-title!
	($ Thegroup .. " trouve un grand campement occupe par quelques voitures d'autres survivants. Camper avec des etrangers est risque, mais il n'y a pas d'autre endroit ou aller.
	None are heading to Canada." $) cyoa-text! basic-tone
		choice( "Camper avec eux" )choice: camp3-crowd-camp
		most-paranoid if
			charchoice( " ne leur fait pas confiance" )choice: camp3-crowd-paranoid
		then
		most-ahole if
			charchoice( " les vole et s'enfuie" )choice: camp3-crowd-rob
		then
		most-grating if
			charchoice( " dis à tout le campe de se 'CALMER'" fcoolit )choice: camp3-crowd-coolit
		then
		choice( "Reprendre la route sans dormir" )choice: camp3-crowd-tired
;

// Repair shop camp

cyoa-choice: camp3-tinker-go
	($ cname .. " passe la nuit a demonter et a remonter des objets. C'est tres bruyant.
	" .. Thegroup .. " sera tres fatigue demain !" $) text!
	mechanicREV mechanic++ nosleep goodfx
;

cyoa: camp3-tinker-pick
	solohuman if
		choosehuman camp3-tinker-go
	then;
	"Qui doit reparer les machines en panne ?" cyoa-text!
	' camp3-tinker-go cyoa-human-choice
;

cyoa: camp3-tinker
	// Have someone tinker. They can get a point of mechanical skill in exchange for group sleeplessness.
	"Magasin de Bric-a-Brac Plutot Utile" cyoa-title!
	($ Thegroup .. " campe dans un batiment en excellent etat. Il semble s'agir d'un vieil atelier de reparation, avec des machines cassees qui trainent un peu partout.
	Ce serait le moment ideal pour s'entrainer a reparer des trucs, mais cela ferait beaucoup de bruit." $) text! +cr fullcamp
		choice( "Dormir, c'est tout" )choice
		choice( "Bricoler toute la nuit" )choice: camp3-tinker-pick
;

// Precious antique cans

: camp3-cans-check args( @ )
	<- @ .cloyalty- if
		($ <- @ .colourname .. " doesn't care." $) +crtext
		<- @ .loyaltyREV
	then;
	<- @ .morale--

;

cyoa-choice: camp3-cans-bort
	char-gen to recruitee ricon recruitee .bortify
	"Le rituel secret est termine !" fbold text!
	"Bort a ete invoque..." rbold +crtext
	cooled-it goblin-laugh
	team-count 3 > if
		($ "Le groupe est au complet, mais Bort a ete invoque. Qui doit partir ?" $) +crtext
			' recruit-replace cyoa-team-choice
	then;
		recruited
;

cyoa-blank: camp3-cans-shoot
		($ Thegroup .. " s'entraine au tir en faisant exploser quelques boites de conserve." $) text!
		' .shooting++ foreach-team-char
		($ "Un homme vetu d'un pantalon rouge court sur le terrain avec une fourche.
		" .. "Mes boites de conserve ! Mes precieuses boites de conserve antiques ! Oh, regardez ce que vous leur avez fait !" rbold ..
		" Il agite la fourche en criant." $) +crtext goodfx badfx
		' camp3-cans-check foreach-team-char
		choice( "Ouch" )choice
		choice( "HAHA" )choice
		cool-it-7 if
			most-grating if
				charchoice( ": TE PRENDS PAS LA TETE, MEC !" rbold )choice: camp3-cans-bort
			then
		then
;

cyoa: camp3-cans
	image-camp-2
	"Big Field" cyoa-title!
	($ Thegroup .. " campe dans un grand champ." $) cyoa-text! +cr fullcamp
		choice( "Quitter le camp" )choice
		roadtrip-has-firearm? if
			"Terrain De Tir" cyoa-title!
			($ gThey .. " trouvent une rangee de boites de conserve posees sur une cloture en bois." $) +crtext
			choice( "Tirer sur les boites de conserve" )choice: camp3-cans-shoot
		then
;

// Setting up barricades before sleeping

cyoa-choice: camp3-trap-pick
	($ cname .. " barricade la fragile maison." $) text!
	mechanic+roll if
		($ cHe .. " renforce rapidement les endroits les plus fragiles. Ça semble maintenant tres securise !" $) +crtext goodfx mechanicREV cchar .mechanical@ 4 < if mechanic++ then +cr fullcamp
	then;
	wits+roll if
		($ cHe .. " improvise. Le resultat n'est pas tres esthetique, mais ça parvient a tenir les zombies a l'ecart." $) +crtext goodfx witsREV mechanicREV cchar .mechanical@ 3 < if mechanic++ then +cr fullcamp
	then;
		($ cHe .. " cloue quelques planches sur des portes, mais elles ne cessent de tomber." $) +crtext witsREV mechanicREV +cr fullcamp
		($ Thegroup .. " est attaque durant la nuit." $) +crtext ouch--all bitefx
;

cyoa-choice: camp3-trap-no
	($ "Ne faisant pas confiance en " .. ghis .. " capacite a barricader la maison, " .. cname .. " reste en faction toute la nuit. " $) text!
	cchar statmod{ .soundsleep? } if
		($ cHe .. " parvient a vaquer a ses occupations normalement le jour suivant." $)
	else
		($ cHe .. " est epuise le matin." $)
	then +text
	sleep-except +cr chowtime

;

cyoa-bridge: camp3-trap-watch
	someicon
	solo if
		soloer choosechar camp3-trap-no
	then;
	"Qui doit monter la garde pour la nuit ?" cyoa-text!
	' camp3-trap-no cyoa-team-choice
;

cyoa-bridge: camp3-trap-list
	someicon
	"Qui doit barricader la maison ?" cyoa-text!
	' camp3-trap-pick cyoa-team-choice
;

cyoa: camp3-trap
	"Barricader avant d'aller se coucher" cyoa-title!
	($ "A la fin de la journée, " .. Thegroup .. " se cache dans une vieille maison pleine de courants d'air. Des zombies rodent a l'exterieur et les portes de la maison tiennent a peine dans leurs gonds.
	Est-ce que quelqu'un devrait barricader la maison ?" $) cyoa-text!
		numhums 4 < if
			' camp3-trap-pick cyoa-human-choice
		else
			choice( "Choisir quelqu'un pour la securiser" )choice: camp3-trap-list
		then
		choice( "Veiller et monter la garde toute la nuit" )choice: camp3-trap-watch
;

// Moosing around

cyoa-choice: camp3-moose-heal
	($ cname .. " tente d'aider l'elan blesse." $) text!
	trunk-medical 1 < if
		($ cname .. " a oublie qu'il n'avait pas de materiel medical !  L'elan apprecie tout de meme le geste, du coup " .. chim .. " ne le malmene qu'a moitie pour s'etre approche." $) +crtext ouch-- bitefx
	then;
	medical+roll if
		($ cHe .. " le rafistole un peu. L'elan est trop trouble par ce geste de gentillesse pour l'attaquer." $) +crtext goodfx -1 trunk-medical+ medicalREV morale2+ loyaltyREV loyalty++
	then;
		($ cHe .. " ne sait pas trop comment s'occuper d'un elan et finit par le mettre en colere ! L'elan donne un violent coup de sabot a " .. cname .. "." $) +crtext morale-- ouch-- medicalREV bitefx
;

cyoa-choice: camp3-moose-shoot
	($ cname .. " essaie de tirer sur l'elan. Ce n'est pas tres fair-play !" $) text!
	roadtrip-has-firearm? not if
		($ Thegroup .. " n'a aucune arme qui fonctionne ! L'elan et " .. cname .. " s'esclaffent. Puis l'elan se met a massacrer tout le monde." $) +crtext ouch2-all bitefx
	then;
	cshooting+ shootingroll or if
		($ cHe .. " aneantit l'elan. " .. Thegroup .. " en tire de la viande filandreuse  qu'" .. gthey .. " peuvent avant de s'en aller." $) +crtext goodfx
		shootingREV 12 trunk-food+
	then;
		($ cHe .. " rate son coup ! L'elan n'est pas content et " .. chim .. " frappe violemment." $) +crtext morale-- ouch2- shootingREV bitefx
;

cyoa-choice: camp3-moose-wrestle
	($ cname .. " fait une prise de catch a l'elan ! Les bras et les sabots se croisent dans une epreuve de force et de volonte." $) text!
	cstrength+ strengthroll or if
		($ cHe .. " gagne ce concours inutile ! C'est un triomphe sur la nature. L'elan ronchonne sur sa defaite et s'eloigne en sautillant." $) +crtext goodfx
		cchar .strength@ 5 > if fitness++ fitnessREV strengthREV else strength++ strengthREV then morale++
	then;
		($ cHe .. " ne gagne pas ! Au mepris des regles de la lutte, l'elan l'ecrase avec ses sabots." $) +crtext morale-- ouch2- strengthREV bitefx
;

cyoa: camp3-moose-2
	image-camp-1
	"L'Elan du Matin" cyoa-title!
	($ "Le matin, il y a un elan devant le camp ! Il a l'air blesse et fixe " .. thegroup .. " du regard.
	Meme blesse, un elan est une creature tres puissante. Il vaut sans doute mieux ne pas lui chercher des noises." $) cyoa-text!
		choice( "Laissez l'elan tranquille" )choice
		medicalest charchoice( " soigne sa blessure." )choice: camp3-moose-heal
		shootest charchoice( " tire sur l'elan." )choice: camp3-moose-shoot
		strongest charchoice( " fait une prise de catch a l'elan." )choice: camp3-moose-wrestle
;

cyoa: camp3-moose
	image-camp-1
	"L'Elan du Matin" cyoa-title!
	($ Thegroup .. " campe pour la nuit, sur un troncon de route tranquille." $) cyoa-text! +cr fullcamp
		choice( "OK" )choice: camp3-moose-2
;

: camp3-precanada-saying
	0 stack
		" se sent confiant quant aux chances de succes !" shove
		" ne pense pas qu'il y ait TANT de zombies que ca." shove
		" est pret a eclater quelques zombs !" shove
		" est pret a partir !" shove
		" est gonfle a bloc pour demain !" shove
		" est impatient de decouvrir le Canada !" shove
		" est impatient !" shove
	shuffles pops nip
;

: camp3-precanada-check args( @ )
	<- @ .cmorale+ if
		($ <- @ .colourname .. camp3-precanada-saying $) +crtext
		<- @ .dexterity++
	then
;

cyoa: camp3-precanada
	"Derniere Halte Avant Le Canada" cyoa-title!
	($ Thegroup .. " se prepare a la journee brutale qui l'attend. Le Canada est proche, mais les previsions concernant les zombies sont mauvaises !" $) cyoa-text! +cr fullcamp
	fullmeal if
		($ "Il reste de la nourriture pour que " .. thegroup .. " prenne un second repas. " $) +crtext
		chowday
		' .morale2+ foreach-team-char
	else
		halfmeal if
			($ "Il reste de la nourriture pour que " .. thegroup .. " puisse manger un peu plus." $) +crtext
			chowday
			' .morale++ foreach-team-char
		then
	then
		' camp3-precanada-check foreach-team-char
		choice( "OK" )choice
;

} }



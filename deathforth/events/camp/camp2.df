
cyoa{ mission{

cyoa: camp2-fightdone
	($ "O pior do cerco zumbi acabou. " .. Thegroup .. "  limpa todos os zumbis restantes do lado de fora e volta para espatifar todos os retardatarios para que " .. gthey .. "  possa montar acampamento." $) cyoa-text! morale++all +cr road{ fullcamp }
;

} }

cyoa{ road{

// Minigolf, no choices

cyoa: camp2-minigolf
	image-golf-course
	"Mini Golfe!" cyoa-title!
	($ Thegroup .. "  monta acampamento no lugar mais seguro que conseguiu encontrar: um parque de minigolfe abandonado. " $) cyoa-text! basic-tone
	+cr fullcamp
	($ "De manha, " .. gthey .. "  encontrar" .. g1s .. "  uma bola de golfe e um taco enquanto procurava pela area. " .. Thegroup .. "  decide jogar uma partida rapida!" $) +crtext
	unfittest choosechar cfitness- if
		cpet? if
			($ cname .. "  corre por ai animado!" $) +crtext morale++
		else
			($ cname .. "  faz alguns exercicios necessarios!" $) +crtext fitness++
		then	
	then
	most-ahole if
		choosechar ($ cname .. "  se cansa do jogo e entao perde a bola de proposito." $) +crtext morale--all
	then;
	strongest-pass if
		cpet? if
		 	($ cname .. "  acidentalmente morde a bola." $) +crtext morale++ 1 WEAPON_GOLF trunk.weapon+
		then;
		strongest choosechar 50 %chance if
			($ cname .. "  acidentalmente destroi o taco." $) +crtext morale++
		else
			($ cname .. "  acidentalmente bate na bola tao longe que ela se perde." $) +crtext fitness++ 1 WEAPON_GOLF trunk.weapon+
		then
	then;
	morale++all 1 WEAPON_GOLF trunk.weapon+
;

// Driving range with different choices

// previously they find a less dangerous and less fun place to camp - fullcamp
cyoa-choice: camp2-fore-flee
	($ Thegroup .. "  decide fugir do campo de golfe. " .. gThey .. "  acabam nao encontrando outro bom lugar para dormir e dirigem durante a maior parte da noite. " .. gThey .. "  ficam muito cansados no dia seguinte." $) cyoa-text! +cr tiredcamp
;
cyoa-choice: camp2-fore-fore
	solo if
		soloer choosechar
		cpet? if
			($ cname .. " ' tenta jogar golfe, mas e dificil fazer isso como um " .. cdogcat .. ". " .. cHe .. " maybe could have done it with a caddy.
			" .. cname .. "  esta completamente sobrecarregado." $) cyoa-text! morale3- ouch-- sting-- bitefx 
		else
			($ cname .. " 'acerta varias bolas de golfe em cabecas de zumbis. E um bom exercicio, mas logo " .. che .. " is overwhelmed by them.
			" .. cHe .. "  escapa, mas passa a noite escondido em um armario. " .. cHe .. "  esta exausto pela manha." $) cyoa-text! fitness++ fitnessREV morale-- sting-- bitefx  
		then someone choosechar alive? if +cr tiredcamp then
	then;
	($ Thegroup .. "  pega varios tacos e alguns baldes de bolas de golfe. Eles comecam a dar tacadas no campo enquanto os zumbis se aproximam." $) cyoa-text! 
	fitness-total 7 > if
		"Eles enviam bola de golfe apos bola de golfe para a horda que se aproxima." +crtext
		strength-total 7 > if
			"A equipe acerta os zumbis do outro lado do campo. Eles nem chegam perto!" +crtext goodfx morale++all
		else
			"'Os zumbis sao atingidos por bolas de golfe, mas nao sao atingidos com forca suficiente para serem desacelerados muito. A equipe acaba lutando contra eles normalmente." +crtext someone .sting-- someone .sting-- bitefx
		then
		unfittest .fitness++ ' .fitnessREV foreach-team-char 
		' .strengthREV foreach-team-char +cr fullcamp
	else
		($ "'E um trabalho arduo, e a equipe acaba se cansando. " .. gThey .. "  sao dominados pelos zumbis." $) +crtext ouch--all ' .fitnessREV foreach-team-char +cr choosehuman alive? if fullcamp then bitefx
	then
	1 WEAPON_GOLF trunk.weapon+
;
cyoa-choice: camp2-fore-mech
	($ cname .. " ' pula em um coletor de bolas de golfe e comeca a atropelar zumbis. E surpreendentemente eficaz. Quando o coletor quebra, a maioria dos zumbis e eliminada." $) cyoa-text! morale++all mechanicREV mechanic++ +cr fullcamp
;
cyoa-bridge: camp2-fore-fight
	($ "'Os zumbis comecam a bater na porta da loja de golfe. Nao ha muito tempo para se preparar antes que a porta quebre!" $) cyoa-text!
		2.4 20 22 rnd 1 siegecast // Formerly 1.3 then 1.7 diff
		"events/siege/region/golfcamp.df" $load-region
		choice( "Tente Sobreviver" )choice: premission
;

cyoa: camp2-fore
	image-golf-course
	"Ultima Resistencia no Campo de Golfe" cyoa-title! 
	choosehuman
	($ Thegroup .. " acampa em uma loja de artigos esportivos perto de um campo de golfe. " .. cname-He .. " 	Olha pela janela e ve um grande grupo de zumbis se aproximando do campo de golfe!" $) cyoa-text! basic-tone 3 WEAPON_GOLF trunk.weapon+
	mechanicest-pass if
		mechanicest charchoice( " liga um coletor de bolas de golfe" )choice: camp2-fore-mech
	then
		choice( "Atire bolas de golfe nos zumbis" )choice: camp2-fore-fore
		choice( "Prepare se para o Cerco!" )choice: camp2-fore-fight
		choice( "Dirija sem dormir" )choice: camp2-fore-flee
;

// No sleep campground

cyoa-choice: camp2-smell-morale
	someicon
	($ Thegroup .. "  suporta o " .. "ZONA DE CHEIRO" rainbold .. "  a noite toda, ate desmaiar. " .. gTheyare .. "  nao fica feliz pela manha, mas " .. gtheywere .. "  pelo menos consegue dormir." $) cyoa-text! morale--all +cr fullcamp
;

cyoa-choice: camp2-smell-no
	image-camp-1
	($ Thegroup .. "  sai do " .. "ZONA DE CHEIRO" rainbold .. ". " .. gThey .. "  pegue" .. g1s .. "  sem dormir, mas se sente" .. g1s .. " como se sair fosse a escolha certa." $) cyoa-text! +cr tiredcamp
;

cyoa: camp2-smell
	image-camp-1
	"Acampamento Fedido" cyoa-title! 
	($ "O acampamento que " .. thegroup .. "  monta cheira muito mal! A fonte do cheiro e um completo misterio." $) text!
		choice( "Aguente o cheiro" )choice: camp2-smell-morale
		choice( "Va embora sem dormir" )choice: camp2-smell-no
;

// Cut right before release due there already being a lot of tired things, plus this one was unavoidable
cyoa: camp2-creepy
	image-camp-1
	"Acampamento Assustador" cyoa-title! 
	($ "O acampamento que " .. thegroup .. " ' tropeca e muito assustador. E dificil dizer por que e tao perturbador." .. gThey .. "  pegue" .. g1s .. "  sem dormir, e se sinta" .. g1s .. "  fora mesmo depois de sair." $) cyoa-text! +cr tiredcamp
;

// Night watch for bandits, or rather thieves

cyoa-choice: camp2-bwatch-drive
	($ Thegroup .. "  se recusa a acampar em uma area perigosa e passa uma noite tensa e sem dormir dirigindo no escuro." $) cyoa-text! morale--all +cr tiredcamp
	50 %chance if "O carro sai da estrada e bate em uma arvore." +crtext  -2 car-chassis+ then
;
cyoa-choice: camp2-bwatch-pick
	($ cname .. "  fica de guarda durante a noite." $) cyoa-text!
	cloyalty- if
		($ cHe .. "  ignora a tarefa e adormece quase instantaneamente. Na manha seguinte, varios suprimentos estao faltando." $) +crtext sleep bandit-supplies2
	else
		($ cHe .. "  consegue ficar acordado, mas se sente muito mal pela manha." $) +crtext
		sleep-except morale--
	then loyaltyREV
	+cr chowtime
;

cyoa-bridge: camp2-bwatch-pick4
	someicon
	"Portugues" cyoa-text!
	' camp2-bwatch-pick cyoa-team-choice
;

cyoa: camp2-bwatch
	image-bandit-watch
	"Vigilancia de Bandidos" cyoa-title! 
	($ "'Esta se aproximando do tempo de acampar, mas houve muitas sinais de bandidos hoje. Alguem precisa ficar acordado em caso de um ataque.	Quem deve ficar de guarda?" $) cyoa-text! basic-tone
		team-count 3 > if
			' camp2-bwatch-pick 2 #cyoa-team-choice
			choice( "Escolha outra pessoa" )choice: camp2-bwatch-pick4
		else
			' camp2-bwatch-pick 3 #cyoa-team-choice
		then
		choice( "Continue dirigindo" )choice: camp2-bwatch-drive
;

// Night watch for zombies

cyoa-choice: camp2-zwatch-awake
	($ cname .. " 'escolhe dormir, apostando que os zumbis nao o encontrarao " .. chim .. "." $) cyoa-text!
	50 %chance if
		($ cHe .. " acorda no meio de ser engolido por um pequeno grupo de zumbis. Sem ninguem para ajuda lo " .. chim .. "', e dificil se soltar." $) +crtext despairbite
	else
		($ cHe .. " dorme durante a noite sem interrupcao." $) +crtext
	then
	someone choosechar alive? if +cr fullcamp then
;
cyoa-choice: camp2-zwatch-shift
	someone choosechar
	($ Thegroup .. " toma turnos dormindo durante a noite." $) text!
	team-count 4 < if 
		($ cname .. " esta muito cansado na manha, e " .. theothers .. " 'nao dormiu bem tambem." $) +crtext sleep-except 5050nosleep morale--all +cr chowtime
	else
		($ "'Com um grupo completo, ha suficientes pessoas para ainda obter muita dormida!" $) +crtext +cr fullcamp
	then
;
cyoa-choice: camp2-zwatch-pick
	($ cname .. " mantem de vigilia contra zumbis errantes." $) cyoa-text!
	cloyalty- if
		cchar statmod{ .soundsleep? } if
			($ cHe .. " deixa de se concentrar e se desvia no meio do cumprimento da guarda. " .. gHe .. "  e emboscado por zumbis, mas consegue lutar contra eles." $) +crtext
			cchar ' .sting-- foreach-team-char-except
		else
			($ cHe .. "  adormece quase que instantaneamente " .. gHe .. "  acorda no meio de ser comido." $) +crtext ouch--all
		then sleep
	else
		($ cHe .. "  consegue ficar acordado, mas se sente grogue pela manha." $)
		+crtext sleep-except morale--
	then loyaltyREV
	someone choosechar alive? if +cr chowtime then
;

cyoa-bridge: camp2-zwatch-pick4
	someicon
	"Portugues" cyoa-text!
	' camp2-zwatch-pick cyoa-team-choice
;

cyoa: camp2-zwatch
	image-zombie-watch
	"Vigia Zumbi" cyoa-title! 
	($ "'Esta ficando tarde e nao ha um bom lugar para acampar. Zumbis solitarios mancam ao redor a distancia, e um pode se aproximar furtivamente de " .. thegroup .. " enquanto " .. gthey .. "  dormir" .. g1s .. ". Quem deve ficar acordado de guarda?" $) cyoa-text! basic-tone
		team-count 3 > if
			' camp2-zwatch-pick 2 #cyoa-team-choice
			choice( "Escolha outra pessoa" )choice: camp2-zwatch-pick4
		else
			' camp2-zwatch-pick 3 #cyoa-team-choice
		then		
		solo if
			choice( "Durma mesmo assim" )choice: camp2-zwatch-awake
		else
			choice( "Fazer turnos" )choice: camp2-zwatch-shift
		then
;

// Berry picking

cyoa-choice: camp2-berry-go
	($ Thegroup .. "  colhe frutas vermelhas durante toda a noite e as coloca em quaisquer recipientes que estejam a mao." $) cyoa-text! numhums 3 * 2 + trunk-food+
	+cr tiredcamp
;

cyoa-choice: camp2-berry-no
	($ Thegroup .. " ' realmente nao quer colher frutas vermelhas por 8 horas." $) cyoa-text!
	+cr fullcamp
;

cyoa: camp2-berry
	image-camp-2
	"Colhendo Frutas Vermelhas em Jogos de Zumbi" cyoa-title!
	($ Thegroup .. " ' monta acampamento para a noite depois de ter certeza de que nao ha perigo aqui." $) cyoa-text! 
		($ gThey .. "  discover" .. g1s .. " ' arbustos de frutas silvestres comestiveis ao redor do acampamento!				As frutas silvestres nao seriam uma grande quantidade de comida para o trabalho e a noite sem dormir, mas cada pedacinho pode ajudar." $) +crtext
		choice( "Pick berries all night" )choice: camp2-berry-go
		choice( "Go to sleep" )choice: camp2-berry-no
;


// Scary Stories

: camp2-scary-noun 
	0 stack
		"GOBLINS!!!!" shove
		"ESQUELETOS!!" shove
		"MAUS CHEIROS!" shove
		"PEIDOS ENORMES!" shove
		"DIARRHEA!!!" shove
		"PEQUENOS FANTASMAS!" shove
		"A TURTLE!!!" shove
		"GATOS MALVADOS!!" shove
		"CONSERTO DE CARRO!" shove
		"CACHORROS BARULHENTOS!" shove
		"DUENDES MALVADOS!" shove
		"ESQUELETOS DANCANTES!" shove
		"ALERGIA A AMENDOIM!" shove
		"LAUGHING GOBLINS!!!" shove
		"DUENDES!!" shove
		"DUENDES AMEACADORES!" shove
	shuffles pops nip
;

cyoa-choice: camp2-scary-go
	($ Thegroup .. "  decide contar historias assustadoras!" $) text!
	solo if
		($ "Mas acontece que " .. soloname .. "  estava sozinho, " .. "TELLING THE SCARY STORIES TO GHOSTS!
		WHOOOOOAAAAAAA!" fbold $) +crtext morale3+all someicon
	else
		choosehuman cicon
		cpet? if
			($ cname .. "  principalmente " .. cbarkmeow .. "s muito mas de uma " .. "MANEIRA ASSUSTADORA!" fbold $) +crtext
		else
			($ cname .. "  faz " .. chis .. "  historia assustadora sobre " .. camp2-scary-noun fbold $) +crtext
		then morale2+all	
	then
	sfx{ creepy 30 fx .delay! }
	($ Thegroup .. " ' se diverte, mas nao dorme o suficiente e fica cansado no dia seguinte." $) +crtext nosleep
;

cyoa: camp2-scary
	image-camp-1
	"Chega de Creepypastas" cyoa-title!
	($ Thegroup .. "  esta sentado ao redor de uma fogueira, descansando antes de um dia brutal amanha." $) text!
		+cr fullcamp
		choice( "Contar historias assustadoras" )choice: camp2-scary-go
		choice( "Apenas va dormir" )choice
;

// Bad Weather camping

: camp2-badweather-check args( @ )
	<- @ oblivious? if
		($ <- @ .colourname .. "  achei que o clima estava bom!" $) +crtext 
		<- @ choosechar morale++ witsattREV
	then;
	<- @ .cattitude+ if 
		($ <- @ .colourname .. " ' nao se importou muito com isso." $) +crtext 
		<- @ .attitudeREV
	then;
	<- @ .morale--
;

cyoa: camp2-badweather
	image-camp-2
	"Camping Miseravel" cyoa-title!
	($ Thegroup .. " ' nao conseguiu encontrar nenhum abrigo seguro, mas conseguiu improvisar um acampamento. O tempo esta terrivel a noite toda!" $) text! ' camp2-badweather-check foreach-team-char +cr fullcamp
	0.4 1 frnd weather! -1 1 frnd wind_force!
;


// Woodstove camping

: camp2-wood-fire args( @ )
	<- @ .cattitude- if <- @ .attitudeREV then;
	<- @ .morale++
;

cyoa-choice: camp2-wood-chopped
	($ cname .. "  corta um monte de lenha e acende o fogao a lenha antes de desmaiar de exaustao.	" .. cHe .. "  ficara muito cansado amanha!" $) text!
	strengthREV strength++ cchar tired goodfx
	"O fogo esta otimo!" +crtext
	' camp2-wood-fire foreach-team-char
;

cyoa: camp2-wood-chop
	solohuman if
		choosehuman camp2-wood-chopped
	then;
	"'Quem deveria ir cortar a lenha? E um trabalho muito duro!" cyoa-text!
	' camp2-wood-chopped cyoa-human-choice
;

cyoa: camp2-wood
	image-cabin-in-woods
	"Cabana com fogao a lenha" cyoa-title!
	($ Thegroup .. "  encontra uma cabana na floresta, sem zumbis a vista. Tem um fogao a lenha, mas sem lenha!" $) text! +cr fullcamp
		choice( "Saia e corte lenha" )choice: camp2-wood-chop
		choice( "Apenas va dormir" )choice
;

// Creepy inn woman

: camp2-airbnb-eat args( @ )
	<- @ paranoid? if
		($ <- @ .colourname .. "  se recusa a come lo!" $) +crtext <- @ .morale-- 
		<- @ .witsREV <- @ .attitudeREV
	then;
	<- @ .morale2+
;

: camp2-airbnb-breakfast
	($ "O estalajadeiro serve um cafe da manha incrivel!" $) +crtext
	' camp2-airbnb-eat foreach-team-char goodfx	sleep
;

cyoa-choice: camp2-airbnb-pay
	($ Thegroup .. " ' paga para passar a noite na pousada. E confortavel." .. "No one wants zombies creepin while they're sleepin hhhHHHHHHeheh." rbold $) text! -10 trunk-food+
	camp2-airbnb-breakfast
;

cyoa-choice: camp2-airbnb-charm
	($ cname .. "  consegue um negocio melhor para a noite." .. "You're a sly one hhhhhhhhhhHHHHHHeheh." rbold $) text! -5 trunk-food+
	camp2-airbnb-breakfast
;

: camp2-airbnb-pres
		($ "...hhhhhhhhhhHHHHHHeheh you must be a big shot.
		I'll show you the presidential suite." rbold $)
		+crtext
;

cyoa-choice: camp2-airbnb-coolit
	cpet? if
		($ cname .. "  faz um " .. "hhhHHHHHeh" rbold .. "  som." $)
	else
		($ cname .. "  diz a mulher para " .. "DIZER NAO BORRIFAR." pbold $)
	then text!
	cool-it-7 if
		camp2-airbnb-pres
		"'Ela mostra a Suite Presidencial! Tem um spa!" +crtext
		morale3+all 
		"Ela tambem mostra a Academia Executiva!" +crtext
		' .fitness++ foreach-team-char ' .strength++ foreach-team-char
		camp2-airbnb-breakfast
		cooled-it goodfx
	else cool-it++
		camp2-airbnb-pres 
		($ "Ela chuta " .. thegroup .. "  para fora " .. gThey .. "  comer" .. g1s .. "  na estrada, mas nunca encontrar" .. g1s .. "  um lugar seguro para dormir." $) +crtext badfx +cr tiredcamp
	then witsattREV 
;

cyoa-choice: camp2-airbnb-nosleep
	($ Thegroup .. "  sai de la bem rapido " .. gThey .. "  comer" .. g1s .. "  na estrada, mas nunca encontrar" .. g1s .. "  um lugar seguro para dormir." $) text! +cr tiredcamp
;

cyoa: camp2-airbnb
	image-old-house
	"Cama e Cafe da Manha" cyoa-title!
	($ Thegroup .. "  encontra uma pousada aberta para negocios! A dona torce as maos: 	" .. "Give me 10 food, I'll watch over you while you sleep hhhHHHHHeh." rbold .. "	Ela e super assustadora." $) text!
		trunk-food 9 > if	
			choice( "Pague os 10 alimentos" )choice: camp2-airbnb-pay
		then
		most-grating if	
			charchoice( "': DIGA NAO BORRIFFE" fcoolit )choice: camp2-airbnb-coolit
		then
		most-charming trunk-food 5 > and if
			charchoice( "  pechincha para 5 alimentos" )choice: camp2-airbnb-charm
		then
		choice( "Va embora sem dormir" )choice: camp2-airbnb-nosleep
;

// Zombie singalong

: camp2-sing-team args( @ )
	<- @ .attitudeREV
	<- @ .cattitude+ if
		<- @ .morale++
	then
	<- @ .cattitude- if
		<- @ .morale--
	then	
;

cyoa-choice: camp2-sing-sing someicon
	($ Thegroup .. "  tenta manter o astral alto cantando. Se o canto era bom ou nao depende principalmente da sua atitude." $) cyoa-text! 3 700 tone 2 500 tone
		' camp2-sing-team foreach-team-char
;

cyoa-choice: camp2-sing-plan
	cwits- if
		($ "Acontece que " .. thegroup .. " ' realmente nao e tao bom em planejar as coisas." $) cyoa-text! morale--all ' .witsREV foreach-team-char badfx
	then;	
	cwits+ witsroll or if
		($ cname .. " ' planeja o dia seguinte. E um bom uso do tempo e ajuda " .. ghim .. "  foco no panorama geral." $) cyoa-text!
			morale++ goodfx
	else
		($ cname .. " ' faz planos elaborados para o dia seguinte. So de manha que " .. ghe .. "  percebe que os planos eram completamente ridiculos." $) cyoa-text!
			morale--all badfx
	then witsREV
;

cyoa: camp2-sing
	"Death Road Sing a Long" cyoa-title!
	($ Thegroup .. "  esta sentado ao redor de uma fogueira. Encontrar este acampamento foi dificil, entao " .. gtheyare .. "  ficando de mau humor." $) cyoa-text! +cr fullcamp
		choice( "Cante uma musica" )choice: camp2-sing-sing
		smartest charchoice( ": Planeje o amanha" )choice: camp2-sing-plan
;
		
} }



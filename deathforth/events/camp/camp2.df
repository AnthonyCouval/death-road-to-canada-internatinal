
cyoa{ mission{

cyoa: camp2-fightdone
	($ "Il peggio dellassedio degli zombi e finito " .. Thegroup .. "  spazza via qualsiasi zombie rimanente allesterno, quindi torna dentro per fare a pezzi qualsiasi ritardatario quindi " .. gthey .. "  puo allestire laccampamento." $) cyoa-text! morale++all +cr road{ fullcamp }
;

} }

cyoa{ road{

// Minigolf, no choices

cyoa: camp2-minigolf
	image-golf-course
	"Mini Golf!" cyoa-title!
	($ Thegroup .. "  si accampa nel posto piu sicuro che riesce a trovare nei dintorni: un parco di mini golf abbandonato. " $) cyoa-text! basic-tone
	+cr fullcamp
	($ "Al mattino, " .. gthey .. "  trova" .. g1s .. "  una pallina da golf e un putter mentre perlustra la zona. " .. Thegroup .. "  decide di fare una partita veloce!" $) +crtext
	unfittest choosechar cfitness- if
		cpet? if
			($ cname .. "  corre in giro eccitato!" $) +crtext morale++
		else
			($ cname .. "  fa un po di sano esercizio!" $) +crtext fitness++
		then	
	then
	most-ahole if
		choosechar ($ cname .. "  si stanca del gioco e poi perde la palla di proposito." $) +crtext morale--all
	then;
	strongest-pass if
		cpet? if
		 	($ cname .. "  morde accidentalmente la palla." $) +crtext morale++ 1 WEAPON_GOLF trunk.weapon+
		then;
		strongest choosechar 50 %chance if
			($ cname .. "  distrugge accidentalmente il putter." $) +crtext morale++
		else
			($ cname .. "  colpisce accidentalmente la palla cosi lontano che si perde." $) +crtext fitness++ 1 WEAPON_GOLF trunk.weapon+
		then
	then;
	morale++all 1 WEAPON_GOLF trunk.weapon+
;

// Driving range with different choices

// previously they find a less dangerous and less fun place to camp - fullcamp
cyoa-choice: camp2-fore-flee
	($ Thegroup .. "  decide di fuggire dal campo da golf " .. gThey .. "  non riesco a trovare altro buon posto per dormire e guido per la maggior parte della notte " .. gThey .. "  sono estremamente stanchi il giorno successivo." $) cyoa-text! +cr tiredcamp
;
cyoa-choice: camp2-fore-fore
	solo if
		soloer choosechar
		cpet? if
			($ cname .. " ' tenta di giocare a golf, ma e difficile fare questo da " .. cdogcat .. ". " .. cHe .. " maybe could have done it with a caddy.
			" .. cname .. "  e completamente sovraccarico." $) cyoa-text! morale3- ouch-- sting-- bitefx 
		else
			($ cname .. " ' colpisce una manciata di palline da golf contro teste di zombie. E buon esercizio, ma presto " .. che .. " is overwhelmed by them.
			" .. cHe .. "  fugge, ma trascorre la notte nascosto in un armadio " .. cHe .. "  e stanchissimo al mattino." $) cyoa-text! fitness++ fitnessREV morale-- sting-- bitefx  
		then someone choosechar alive? if +cr tiredcamp then
	then;
	($ Thegroup .. "  prende una manciata di club e ottiene delle palline da golf. Iniziano a sparare tiri sui campi da tennisto mentre i zombie si avvicinano." $) cyoa-text! 
	fitness-total 7 > if
		"Lanciano palla dopo palla contro lordea di zombie." +crtext
		strength-total 7 > if
			"I team batte i zombie con palle da golf da lontano. Non arrivano mai vicini!" +crtext goodfx morale++all
		else
			"'Gli zombie vengono colpiti da palline da golf, ma non sono toccati abbastanza duramente per rallentare molto. Il team deve combattere loro normalmente." +crtext someone .sting-- someone .sting-- bitefx
		then
		unfittest .fitness++ ' .fitnessREV foreach-team-char 
		' .strengthREV foreach-team-char +cr fullcamp
	else
		($ "'E duro lavoro e il team si stanca principalmente a se stesso. " .. gThey .. "  sono sopraffatti dagli zombie." $) +crtext ouch--all ' .fitnessREV foreach-team-char +cr choosehuman alive? if fullcamp then bitefx
	then
	1 WEAPON_GOLF trunk.weapon+
;
cyoa-choice: camp2-fore-mech
	($ cname .. " ' salta in un raccoglitore di palline da golf e inizia a investire gli zombie. E sorprendentemente efficace. Quando il raccoglitore si rompe, la maggior parte degli zombie e stata eliminata." $) cyoa-text! morale++all mechanicREV mechanic++ +cr fullcamp
;
cyoa-bridge: camp2-fore-fight
	($ "'Gli zombie iniziano a colpire la porta del pro shop. Non c'e molto tempo per prepararsi prima che la porta si rompa!" $) cyoa-text!
		2.4 20 22 rnd 1 siegecast // Formerly 1.3 then 1.7 diff
		"events/siege/region/golfcamp.df" $load-region
		choice( "Cerca di sopravvivere" )choice: premission
;

cyoa: camp2-fore
	image-golf-course
	"Ultima resistenza al campo pratica" cyoa-title! 
	choosehuman
	($ Thegroup .. "  si accampa in un pro shop vicino a un campo da golf. " .. cname-He .. "  guarda fuori dalla finestra e vede un grande gruppo di zombi avvicinarsi dal campo pratica!	" $) cyoa-text! basic-tone 3 WEAPON_GOLF trunk.weapon+
	mechanicest-pass if
		mechanicest charchoice( "  collega i fili di un raccoglitore di palline da golf" )choice: camp2-fore-mech
	then
		choice( "Spara palline da golf agli zombie" )choice: camp2-fore-fore
		choice( "Preparati allassedio!" )choice: camp2-fore-fight
		choice( "Guidare via senza dormire" )choice: camp2-fore-flee
;

// No sleep campground

cyoa-choice: camp2-smell-morale
	someicon
	($ Thegroup .. "  sopporta il " .. "ZONA PUZZA" rainbold .. "  tutta la notte, fino allo svenimento. " .. gTheyare .. "  non sono felice al mattino, ma " .. gtheywere .. "  almeno riesco a dormire." $) cyoa-text! morale--all +cr fullcamp
;

cyoa-choice: camp2-smell-no
	image-camp-1
	($ Thegroup .. "  esce dal " .. "ZONA PUZZA" rainbold .. ". " .. gThey .. "  ottenere" .. g1s .. "  non dormo, ma mi sento" .. g1s .. "  come se lasciare fosse stata la scelta giusta." $) cyoa-text! +cr tiredcamp
;

cyoa: camp2-smell
	image-camp-1
	"Campeggio puzzolente" cyoa-title! 
	($ "Il campeggio che " .. thegroup .. "  si installa puzza terribilmente! La fonte dellodore e un completo mistero." $) text!
		choice( "Sopportare lodore" )choice: camp2-smell-morale
		choice( "Partire senza dormire" )choice: camp2-smell-no
;

// Cut right before release due there already being a lot of tired things, plus this one was unavoidable
cyoa: camp2-creepy
	image-camp-1
	"Campeggio inquietante" cyoa-title! 
	($ "Il campeggio che " .. thegroup .. " ' si imbatte e molto inquietante. E difficile capire perche sia cosi inquietante." .. gThey .. "  ottenere" .. g1s .. "  non dormo e mi sento" .. g1s .. " spento anche dopo aver lasciato." $) cyoa-text! +cr tiredcamp
;

// Night watch for bandits, or rather thieves

cyoa-choice: camp2-bwatch-drive
	($ Thegroup .. " rifiuta di accamparsi in una zona pericolosa e trascorre una notte tesa e insonne alla guida nel buio." $) cyoa-text! morale--all +cr tiredcamp
	50 %chance if "La macchina sbanda fuori strada e si schianta contro un albero." +crtext  -2 car-chassis+ then
;
cyoa-choice: camp2-bwatch-pick
	($ cname .. " fa la guardia per la notte." $) cyoa-text!
	cloyalty- if
		($ cHe .. " ignora il compito e si addormenta quasi allistante. La mattina dopo, mancano un sacco di provviste." $) +crtext sleep bandit-supplies2
	else
		($ cHe .. " riesce a rimanere sveglio, ma si sente piuttosto male la mattina." $) +crtext
		sleep-except morale--
	then loyaltyREV
	+cr chowtime
;

cyoa-bridge: camp2-bwatch-pick4
	someicon
	"Chi dovrebbe restare sveglio di guardia?" cyoa-text!
	' camp2-bwatch-pick cyoa-team-choice
;

cyoa: camp2-bwatch
	image-bandit-watch
	"Guardia dei banditi" cyoa-title! 
	($ "'E quasi ora di accamparsi, ma ci sono stati molti segnali di banditi oggi. Qualcuno deve restare sveglio in caso di attacco. Chi dovrebbe restare sveglio di guardia?" $) cyoa-text! basic-tone
		team-count 3 > if
			' camp2-bwatch-pick 2 #cyoa-team-choice
			choice( "Scegli qualcun altro" )choice: camp2-bwatch-pick4
		else
			' camp2-bwatch-pick 3 #cyoa-team-choice
		then
		choice( "Continua a guidare" )choice: camp2-bwatch-drive
;

// Night watch for zombies

cyoa-choice: camp2-zwatch-awake
	($ cname .. " 'decide di andare a dormire, scommettendo che gli zombi non troveranno " .. chim .. "." $) cyoa-text!
	50 %chance if
		($ cHe .. " si sveglia nel bel mezzo di essere mangiato da un piccolo gruppo di zombi. Senza nessuno che lo aiuti " .. chim .. "', e difficile liberarsi." $) +crtext despairbite
	else
		($ cHe .. " dorme tutta la notte senza interruzioni." $) +crtext
	then
	someone choosechar alive? if +cr fullcamp then
;
cyoa-choice: camp2-zwatch-shift
	someone choosechar
	($ Thegroup .. " si alternano nel dormire durante la notte." $) text!
	team-count 4 < if 
		($ cname .. " e molto stanco la mattina e " .. theothers .. " ' non ha dormito granche bene neanche." $) +crtext sleep-except 5050nosleep morale--all +cr chowtime
	else
		($ "'Con un gruppo al completo, ci sono abbastanza persone per riuscire comunque a dormire a sufficienza!" $) +crtext +cr fullcamp
	then
;
cyoa-choice: camp2-zwatch-pick
	($ cname .. "  fa la guardia agli zombie che gironzolano." $) cyoa-text!
	cloyalty- if
		cchar statmod{ .soundsleep? } if
			($ cHe .. "  smette di prestare attenzione e se ne va in giro nel bel mezzo della guardia " .. gHe .. "  viene assalito dagli zombie, ma riesce a combatterli." $) +crtext
			cchar ' .sting-- foreach-team-char-except
		else
			($ cHe .. "  si addormenta quasi allistante " .. gHe .. "  si sveglia nel bel mezzo del pasto." $) +crtext ouch--all
		then sleep
	else
		($ cHe .. "  riesce a restare sveglio, ma la mattina si sente intontito." $)
		+crtext sleep-except morale--
	then loyaltyREV
	someone choosechar alive? if +cr chowtime then
;

cyoa-bridge: camp2-zwatch-pick4
	someicon
	"Chi dovrebbe restare sveglio di guardia?" cyoa-text!
	' camp2-zwatch-pick cyoa-team-choice
;

cyoa: camp2-zwatch
	image-zombie-watch
	"Guardia zombie" cyoa-title! 
	($ "'Si fa tardi e non c'e un gran posto per accamparsi. Zombie solitari zoppicano in lontananza e qualcuno potrebbe avvicinarsi di soppiatto a " .. thegroup .. "  mentre " .. gthey .. "  dormire" .. g1s .. ". Chi dovrebbe restare sveglio di guardia?" $) cyoa-text! basic-tone
		team-count 3 > if
			' camp2-zwatch-pick 2 #cyoa-team-choice
			choice( "Scegli qualcun altro" )choice: camp2-zwatch-pick4
		else
			' camp2-zwatch-pick 3 #cyoa-team-choice
		then		
		solo if
			choice( "Dormire comunque" )choice: camp2-zwatch-awake
		else
			choice( "Fare i turni" )choice: camp2-zwatch-shift
		then
;

// Berry picking

cyoa-choice: camp2-berry-go
	($ Thegroup .. " raccoglie bacche tutta la notte e le mette in qualsiasi contenitore abbia a portata di mano." $) cyoa-text! numhums 3 * 2 + trunk-food+
	+cr tiredcamp
;

cyoa-choice: camp2-berry-no
	($ Thegroup .. " 'non vuole proprio raccogliere bacche per 8 ore." $) cyoa-text!
	+cr fullcamp
;

cyoa: camp2-berry
	image-camp-2
	"Raccolta di bacche nei giochi di zombi" cyoa-title!
	($ Thegroup .. " 'si accampa per la notte dopo essersi assicurato che non ci siano pericoli." $) cyoa-text! 
		($ gThey .. " scopri" .. g1s .. " 'cespugli di bacche commestibili intorno al campeggio! Le bacche non sarebbero una grande quantita di cibo per il lavoro e la notte insonne, ma ogni piccolo aiuto e ben accetto." $) +crtext
		choice( "Raccogli bacche tutta la notte" )choice: camp2-berry-go
		choice( "Vai a dormire" )choice: camp2-berry-no
;


// Scary Stories

: camp2-scary-noun 
	0 stack
		"GOBLINS!!!!" shove
		"SCHELETRI!!" shove
		"CATTIVI ODORI!" shove
		"ENORMI SCORREGGE!" shove
		"DIARRHEA!!!" shove
		"PICCOLI FANTASMI!" shove
		"A TURTLE!!!" shove
		"GATTI CATTIVI!!" shove
		"RIPARAZIONE DELLAUTO!" shove
		"CANI GAUGGIANTI!" shove
		"GOBLIN MALVAGI!" shove
		"SCHELETRI BALLERINI!" shove
		"ALLERGIE ALLE ARACHIDI!" shove
		"LAUGHING GOBLINS!!!" shove
		"GOBLIN!!" shove
		"GOBLIN MINACCIOSI!" shove
	shuffles pops nip
;

cyoa-choice: camp2-scary-go
	($ Thegroup .. "  decide di raccontare storie paurose!" $) text!
	solo if
		($ "Ma scopre che " .. soloname .. "  era solo, " .. "TELLING THE SCARY STORIES TO GHOSTS!
		WHOOOOOAAAAAAA!" fbold $) +crtext morale3+all someicon
	else
		choosehuman cicon
		cpet? if
			($ cname .. "  per lo piu " .. cbarkmeow .. " e un sacco ma in modo " .. "SPAVENTOSO!" fbold $) +crtext
		else
			($ cname .. "  rende " .. chis .. "  storia paurosa su " .. camp2-scary-noun fbold $) +crtext
		then morale2+all	
	then
	sfx{ creepy 30 fx .delay! }
	($ Thegroup .. " ' si diverte, ma non dorme abbastanza e il giorno dopo e stanco." $) +crtext nosleep
;

cyoa: camp2-scary
	image-camp-1
	"No More Creepypastas" cyoa-title!
	($ Thegroup .. "  e seduto attorno a un fuoco da campo, riposando prima di una brutale giornata domani." $) text!
		+cr fullcamp
		choice( "Racconta storie spaventose" )choice: camp2-scary-go
		choice( "Vai a dormire" )choice
;

// Bad Weather camping

: camp2-badweather-check args( @ )
	<- @ oblivious? if
		($ <- @ .colourname .. "  pensava che il tempo fosse bello!" $) +crtext 
		<- @ choosechar morale++ witsattREV
	then;
	<- @ .cattitude+ if 
		($ <- @ .colourname .. " ' non ci badava molto." $) +crtext 
		<- @ .attitudeREV
	then;
	<- @ .morale--
;

cyoa: camp2-badweather
	image-camp-2
	"Campeggio miserabile" cyoa-title!
	($ Thegroup .. " ' non riusciva a trovare rifugi sicuri, ma e riuscito a mettere insieme un campeggio. Il tempo e terribile tutta la notte!" $) text! ' camp2-badweather-check foreach-team-char +cr fullcamp
	0.4 1 frnd weather! -1 1 frnd wind_force!
;


// Woodstove camping

: camp2-wood-fire args( @ )
	<- @ .cattitude- if <- @ .attitudeREV then;
	<- @ .morale++
;

cyoa-choice: camp2-wood-chopped
	($ cname .. "  taglia un mucchio di legna e accende la stufa a legna prima di svenire per sfinimento." .. cHe .. "  sara molto stanco domani!" $) text!
	strengthREV strength++ cchar tired goodfx
	"Il fuoco e fantastico!" +crtext
	' camp2-wood-fire foreach-team-char
;

cyoa: camp2-wood-chop
	solohuman if
		choosehuman camp2-wood-chopped
	then;
	"'Chi dovrebbe andare a tagliare la legna? E davvero un duro lavoro!" cyoa-text!
	' camp2-wood-chopped cyoa-human-choice
;

cyoa: camp2-wood
	image-cabin-in-woods
	"Capanna con stufa a legna" cyoa-title!
	($ Thegroup .. "  trova una capanna nel bosco, senza zombie in vista. Ha una stufa a legna, ma niente legna!" $) text! +cr fullcamp
		choice( "Esci e taglia la legna" )choice: camp2-wood-chop
		choice( "Vai a dormire" )choice
;

// Creepy inn woman

: camp2-airbnb-eat args( @ )
	<- @ paranoid? if
		($ <- @ .colourname .. "  si rifiuta di mangiarlo!" $) +crtext <- @ .morale-- 
		<- @ .witsREV <- @ .attitudeREV
	then;
	<- @ .morale2+
;

: camp2-airbnb-breakfast
	($ "Loste serve una colazione fantastica!" $) +crtext
	' camp2-airbnb-eat foreach-team-char goodfx	sleep
;

cyoa-choice: camp2-airbnb-pay
	($ Thegroup .. " ' paga per pernottare una notte alla locanda. E confortevole.	" .. "No one wants zombies creepin while they're sleepin hhhHHHHHHeheh." rbold $) text! -10 trunk-food+
	camp2-airbnb-breakfast
;

cyoa-choice: camp2-airbnb-charm
	($ cname .. "  ottiene un affare migliore per la notte.	" .. "You're a sly one hhhhhhhhhhHHHHHHeheh." rbold $) text! -5 trunk-food+
	camp2-airbnb-breakfast
;

: camp2-airbnb-pres
		($ "...hhhhhhhhhhHHHHHHeheh you must be a big shot.
		I'll show you the presidential suite." rbold $)
		+crtext
;

cyoa-choice: camp2-airbnb-coolit
	cpet? if
		($ cname .. "  fa un " .. "hhhHHHHHeh" rbold .. "  suono." $)
	else
		($ cname .. "  dice alla donna di " .. "DILLO NON SPRUZZARLO." pbold $)
	then text!
	cool-it-7 if
		camp2-airbnb-pres
		"'Mostra la suite presidenziale! Ha una spa!" +crtext
		morale3+all 
		"Mostra anche la palestra esecutiva!" +crtext
		' .fitness++ foreach-team-char ' .strength++ foreach-team-char
		camp2-airbnb-breakfast
		cooled-it goodfx
	else cool-it++
		camp2-airbnb-pres 
		($ "Lei prende a calci " .. thegroup .. "  fuori " .. gThey .. "  mangiare" .. g1s .. "  in strada, ma non trova mai" .. g1s .. "  un posto sicuro dove dormire." $) +crtext badfx +cr tiredcamp
	then witsattREV 
;

cyoa-choice: camp2-airbnb-nosleep
	($ Thegroup .. "  sgomma via da li piuttosto velocemente. " .. gThey .. "  mangiare" .. g1s .. "  in strada, ma non trova mai" .. g1s .. "  un posto sicuro dove dormire." $) text! +cr tiredcamp
;

cyoa: camp2-airbnb
	image-old-house
	"Bed & Breakfast" cyoa-title!
	($ Thegroup .. "  trova una locanda, aperta per affari! La proprietaria si torce le mani: 	" .. "Give me 10 food, I'll watch over you while you sleep hhhHHHHHeh." rbold .. "	E super inquietante." $) text!
		trunk-food 9 > if	
			choice( "Paga i 10 cibi" )choice: camp2-airbnb-pay
		then
		most-grating if	
			charchoice( "': DILLO NON SPRUZZARLO" fcoolit )choice: camp2-airbnb-coolit
		then
		most-charming trunk-food 5 > and if
			charchoice( "  contratta a 5 cibi" )choice: camp2-airbnb-charm
		then
		choice( "Partire senza dormire" )choice: camp2-airbnb-nosleep
;

// Zombie singalong

: camp2-sing-team args( @ )
	<- @ .attitudeREV
	<- @ .cattitude+ if
		<- @ .morale++
	then
	<- @ .cattitude- if
		<- @ .morale--
	then	
;

cyoa-choice: camp2-sing-sing someicon
	($ Thegroup .. "  cerca di mantenere alto il morale cantando. Se il canto fosse buono o meno dipende principalmente dal tuo atteggiamento." $) cyoa-text! 3 700 tone 2 500 tone
		' camp2-sing-team foreach-team-char
;

cyoa-choice: camp2-sing-plan
	cwits- if
		($ "Si scopre che " .. thegroup .. " ' non e poi cosi bravo a pianificare le cose." $) cyoa-text! morale--all ' .witsREV foreach-team-char badfx
	then;	
	cwits+ witsroll or if
		($ cname .. " ' pianifica il giorno successivo. E un buon uso del tempo e aiuta " .. ghim .. "  si concentra sul quadro generale." $) cyoa-text!
			morale++ goodfx
	else
		($ cname .. " ' fa piani elaborati per il giorno successivo. E solo al mattino che " .. ghe .. "  si accorge che i piani erano completamente folli." $) cyoa-text!
			morale--all badfx
	then witsREV
;

cyoa: camp2-sing
	"Death Road Sing a Long" cyoa-title!
	($ Thegroup .. "  e seduto attorno a un falo. Trovare questo campeggio e stato difficile, quindi " .. gtheyare .. "  si mette di cattivo umore." $) cyoa-text! +cr fullcamp
		choice( "Canta una canzone" )choice: camp2-sing-sing
		smartest charchoice( ": Pianifica domani" )choice: camp2-sing-plan
;
		
} }




uses events/trade/trader-camp-shared.df

0 var trade-deck

cyoa{ road{ 
	
: trade-camp-special1 "events/trade/trader-camp-region-special1.df" ; // Sci Lab, weapon trader + single solo trader
: trade-camp-special2 "events/trade/trader-camp-region-special2.df" ; // Ren Faire, recruit + weapon trader
: trade-camp-special3 "events/trade/trader-camp-region-special3.df" ; // Swole Gym, recruit + weapon trader
: trade-camp-special4 "events/trade/trader-camp-region-special4.df" ; // Open Mall, weapon+gas+recruit
: trade-camp-special5 "events/trade/trader-camp-region-special5.df" ; // The Last Arcade, Weapon Trader and Gas Trader
: trade-camp-special6 "events/trade/trader-camp-region-special6.df" ; // Prepper Paradise, Only weapon trader, but had a lot of stuff in it
: trade-camp-special7 "events/trade/trader-camp-region-special7.df" ; // Functional Hospital, weapon trader only but special recruit option
: trade-camp-special8 "events/trade/trader-camp-region-special8.df" ; // Mechanical Fortress, gas trader and weapon trader
	
// 40 trunk-food+ "events/trade/trader-camp-region-special5.df" $load-region to-mission
	
	deck-new
		card: trade-camp-special1
		card: trade-camp-special2
		card: trade-camp-special3
		card: trade-camp-special4
		card: trade-camp-special5
		card: trade-camp-special6
		card: trade-camp-special7
		card: trade-camp-special8
			
	constant TRADE-CAMP-SPECIAL-DECK
	TRADE-CAMP-SPECIAL-DECK var trade-camp-special-deck
	TRADE-CAMP-SPECIAL-DECK shuffles to trade-camp-special-deck
	
defer trade-camp-draw-instead

: trade-camp-special-draw
	regiondef{ cityseeker? } if 0 else 50 then
	%chance if
		trade-camp-draw-instead
	then;
	trade-camp-special-deck depths 0= if TRADE-CAMP-SPECIAL-DECK shuffles to trade-camp-special-deck then drawcard
;
	
: trade-camp1 "events/trade/trader-camp-region1.df" ; // 3 traders, two loot houses
: trade-camp2 "events/trade/trader-camp-region2.df" ; // campfire, 3 traders in line, woods and graveyard
: trade-camp3 "events/trade/trader-camp-region3.df" ; // campfire + 3 traders in square, woods at north
: trade-camp4 "events/trade/trader-camp-region4.df" ; // campfire, gasbuy, 3 traders, middle road
: trade-camp5 "events/trade/trader-camp-region5.df" ; // campfire, gasbuy, 4 traders, middle road
: trade-camp6 "events/trade/trader-camp-region6.df" ; // 3 traders, loothouse
: trade-camp7 "events/trade/trader-camp-region7.df" ; // 2 traders, weapon buyer, graveyard
: trade-camp8 "events/trade/trader-camp-region8.df" ; // 3 trader houses
: trade-camp9 "events/trade/trader-camp-region9.df" ; // Bridge to 2 loot houses, gasbuy, weaponbuy, 3 traders
: trade-camp10 "events/trade/trader-camp-region10.df" ; // Yallmart with 3 traders, weapon buyer
// FEMUR update
: trade-camp11 "events/trade/trader-camp-region11.df" ; // The Fence of Values, 4 traders
: trade-camp12 "events/trade/trader-camp-region12.df" ; // Ruined camp, special trader only + zombies
: trade-camp13 "events/trade/trader-camp-region13.df" ; // One story barn with eggs, 3 traders, weapon buyer
: trade-camp14 "events/trade/trader-camp-region14.df" ; // Bazaar with all trimmings and 5 traders
: trade-camp15 "events/trade/trader-camp-region15.df" ; // House with 3 traders, magazine chance, gasbuy

	deck-new
		card: trade-camp1
		card: trade-camp2 
		card: trade-camp3
		card: trade-camp4
		card: trade-camp5
		card: trade-camp6
		card: trade-camp7
		card: trade-camp8
		card: trade-camp9
		card: trade-camp10
		card: trade-camp11
		card: trade-camp12
		card: trade-camp13
		card: trade-camp14
		card: trade-camp15
		2 cards: trade-camp-special-draw		
			
	constant TRADE-CAMP-DECK
	TRADE-CAMP-DECK var trade-camp-deck
	TRADE-CAMP-DECK shuffles to trade-camp-deck
	
: trade-camp-draw
	trade-camp-deck depths 0= if TRADE-CAMP-DECK shuffles to trade-camp-deck then drawcard
; ' trade-camp-draw to trade-camp-draw-instead

: gototradecamp 
	empty // Preventing overflow from rarely blanking out a camp
	road-trip-days nearcanada-day = if
		"events/trade/trader-camp-final.df"
	else
		trade-camp-draw	
	then	
	$load-region to-mission 
;

: trade-draw
	choice( "Visitar el campamento de comerciantes" )choice: gototradecamp
	choice( "Ignorar a los comerciantes" )choice
;

: trade-msg ($ Thegroup .. "  se topa con un campamento de comerciantes!" $) ;

: near-last-day-warn
	road-trip-days 3 + nearcanada-day = if 
	"Acercandose a Canada" cyoa-title!
	"Canada esta cerca! Esta es la penultima oportunidad para comerciar!" else trade-msg then +crtext foodamt
;

: last-day-warn 
	road-trip-days nearcanada-day = if
	"Ultima tienda antes de Canada" cyoa-title!
	"Esta es la ultima oportunidad para comerciar! Buena suerte!" else trade-msg then +crtext foodamt
;

: end-trade-draw 
	cyoa-text! +cr foodamt trade-msg +crtext trade-draw 
;

cyoa: tradeevent
	"Comerciando en Death Road" cyoa-title!
	road-trip-days 3 = if
		"Fuera de Canada, la mayor parte de la sociedad y la civilizacion han sido destruidas. Todavia puedes encontrar personas que comercian, con alimentos en conserva como nueva moneda." 
		end-trade-draw
	then;
	road-trip-days 6 = if
		($ Thegroup .. " ' siente una sensacion de pavor. Algo peligroso se acerca. El camino parece llenarse cada vez mas de muertos vivientes y se estan poniendo nerviosos." $) cyoa-text!
		near-last-day-warn
		trade-draw
	then;
	road-trip-days 9 = if
		"'El viaje a Canada solia tardar unos dias, como maximo. El apocalipsis lleno las carreteras de escombros, hordas de zombis y muchos coches abandonados. Ahora es un viaje largo." cyoa-text!
		last-day-warn
		trade-draw
	then;
	road-trip-days 12 = if
		($ "El viaje por carretera continua. El cielo comienza a ponerse rojo ominoso, como si el universo quisiera " .. thegroup .. "  fracasar." $) cyoa-text!
		near-last-day-warn
		trade-draw
	then;
	road-trip-days 15 = if
		($ "Un enorme ejercito de muertos vivientes se tambalea alrededor de la carniceria que rodea la frontera con Canada. Muchos han intentado pasar. La mayoria solo logra que sus cuerpos se agreguen a la horda.		Quizas " .. yourteam .. " will be different..." $) cyoa-text!
		last-day-warn
		trade-draw
	then;
	road-trip-days 24 = if
		($ "'Ha sido un camino muy largo y sinuoso hacia Canada. " .. Thegroup .. "  ahora esta a solo un par de dias!" $) cyoa-text!
		near-last-day-warn
		trade-draw
	then;
	road-trip-days 25 = if
		($ "El final esta a la vista. Al final de este largo camino hay una gran cantidad de zombis, el resultado final natural de que las hordas se hacen un poco mas grandes cada pocos dias de viaje.		Quizas " .. yourteam .. "  puede lograrlo!" $) cyoa-text!
		last-day-warn
		trade-draw
	then;
	50 %chance if
		($ Thegroup .. "  esta conduciendo por un largo tramo de carretera. No hay amenazas en el horizonte, pero tampoco mucho mas de interes. " $)
	else
		($ Thegroup .. "  continua progresando por el Camino de la Muerte. Mientras una persona siga viva, el sueno de Canada seguira vivo." $)
	then cyoa-text!
	+cr foodamt
	($ "Inesperadamente, " .. thegroup .. "  encuentra a alguien " .. gthey .. "  puede comerciar con: " $)
		+crtext trade-draw
	
;

} }

uses paul/region-layout.df

cyoa{

0 value rfbuy-heavy-1
0 value rfbuy-heavy-2
0 value rfbuy-heavy-3
0 value rfbuy-mid-1
0 value rfbuy-mid-2
0 value rfbuy-mid-3
0 value rfbuy-light-1
0 value rfbuy-light-3

// Light Knight

cyoa-choice: renfaire-light-1
	iicon ($ pname .. "  kauft einen " .. "Deutsch sein" bbold .. "!
	" .. "'Ich habe den anderen von diesem Paar verloren!	Wenn du damit einen Zombie schlagst, fliegen sie normalerweise!" rbold $) text!
	goodfx 1 WEAPON_GAUNTLET trunk.weapon+ -3 trunk-food+ ' rfbuy-light-1 ++
;

cyoa-choice: renfaire-light-2
	iicon ($ pname .. "  kauft einen " .. "DOLCH" bbold .. "!
	" .. "Ich habe eine Menge davon! Kaufe sie als Souvenirs fur die ganze Familie!" rbold $) text!
	goodfx 1 WEAPON_DAGGER trunk.weapon+ -5 trunk-food+
;

cyoa-choice: renfaire-light-3
	iicon ($ pname .. "  kauft einen " .. "GLADIUS" bbold .. "!
	" .. "'Das ist mein Lieblingsschwert! Es ist so schnell! Es wird auch dein Lieblingsschwert werden!" rbold $) text!
	goodfx 1 WEAPON_GLADIUS trunk.weapon+ -12 trunk-food+ ' rfbuy-light-3 ++
;

cyoa: renfaire-light
	"Light Knight" cyoa-title!
	"Sup dein! Ich handele mit leichten Rustungen! Fur den Ritter mit kleinem Budget!" rbold text!
	+cr foodamt
		trunk-food 3 < if
			"Der Ritter bemerkt deinen Mangel an Nahrung und schenkt dir einen kostenlosen Dolch!" +crtext 
			clear-cyoa 1 WEAPON_DAGGER trunk.weapon+ goodfx
		then;
		choice( "Gehen Sie vorerst" )choice
		trunk-food 2 > rfbuy-light-1 1 < and if
			choice( "Spare Gauntlet kaufen   3 Essen" )choice: renfaire-light-1
		then
		trunk-food 4 > if
			choice( "Dolch kaufen   5 Essen" )choice: renfaire-light-2
		then
		trunk-food 11 > rfbuy-light-3 1 < and if
			choice( "Gladius kaufen   12 Essen" )choice: renfaire-light-3
		then
;

// Middle Knight

cyoa-choice: renfaire-mid-1
	iicon ($ pname .. "  kauft einen " .. "MORGENSTERN" bbold .. "!
	" .. "Schickt Feinde fliegen! Auf die Spaltung kann man sich nur schwer verlassen." rbold $) text!
	goodfx 1 WEAPON_MORNING trunk.weapon+ -15 trunk-food+ ' rfbuy-mid-1 ++
;

cyoa-choice: renfaire-mid-2
	iicon ($ pname .. "  kauft einen " .. "RITTERSCHWERT" bbold .. "!
	" .. "Uberaus zuverlassig!" rbold $) text!
	goodfx 1 WEAPON_KNIGHT trunk.weapon+ -15 trunk-food+ ' rfbuy-mid-2 ++
;

cyoa-choice: renfaire-mid-3
	iicon ($ pname .. "  kauft einen " .. "HALBERD" bbold .. "!
	" .. "Leichter als eine Tongewehr, mit demselben beeindruckenden Schneiden!" rbold $) text!
	goodfx 1 WEAPON_HALBERD trunk.weapon+ -24 trunk-food+ ' rfbuy-mid-3 ++
;

cyoa: renfaire-mid
	"Ritter der goldenen Haare" cyoa-title!
	"Hallo. Meine Rustung ist niemals zu leicht oder zu schwer. Sie sind perfekt." rbold text! +cr foodamt
		trunk-food 15 < if
			"'Du kannst nichts von diesem Ritter aufwenden." +crtext 
		then;
		choice( "Gehen Sie vorerst" )choice
		trunk-food 14 > rfbuy-mid-1 1 < and if
			choice( "Kauf Morgenstern   15 Nahrung" )choice: renfaire-mid-1
		then
		trunk-food 14 > rfbuy-mid-2 1 < and if
			choice( "Kauf Ritterschwert   15 Nahrung" )choice: renfaire-mid-2
		then
		trunk-food 23 > rfbuy-mid-3 1 < and if
			choice( "Kauf Halberd   24 Nahrung" )choice: renfaire-mid-3
		then
;

// Heavy Knight

cyoa-choice: renfaire-heavy-1
	iicon ($ pname .. "  kauft einen " .. "MEDIEVAL AXE" bbold .. "!
	" .. "Not as chivalrous as a sword. Still, a certain appeal....." rbold $) text!
	goodfx 1 WEAPON_MEDIEVAL trunk.weapon+ -15 trunk-food+ ' rfbuy-heavy-1 ++
;

cyoa-choice: renfaire-heavy-2
	iicon ($ pname .. "  kauft einen " .. "MACHTIGES SCHWERT" bbold .. "!
	" .. "Wahrlich! Ein " rbold .. "Chivalrous Blade......." rainbold $) text!
	goodfx 1 WEAPON_CLAYMORE trunk.weapon+ -30 trunk-food+ ' rfbuy-heavy-2 ++
;

cyoa-choice: renfaire-heavy-3
	iicon ($ pname .. "  kauft einen " .. "MEGA RITTERSCHWERD" bbold .. "!
	" .. "The... perfect sword....." rbold $) text!
	($ "Der Ritter wirft wie versprochen das Versprechene ein " .. "Ritterschaft!" fbold $) +crtext
	goodfx 1 WEAPON_MEGAKNIGHT trunk.weapon+ -35 trunk-food+ ' rfbuy-heavy-3 ++
	' .strength++ foreach-team-char
;

cyoa: renfaire-heavy
	"Schweres LARP" cyoa-title!
	"I only selleth the HEAVIEST ARMAMENTS!
	If thou havest the coin...... and mettle....??" rbold text!
	rfbuy-heavy-3 1 < if
		"Der Ritter bietet ein besonderes Angebot wenn Sie die MegaKnight Blade kaufen: " +crtext
		"Kostenloses Ritterliches Krafttraining" rainbold +text 
	then +cr foodamt
		trunk-food 15 < if
			"Der Ritter spottet uber deinen Mangel an Nahrung." +crtext 
		then;
		choice( "Gehen Sie vorerst" )choice
		trunk-food 14 > rfbuy-heavy-1 1 < and if
			choice( "1 gold" )choice: renfaire-heavy-1
		then
		trunk-food 29 > rfbuy-heavy-2 1 < and if
			choice( "Kaufe machtiges Claymore   30 Nahrung" )choice: renfaire-heavy-2
		then
		trunk-food 34 > rfbuy-heavy-3 1 < and if
			choice( "Kaufe MegaKnight Klinge   35 Nahrung" )choice: renfaire-heavy-3
		then
;

cyoa-choice: renfaire-armor-buy
	iicon ($ pname .. "  kauft die " .. "RUSTUNG" bbold .. "  und legt sie an!	" .. "Endlich! Ich kann diesen Mittelaltermarkt verlassen!" pbold $) text!
	goodfx -40 trunk-food+ clear-cyoa
	3 for pchar .vitality++ pchar .health++ next 53 pchar .specialbody! 0 interactee .character@ .specialbody!
	
;

cyoa: renfaire-armor
	"Rittertum kaufen" cyoa-title!
	"'Ich habe genug von diesem LARP!	Ich kann dir meine Rustung verkaufen. Ich habe keinen Helm, also wirst du nicht vollstandig geschutzt sein. Du wirst trotzdem viel mehr Schaden einstecken konnen als zuvor!	Nur 40 Nahrung!" pbold text! foodamt
		trunk-food 40 < if
			"'Du kannst dir die Rustung nicht leisten." +crtext 
		then;
		choice( "Gehen Sie vorerst" )choice
		trunk-food 39 > if
			pchar charchoice( ": Gib mir diese Rustung!   40 Nahrung " )choice: renfaire-armor-buy
		then
;

cyoa-bridge: renfaire-bard-recruit
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	5 recruit-yes-food "'Ich bin bereit fur L.A.R.P.!" pbold +crtext "*" rbold +text
	"'*UBERSETZERHINWEIS: ''Live Action Role Play''" rbold +crtext
	recruitee .witsREV recruitee .attitudeREV
;

cyoa: renfaire-bard
	"Liedanfrage" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"I shall sing you all the songs you like.
	All I requireth... is 5 food!" pbold text!
		choice( "Lassen" )choice
		trunk-food 4 > if
			choice( "Rekrutiere den Barden   5 Nahrung" )choice: renfaire-bard-recruit
		then
;

}

map{ regiondef{

trade-camp-reset

: renfaire-bard
	still-npc .renfairify cyoa{ ' renfaire-bard cyoa! }
	' trader-event lt .events! 1 lt .timer!
;

: renfaire-knight still-npc .knightify ' trader-event lt .events! 1 lt .timer! ;
// : renfaire ;

: renfaire-knight-heavy 
	renfaire-knight cyoa{ ' renfaire-heavy cyoa! } WEAPON_MEGAKNIGHT lt .character@ .weapona! ;
: renfaire-knight-mid 
	renfaire-knight cyoa{ ' renfaire-mid cyoa! } WEAPON_HALBERD lt .character@ .weapona! ;
: renfaire-knight-light
	renfaire-knight cyoa{ ' renfaire-light cyoa! } WEAPON_GLADIUS lt .character@ .weapona! ;
: renfaire-knight-armor
	53 still-npc .specialbody! ' trader-event lt .events! 1 lt .timer! cyoa{ ' renfaire-armor cyoa! } ;

0 value gbx
0 value gby
0 value gbx2
0 value gby2

' plotgrass >defer ft

: fenceit	args( x y s )

	building_misc_tiles_base_id 14 7 * + 7 + <- s + -> s

	2 for
		<- s i 1- 14 * + <- x <- y solidwall
		<- x <- y tile@ LAYER_WALLTOP 0 0 .tile_layer.off!
		<- x <- y tile@ LAYER_WALLTOP rnd5050 .tile_layer.flipx!
		<- y 1- -> y
	next
;

: pt
	xyp.xy ft // Plot the current floor by default

	xyp.i char < = if xyp.xy to gby to gbx then;
	xyp.i char > = if gbx 1- gby xyp.xy swap 1+ swap gravellot then;
	xyp.i char ( = if xyp.xy to gby2 to gbx2 then;
	xyp.i char ) = if gbx2 1- gby2 xyp.xy swap 1+ swap gravellot then;
	
	xyp.i char K = if renfaire-knight lt-here then;
	xyp.i char H = if renfaire-knight-heavy lt-here then;
	xyp.i char M = if renfaire-knight-mid lt-here then;
	xyp.i char L = if renfaire-knight-light lt-here then;
	xyp.i char S = if renfaire-knight-armor lt-here then;
	xyp.i char B = if renfaire-bard lt-here then;
	
	xyp.i char x = if ' plotdirt to ft then;
	xyp.i char G = if ' plotgrass to ft xyp.xy ft then;
	xyp.i char $ = if xyp.xy grassedge then;
	xyp.i char { = if dirtstart then;
	xyp.i char } = if dirtend then;

	xyp.i char A = if trader-awning lt-here then;
	xyp.i char t = if pictable2 lt-here 8 rdrift then;		
	xyp.i char w = if pine lt-here drift then;
	xyp.i char V = if tradercarhere then;
	xyp.i char f = if campfirehere then;
	
	xyp.i char 1 = if xyp.xy xyp.x 1 & if 2 else rnd5050 then fenceit then;
	xyp.i char 2 = if xyp.xy 3 fenceit then;
	xyp.i char 3 = if xyp.xy 4 fenceit then;
	
	xyp.i char W = if trader-weapon-buyer then;
	xyp.i char T = if traderhere then;
	xyp.i char Z = if tnomey-spawn then;
;

"
.........................
..w....w....w....w....w..
.........................
.........................
1111111111111111111111111
....<....A..A..A..A......
.........H..M..L..S......
.....t...................
.........................
...........(........>....
................{........
.................B.......
.....W..Z..........f.....
.........................
.............)........}..
.........................
11111111112...31111111111
........................x
$$$$$$$$$$$$$$$$$$$$$$$$$
.........................
.........................
.........................
.........................
.........................
.........................
........................."
constant maplayout

: plotmap
	0 -1 maplayout xyplot: pt
;

: site-setup
	1 no_time!
	7 14 rnd o'clock

	' plotgrass to ft

	30 %chance if 0 0.5 frnd weather! -1 2 frnd wind_force! else -1 1 frnd wind_force! then

	plotmap
	mission{ no-edge-spawns }

	// Put char and chars in middle of map
	20 %mv-up
	30 %chars-up
;

region: specialcamp-2
	w: maplayout layoutw
	h: maplayout layouth

	trade-mode

	post-script: site-setup

} }

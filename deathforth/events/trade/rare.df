cyoa{ regiondef{

// Witch and Cauldron

: black-cat

	// Black cat
	new-npc lt .character@ .catify
	8 lt .character@ .bodytype!
	lt .character@ .default-ai-setup
;

: .my-cat-thing .user.ival@ #thing ;

: .get-black-cat args( chara )

	black-cat

	// Store the cat's THINGID for the witch
	lt thing# <- chara .user.ival!

	// Store the witch's THINGID for the cat
//	<- chara .thing@ thing# lt <- chara .user.ival!
;

: witch-done
	($ "A bruxa gargalha e depois conta " .. thegroup .. " ir embora" $) +crtext clear-cyoa
;

: charm-witch
	most-charming if
		choosechar ($ " A bruxa da " .. cname .. " 'uma vassoura de bruxa gratuita." .. "TALVEZ VOCE POSSA SER UMA BRUXA UM DIA! *GARGALHANDO*" pbold $) 
		+cr +text 1 WEAPON_WITCH trunk.weapon+ cicon
	then
;

cyoa-bridge: trade-witch-20
	($ Thegroup .. " suborna a bruxa para entrar no time!" .. "POSSO LEVAR MEU CALDEIRAO COMIGO?" pbold $) cyoa-text! basic-tone
	-20 trunk-food+
	clear-cyoa cchar to recruitee
	team-full? if
		($ "O grupo esta cheio. Quem voce gostaria de substituir para dar lugar a bruxa?" $) +crtext
		' recruit-replace cyoa-team-choice
	else
		recruited
	then

	charm-witch
;

: trade-witch-roll 
	dup .cwits+ if .morale-- else .morale3+ then 
;

: mew-recruit recruitee .thing@ statmod{ .mew } ;

cyoa-bridge: trade-witch-10
	($ Thegroup .. " buys a BLACK CAT...
	" .. "BEM, ACHO QUE VOCE PODE LIDAR COM UM ANIMAL DE ESTIMACAO!" pbold $) cyoa-text! basic-tone

	interactee .character@ .my-cat-thing .character@ to recruitee

	ricon

	-13 trunk-food+ ricon
	witch-done
	team-full? if
		($ "O grupo esta cheio. Quem voce gostaria de substituir para dar lugar ao gato preto?" $) +crtext
		' recruit-replace cyoa-team-choice
	else
		recruited
	then
	charm-witch

	1 ' mew-recruit timer_add
;

cyoa-choice: trade-witch-5
	($ Thegroup .. " 'compra uma vassoura de bruxa!" .. "ESTA VASSOURA FOI INFUNDIDA COM O PODER DA MAGIA NEGRA, POR ISSO ELA VARRE DUAS VEZES MELHOR QUE UMA VASSOURA NORMAL!" pbold $) cyoa-text! basic-tone
	1 WEAPON_WITCH trunk.weapon+ -5 trunk-food+
	witch-done
;

cyoa-choice: trade-witch-1
	($ pname .. " pergunta a bruxa sobre seu caldeirao." .. "'VOCE PODE BEBER DELA SE QUISER! E MUITO VENENOSO." pbold $) cyoa-text! basic-tone
	witch-done
;

cyoa-choice: trade-witch-coolit
	cpet? if
		($ cname .. " 'solta um grande pum contra o caldeirao da bruxa." $)
	else
		($ cname .. " diz ao feiticeiro comerciante para " .. "SE ACALMAR. TAMBEM, BELO CALDEIRAO! NAO!" pbold $) then cyoa-text!
	cool-it++
		($ "A bruxa agarra " .. cname .. ".
		" .. "MAKE FUN OF THE WITCH'S BREW?
			GET THROWN IN THE WITCH'S STEW!!" rbold $) badfx +crtext 
		($ "A bruxa joga " .. chim .. "  no caldeirao!" $) +crtext
		cpet? if
			($ cname .. "  esta completamente envolto em ensopado venenoso! Que delicia!" $) +crtext
			cchar .sting2-
			cchar .morale2+
			cchar .vitality++
		else
			($ cname .. "    sAUDE MAXIMA REDUZIDA PARA 1" rbold $) +crtext
			cchar .skelbody
			cchar .morale2-
			cchar .dexterity++
		then
			attitudeREV witsREV trade-flee
;

cyoa: trade-rare-witch
	"A Bruxa e o Caldeirao" cyoa-title!
	nearest .character@ choosechar cicon
	($ "'Voce esta atrapalhando essa bruxa de fazer atividades de bruxa." .. "'VOCE PODE ME INCOMODAR UMA VEZ E SOMENTE UMA VEZ, ESTOU OCUPADA COM MINHAS ATIVIDADES DE BRUXA!" pbold $) cyoa-text! basic-tone foodamt
	choice( "Fique quieto por enquanto" )choice
	most-grating if 
		charchoice( "  diz a ela para SE ACALMAR" fcoolit )choice: trade-witch-coolit
	then
	trunk-food 5 < if
		choice( "Pergunte sobre o Caldeirao" )choice: trade-witch-1
	else
		trunk-food 19 > if
			choice( "Recrute a Bruxa por 20 alimentos" )choice: trade-witch-20
		then
		trunk-food 12 > if
			choice( "Gato Preto por 13 alimentos" )choice: trade-witch-10
		then
		trunk-food 4 > if
			choice( "'Vassoura de Bruxa por 5 alimentos" )choice: trade-witch-5
		then
	then
;

0 value kip-lab-zap

: kip-lab-foodcheck kip-lab-zap 1 < if 5 else 20 then ;

cyoa-choice: trade-rare-lab-go
	"Aprimoramento Genetico" cyoa-title!
	($ cname .. "  entra na maquina e e atingido por " .. "RAIOS COSMICOS" fbold .. ". " .. cHe .. "  sente se energizado!" $) text!
	strength++ fitness++ cchar .fullheal
	lab-effect depths 0= if LAB-EFFECT shuffles to lab-effect then
	drawcard 
	kip-lab-foodcheck -1 * trunk-food+
	' kip-lab-zap ++

	// FX
	sfx{ growl 5000 ramp_start! 10000 ramp_end! 2 scale! 1000 d! 0.95 s! 1000 r! }
	-3 fade_to 0 1 0 fade_clr 60 fade_count!
	10 0.95 rumble-gui
;

cyoa-bridge: trade-rare-lab-pick
	solo if
		someone choosechar trade-rare-lab-go
	then;
		"COSMIC RAAAAAAAAYS" cyoa-title!
		($ "Quem deveria ser atingido por " .. "RAIOS COSMICOS?" fbold $) text!
		' trade-rare-lab-go cyoa-team-choice
;

cyoa: trade-rare-lab
	"'Maquina de Ciencia do Kip" cyoa-title!
	nearest .character@ choosechar cicon
	($ "'E uma especie de lagarto alienigena. Ninguem parece se importar.	" .. "Por uma pequena taxa posso baguncar permanentemente seu codigo genetico! " pbold $) cyoa-text! basic-tone
	kip-lab-zap 1 < if
		($ "'75% de desconto no primeiro zap! Isso e um negocio!" pbold $)
	else
		($ "EM GRANDE ESTILO!" fbold $)
	then +text
	+cr foodamt
	choice( "Recusar oferta generosa" )choice
	trunk-food kip-lab-foodcheck < if
		"'Infelizmente, voce nao pode pagar por este otimo negocio." +crtext
	else
		choice( "Parece bom!   " .. kip-lab-foodcheck .. "  comida" )choice: trade-rare-lab-pick
	then
;

// Head Swap Doctor

0 var 3-dochead-stack

: headicon 7 * special_chars_base_id + 5 + cyoa-choice-iconid! ;

: cyoa-3-docheads
	3-dochead-stack sizes for 3-dochead-stack drawcard
	next drop
;

defer trade-rare-doc-repick

: dochead-cancel
	text!
	"Quem deveria pegar esta cabeca?" +crtext
	choice( "Escolha uma cabeca diferente" )choice: trade-rare-doc-repick
;
: dochead-done text! goodfx clear-cyoa -5 trunk-food+ ;

// The Heads

cyoa-choice: doc-chicken-go
	($ cname .. " ' recebe uma cabeca de galinha novinha em folha! Ela e infundida com todos os poderes e habilidades de uma galinha." $) dochead-done
		($ cname .. "  agora tem um CEREBRO MINUSCULO!" rbold $) +crtext
		86 cchar .specialhead!
		0 cchar .wits! witsREV
		-1 STAT_VITALITY cchar .stat+ sting--
		dexterity++
;
cyoa-bridge: doc-chicken-go4
	"'Quem deve ficar com a cabeca da galinha?" text!
	' doc-chicken-go cyoa-human-choice

;
cyoa-bridge: doc-chicken "'Uma galinha nao e conhecida por sua resistencia ou inteligencia, mas, por outro lado, elas podem voar bem rapido." dochead-cancel
	numhums 3 > if
		' doc-chicken-go 2 #cyoa-human-choice
		choice( "Escolha outra pessoa" )choice: doc-chicken-go4
	else
		' doc-chicken-go cyoa-human-choice
	then
;

cyoa-choice: doc-hero-go
	($ cname .. "  recebe a cabeca de um heroi!" $) dochead-done
		($ cname .. "  recebe uma atitude mais heroica!" rbold $) +crtext
		18 cchar .specialhead! -4 cchar .headoff.y!
		6 cchar .loyalty! loyaltyREV 6 cchar .attitude! attitudeREV
;
cyoa-bridge: doc-hero-go4
	"'Quem deve ficar com a cabeca do heroi?" text!
	' doc-hero-go cyoa-human-choice

;
cyoa-bridge: doc-hero "Mesmo flutuando em um jarro, esta cabeca de queixo quadrado parece um daqueles tipos heroicos leais e positivos." dochead-cancel
	numhums 3 > if
		' doc-hero-go 2 #cyoa-human-choice
		choice( "Escolha outra pessoa" )choice: doc-hero-go4
	else
		' doc-hero-go cyoa-human-choice
	then
;

cyoa-choice: doc-dog-go
	($ cname .. " ' recebe uma cabeca de cachorro!" $) dochead-done
		($ cname .. "  agora tem um CEREBRO MINUSCULO MAS LEAL!" rbold $) +crtext
		22 cchar .specialhead! -4 cchar .headoff.y!
		0 cchar .wits! witsREV 6 cchar .loyalty! loyaltyREV
;
cyoa-bridge: doc-dog-go4
	"'Quem deve ficar com a cabeca do cachorro?" text!
	' doc-dog-go cyoa-human-choice

;
cyoa-bridge: doc-dog "'Os caes sao os melhores amigos do homem! Uma cabeca de cachorro provavelmente seria uma grande melhoria em relacao a sua cabeca traicoeira de nao cachorro. Como bonus, a cabeca esta fumando um cachimbo." dochead-cancel
	numhums 3 > if
		' doc-dog-go 2 #cyoa-human-choice
		choice( "Escolha outra pessoa" )choice: doc-dog-go4
	else
		' doc-dog-go cyoa-human-choice
	then
;

cyoa-choice: doc-clown-go
	($ cname .. " ' agora tem uma cabeca de palhaco. Nada de bom pode resultar disso." $) dochead-done
		29 cchar .specialhead! -2 cchar .headoff.y!
		0 cchar .loyalty!
		// Change specialtype to clown without changing body?
		cchar morale--all-except
;
cyoa-bridge: doc-clown-go4
	"'Quem deve ficar com a cabeca do palhaco?" text!
	' doc-clown-go cyoa-human-choice

;
cyoa-bridge: doc-clown "'O medico mostra uma cabeca de palhaco flutuando em um jarro. E assustador. Isso PARECE uma ma ideia." dochead-cancel
	numhums 3 > if
		' doc-clown-go 2 #cyoa-human-choice
		choice( "Escolha outra pessoa" )choice: doc-clown-go4
	else
		' doc-clown-go cyoa-human-choice
	then
;

cyoa-choice: doc-gnome-go
	($ cname .. "  agora esta se sentindo um pouco gnomo." $) dochead-done
		25 %chance if
			33
		else
			75 
		then cchar .specialhead! -1 cchar .headoff.x!
		WEAPON_POKIN cchar .weapon_default!		
;
cyoa-bridge: doc-gnome-go4
	"'Quem deveria ficar com a cabeca do gnomo?" text!
	' doc-gnome-go cyoa-human-choice

;
cyoa-bridge: doc-gnome "O medico se oferece para substituir sua cabeca por um gnomo. Sim, a criatura da floresta fantastica." dochead-cancel
	numhums 3 > if
		' doc-gnome-go 2 #cyoa-human-choice
		choice( "Escolha outra pessoa" )choice: doc-gnome-go4
	else
		' doc-gnome-go cyoa-human-choice
	then
;

cyoa-choice: doc-robot-go
	($ cname .. "  e uma especie de ciborgue. Espero que " .. che .. "  agora pode falar com o carro na proxima vez que ele quebrar." $) dochead-done
		($ cname .. "' MAXIMO MECANICO AUMENTADO!" rbold $) +crtext
		50 %chance if
			39
		else
			40 
		then cchar .specialhead! -1 cchar .headoff.y!
		1 STAT_MECHANICAL cchar .bonus+ mechanicREV
;
cyoa-bridge: doc-robot-go4
	"'Quem deveria ficar com a cabeca do robo?" text!
	' doc-robot-go cyoa-human-choice

;
cyoa-bridge: doc-robot "O medico mostra uma elegante cabeca de robo. Torne se um com as maquinas!" dochead-cancel
	numhums 3 > if
		' doc-robot-go 2 #cyoa-human-choice
		choice( "Escolha outra pessoa" )choice: doc-robot-go4
	else
		' doc-robot-go cyoa-human-choice
	then
;

cyoa-choice: doc-alien-go
	($ cname .. " ' agora esta infundido com INTELIGENCIA ALIENIGENA! Embora, em retrospecto, e provavel que o alienigena nao fosse o mais inteligente de sua especie." $) dochead-done
		51 cchar .specialhead! -1 cchar .headoff.y!
		6 cchar .wits! witsREV
;
cyoa-bridge: doc-alien-go4
	"'Quem deveria ficar com a cabeca do alienigena?" text!
	' doc-alien-go cyoa-human-choice

;
cyoa-bridge: doc-alien "O medico apresenta uma cabeca alienigena em um jarro. A VERDADE ESTA LA FORA. O alienigena parece uma forma de vida mais evoluida e inteligente, embora nao tao evoluida a ponto de evitar ser preservado em um pequeno jarro." dochead-cancel
	numhums 3 > if
		' doc-alien-go 2 #cyoa-human-choice
		choice( "Escolha outra pessoa" )choice: doc-alien-go4
	else
		' doc-alien-go cyoa-human-choice
	then
;

cyoa-choice: doc-beaver-go
	($ cname .. "  agora e meio castor! Este glorioso simbolo do Canada melhora o moral." $) dochead-done
		77 cchar .specialhead! 5 cchar .headoff.y! 3 cchar .headoff.x!
		fitness++ fitnessREV
		morale2+all
;
cyoa-bridge: doc-beaver-go4
	"'Quem deveria ficar com a cabeca do castor?" text!
	' doc-beaver-go cyoa-human-choice

;
cyoa-bridge: doc-beaver "'Beaveres sao conhecidos pela sua capacidade para exercicio aerobico. Estao sempre dancando e fazendo saltos. Penso que e isso que ouvi em documentarios de natureza." dochead-cancel
	numhums 3 > if
		' doc-beaver-go 2 #cyoa-human-choice
		choice( "Escolha outra pessoa" )choice: doc-beaver-go4
	else
		' doc-beaver-go cyoa-human-choice
	then
;

cyoa-choice: doc-skele-go
	($ cname .. "  agora tem um cranio em vez de cabeca. Isso e " .. "Muito legal." fbold $) dochead-done
		($ cname .. "'Seu MAXIMO FORCA, FISCALIZACAO E TIRADENTES AUMENTADOS!" rbold $) +crtext
		78 cchar .specialhead! -1 cchar .headoff.y!
		-1 STAT_VITALITY cchar .stat+ sting--
		1 STAT_STRENGTH cchar .bonus+ 
		1 STAT_FITNESS cchar .bonus+
		1 STAT_SHOOTING cchar .bonus+		
;
cyoa-bridge: doc-skele-go4
	"'Quem deve receber a cabeca do esqueleto?" text!
	' doc-skele-go cyoa-human-choice

;
cyoa-bridge: doc-skele "'O medico diz que vai trazer a melhor cabeca de transplante, e ela acaba sendo um cranio. Intrigante." dochead-cancel
	numhums 3 > if
		' doc-skele-go 2 #cyoa-human-choice
		choice( "Escolha outra pessoa" )choice: doc-skele-go4
	else
		' doc-skele-go cyoa-human-choice
	then
;

cyoa-choice: doc-swole-go
	($ cname .. "  agora tem o potencial de se tornar muito musculoso para controlar!" $) dochead-done
		($ cname .. "'Seu MAXIMO FORCA AUMENTADA!" rbold $) +crtext
		25 cchar .specialhead! -4 cchar .headoff.y!
		1 STAT_STRENGTH cchar .bonus+ strengthREV	
;
cyoa-bridge: doc-swole-go4
	"'Quem deve receber a cabeca do bodybuilder?" text!
	' doc-swole-go cyoa-human-choice

;
cyoa-bridge: doc-swole "'Muita forca de um bodybuilder vem da condicionamento mental. Assim ter a cabeca de um bodybuilder deveria te permitir atingir novas alturas de forca!" dochead-cancel
	numhums 3 > if
		' doc-swole-go 2 #cyoa-human-choice
		choice( "Escolha outra pessoa" )choice: doc-swole-go4
	else
		' doc-swole-go cyoa-human-choice
	then
;

cyoa-choice: doc-mallcop-go
	($ cname .. "  agora tem a resistencia e precisao de armas de um guarda mall" $) dochead-done
		($ cname .. "'Seu MAXIMO TIRADENTES DECREVEU!" rbold $) +crtext
		50 %chance if 69 else 70 then
		cchar .specialhead! 0 cchar .headoff.y!
		-2 STAT_SHOOTING cchar .bonus+ shootingREV
		vitality++ cchar .fullheal
;
cyoa-bridge: doc-mallcop-go4
	"'Quem deve ficar com a cabeca do policial do shopping?" text!
	' doc-mallcop-go cyoa-human-choice
;
cyoa-bridge: doc-mallcop "Quando o zomborpocalipse acontecer, os policiais do shopping herdarao as ruinas da Terra.	   Antigo Livro da Profecia do Policial do Shopping" rbold dochead-cancel
	numhums 3 > if
		' doc-mallcop-go 2 #cyoa-human-choice
		choice( "Escolha outra pessoa" )choice: doc-mallcop-go4
	else
		' doc-mallcop-go cyoa-human-choice
	then
;

cyoa-choice: doc-doc-go
	($ cname .. "  meio que parece um medico de verdade. Ha algumas duvidas porque " .. che .. " ' nao pegou a roupa de medico como parte do transplante." $) dochead-done
		($ cname .. "' AUMENTO MEDICO MAXIMO!" rbold $) +crtext
		24 cchar .specialhead! -1 cchar .headoff.y!
		0 cchar .loyalty!
		1 STAT_MEDICAL cchar .bonus+ medicalREV
		78 victim .specialhead! -1 victim .headoff.y!
;
cyoa-bridge: doc-doc-go4
	"'Quem deve ficar com a cabeca do medico louco?" text!
	' doc-doc-go cyoa-human-choice
;
cyoa-bridge: doc-doc "'O medico aponta para sua cabeca. Ele afirma que e a segunda melhor que ele tem em estoque." dochead-cancel
	numhums 3 > if
		' doc-doc-go 2 #cyoa-human-choice
		choice( "Escolha outra pessoa" )choice: doc-doc-go4
	else
		' doc-doc-go cyoa-human-choice
	then
;

// End Heads

deck-new
	"Frango" choice-card: doc-chicken
	"'Cabeca de Heroi" choice-card: doc-hero
	"'Cabeca de Cachorro" choice-card: doc-dog
	"Palhaco" choice-card: doc-clown
	"Zona dos Gnomos" choice-card: doc-gnome
	"Cabeca de Robo" choice-card: doc-robot
	"Alienigena" choice-card: doc-alien
	"Cabeca de castor minusculo" choice-card: doc-beaver
	"Uma caveira" choice-card: doc-skele
	"Fisiculturista" choice-card: doc-swole
	"Policial de Shopping" choice-card: doc-mallcop
	"Medico louco" choice-card: doc-doc
shuffles value dochead-pick-stack-base // Shuffle before-hand, and just pick the top 3

: draw-3-dochead 3 stack sizes for
		dochead-pick-stack-base i picks nip shove
	next
	to 3-dochead-stack
;

: pick-3-docheads
	choice( "Mantenha a cabeca velha" )choice
	draw-3-dochead
	cyoa-3-docheads
;

cyoa-bridge: trade-rare-doc-pick
	($ "'Otimo! Aqui esta o que tenho pronto para ir.	Deixe me saber em que cabeca voce esta interessado! So tenho tempo para um transplante. Vou " pbold .. "GOLFE" rbold .. " ' depois disso porque sou um " pbold .. "Doutor." rbold $) text!
		"Uma cabeca nova em folha" cyoa-title!
		
		pick-3-docheads
;

' trade-rare-doc-pick to trade-rare-doc-repick

cyoa: trade-rare-doc
	"Medico de renome" cyoa-title!
	nearest .character@ choosechar cicon
	cchar to victim
	($ "'E um medico. A " .. "REAL" rbold .. " portugues" .. "Let's cut right to the chase... I can replace your head with a better head. " pbold $) cyoa-text! basic-tone
		numhums 1 < if
			($ "Wait a second... get out of here! I'm not a vet!" rbold $) +text
		else
			($ "'Eu NAO estou louco! Mas eu estou ficando louco, entao tem um grande desconto. So 5 comidas!" rbold $) +text +cr foodamt
		then
	
	trunk-food 5 < numhums 1 < or if
		"'Parece que voce esta preso com sua velha cabeca." +crtext
		choice( "Partir com grande vergonha" )choice
	else
		choice( "De jeito nenhum, Jose" )choice
		choice( "Me mostre o que voce tem!" )choice: trade-rare-doc-pick
	then
;

// Barkley 2 Wizard

0 value wiz-dailyfortune

: diewiz pbold .. "  morrer uma morte terrivel!" fbold ;

0 stack
	"Voce encontrara um palhaco misterioso e entao voce ira" shove
	"Vejo voce encontrando um enorme suprimento de comida em um futuro proximo, apos o qual voce cometera um erro e" shove
	"Voce encontrara o amor verdadeiro na Estrada da Morte! Pouco depois disso voce ira" shove
	"'Voce nao chegara ao Canada, em vez disso voce ira" shove
	"Voce se recusara a ajudar alguem em necessidade, e o destino ira puni lo fazendo voce" shove
	"Voce chegara ao Canada! Eu vejo isso! Entao voce ira" shove
	"'Voce sera sucedido por um exercito de caes superinteligentes que farao uma nova civilizacao. Eles substituirao a humanidade. Voce nao vera isso porque voce ira" shove
	"Os zumbis ficam entediados e desistem. Isso acontece muito depois de voce" shove
	"Of your team" rbold shove
	"4  22 " shove
	"Will be better for it" shove
	"Todos os zumbis sao substituidos por lobisomens e voce" shove
	"Only find the Crypt of Decay" shove
	"Tu vais. It will  Ele vai" shove
	"'Provavelmente vera apenas a morte no seu futuro, infelizmente. Voce provavelmente" shove
	"As profecias falam de voce! Elas afirmam que voce" shove
	"'Voce chegara ao Canada! E previsto! Brincadeira, voce" shove
value wizard-fortune-base
wizard-fortune-base shuffles var wizard-fortune

cyoa-choice: trade-rare-wiz-fortune
	"SUA SORTE" cyoa-title!
	($ wizard-fortune pops nip diewiz $) text!
	-1 trunk-food+
	' wiz-dailyfortune ++
	morale--all
	someone choosechar
	($ cname .. "  resolve desafiar o destino!" $) +crtext
		($ cname .. "    sAUDE E SORTE MAXIMAS AUMENTADAS!" rbold $) +crtext
		vitality++
		777777777 cchar .seed!
;

cyoa-choice: trade-rare-wiz-tarot-go
	($ "A leitura de taro revela verdades pessoais incriveis sobre " .. cname .. "." $) text!
	-5 trunk-food+
	cchar .showitall
;

cyoa-bridge: trade-rare-wiz-tarot
	solo if
		someone choosechar trade-rare-wiz-tarot-go
	then;
	"Quem deve fazer a leitura de taro? Isso revelara muito sobre eles." text!
	' trade-rare-wiz-tarot-go cyoa-team-choice
;

cyoa-bridge: trade-rare-wiz-recruit
	($ Thegroup .. "  contrata o mago!	" .. "As profecias ainda dizem que voce" diewiz $) cyoa-text! basic-tone
	-20 trunk-food+
	clear-cyoa cchar to recruitee witsREV
	team-full? if
		($ "O grupo esta cheio.		Quem voce gostaria de substituir para abrir espaco para o mago?" $) +crtext
		' recruit-replace cyoa-team-choice
	else
		recruited
	then	

;

cyoa: trade-rare-wiz
	"O Mago" cyoa-title!
	nearest .character@ choosechar cicon
	($ "Esse cara parece que pode ser um mago.	" .. "Portugues" pbold $) cyoa-text! basic-tone
	+text +cr foodamt
	trunk-food 1 < if
		($ "'Sorte gratis: Voce vai morrer de fome! Entao voce vai" diewiz $) +crtext
		choice( "Legal, uma fortuna gratis!" )choice
	else
		choice( "Nao de nada a este FRAUDE" )choice
		wiz-dailyfortune 1 < if
			choice( "Receber fortuna   1 comida" )choice: trade-rare-wiz-fortune
		then
	then
	trunk-food 4 > if
		choice( "Fazer uma leitura de taro   5 comida" )choice: trade-rare-wiz-tarot
	then
	trunk-food 19 > if
		choice( "Recrutar mago   20 comida" )choice: trade-rare-wiz-recruit
	then
;

// Book seller

0 value rarebook-cheap
0 value rarebook-dog
0 value rarebook-map

: trade-rare-book-cheap
	' rarebook-cheap ++
	cyoa{ bookmag-get2 }
	-5 trunk-food+
;

cyoa-choice: trade-rare-book-dog
	' rarebook-dog ++
	($ Thegroup .. "  le " .. "O GUIA DEFINITIVO PARA CAES" pbold .. ". Este livro enorme tem todas as paginas preenchidas com conhecimento sobre caes." $) cyoa-text! goodfx
	($ gHe .. "  ganha o privilegio: " .. "AMIGO DE CACHORRO!" fbold $) +crtext
	statmod{ ' .dogpal foreach-human-char }
	-15 trunk-food+
;

cyoa-choice: trade-rare-book-map
	' rarebook-map ++
	($ Thegroup .. "  compra " .. "'ATLAS DE MAPAS DE P.L. PLARKEN" pbold .. ". Ele contem mapas de todas as ruas daqui ate o Canada. Isso sera extremamente valioso." $) cyoa-text! goodfx
	($ gHe .. "  ganha o privilegio: " .. "EXPLORADOR!" fbold $) +crtext
	statmod{ ' .explorer foreach-human-char }
	-15 trunk-food+
;

: trade-rare-book-draw
	choice( bookmagtitle .. "    5 alimentos" )choice: trade-rare-book-cheap
;

cyoa: trade-rare-book
	"Livros Raros" cyoa-title!
	nearest .character@ choosechar cicon
	($ "	Esta comerciante de livros fala sobre como ela esta vendendo livros raros valiosos." .. "'Por favor, nao mexa nos livros de exibicao! Eles sao falsos, eu os coloquei para confundir ladroes de livros." pbold $) cyoa-text! basic-tone
	+text +cr foodamt
	trunk-food 5 < if
		($ "Voce nao tem comida suficiente para comprar nenhum dos bons livros!" $) +crtext
		choice( "Eu queria aqueles livros" )choice
	else
		choice( "Os livros sao para nerds" )choice
		trunk-food 4 > rarebook-cheap 1 < and if
			trade-rare-book-draw
		then
		trunk-food 14 > if
			rarebook-dog 1 < if
				choice( "O GUIA DEFINITIVO PARA CAES" pbold .. "    15 alimentos" )choice: trade-rare-book-dog
			then
			rarebook-map 1 < if
				choice( "'ATLAS DE MAPAS DE P.L. PLARKEN" pbold .. "    15 alimentos" )choice: trade-rare-book-map
			then
		then
	then
;

// COLD STEEL

cyoa-choice: trade-rare-coldsteel-buy
	($ Thegroup .. "  compra a espada gigante.	" .. "Voce nao se arrependera de comprar uma espada Cold Steel!" pbold $) cyoa-text! basic-tone
	1 WEAPON_ZWEI trunk.weapon+ -45 trunk-food+
	clear-cyoa goodfx
;

cyoa-bridge: trade-rare-coldsteel-demo
	clear-cyoa cchar to recruitee goodfx 
	($ "'Ele pensou que voce nunca perguntaria.	" .. Thegroup .. "  recruta temporariamente " .. rname .. "  para que ele possa fornecer uma demonstracao ao vivo.	" .. "'Voce ficara surpreso com o poder da Greatsword Cold Steel!" pbold $) cyoa-text! basic-tone
	team-full? if
		($ "O grupo esta cheio.		Quem voce gostaria de substituir para abrir espaco?" $) +crtext
		recruit-replace?
	else
		recruited
	then	

;

cyoa-bridge: trade-rare-coldsteel-demo-fullgroup
	($ "Seu grupo esta cheio. Se voce pedir a demonstracao, " .. "voce tera que escolher um membro da equipe para inicializar do grupo! Voce ganhara entao um recruta temporario para o proximo local." rbold $) cyoa-text!
	choice( "Cancelar a demonstracao" )choice
	choice( "Abandone alguem para demonstracao" )choice: trade-rare-coldsteel-demo
;

cyoa: trade-rare-coldsteel
	"Facas de aco frio" cyoa-title!
	nearest .character@ choosechar cicon
	($ "'Estou vendendo a melhor arma que voce ja viu, antes ou depois do apocalipse zumbi. E a Espada Grande de Aco Frio da Cold Steel Knives! Apenas 45 comidas!" pbold $) cyoa-text! basic-tone
	+text +cr foodamt
	choice( "'Nao sou bom o suficiente para esta espada " .. DX )choice
	trunk-food 44 > if
		choice( "Compre a Espada Grande   45 comidas" )choice: trade-rare-coldsteel-buy
	then
	team-full? if
		choice( "Peca uma demonstracao" )choice: trade-rare-coldsteel-demo-fullgroup
	else
		choice( "Peca uma demonstracao" )choice: trade-rare-coldsteel-demo
	then

;

// SKELETAL KNIGHT

0 value skelknight-bomb
0 value skelknight-hammer
0 value skelknight-gladius

cyoa-bridge: trade-skelknight-25
	($ Thegroup .. "  compra a " .. "MARTELO DE CRANIO." fbold .. "Contemple o incrivel poder destrutivo do meu MARTELO DE CRANIO sobressalente." pbold $) cyoa-text! basic-tone
	1 WEAPON_SKULLMACE trunk.weapon+ -25 trunk-food+ ' skelknight-hammer ++ goodfx
;

cyoa-bridge: trade-skelknight-15
	($ Thegroup .. " ' compra a espada curta do esqueleto!" .. "'Este gladio e uma otima espada, mas nunca gostei porque nao e com tema de caveira." pbold $) cyoa-text! basic-tone
	1 WEAPON_GLADIUS trunk.weapon+ -15 trunk-food+ ' skelknight-gladius ++
;

cyoa-choice: trade-skelknight-5
	($ Thegroup .. "  compra cinco Bombas de Caveira!" .. "Estas caveiras sao encantadas com o devastador PODER DA CAVEIRA!" pbold $) cyoa-text! basic-tone
	5 WEAPON_BOMBSKULL trunk.weapon+ -5 trunk-food+ ' skelknight-bomb ++
;

cyoa-choice: trade-skelknight-1
	($ pname .. "  pergunta ao esqueleto por que ele precisa de comida." .. "Porque eu como, seu idiota!" pbold $) cyoa-text!
	pchar .morale-- badfx
;

cyoa-choice: trade-skelknight-coolit
	cpet? if
		($ cname .. " ' peida na pilha de ossos do cavaleiro esqueleto." $)
	else
		($ cname .. "  diz ao cavaleiro esqueleto para " .. "SE ACALMAR." pbold $)
	then cyoa-text!
	cool-it-7 if
		($ "Seu cranio faz barulhos de rangido rapidos. Riso?		" .. "'Nao ouco uma piada dessas ha dez seculos!" pbold $) goodfx +crtext
			WEAPON_GLADIUS recruitee .weaponb!
			1 recruitee .weapon_nodropb!
			WEAPON_BOMBSKULL recruitee .weaponc!
			3 recruitee .stackedc!
		cooled-it 
		team-full? if
			($ "Ele insiste em se juntar. Quem voce gostaria de substituir para abrir espaco para " .. rname .. "?" $) +crtext
			' recruit-replace cyoa-team-choice
		else
			recruited
		then clear-cyoa recruitee .strengthREV recruitee .fitnessREV
	else cool-it++
		($ "VOCE SE AQUECE! VOCE SE AQUECE!!" pbold .. "		O cavaleiro fortemente armado ameaca enviar " .. thegroup .. "  para o " .. "ZONE DE OSSO" fbold $) +crtext badfx trade-flee
	then attitudeREV witsREV

;

cyoa: trade-rare-skelknight
	"Cavaleiro Esqueleto" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	($ "'E um esqueleto animado, vestindo armadura de placas.	" .. "'Eu tambem odeio zumbis. Eles estao cobertos de pele e carne. E nojento!" pbold $) cyoa-text!
	skelknight-bomb 1 < skelknight-hammer 1 < skelknight-gladius 1 < or or if 
		"'Vou vender algumas das minhas armas sobressalentes, se lhe agradar."
	else
		"'Isso e tudo que posso poupar! Divirta se com sua cruzada."
	then pbold +crtext
	basic-tone foodamt
	choice( "Nao obrigado" )choice
	most-grating if 
		charchoice( " lhe diz para ATRASAR" fcoolit )choice: trade-skelknight-coolit
	then
	trunk-food 5 < if
		choice( "Pergunte por que ele precisa de comida" )choice: trade-skelknight-1
	else
		trunk-food 24 > skelknight-hammer 1 < and if
			choice( "Martelo de Cranio por 25 alimentos" )choice: trade-skelknight-25
		then
		trunk-food 14 > skelknight-gladius 1 < and if
			choice( "Gladius por 15 de comida" )choice: trade-skelknight-15
		then
		trunk-food 4 > skelknight-bomb 1 < and if
			choice( "5 Bomb Skulls por 5 de comida" )choice: trade-skelknight-5
		then
	then
;

// Sleepy Trader

0 value sleepy-ate

: sleepy-ate-food sleepy-ate 1 < if	1 else sleepy-ate 2 < if 2 else 4 then then ;

cyoa-choice: trade-rare-sleep-feed
	($ pname .. " ' enfia comida no rosto da pessoa adormecida." $) text!
	sleepy-ate-food -1 * trunk-food+
	' sleepy-ate ++
	sleepy-ate 2 > if
		($ pname .. "  comeca a entender " .. vhis .. "  tranquilidade zen perfeita e aprende o " .. "DORMINHOCO" rbold .. "  habilidade!" $) +crtext 
		statmod{ pchar .soundsleep }
		($ "Alguem percebe que " .. pname .. " ' tem enfiado grandes quantidades de comida na boca de uma pessoa adormecida." $) +crtext
		"EI! SAIA DESTE ACAMPAMENTO AGORA MESMO!" rbold +crtext
		($ Thegroup .. "  foge correndo do acampamento de comerciantes!" $) +crtext
		badfx quitmission		
	else
		($ vHe .. "  fala bobagens em " .. vhis .. "  dormir.		Ou pelo menos parece bobagem, no comeco. " .. pname .. " ' pensa sobre isso e percebe que e um conhecimento valioso." $) +crtext goodfx
		pchar rewardevent-random
	then
;

cyoa: trade-rare-sleep
	"Trader Dorminhoco" cyoa-title! iicon
	interactee .character@ to victim
	"Essa pessoa esta dormindo. Como ela pode dormir tao profundamente no meio do zombocalipse? E ainda por cima no chao." text!
	($ vHe .. "  deve ter tudo resolvido." $) +crtext
	sleepy-ate 1 > if
		($ RED .. "AVISO: Enfiar mais 4 alimentos em " .. vhis .. "  a boca vai te causar problemas, mas tambem dara " .. pname .. "  uma nova habilidade!" rbold $) +crtext
	else
		
	then
		choice( "Deixe " .. vhim .. "  sozinho" )choice
		trunk-food sleepy-ate-food 1 - > if
			pchar charchoice( ": Alimente " .. vhim .. " " .. sleepy-ate-food  .. "  comida" )choice: trade-rare-sleep-feed
		then
;

// Chicken

0 value chicken-egg

cyoa-choice: trade-chicken-6
	($ pname .. "  compra meia duzia de ovos. Zumbis cuidado!	" .. "BAKKAAAAAAAAAAAAW!" fbold $) cyoa-text!
	goodfx 6 weapondef{ WEAPON_EGG } trunk.weapon+ -6 trunk-food+ 6 ' chicken-egg +!
;

cyoa-choice: trade-chicken-3
	($ pname .. "  compra alguns ovos, adicionando ao arsenal de ovos.	" .. "BAKAAAAW!" rbold $) cyoa-text!
	goodfx 3 weapondef{ WEAPON_EGG } trunk.weapon+ -3 trunk-food+ 3 ' chicken-egg +!
;

cyoa-choice: trade-chicken-1
	($ pname .. "  compra um unico ovo precioso.	" .. "CACAREJO!" pbold $) cyoa-text!
	goodfx 1 weapondef{ WEAPON_EGG } trunk.weapon+ -1 trunk-food+ 1 ' chicken-egg +!
;

cyoa: trade-rare-chicken
	"Galinha Caipira" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	($ "Esta galinha abriu um negocio para si mesma!	" .. "CACAREJO!" pbold $) cyoa-text!
	chicken-egg 11 > if
		"A galinha ficou sem ovos!" +crtext clear-cyoa
	then;
	basic-tone +cr foodamt
	trunk-food 1 < if
		"A galinha tem pena de voce e lhe da um ovo de graca." +crtext
		goodfx 1 weapondef{ WEAPON_EGG }  trunk.weapon+ clear-cyoa
	else
		"Vende umos ovos muito frescos! 1 comida por ovos." +crtext
		choice( "Nao obrigado" )choice
		trunk-food 5 > chicken-egg 7 < and if
			choice( "Compra 6 ovos" )choice: trade-chicken-6
		then
		trunk-food 2 > chicken-egg 10 < and if
			choice( "Compra 3 ovos" )choice: trade-chicken-3
		then
		chicken-egg 12 < if
			choice( "Compra 1 ovos" )choice: trade-chicken-1
		then
	then
;

// Nerd

0 value nerd-green
0 value nerd-red

cyoa-choice: trade-nerd-red
	($ pname .. " compra uma torre vermelha. E a torre mais potente. Lance a e veja zombis serem arrasados." .. "No more zombie problems!
	Until the turret runs out of ammo..." pbold $) cyoa-text!
	goodfx 1 WEAPON_TURRETRED trunk.weapon+ -12 trunk-food+ ' nerd-red ++
;

cyoa-choice: trade-nerd-green
	($ pname .. " compra uma torre verde. Coloque a no chao e veja ela andar." .. "Podem ajudar muito em uma luta!" pbold $) cyoa-text!
	goodfx 1 WEAPON_TURRETGREEN trunk.weapon+ -6 trunk-food+ ' nerd-green ++
;

cyoa: trade-rare-nerd
	"Que nerd" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	($ "Este nerd esta cercado por brinquedos de nerd que tocam.	" .. "'Nao sou um nerd! Tenho muitas interesses! Vou para barras esportivas!'" pbold $) cyoa-text!
	nerd-green 1 > nerd-red 0 > and if
		"O nerd acabou de se esgotar!" +crtext clear-cyoa
	then;
	basic-tone +cr foodamt
	trunk-food 6 < if
		"'Voce nao pode comprar nada. O nerd te da uma bola pudente como premio." +crtext
		goodfx 1 WEAPON_PUKEYBALL trunk.weapon+ clear-cyoa
	else
		choice( "Nao obrigado" )choice
		trunk-food 5 > nerd-green 2 < and if
			choice( "Compre Torre Verde   6 alimentos" )choice: trade-nerd-green
		then
		trunk-food 11 > nerd-red 1 < and if
			choice( "Compre Torre Vermelha   12 alimentos" )choice: trade-nerd-red
		then
	then
;

} } 

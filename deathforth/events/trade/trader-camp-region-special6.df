uses paul/region-layout.df
// "../deathforth/rooms/TEMPROOM.txt" $load-rooms
	
" 
:map 2 
:tags prepperstairs
:scriptA prepperturret 
:size 7 10 
:off 10 3 
:tiles 
2222222
2333332
21A1A12
2111112
2112112
2112662
2112662
2112112
2222222
3333333

:map 2
:tags prepperend  
:scriptA preppertrader
:scriptB still-npc .redturretify
:scriptC bed 
:scriptD nkitchenset 
:scriptE diningset 
:scriptF nbathsetsmall 2 movex 
:scriptG prepper-magchance 
:scriptH livingset
:size 27 23 
:off 1 1 
:tiles 
000000000002222200000000000
000000000002333200000000000
000000000002111200000000000
000000000002666200000000000
000000000002666200000000000
222222000002666200000002222
233222222222666200000002332
2C12223333326662000222221C2
21133211F113111200023333112
2A111211G111111222221111112
211212222222212222221222112
211213333333313333331232112
2C12111111111111111112021C2
222222221222222222122222222
333233331333333333133332333
0002111111111D111111A112000
00021A111111111111111112000
000211111122222221111A12000
000211111122222221111112000
00021111E123333321H11112000
00021B111120000021111B12000
000222222220000022222222000
000333333330000033333333000
"
value PREPPERCAMP
PREPPERCAMP $parse-rooms

cyoa{

: prepperturret-grab clear-cyoa deleteme goodfx turretgreen drop southofcyoa ;

cyoa-choice: prepperturret-hack
	cmechanic+ if
		($ pname .. " francais" .. "GRATUIT TORRETTE DE DEFENSE!" rainbold $) cyoa-text! mechanicREV prepperturret-grab
	else
		($ pname .. "  essaie de faire usage de la force brute pour ramener la torrette dans son etat de transport!		" .. "GRAAAAAAAAAAAAAH!" rbold $) cyoa-text!
		cstrength+ if
			"Cela fonctionne a merveille!" +crtext prepperturret-grab
		else
			($ "'Cela fonctionne environ aussi bien que vous le prevoyez. En outre, " .. pname .. "  subit une choc assez fort." $) +crtext bitefx sting-- morale--
		then strengthREV 
	then
;

cyoa: prepperturret
	"Voleur de torrette" cyoa-title!
	picon!
	($ "Cette torrette fait de la torrure. Tourne, scanne pour ennemis, et ainsi de suite. Quel spectacle!	Un petit probleme: " .. "'C'est pas TA torrette!" rbold $) cyoa-text!
		choice( "Laisse le" )choice
		pchar charchoice( " essaye de voler cette torrette" )choice: prepperturret-hack
;

0 value pt1-1
0 value pt1-2
0 value pt1-3

cyoa-choice: trade-p1-3
	($ Thegroup .. " achete un jeteur de napalm modifie pour une capacite double!	" .. "Attention aux tirs amis. Serieusement, faites attention a vos coequipiers." pbold $) cyoa-text! goodfx
	100 WEAPON_NAPALM trunk.weapon+ -16 trunk-food+ someicon ' pt1-3 ++
;

cyoa-choice: trade-p1-2
	($ Thegroup .. " achete un bazooka modifie avec huit roquettes!" .. "Carnage TOTAL ! Jadore ca !" pbold $) cyoa-text! goodfx
	8 WEAPON_BAZOOKA trunk.weapon+ -12 trunk-food+ someicon ' pt1-2 ++
;

cyoa-choice: trade-p1-1
	($ Thegroup .. "  achete un lance grenades modifie avec 15 grenades !	" .. "Voyez combien de zombies vous pouvez ecraser en meme temps !" pbold $) cyoa-text! goodfx
	15 WEAPON_GLAUNCH trunk.weapon+ -12 trunk-food+ someicon ' pt1-1 ++
;

cyoa: preptrader1
	"Armes de Carnage XL" cyoa-title!
	($ "J'ai deja modifie toutes ces armes DEVASTATRICES pour avoir une capacite double ou plus! Vous ne pouvez pas les obtenir ailleurs!" pbold $) cyoa-text!
	trunk-food 11 > if "La on commence a parler." +crtext then
	basic-tone foodamt
	trunk-food 12 < if
		($ Thegroup .. " ' est DEVASTE d'apprendre qu'il ne peut rien se permettre." $) +crtext
	else
		choice( "Partir pour linstant" )choice back-esc
		trunk-food 15 > pt1-3 1 < and if
			choice( "'Lance flammes XL   100 palmiers   16 nourritures" )choice: trade-p1-3
		then
		trunk-food 11 > pt1-2 1 < and if
			choice( "Bazooka XL   8 tirs   12 nourritures" )choice: trade-p1-2
		then
		trunk-food 11 > pt1-1 1 < and if
			choice( "Lance grenades XL   15 tirs   12 nourritures" )choice: trade-p1-1
		then
	then
;

0 value pt2-1
0 value pt2-2
0 value pt2-3

cyoa-choice: trade-p2-3
	($ Thegroup .. "  achete le " .. "Minigun !" fbold .. "  Cest le summum de la livraison de munitions pour fusil !" .. "Je lai vendu avec une remise massive parce que jai accidentellement eu des taches de beurre de cacahuete dessus." pbold $) cyoa-text! goodfx
	1 WEAPON_MINIGUN trunk.weapon+ -35 trunk-food+ someicon ' pt2-3 ++
;
	
cyoa-choice: trade-p2-2
	($ Thegroup .. "  achete le " .. "BIG DAWG" rbold .. "'. C'est un fusil de chasse personnalise unique en son genre ! Chaque tir est une enorme explosion d'eclaboussures de zombies." $) cyoa-text! goodfx ' pt2-2 ++
	1 WEAPON_DAWG trunk.weapon+ -20 trunk-food+ picon
;

cyoa-choice: trade-p2-1
	($ Thegroup .. "  achete le " .. "PISTOLET DORE" rbold .. ". Il a une puissance et une penetration de tir inhabituelles pour un pistolet, en plus il est beau et brillant." $) cyoa-text! goodfx ' pt2-1 ++
	1 WEAPON_GOLDPISTOL trunk.weapon+ -15 trunk-food+ picon
;

cyoa: preptrader2
	"Armes de pointe" cyoa-title!
	($ "Bienvenue dans " pbold .. "PARADIS DES PREPARATEURS !" rainbold .. " ' Je ne vends que les MEILLEURES armes ! Le plus gros fusil de chasse, le fusil automatique le plus rapide et le pistolet le plus brillant !" pbold $) cyoa-text!
	basic-tone foodamt
	trunk-food 15 < if
		($ Thegroup .. "  manque de nourriture pour acheter quoi que ce soit !" $) +crtext
		($ "Pas de sou" pbold .. " Le commercant cligne de lil puis donne " .. thegroup .. " le la l les " .. "PETIT CHIEN!" rbold $) +crtext goodfx 1 WEAPON_LILDAWG trunk.weapon+ clear-cyoa
	else
		choice( "Partir pour linstant" )choice back-esc
		trunk-food 34 > pt2-3 1 < and if
			choice( "Minigun" )choice: trade-p2-3
		then
		trunk-food 19 > pt2-2 1 < and if
			choice( "Shotgun shell" )choice: trade-p2-2
		then
		trunk-food 14 > pt2-1 1 < and if
			choice( "1 bullet" )choice: trade-p2-1
		then
	then
;

0 value pt3-1
0 value pt3-2
0 value pt3-3

cyoa-choice: trade-p3-3
	($ Thegroup .. "  achete le " .. "Spray de guerison XL!" rbold .. " Utilisez le pour vous soigner, ou pour infliger des degats a un groupe de zombies." .. "Parfois, jen mets meme sur ma nourriture." pbold $) cyoa-text! goodfx
	10 WEAPON_HEALSPRAY trunk.weapon+ -10 trunk-food+ someicon ' pt3-3 ++
;
	
cyoa-choice: trade-p3-2
	($ Thegroup .. "  achete un " .. "COUTEAU DE SURVIE" rbold .. "'. C'est leger, facile a balancer meme si vous etes epuise, et peut constamment frapper deux zombies en un seul coup." $) cyoa-text! goodfx ' pt3-2 ++
	1 WEAPON_SURVKNIFE trunk.weapon+ -6 trunk-food+ picon
;

cyoa-choice: trade-p3-1
	($ Thegroup .. "  achete un " .. "ARC AVEC 50 FLECHES" rbold .. ". Les arcs beneficient de chaque statistique: force, condition physique et tir, ce qui rend les arcs plus puissants." $) cyoa-text! goodfx ' pt3-1 ++
	50 WEAPON_BOW trunk.weapon+ -5 trunk-food+ picon
;

cyoa: preptrader3
	"Equipement de survie" cyoa-title!
	($ "'J'echange du materiel de survie abordable et efficace! Les arcs sont parfaits pour abattre des zombies en silence, surtout si vous etes suffisamment athletique pour bien les utiliser. Si vous avez la nourriture, j'ai beaucoup de fleches supplementaires en stock." pbold $) cyoa-text!
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. "  na pas assez de nourriture pour acheter un arc!" $) +crtext
		($ "Le commercant prend pitie de " .. thegroup .. "  et distribue " .. "un couteau de survie gratuit et un arc avec 12 fleches!" rbold $) +crtext goodfx 12 WEAPON_BOW trunk.weapon+ 1 WEAPON_SURVKNIFE trunk.weapon+ clear-cyoa
	else
		choice( "Partir pour linstant" )choice back-esc
		trunk-food 9 > pt3-3 1 < and if
			choice( "Spray de guerison XL   10 pulverisations   10 aliments" )choice: trade-p3-3
		then
		trunk-food 5 > pt3-2 2 < and if
			choice( "Couteau de survie   6 aliments" )choice: trade-p3-2
		then
		trunk-food 4 > pt3-1 8 < and if
			choice( "Arc avec 50 fleches   5 aliments" )choice: trade-p3-1
		then
	then
;

: delta-good args( chara )
	"Bon " pchar .name@ $cat
	"Mauvais " pchar .name@ $cat pchar .name!
;

: delta-cool args( chara )
	"Cool " <- chara .name@ $cat
;

: deltrarecruit-rename
	ricon recruitee .name! ($ "DELTA" pbold .. "''s'appelle maintenant " .. rname .. "!" $) cyoa-text! goodfx
;

cyoa-choice: deltarecruit-name1
	"Francais" deltrarecruit-rename
	"Vingt et un millions quatre cent quarante six mille trois cent quarante cinq" +crtext
;

cyoa-choice: deltarecruit-name2
	"Harold Hamm" deltrarecruit-rename 
	$( rname .. " quatre vingt neuf millions huit cent quatre vingt douze mille cinq cent quarante quatre" $) +crtext 
;

cyoa-choice: deltarecruit-name3
	cchar delta-cool deltrarecruit-rename 
	($ "Cinq millions sept cent trois mille deux" $) +crtext
;

cyoa-choice: deltarecruit-name4
	cchar delta-good deltrarecruit-rename 
	"On million one hundred neuf mille six cent soixante six" +crtext
	($ pname .. " quatre vingt quatre mille cinq cent dix neuf" $) +crtext
	pchar .morale--
;

cyoa-bridge: deltarecruit-name
	ricon "Recrute DELTA   Dix rations de nourriture" text!
	choice( "Francais" )choice: deltarecruit-name1
	choice( "Harold Hamm" )choice: deltarecruit-name2
	choice( "Cool " .. rname )choice: deltarecruit-name3
	choice( "Bon " .. pchar .name@ )choice: deltarecruit-name4
;

cyoa-bridge: deltarecruit-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	($ "Now without a cause, DELTA decides to follow a new master...... YOU..........." rbold $) cyoa-text!
	-10 trunk-food+ goodfx
	
	team-full? if
		($ "Le groupe est plein. Qui souhaitez vous remplacer pour faire de la place a " .. rname .. "?" $) +crtext
		recruit-replace?
	else
		recruited
		($ "Voulez vous donner " .. rname .. " pROJET DELTA" $) +crtext
		choice( "NON" )choice
		choice( "OUI" )choice: deltarecruit-name
	then
	
	WEAPON_SHOTGUN recruitee .weaponb! 	20 LOOT_SHOTGUN_SHELL recruitee .lootamt!
;

cyoa: deltarecruit
	"PROJET: DELTA" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	($ cname .. " is a supersoldier dog trained by the government.
	Now that the government has fallen, DELTA seeks a new master......... for the right price........" rbold $) cyoa-text!
	" (Environ dix boites de nourriture)" bbold +text basic-tone
	trunk-food 9 > if
		choice( "Partir pour linstant" )choice
		choice( "Recrute DELTA   Dix rations de nourriture" )choice: deltarecruit-go
	else
		choice( "'Ne peut pas se passer de la nourriture" )choice
	then		
;
	
}

roomgen{


: .pt1 args( ch ) 
	10 <- ch .hattype! // Helmet, modded explosives
;
: .pt2 args( ch ) 
	11 <- ch .hattype! // General's Hat, fancy weapons
;
: .pt3 args( ch ) 
	12 <- ch .hattype! // Camo Hat, cheap survival
;

: preptradercommon
	still-npc regiondef{ ' trader-event lt .events! 1 lt .timer! }
	
;

: preptrader1
	preptradercommon .pt1 cyoa{ ' preptrader1 cyoa! }
;

: preptrader2
	preptradercommon .pt2 cyoa{ ' preptrader2 cyoa! }
;

: preptrader3
	preptradercommon .pt3 cyoa{ ' preptrader3 cyoa! }
;

: preptrader4
	preptradercommon .deltaify cyoa{ ' deltarecruit cyoa! }
;

		0 stack
			' preptrader1 shove
			' preptrader2 shove
			' preptrader3 shove
			' preptrader4 shove
		value preptrader-base
		preptrader-base shuffles var preptrader-picker

: preppertrader
	preptrader-picker depths 0= if preptrader-base shuffles to preptrader-picker then pops nip execute ;

: prepperturret
	still-npc .greenturretify cyoa{ ' prepperturret cyoa! } ;

}

locdef{

location: preppercamp
		max-rooms: 2
		entry-tags: prepperstairs
		script: set-theme-bunker
		pre-script: set-theme-bunker
		post-script: nad 0 %zombs
		
		: prepstart
			s edge-inc s random-edge-door "pret a etre envoye" only-with-door
		;
		
		
		room-choice: prepperstairs
			room-weight: 10
			room-count: 1
			room-post-script: prepstart
			filter-only
		
		room-choice: prepperend
			room-weight: 10
			room-count: 1
			filter-only

}
		
root{

: prepper-magchance
	20 %chance if
		magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! drift
	then
;

}
		
map{ regiondef{

trade-camp-reset

0 value gbx
0 value gby
0 value gbx2
0 value gby2
0 value bfx
0 value bfy

: bldg tanchor!
	"camp de preparation" $picklocid addloc

	prefab{ 5 bunker }
	
;

: preplogs-big
	2 for biglog 50 %chance if 40 160 else 270 390 then rnd 30 120 rnd lt .pos! next
	ultimalog 10 420 rnd 10 40 rnd lt .pos!
	// lt-here 60 rdrift
;

: pt
	
	xyp.i char 1 = if xyp.x to bfx then;
	xyp.i char 2 = if bfx xyp.y xyp.x barbfence then;

	xyp.i char t = if pine lt-here drift then;
	// xyp.i char S = if trader-food-for-gas then;
	xyp.i char U = if tnomey-spawn then;
	xyp.i char W = if trader-weapon-buyer then;
	
	xyp.i char L = if 2 for log lt-here 40 rdrift next then;
	xyp.i char B = if preplogs-big then;
	
	xyp.i char @ = if xyp.xy bldg then;

	xyp.i char 5 = if xyp.xy to gby to gbx then;
	xyp.i char 6 = if gbx 1- gby xyp.xy swap 1+ swap 4dup gravelbox grasshole then;
	
	xyp.i char < = if xyp.xy to gby2 to gbx2 then;
	xyp.i char > = if gbx2 1- gby2 xyp.xy map{ 4dup dirtbox grasshole } then;
;

"
...........................
..t.t.t.t.t.t.t.t.t.t.t.t..
.......................B...
..t.t.....5...........t.t..
...........................
..t.t.................t.t..
...........................
..t.t..L...........L..t.t..
...........@...............
..t.t.................t.t..
................6..........
..t.t.................t.t..
...........................
..t.t.................t.t..
...........................
.<.........................
1..........2....1.........2
......W.............U......
...........................
...........>...............
...........................
...........................
...........................
...........................
...........................
...........................
..........................."
constant maplayout

: specialcamp-6
	0 -1 maplayout xyplot: pt
;


: site-setup

	// Clear the map with grass
	-1 -1 w h grassbox

	// Plot trader stuff
	specialcamp-6

	// Add dirt road at bottom
	-1 h 5 - w h 4dup map{ dirtbox grasshole }

	1 no_time!
	7 14 rnd o'clock
	50 %chance if 1 2 frnd weather! -1 -2 frnd wind_force! else 1 2 frnd wind_force! then
	
	mission{ no-edge-spawns }
	
	18 %mv-up
	30 %chars-up
;

	 region: specialcamp-6
		w: maplayout layoutw
		h: maplayout layouth

		trade-mode

		post-script: site-setup
} }

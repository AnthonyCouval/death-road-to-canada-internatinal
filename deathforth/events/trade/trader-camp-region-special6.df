uses paul/region-layout.df
// "../deathforth/rooms/TEMPROOM.txt" $load-rooms
	
" 
:map 2 
:tags prepperstairs
:scriptA prepperturret 
:size 7 10 
:off 10 3 
:tiles 
2222222
2333332
21A1A12
2111112
2112112
2112662
2112662
2112112
2222222
3333333

:map 2
:tags prepperend  
:scriptA preppertrader
:scriptB still-npc .redturretify
:scriptC bed 
:scriptD nkitchenset 
:scriptE diningset 
:scriptF nbathsetsmall 2 movex 
:scriptG prepper-magchance 
:scriptH livingset
:size 27 23 
:off 1 1 
:tiles 
000000000002222200000000000
000000000002333200000000000
000000000002111200000000000
000000000002666200000000000
000000000002666200000000000
222222000002666200000002222
233222222222666200000002332
2C12223333326662000222221C2
21133211F113111200023333112
2A111211G111111222221111112
211212222222212222221222112
211213333333313333331232112
2C12111111111111111112021C2
222222221222222222122222222
333233331333333333133332333
0002111111111D111111A112000
00021A111111111111111112000
000211111122222221111A12000
000211111122222221111112000
00021111E123333321H11112000
00021B111120000021111B12000
000222222220000022222222000
000333333330000033333333000
"
value PREPPERCAMP
PREPPERCAMP $parse-rooms

cyoa{

: prepperturret-grab clear-cyoa deleteme goodfx turretgreen drop southofcyoa ;

cyoa-choice: prepperturret-hack
	cmechanic+ if
		($ pname .. " usa sus habilidades para descubrir como restablecer la torreta a su estado desplegable!		" .. "TORRETA GRATIS!" rainbold $) cyoa-text! mechanicREV prepperturret-grab
	else
		($ pname .. " intenta usar la fuerza bruta para aplastar la torreta de nuevo a su estado de transporte		" .. "GRAAAAAAAAAAAAAH!" rbold $) cyoa-text!
		cstrength+ if
			"Sorprendentemente, esto realmente funciona!" +crtext prepperturret-grab
		else
			($ "'Esto funciona tan bien como cabria esperar. Ademas, " .. pname .. " recibe una descarga bastante fuerte." $) +crtext bitefx sting-- morale--
		then strengthREV 
	then
;

cyoa: prepperturret
	"Ladron de torretas" cyoa-title!
	picon!
	($ "Esta torreta esta haciendo cosas de torreta. Zumbando, buscando hostiles, etc. Guau, mirala funcionar! Un pequeno problema: " .. "'No es TU torreta!" rbold $) cyoa-text!
		choice( "Dejalo en paz" )choice
		pchar charchoice( " intenta robar la torreta" )choice: prepperturret-hack
;

0 value pt1-1
0 value pt1-2
0 value pt1-3

cyoa-choice: trade-p1-3
	($ Thegroup .. " compra un lanzallamas Napalm modificado para capacidad doble!	" .. "Habla sobre fuego amigo! En serio, ten cuidado con tus companeros de equipo." pbold $) cyoa-text! goodfx
	100 WEAPON_NAPALM trunk.weapon+ -16 trunk-food+ someicon ' pt1-3 ++
;

cyoa-choice: trade-p1-2
	($ Thegroup .. " compra una bazuca modificada con ocho cohetes!	" .. "Carnage TOTAL! Me encanta!" pbold $) cyoa-text! goodfx
	8 WEAPON_BAZOOKA trunk.weapon+ -12 trunk-food+ someicon ' pt1-2 ++
;

cyoa-choice: trade-p1-1
	($ Thegroup .. " compra un lanzagranadas modificado con 15 granadas!	" .. "Mira cuantos zombis puedes aplastar a la vez!" pbold $) cyoa-text! goodfx
	15 WEAPON_GLAUNCH trunk.weapon+ -12 trunk-food+ someicon ' pt1-1 ++
;

cyoa: preptrader1
	"Armas de carniceria XL" cyoa-title!
	($ "'Ya he modificado todas estas armas ya DEVASTADORAS para que tengan doble capacidad o mas! No puedes conseguirlas en ningun otro lado!" pbold $) cyoa-text!
	trunk-food 11 > if "'AHORA estamos hablando." +crtext then
	basic-tone foodamt
	trunk-food 12 < if
		($ Thegroup .. " 'esta DEVASTADO al saber que no puede permitirse nada." $) +crtext
	else
		choice( "Dejalo por ahora" )choice back-esc
		trunk-food 15 > pt1-3 1 < and if
			choice( "'Lanzallamas Napalm XL   100 'palma   16 alimentos" )choice: trade-p1-3
		then
		trunk-food 11 > pt1-2 1 < and if
			choice( "Bazuca XL   8 disparos   12 alimentos" )choice: trade-p1-2
		then
		trunk-food 11 > pt1-1 1 < and if
			choice( "Lanzagranadas XL   15 disparos   12 alimentos" )choice: trade-p1-1
		then
	then
;

0 value pt2-1
0 value pt2-2
0 value pt2-3

cyoa-choice: trade-p2-3
	($ Thegroup .. "  compra la " .. "Ametralladora!" fbold .. "  Es el pinaculo de la entrega de municion para rifles!	" .. "Lo vendi con un descuento enorme porque accidentalmente le puse manchas de mantequilla de mani." pbold $) cyoa-text! goodfx
	1 WEAPON_MINIGUN trunk.weapon+ -35 trunk-food+ someicon ' pt2-3 ++
;
	
cyoa-choice: trade-p2-2
	($ Thegroup .. "  compra la " .. "GRAN PERRO" rbold .. "'. Es una escopeta personalizada unica en su clase! Cada disparo es una explosion masiva de salpicaduras de zombies." $) cyoa-text! goodfx ' pt2-2 ++
	1 WEAPON_DAWG trunk.weapon+ -20 trunk-food+ picon
;

cyoa-choice: trade-p2-1
	($ Thegroup .. "  compra la " .. "PISTOLA DORADA" rbold .. ". Tiene una cantidad inusual de poder y penetracion de disparo para una pistola, ademas de que es bonita y brillante." $) cyoa-text! goodfx ' pt2-1 ++
	1 WEAPON_GOLDPISTOL trunk.weapon+ -15 trunk-food+ picon
;

cyoa: preptrader2
	"Armas pinaculo" cyoa-title!
	($ "Bienvenido a " pbold .. "PARAISO DEL PREPARADOR!" rainbold .. " ' Estoy vendiendo solo las MEJORES armas! La escopeta mas grande, el rifle automatico mas rapido y la pistola mas brillante!" pbold $) cyoa-text!
	basic-tone foodamt
	trunk-food 15 < if
		($ Thegroup .. "  no tiene suficiente comida para comprar nada!" $) +crtext
		($ "'No te preocupes, te tengo cubierto." pbold .. "  El comerciante guina un ojo y luego le da " .. thegroup .. "  el " .. "'PERRITO PEQUENO!" rbold $) +crtext goodfx 1 WEAPON_LILDAWG trunk.weapon+ clear-cyoa
	else
		choice( "Dejalo por ahora" )choice back-esc
		trunk-food 34 > pt2-3 1 < and if
			choice( "Minigun   35 comida" )choice: trade-p2-3
		then
		trunk-food 19 > pt2-2 1 < and if
			choice( "Perro Grande Escopeta   20 comida" )choice: trade-p2-2
		then
		trunk-food 14 > pt2-1 1 < and if
			choice( "Pistola de Oro   15 comida" )choice: trade-p2-1
		then
	then
;

0 value pt3-1
0 value pt3-2
0 value pt3-3

cyoa-choice: trade-p3-3
	($ Thegroup .. "  compra la " .. "Spray de Curacion XL!" rbold .. "  Usela para curarte a ti mismo, o para danar a un grupo de zombis.	" .. "A veces, incluso la esprayo en mi comida." pbold $) cyoa-text! goodfx
	10 WEAPON_HEALSPRAY trunk.weapon+ -10 trunk-food+ someicon ' pt3-3 ++
;
	
cyoa-choice: trade-p3-2
	($ Thegroup .. "  compra un " .. "Cuchillo de Supervivencia" rbold .. "'. Es ligero, facil de golpear aun cuando estas agotado, y puede cortar a dos zombis en una sola hachazo." $) cyoa-text! goodfx ' pt3-2 ++
	1 WEAPON_SURVKNIFE trunk.weapon+ -6 trunk-food+ picon
;

cyoa-choice: trade-p3-1
	($ Thegroup .. "  compra un " .. "ARCO CON 50 FLECHAS" rbold .. ". Los arcos se benefician de cada estadistica: fuerza, aptitud fisica y tiro todos los hace mas poderosos." $) cyoa-text! goodfx ' pt3-1 ++
	50 WEAPON_BOW trunk.weapon+ -5 trunk-food+ picon
;

cyoa: preptrader3
	"Equipo de Supervivencia" cyoa-title!
	($ "'Estoy intercambiando algunos articulos de supervivencia asequibles y efectivos!	Los arcos son excelentes para matar a los zombis silenciosamente, especialmente si eres atletico lo suficiente como para usarlos bien. Si tienes la comida, tengo muchas flechas adicionales en el inventario." pbold $) cyoa-text!
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. "  carece de comida suficiente para comprar incluso un arco!" $) +crtext
		($ "El comerciante toma algo de lastima por " .. thegroup .. " y reparte " .. "un cuchillo de supervivencia gratis, y un arco con 12 flechas!" rbold $) +crtext goodfx 12 WEAPON_BOW trunk.weapon+ 1 WEAPON_SURVKNIFE trunk.weapon+ clear-cyoa
	else
		choice( "Dejalo por ahora" )choice back-esc
		trunk-food 9 > pt3-3 1 < and if
			choice( "1010  1" )choice: trade-p3-3
		then
		trunk-food 5 > pt3-2 2 < and if
			choice( "Knife" )choice: trade-p3-2
		then
		trunk-food 4 > pt3-1 8 < and if
			choice( "5 arrows" )choice: trade-p3-1
		then
	then
;

: delta-good args( chara )
	"Bueno " pchar .name@ $cat
	"Malo " pchar .name@ $cat pchar .name!
;

: delta-cool args( chara )
	"Genial " <- chara .name@ $cat
;

: deltrarecruit-rename
	ricon recruitee .name! ($ "DELTA" pbold .. "''s nombre ahora es " .. rname .. "!" $) cyoa-text! goodfx
;

cyoa-choice: deltarecruit-name1
	"Smellberto" deltrarecruit-rename
	"Que nombre nuevo tan absolutamente horrible! Y, sin embargo, tiene un toque de exotico." +crtext
;

cyoa-choice: deltarecruit-name2
	"Harry Hamm" deltrarecruit-rename 
	$( rname .. " ' era uno de los nombres de perro mas populares, justo antes del Zombocalypse. No debe confundirse con el poderoso nombre humano, 'Harry Ham'." $) +crtext 
;

cyoa-choice: deltarecruit-name3
	cchar delta-cool deltrarecruit-rename 
	($ "Este nombre es aun mas genial, de alguna manera." $) +crtext
;

cyoa-choice: deltarecruit-name4
	cchar delta-good deltrarecruit-rename 
	"Que gran nombre!" +crtext
	($ pname .. "  se siente infeliz." $) +crtext
	pchar .morale--
;

cyoa-bridge: deltarecruit-name
	ricon "'Como deberia ser el nuevo nombre del perro supersoldado?" text!
	choice( "Smellberto" )choice: deltarecruit-name1
	choice( "Harry Hamm" )choice: deltarecruit-name2
	choice( "Genial " .. rname )choice: deltarecruit-name3
	choice( "Bueno " .. pchar .name@ )choice: deltarecruit-name4
;

cyoa-bridge: deltarecruit-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	($ "Now without a cause, DELTA decides to follow a new master...... YOU..........." rbold $) cyoa-text!
	-10 trunk-food+ goodfx
	
	team-full? if
		($ "El grupo esta lleno. A quien te gustaria reemplazar para hacerle un lugar a " .. rname .. "?" $) +crtext
		recruit-replace?
	else
		recruited
		($ "Quieres darle a " .. rname .. "  un nuevo nombre?" $) +crtext
		choice( "NO" )choice
		choice( "SI" )choice: deltarecruit-name
	then
	
	WEAPON_SHOTGUN recruitee .weaponb! 	20 LOOT_SHOTGUN_SHELL recruitee .lootamt!
;

cyoa: deltarecruit
	"PROYECTO: DELTA" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	($ cname .. " is a supersoldier dog trained by the government.
	Now that the government has fallen, DELTA seeks a new master......... for the right price........" rbold $) cyoa-text!
	" " bbold +text basic-tone
	trunk-food 9 > if
		choice( "Dejalo por ahora" )choice
		choice( "Recluta DELTA   10 alimentos" )choice: deltarecruit-go
	else
		choice( "'No puedo prescindir de la comida" )choice
	then		
;
	
}

roomgen{


: .pt1 args( ch ) 
	10 <- ch .hattype! // Helmet, modded explosives
;
: .pt2 args( ch ) 
	11 <- ch .hattype! // General's Hat, fancy weapons
;
: .pt3 args( ch ) 
	12 <- ch .hattype! // Camo Hat, cheap survival
;

: preptradercommon
	still-npc regiondef{ ' trader-event lt .events! 1 lt .timer! }
	
;

: preptrader1
	preptradercommon .pt1 cyoa{ ' preptrader1 cyoa! }
;

: preptrader2
	preptradercommon .pt2 cyoa{ ' preptrader2 cyoa! }
;

: preptrader3
	preptradercommon .pt3 cyoa{ ' preptrader3 cyoa! }
;

: preptrader4
	preptradercommon .deltaify cyoa{ ' deltarecruit cyoa! }
;

		0 stack
			' preptrader1 shove
			' preptrader2 shove
			' preptrader3 shove
			' preptrader4 shove
		value preptrader-base
		preptrader-base shuffles var preptrader-picker

: preppertrader
	preptrader-picker depths 0= if preptrader-base shuffles to preptrader-picker then pops nip execute ;

: prepperturret
	still-npc .greenturretify cyoa{ ' prepperturret cyoa! } ;

}

locdef{

location: preppercamp
		max-rooms: 2
		entry-tags: prepperstairs
		script: set-theme-bunker
		pre-script: set-theme-bunker
		post-script: nad 0 %zombs
		
		: prepstart
			s edge-inc s random-edge-door "preparador" only-with-door
		;
		
		
		room-choice: prepperstairs
			room-weight: 10
			room-count: 1
			room-post-script: prepstart
			filter-only
		
		room-choice: prepperend
			room-weight: 10
			room-count: 1
			filter-only

}
		
root{

: prepper-magchance
	20 %chance if
		magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! drift
	then
;

}
		
map{ regiondef{

trade-camp-reset

0 value gbx
0 value gby
0 value gbx2
0 value gby2
0 value bfx
0 value bfy

: bldg tanchor!
	"campamento de preparadores" $picklocid addloc

	prefab{ 5 bunker }
	
;

: preplogs-big
	2 for biglog 50 %chance if 40 160 else 270 390 then rnd 30 120 rnd lt .pos! next
	ultimalog 10 420 rnd 10 40 rnd lt .pos!
	// lt-here 60 rdrift
;

: pt
	
	xyp.i char 1 = if xyp.x to bfx then;
	xyp.i char 2 = if bfx xyp.y xyp.x barbfence then;

	xyp.i char t = if pine lt-here drift then;
	// xyp.i char S = if trader-food-for-gas then;
	xyp.i char U = if tnomey-spawn then;
	xyp.i char W = if trader-weapon-buyer then;
	
	xyp.i char L = if 2 for log lt-here 40 rdrift next then;
	xyp.i char B = if preplogs-big then;
	
	xyp.i char @ = if xyp.xy bldg then;

	xyp.i char 5 = if xyp.xy to gby to gbx then;
	xyp.i char 6 = if gbx 1- gby xyp.xy swap 1+ swap 4dup gravelbox grasshole then;
	
	xyp.i char < = if xyp.xy to gby2 to gbx2 then;
	xyp.i char > = if gbx2 1- gby2 xyp.xy map{ 4dup dirtbox grasshole } then;
;

"
...........................
..t.t.t.t.t.t.t.t.t.t.t.t..
.......................B...
..t.t.....5...........t.t..
...........................
..t.t.................t.t..
...........................
..t.t..L...........L..t.t..
...........@...............
..t.t.................t.t..
................6..........
..t.t.................t.t..
...........................
..t.t.................t.t..
...........................
.<.........................
1..........2....1.........2
......W.............U......
...........................
...........>...............
...........................
...........................
...........................
...........................
...........................
...........................
..........................."
constant maplayout

: specialcamp-6
	0 -1 maplayout xyplot: pt
;


: site-setup

	// Clear the map with grass
	-1 -1 w h grassbox

	// Plot trader stuff
	specialcamp-6

	// Add dirt road at bottom
	-1 h 5 - w h 4dup map{ dirtbox grasshole }

	1 no_time!
	7 14 rnd o'clock
	50 %chance if 1 2 frnd weather! -1 -2 frnd wind_force! else 1 2 frnd wind_force! then
	
	mission{ no-edge-spawns }
	
	18 %mv-up
	30 %chars-up
;

	 region: specialcamp-6
		w: maplayout layoutw
		h: maplayout layouth

		trade-mode

		post-script: site-setup
} }

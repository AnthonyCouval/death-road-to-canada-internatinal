uses paul/region-layout.df
// "../deathforth/rooms/TEMPROOM.txt" $load-rooms
	
" 
:map 2 
:tags prepperstairs
:scriptA prepperturret 
:size 7 10 
:off 10 3 
:tiles 
2222222
2333332
21A1A12
2111112
2112112
2112662
2112662
2112112
2222222
3333333

:map 2
:tags prepperend  
:scriptA preppertrader
:scriptB still-npc .redturretify
:scriptC bed 
:scriptD nkitchenset 
:scriptE diningset 
:scriptF nbathsetsmall 2 movex 
:scriptG prepper-magchance 
:scriptH livingset
:size 27 23 
:off 1 1 
:tiles 
000000000002222200000000000
000000000002333200000000000
000000000002111200000000000
000000000002666200000000000
000000000002666200000000000
222222000002666200000002222
233222222222666200000002332
2C12223333326662000222221C2
21133211F113111200023333112
2A111211G111111222221111112
211212222222212222221222112
211213333333313333331232112
2C12111111111111111112021C2
222222221222222222122222222
333233331333333333133332333
0002111111111D111111A112000
00021A111111111111111112000
000211111122222221111A12000
000211111122222221111112000
00021111E123333321H11112000
00021B111120000021111B12000
000222222220000022222222000
000333333330000033333333000
"
value PREPPERCAMP
PREPPERCAMP $parse-rooms

cyoa{

: prepperturret-grab clear-cyoa deleteme goodfx turretgreen drop southofcyoa ;

cyoa-choice: prepperturret-hack
	cmechanic+ if
		($ pname .. " utilisez vos competences pour determiner comment remettre le tourelle en etat deployable !." .. "LIBRE TOURRETTE !" rainbold $) cyoa-text! mechanicREV prepperturret-grab
	else
		($ pname .. " essayez de faire usage de la force brutale pour rentrer le canon dans son etat portant !." .. "GRAAAAAAAAAAAAAH!" rbold $) cyoa-text!
		cstrength+ if
			"Incrediblement, cela fonctionne bien !" +crtext prepperturret-grab
		else
			($ "Ceci fonctionne environ aussi bien qu'on s'y attendait. en outre, " .. pname .. " se fait electrocuter assez gravement." $) +crtext bitefx sting-- morale--
		then strengthREV 
	then
;

cyoa: prepperturret
	"Voleur de tourelle" cyoa-title!
	picon!
	($ "Cette tourelle effectue des operations de tourelle. tournoyant, scanne des hotes ennemis, et cetera. heureusement, il y a juste un petit probleme : " .. "C'est pas tour retour votre !" rbold $) cyoa-text!
		choice( "Gache les choses" )choice
		pchar charchoice( " essaye de voler le canon" )choice: prepperturret-hack
;

0 value pt1-1
0 value pt1-2
0 value pt1-3

cyoa-choice: trade-p1-3
	($ Thegroup .. " achete une napalmeur modifie a double capacite !" .. "Prise de garde pour le feu ami ! vraiment, garder une vigilance particuliere envers vos camarades d'equipe." pbold $) cyoa-text! goodfx
	100 WEAPON_NAPALM trunk.weapon+ -16 trunk-food+ someicon ' pt1-3 ++
;

cyoa-choice: trade-p1-2
	($ Thegroup .. " achete une bazooka modifiee avec huit roquettes !" .. "Une totalite de destruction ! je l'aime !" pbold $) cyoa-text! goodfx
	8 WEAPON_BAZOOKA trunk.weapon+ -12 trunk-food+ someicon ' pt1-2 ++
;

cyoa-choice: trade-p1-1
	($ Thegroup .. " achetez un lance-grenades modifie avec quinze grenades !" .. "Regardez comment bien des zombies vous pouvez ecraser en meme temps !" pbold $) cyoa-text! goodfx
	15 WEAPON_GLAUNCH trunk.weapon+ -12 trunk-food+ someicon ' pt1-1 ++
;

cyoa: preptrader1
	"Armes de destruction carnage xl" cyoa-title!
	($ "Vous avez modifie toutes ces arme devastatrices a double capacite ou plus ! vous ne les pouvez obtenir nulle part ailleurs !" pbold $) cyoa-text!
	trunk-food 11 > if "Points gagnes aux zombos !" +crtext then
	basic-tone foodamt
	trunk-food 12 < if
		($ Thegroup .. " est devaste" $) +crtext
	else
		choice( "Laissez-le pour le moment" )choice back-esc
		trunk-food 15 > pt1-3 1 < and if
			choice( "Lance-roquettes napalm xl - 100 palmes - 16 pieds" )choice: trade-p1-3
		then
		trunk-food 11 > pt1-2 1 < and if
			choice( "Rochettes bazooka xl - 8 coups - 12 aliments" )choice: trade-p1-2
		then
		trunk-food 11 > pt1-1 1 < and if
			choice( "Lance-granades xl - 15 tirs - 12 nourriture" )choice: trade-p1-1
		then
	then
;

0 value pt2-1
0 value pt2-2
0 value pt2-3

cyoa-choice: trade-p2-3
	($ Thegroup .. " achete " .. "Credits !" fbold .. " C'est le sommet de la livraison de munitions de fusil !" .. "J'ai vendu cela a un prix considerablement reduit en raison de taches de beurre de karite accidentelles." pbold $) cyoa-text! goodfx
	1 WEAPON_MINIGUN trunk.weapon+ -35 trunk-food+ someicon ' pt2-3 ++
;
	
cyoa-choice: trade-p2-2
	($ Thegroup .. " achete " .. "PERD" rbold .. "c'est un fusil a chaque coup unique, chaque tir est une explosion de dechiquetage de zombies massif." $) cyoa-text! goodfx ' pt2-2 ++
	1 WEAPON_DAWG trunk.weapon+ -20 trunk-food+ picon
;

cyoa-choice: trade-p2-1
	($ Thegroup .. " achete " .. "PISTO D'OR" rbold .. "cette arme a feu a pistolet presente une puissance et une penetration de munition inusitee pour un pistolet, et elle est agreablement brillante." $) cyoa-text! goodfx ' pt2-1 ++
	1 WEAPON_GOLDPISTOL trunk.weapon+ -15 trunk-food+ picon
;

cyoa: preptrader2
	"Armes de sommet" cyoa-title!
	($ "Approvisionnements... . " pbold .. "PREPARATION PARADIS !" rainbold .. " Je vends uniquement les meilleurs armes ! la canonniere la plus grande, le fusil automatique le plus rapide, et le pistolet le plus brillant !" pbold $) cyoa-text!
	basic-tone foodamt
	trunk-food 15 < if
		($ Thegroup .. " manque assez d'argent pour acheter quoi que ce soit en raison de la carence alimentaire." $) +crtext
		($ "N'en fais pas cas, je suis pret pour vous." pbold .. " Un signal discret : il baisse un paupiere. " .. thegroup .. " le plus grand nombre " .. "OU EST NOTRE PETITE CHIENNE?" rbold $) +crtext goodfx 1 WEAPON_LILDAWG trunk.weapon+ clear-cyoa
	else
		choice( "Laissez-le pour le moment" )choice back-esc
		trunk-food 34 > pt2-3 1 < and if
			choice( "Mitrailleuse miniature - 35 unites de nourriture" )choice: trade-p2-3
		then
		trunk-food 19 > pt2-2 1 < and if
			choice( "Canin geant fusil a chasse - 20 aliments" )choice: trade-p2-2
		then
		trunk-food 14 > pt2-1 1 < and if
			choice( "Pisto d'or - 15 alimentations" )choice: trade-p2-1
		then
	then
;

0 value pt3-1
0 value pt3-2
0 value pt3-3

cyoa-choice: trade-p3-3
	($ Thegroup .. " achete " .. "Dix-huit obus de fusil a balles - dix" rbold .. " Instuction : utilisez-la pour guerir vous-meme ou pour blesser un groupe de zombies... ." .. "Parfois, je l'arrose meme sur mon aliment." pbold $) cyoa-text! goodfx
	10 WEAPON_HEALSPRAY trunk.weapon+ -10 trunk-food+ someicon ' pt3-3 ++
;
	
cyoa-choice: trade-p3-2
	($ Thegroup .. " achete une " .. "COUPE-GAUCHE DE SURVIE" rbold .. "cette arme est legere et facile a balancer meme quand on est epuise, et peut frappes de maniere consistrante deux zombies par coup de hache." $) cyoa-text! goodfx ' pt3-2 ++
	1 WEAPON_SURVKNIFE trunk.weapon+ -6 trunk-food+ picon
;

cyoa-choice: trade-p3-1
	($ Thegroup .. " achete une " .. "PRATIQUER LA CIBLE" rbold .. "les fleches beneficient de chaque statistique : force, endurance et tir sont tous utiles pour rendre les arcs plus puissants." $) cyoa-text! goodfx ' pt3-1 ++
	50 WEAPON_BOW trunk.weapon+ -5 trunk-food+ picon
;

cyoa: preptrader3
	"Materiel d'equipement de survie" cyoa-title!
	($ "Vous pouvez m'acheter des accessoires de survie a faible cout et efficace ! les arc sont excellents pour abattre silencieusement les zombies, surtout si vous etes assez athletiques pour les utiliser bien. si vous en avez la nourriture, j'en ai beaucoup d'epines supplementaires a vendre." pbold $) cyoa-text!
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " manque assez d'aliments pour en acheter une arconnette !" $) +crtext
		($ "Le negociant se montre un peu mecidot envers : " .. thegroup .. " distribue et met en main. " .. "une lame de survie gratuite, et une arche avec 12 fleches !" rbold $) +crtext goodfx 12 WEAPON_BOW trunk.weapon+ 1 WEAPON_SURVKNIFE trunk.weapon+ clear-cyoa
	else
		choice( "Laissez-le pour le moment" )choice back-esc
		trunk-food 9 > pt3-3 1 < and if
			choice( "Spray a guerison xl - 10 pulverisations - 10 aliments" )choice: trade-p3-3
		then
		trunk-food 5 > pt3-2 2 < and if
			choice( "Couteau de survie - 6 alimentations" )choice: trade-p3-2
		then
		trunk-food 4 > pt3-1 8 < and if
			choice( "Couper la reverence avec 50 fleches - 5 aliments" )choice: trade-p3-1
		then
	then
;

: delta-good args( chara )
	"Bon " pchar .name@ $cat
	"Mauvais " pchar .name@ $cat pchar .name!
;

: delta-cool args( chara )
	"Frais : cool " <- chara .name@ $cat
;

: deltrarecruit-rename
	ricon recruitee .name! ($ "DELTA" pbold .. "son nom est maintenant " .. rname .. "!" $) cyoa-text! goodfx
;

cyoa-choice: deltarecruit-name1
	"Sentez bertrand" deltrarecruit-rename
	"Ce nom neuf est totalement deplaisant ! et pourtant, il y a un peu de l'exotique." +crtext
;

cyoa-choice: deltarecruit-name2
	"Veuillez frapper. #must stab# ###" deltrarecruit-rename 
	$( rname .. " avait ete l'un des noms de chien les plus populaires, juste avant la zombocalypse. ne le confondez pas avec le tres puissant nom humain, 'harry ham'." $) +crtext 
;

cyoa-choice: deltarecruit-name3
	cchar delta-cool deltrarecruit-rename 
	($ "Cette nom est encore plus cool, quelque chose." $) +crtext
;

cyoa-choice: deltarecruit-name4
	cchar delta-good deltrarecruit-rename 
	"C'est un nom magnifique !" +crtext
	($ pname .. " se sent un peu malheureux." $) +crtext
	pchar .morale--
;

cyoa-bridge: deltarecruit-name
	ricon "Chien supersoldat : nom nouveau : chien heros ou chien de guerre exceptionnel." text!
	choice( "Sentez bertrand" )choice: deltarecruit-name1
	choice( "Veuillez frapper. #must stab# ###" )choice: deltarecruit-name2
	choice( "Frais : cool " .. rname )choice: deltarecruit-name3
	choice( "Bon " .. pchar .name@ )choice: deltarecruit-name4
;

cyoa-bridge: deltarecruit-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	($ "Now without a cause, DELTA decides to follow a new master...... YOU..........." rbold $) cyoa-text!
	-10 trunk-food+ goodfx
	
	team-full? if
		($ "Vous devez laisser quelqu'un pour que place soit disponible. quel personne voulez-vous remplacer ? " .. rname .. "?" $) +crtext
		recruit-replace?
	else
		recruited
		($ "Commencent a faire pressure " .. rname .. " une nouvelle appellation ?" $) +crtext
		choice( "NON" )choice
		choice( "OUI" )choice: deltarecruit-name
	then
	
	WEAPON_SHOTGUN recruitee .weaponb! 	20 LOOT_SHOTGUN_SHELL recruitee .lootamt!
;

cyoa: deltarecruit
	"LAISSER TOMBER LA CHAISE." cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	($ cname .. " is a supersoldier dog trained by the government.
	Now that the government has fallen, DELTA seeks a new master......... for the right price........" rbold $) cyoa-text!
	" environ dix boites de nourriture en conserves." bbold +text basic-tone
	trunk-food 9 > if
		choice( "Laissez-le pour le moment" )choice
		choice( "Recrutez delta - 10 aliments" )choice: deltarecruit-go
	else
		choice( "Desole, je ne peux pas m'en passer de la nourriture." )choice
	then		
;
	
}

roomgen{


: .pt1 args( ch ) 
	10 <- ch .hattype! // Helmet, modded explosives
;
: .pt2 args( ch ) 
	11 <- ch .hattype! // General's Hat, fancy weapons
;
: .pt3 args( ch ) 
	12 <- ch .hattype! // Camo Hat, cheap survival
;

: preptradercommon
	still-npc regiondef{ ' trader-event lt .events! 1 lt .timer! }
	
;

: preptrader1
	preptradercommon .pt1 cyoa{ ' preptrader1 cyoa! }
;

: preptrader2
	preptradercommon .pt2 cyoa{ ' preptrader2 cyoa! }
;

: preptrader3
	preptradercommon .pt3 cyoa{ ' preptrader3 cyoa! }
;

: preptrader4
	preptradercommon .deltaify cyoa{ ' deltarecruit cyoa! }
;

		0 stack
			' preptrader1 shove
			' preptrader2 shove
			' preptrader3 shove
			' preptrader4 shove
		value preptrader-base
		preptrader-base shuffles var preptrader-picker

: preppertrader
	preptrader-picker depths 0= if preptrader-base shuffles to preptrader-picker then pops nip execute ;

: prepperturret
	still-npc .greenturretify cyoa{ ' prepperturret cyoa! } ;

}

locdef{

location: preppercamp
		max-rooms: 2
		entry-tags: prepperstairs
		script: set-theme-bunker
		pre-script: set-theme-bunker
		post-script: nad 0 %zombs
		
		: prepstart
			s edge-inc s random-edge-door "preparer en fin de compte" only-with-door
		;
		
		
		room-choice: prepperstairs
			room-weight: 10
			room-count: 1
			room-post-script: prepstart
			filter-only
		
		room-choice: prepperend
			room-weight: 10
			room-count: 1
			filter-only

}
		
root{

: prepper-magchance
	20 %chance if
		magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! drift
	then
;

}
		
map{ regiondef{

trade-camp-reset

0 value gbx
0 value gby
0 value gbx2
0 value gby2
0 value bfx
0 value bfy

: bldg tanchor!
	"camp de preparation" $picklocid addloc

	prefab{ 5 bunker }
	
;

: preplogs-big
	2 for biglog 50 %chance if 40 160 else 270 390 then rnd 30 120 rnd lt .pos! next
	ultimalog 10 420 rnd 10 40 rnd lt .pos!
	// lt-here 60 rdrift
;

: pt
	
	xyp.i char 1 = if xyp.x to bfx then;
	xyp.i char 2 = if bfx xyp.y xyp.x barbfence then;

	xyp.i char t = if pine lt-here drift then;
	// xyp.i char S = if trader-food-for-gas then;
	xyp.i char U = if tnomey-spawn then;
	xyp.i char W = if trader-weapon-buyer then;
	
	xyp.i char L = if 2 for log lt-here 40 rdrift next then;
	xyp.i char B = if preplogs-big then;
	
	xyp.i char @ = if xyp.xy bldg then;

	xyp.i char 5 = if xyp.xy to gby to gbx then;
	xyp.i char 6 = if gbx 1- gby xyp.xy swap 1+ swap 4dup gravelbox grasshole then;
	
	xyp.i char < = if xyp.xy to gby2 to gbx2 then;
	xyp.i char > = if gbx2 1- gby2 xyp.xy map{ 4dup dirtbox grasshole } then;
;

"
...........................
..t.t.t.t.t.t.t.t.t.t.t.t..
.......................B...
..t.t.....5...........t.t..
...........................
..t.t.................t.t..
...........................
..t.t..L...........L..t.t..
...........@...............
..t.t.................t.t..
................6..........
..t.t.................t.t..
...........................
..t.t.................t.t..
...........................
.<.........................
1..........2....1.........2
......W.............U......
...........................
...........>...............
...........................
...........................
...........................
...........................
...........................
...........................
..........................."
constant maplayout

: specialcamp-6
	0 -1 maplayout xyplot: pt
;


: site-setup

	// Clear the map with grass
	-1 -1 w h grassbox

	// Plot trader stuff
	specialcamp-6

	// Add dirt road at bottom
	-1 h 5 - w h 4dup map{ dirtbox grasshole }

	1 no_time!
	7 14 rnd o'clock
	50 %chance if 1 2 frnd weather! -1 -2 frnd wind_force! else 1 2 frnd wind_force! then
	
	mission{ no-edge-spawns }
	
	18 %mv-up
	30 %chars-up
;

	 region: specialcamp-6
		w: maplayout layoutw
		h: maplayout layouth

		trade-mode

		post-script: site-setup
} }

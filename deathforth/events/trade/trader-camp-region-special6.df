uses paul/region-layout.df
// "../deathforth/rooms/TEMPROOM.txt" $load-rooms
	
" 
:map 2 
:tags prepperstairs
:scriptA prepperturret 
:size 7 10 
:off 10 3 
:tiles 
2222222
2333332
21A1A12
2111112
2112112
2112662
2112662
2112112
2222222
3333333

:map 2
:tags prepperend  
:scriptA preppertrader
:scriptB still-npc .redturretify
:scriptC bed 
:scriptD nkitchenset 
:scriptE diningset 
:scriptF nbathsetsmall 2 movex 
:scriptG prepper-magchance 
:scriptH livingset
:size 27 23 
:off 1 1 
:tiles 
000000000002222200000000000
000000000002333200000000000
000000000002111200000000000
000000000002666200000000000
000000000002666200000000000
222222000002666200000002222
233222222222666200000002332
2C12223333326662000222221C2
21133211F113111200023333112
2A111211G111111222221111112
211212222222212222221222112
211213333333313333331232112
2C12111111111111111112021C2
222222221222222222122222222
333233331333333333133332333
0002111111111D111111A112000
00021A111111111111111112000
000211111122222221111A12000
000211111122222221111112000
00021111E123333321H11112000
00021B111120000021111B12000
000222222220000022222222000
000333333330000033333333000
"
value PREPPERCAMP
PREPPERCAMP $parse-rooms

cyoa{

: prepperturret-grab clear-cyoa deleteme goodfx turretgreen drop southofcyoa ;

cyoa-choice: prepperturret-hack
	cmechanic+ if
		($ pname .. " usam suas habilidades para descobrir como reiniciar o canhao no estado de implantacao!		" .. "CANHaO GRATUITO!" rainbold $) cyoa-text! mechanicREV prepperturret-grab
	else
		($ pname .. " tenta usar forca bruta para empurrar o canhao de volta para seu estado transportavel!		" .. "GRAAAAAAAAAAAAAH!" rbold $) cyoa-text!
		cstrength+ if
			"Incredivelmente, isso funciona!" +crtext prepperturret-grab
		else
			($ "'Isso funciona mais ou menos como se esperava. Alem disso,		 " .. pname .. " recebe uma choque bastante intenso." $) +crtext bitefx sting-- morale--
		then strengthREV 
	then
;

cyoa: prepperturret
	"Ladrao de Canhao" cyoa-title!
	picon!
	($ "Este canhao esta fazendo coisas de canhao. Girando, procurando inimigos, e assim por diante. Olha como ele vai!	Uma pequena questao:		 " .. "'Nao e SEU canhao!" rbold $) cyoa-text!
		choice( "Deixeo em paz" )choice
		pchar charchoice( " tenta roubar o canhao" )choice: prepperturret-hack
;

0 value pt1-1
0 value pt1-2
0 value pt1-3

cyoa-choice: trade-p1-3
	($ Thegroup .. "  Compra um Lancador de Napalm modificado para o dobro da capacidade!" .. "P" pbold $) cyoa-text! goodfx
	100 WEAPON_NAPALM trunk.weapon+ -16 trunk-food+ someicon ' pt1-3 ++
;

cyoa-choice: trade-p1-2
	($ Thegroup .. " compra uma bazuca modificada com oito foguetes!" .. "Carnagina TOTAL! Eu amo isso!" pbold $) cyoa-text! goodfx
	8 WEAPON_BAZOOKA trunk.weapon+ -12 trunk-food+ someicon ' pt1-2 ++
;

cyoa-choice: trade-p1-1
	($ Thegroup .. " compra um lancador de granadas modificado com 15 granadas!" .. "Veja quantos zumbis voce pode esmagar de uma vez!" pbold $) cyoa-text! goodfx
	15 WEAPON_GLAUNCH trunk.weapon+ -12 trunk-food+ someicon ' pt1-1 ++
;

cyoa: preptrader1
	"Armas de carnificina XL" cyoa-title!
	($ "'Eu ja modifiquei todas essas armas DEVASTADORAS para ter capacidade dupla ou mais! Voce nao consegue isso em nenhum outro lugar!" pbold $) cyoa-text!
	trunk-food 11 > if "'AGORA estamos falando." +crtext then
	basic-tone foodamt
	trunk-food 12 < if
		($ Thegroup .. " 'fica DEVASTADO ao saber que nao pode pagar por nada." $) +crtext
	else
		choice( "Sai agora" )choice back-esc
		trunk-food 15 > pt1-3 1 < and if
			choice( "'Lancador de napalm XL - 100 'palma - 16 alimentos" )choice: trade-p1-3
		then
		trunk-food 11 > pt1-2 1 < and if
			choice( "Bazuca XL - 8 tiros - 12 alimentos" )choice: trade-p1-2
		then
		trunk-food 11 > pt1-1 1 < and if
			choice( "Lancador de granadas XL - 15 tiros - 12 alimentos" )choice: trade-p1-1
		then
	then
;

0 value pt2-1
0 value pt2-2
0 value pt2-3

cyoa-choice: trade-p2-3
	($ Thegroup .. "  compra o " .. "Minigun!" fbold .. " E o auge da entrega de municao de rifle!" .. "Vendi isso com um grande desconto porque acidentalmente coloquei manchas de manteiga de amendoim nele." pbold $) cyoa-text! goodfx
	1 WEAPON_MINIGUN trunk.weapon+ -35 trunk-food+ someicon ' pt2-3 ++
;
	
cyoa-choice: trade-p2-2
	($ Thegroup .. "  compra o " .. "BIG DAWG" rbold .. ". E uma espingarda customizada unica! Cada tiro e uma explosao macica de zumbis espirrando." $) cyoa-text! goodfx ' pt2-2 ++
	1 WEAPON_DAWG trunk.weapon+ -20 trunk-food+ picon
;

cyoa-choice: trade-p2-1
	($ Thegroup .. "  compra o " .. "PISTOLA Dourada" rbold .. ". Ela tem uma quantidade inusual de poder e penetracao de bala para um pistola, alem disso e linda e brilhante." $) cyoa-text! goodfx ' pt2-1 ++
	1 WEAPON_GOLDPISTOL trunk.weapon+ -15 trunk-food+ picon
;

cyoa: preptrader2
	"Arsenais Cume" cyoa-title!
	($ "Bemvindo a " pbold .. "PARADISO DO PREPPER!" rainbold .. " ' Eu vendo apenas as melhores armas! A maior espingarda, o rifle automatico mais rapido e a pistola mais brilhante!" pbold $) cyoa-text!
	basic-tone foodamt
	trunk-food 15 < if
		($ Thegroup .. "  carece de suficiente comida para comprar nada!" $) +crtext
		($ "'Nao te preocupes, estou aqui para te ajudar." pbold .. "  O vendedor arqueia e depois date " .. thegroup .. "  a " .. "'CACHORROTO" rbold $) +crtext goodfx 1 WEAPON_LILDAWG trunk.weapon+ clear-cyoa
	else
		choice( "Sai agora" )choice back-esc
		trunk-food 34 > pt2-3 1 < and if
			choice( "Minigun - 35 comida" )choice: trade-p2-3
		then
		trunk-food 19 > pt2-2 1 < and if
			choice( "ESPINGARDa BIG DAWG - 20 comida" )choice: trade-p2-2
		then
		trunk-food 14 > pt2-1 1 < and if
			choice( "1 bullet" )choice: trade-p2-1
		then
	then
;

0 value pt3-1
0 value pt3-2
0 value pt3-3

cyoa-choice: trade-p3-3
	($ Thegroup .. "  compra o " .. "'Spray de Cura XL!'" rbold .. " 	Utilize para se curar, ou para causar dano a um grupo de zumbis." .. "As vezes, ate o coloco na comida." pbold $) cyoa-text! goodfx
	10 WEAPON_HEALSPRAY trunk.weapon+ -10 trunk-food+ someicon ' pt3-3 ++
;
	
cyoa-choice: trade-p3-2
	($ Thegroup .. " compra um " .. "FACA DE SOBREVIVENCIA" rbold .. ". It's light, easy to swing even if exhausted, and can consistently hit two zombies in one slash." $) cyoa-text! goodfx ' pt3-2 ++
	1 WEAPON_SURVKNIFE trunk.weapon+ -6 trunk-food+ picon
;

cyoa-choice: trade-p3-1
	($ Thegroup .. " compra um " .. "ARCO COM 50 FLECHAS" rbold .. ". Arcos se beneficiam de todas as estatisticas: forca, condicionamento fisico e tiro tornam os arcos mais poderosos." $) cyoa-text! goodfx ' pt3-1 ++
	50 WEAPON_BOW trunk.weapon+ -5 trunk-food+ picon
;

cyoa: preptrader3
	"Equipamento de Sobrevivencia" cyoa-title!
	($ "'Estou negociando alguns equipamentos de sobrevivencia acessiveis e eficazes! Arcos sao otimos para derrubar zumbis silenciosamente, especialmente se voce for atletico o suficiente para usalos bem. Se voce tiver comida, tenho muitas flechas extras em estoque." pbold $) cyoa-text!
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. "  nao tem comida suficiente nem para comprar um arco!" $) +crtext
		($ "O comerciante tem um pouco de pena de " .. thegroup .. "  e distribui " .. "uma faca de sobrevivencia gratis e um arco com 12 flechas!" rbold $) +crtext goodfx 12 WEAPON_BOW trunk.weapon+ 1 WEAPON_SURVKNIFE trunk.weapon+ clear-cyoa
	else
		choice( "Sai agora" )choice back-esc
		trunk-food 9 > pt3-3 1 < and if
			choice( "Spray de Cura XL - 10 Sprays - 10 alimentos" )choice: trade-p3-3
		then
		trunk-food 5 > pt3-2 2 < and if
			choice( "Faca de Sobrevivencia - 6 alimentos" )choice: trade-p3-2
		then
		trunk-food 4 > pt3-1 8 < and if
			choice( "Arco com 50 flechas - 5 alimentos" )choice: trade-p3-1
		then
	then
;

: delta-good args( chara )
	"Bom " pchar .name@ $cat
	"Mau " pchar .name@ $cat pchar .name!
;

: delta-cool args( chara )
	"Legal " <- chara .name@ $cat
;

: deltrarecruit-rename
	ricon recruitee .name! ($ "DELTA" pbold .. "'' agora e " .. rname .. "!" $) cyoa-text! goodfx
;

cyoa-choice: deltarecruit-name1
	"Portugues" deltrarecruit-rename
	"Que nome completamente horrivel! E ainda assim, tem um toque de exotismo." +crtext
;

cyoa-choice: deltarecruit-name2
	"Harry Martelo" deltrarecruit-rename 
	$( rname .. " ' era um dos nomes de cao mais populares, antes do Zombocalipse. Nao confundir com o nome humano muito poderoso, 'Harry Ham'." $) +crtext 
;

cyoa-choice: deltarecruit-name3
	cchar delta-cool deltrarecruit-rename 
	($ "Este nome e ainda mais legal, alguma coisa." $) +crtext
;

cyoa-choice: deltarecruit-name4
	cchar delta-good deltrarecruit-rename 
	"Que nome incrivel!" +crtext
	($ pname .. " sentindo triste." $) +crtext
	pchar .morale--
;

cyoa-bridge: deltarecruit-name
	ricon "'Qual deve ser o novo nome do caosoldado SUPER?" text!
	choice( "Portugues" )choice: deltarecruit-name1
	choice( "Harry Martelo" )choice: deltarecruit-name2
	choice( "Legal " .. rname )choice: deltarecruit-name3
	choice( "Bom " .. pchar .name@ )choice: deltarecruit-name4
;

cyoa-bridge: deltarecruit-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	($ "Now without a cause, DELTA decides to follow a new master...... YOU..........." rbold $) cyoa-text!
	-10 trunk-food+ goodfx
	
	team-full? if
		($ "O grupo esta cheio.		Quem voce gostaria de substituir para deixar lugar para " .. rname .. "?" $) +crtext
		recruit-replace?
	else
		recruited
		($ "Voce gostaria de dar um novo nome a " .. rname .. "  um nome?" $) +crtext
		choice( "NaO" )choice
		choice( "SIM" )choice: deltarecruit-name
	then
	
	WEAPON_SHOTGUN recruitee .weaponb! 	20 LOOT_SHOTGUN_SHELL recruitee .lootamt!
;

cyoa: deltarecruit
	"PROJETO: DELTA" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	($ cname .. " is a supersoldier dog trained by the government.
	Now that the government has fallen, DELTA seeks a new master......... for the right price........" rbold $) cyoa-text!
	" " bbold +text basic-tone
	trunk-food 9 > if
		choice( "Sai agora" )choice
		choice( "Recruta DELTA - 10 lata de comida" )choice: deltarecruit-go
	else
		choice( "Nao posso poupar a comida" )choice
	then		
;
	
}

roomgen{


: .pt1 args( ch ) 
	10 <- ch .hattype! // Helmet, modded explosives
;
: .pt2 args( ch ) 
	11 <- ch .hattype! // General's Hat, fancy weapons
;
: .pt3 args( ch ) 
	12 <- ch .hattype! // Camo Hat, cheap survival
;

: preptradercommon
	still-npc regiondef{ ' trader-event lt .events! 1 lt .timer! }
	
;

: preptrader1
	preptradercommon .pt1 cyoa{ ' preptrader1 cyoa! }
;

: preptrader2
	preptradercommon .pt2 cyoa{ ' preptrader2 cyoa! }
;

: preptrader3
	preptradercommon .pt3 cyoa{ ' preptrader3 cyoa! }
;

: preptrader4
	preptradercommon .deltaify cyoa{ ' deltarecruit cyoa! }
;

		0 stack
			' preptrader1 shove
			' preptrader2 shove
			' preptrader3 shove
			' preptrader4 shove
		value preptrader-base
		preptrader-base shuffles var preptrader-picker

: preppertrader
	preptrader-picker depths 0= if preptrader-base shuffles to preptrader-picker then pops nip execute ;

: prepperturret
	still-npc .greenturretify cyoa{ ' prepperturret cyoa! } ;

}

locdef{

location: preppercamp
		max-rooms: 2
		entry-tags: prepperstairs
		script: set-theme-bunker
		pre-script: set-theme-bunker
		post-script: nad 0 %zombs
		
		: prepstart
			s edge-inc s random-edge-door "prepperendo" only-with-door
		;
		
		
		room-choice: prepperstairs
			room-weight: 10
			room-count: 1
			room-post-script: prepstart
			filter-only
		
		room-choice: prepperend
			room-weight: 10
			room-count: 1
			filter-only

}
		
root{

: prepper-magchance
	20 %chance if
		magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! drift
	then
;

}
		
map{ regiondef{

trade-camp-reset

0 value gbx
0 value gby
0 value gbx2
0 value gby2
0 value bfx
0 value bfy

: bldg tanchor!
	"acampamento de prepper" $picklocid addloc

	prefab{ 5 bunker }
	
;

: preplogs-big
	2 for biglog 50 %chance if 40 160 else 270 390 then rnd 30 120 rnd lt .pos! next
	ultimalog 10 420 rnd 10 40 rnd lt .pos!
	// lt-here 60 rdrift
;

: pt
	
	xyp.i char 1 = if xyp.x to bfx then;
	xyp.i char 2 = if bfx xyp.y xyp.x barbfence then;

	xyp.i char t = if pine lt-here drift then;
	// xyp.i char S = if trader-food-for-gas then;
	xyp.i char U = if tnomey-spawn then;
	xyp.i char W = if trader-weapon-buyer then;
	
	xyp.i char L = if 2 for log lt-here 40 rdrift next then;
	xyp.i char B = if preplogs-big then;
	
	xyp.i char @ = if xyp.xy bldg then;

	xyp.i char 5 = if xyp.xy to gby to gbx then;
	xyp.i char 6 = if gbx 1- gby xyp.xy swap 1+ swap 4dup gravelbox grasshole then;
	
	xyp.i char < = if xyp.xy to gby2 to gbx2 then;
	xyp.i char > = if gbx2 1- gby2 xyp.xy map{ 4dup dirtbox grasshole } then;
;

"
...........................
..t.t.t.t.t.t.t.t.t.t.t.t..
.......................B...
..t.t.....5...........t.t..
...........................
..t.t.................t.t..
...........................
..t.t..L...........L..t.t..
...........@...............
..t.t.................t.t..
................6..........
..t.t.................t.t..
...........................
..t.t.................t.t..
...........................
.<.........................
1..........2....1.........2
......W.............U......
...........................
...........>...............
...........................
...........................
...........................
...........................
...........................
...........................
..........................."
constant maplayout

: specialcamp-6
	0 -1 maplayout xyplot: pt
;


: site-setup

	// Clear the map with grass
	-1 -1 w h grassbox

	// Plot trader stuff
	specialcamp-6

	// Add dirt road at bottom
	-1 h 5 - w h 4dup map{ dirtbox grasshole }

	1 no_time!
	7 14 rnd o'clock
	50 %chance if 1 2 frnd weather! -1 -2 frnd wind_force! else 1 2 frnd wind_force! then
	
	mission{ no-edge-spawns }
	
	18 %mv-up
	30 %chars-up
;

	 region: specialcamp-6
		w: maplayout layoutw
		h: maplayout layouth

		trade-mode

		post-script: site-setup
} }

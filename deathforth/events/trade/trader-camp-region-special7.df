uses paul/region-layout.df
"events/inner/clinicloc.df" $load

// "../deathforth/rooms/TEMPROOM.txt" $load-rooms
	
" 
:map 1 
:tags tradehospital  
:scriptA hospbag 
:scriptB hosptrader 
:scriptC regiondef{ trader-weapon-buyer-inside } 
:scriptD regiondef{ tnomey-spawn-inside } 
:scriptE hospitaldecomain 
:scriptF hospitaldecoside 
:scriptG hosptoilets 
:scriptH hospitalwall 
:size 45 22 
:off 0 0 
:tiles 
000000000000000000000000000000000000000222222
000002222222222222222222000000000000000233332
000002333332333332333332222222220000000211112
0000021E1EH2HEHE121EHE12333333320000000211112
00000211B11211B11211B1121111G1120000000211112
2222221111121111F2F11112111111120000000222222
233332F111F2F111121111F2111111120000000333333
2EHE12221222221222221222122222220000000000000
21B113331333331333331333133323330000000000000
211111H111H111111H111111111F20000000000000000
211111111111111111111111111120000000000000000
21F1F2222222221122222222222120000000000000000
222222333333321133333333333120000000000000000
333333000000021111111C11111120000000000000000
0000000000000211A1111111111120000000000000000
0000000000000211111111111D1120000000000000000
000000000000021111111111111120000000000000000
000000000000021122222222222220000000000000000
000000000000021123333333333330000000000000000
000000000000021120000000000000000000000000000
000000000000022220000000000000000000000000000
000000000000033330000000000000000000000000000

"
value TRADEHOSPITAL
TRADEHOSPITAL $parse-rooms

cyoa{

cyoa-choice: hosptrader1-go
	"QUEMADO POR SIEMPRE" cyoa-title!
	($ "El doctor " .. cname .. " ' una pequena hoja de papel. Dice:" .. "ur a dingus" pbold $) cyoa-text!
	cchar statmod{ .fireproof? } not if
		"OH!
		OH! BURNED! OH!!!!!" fbold +crtext
		($ cname .. "  se siente emocionalmente devastado por el dano de fuego de esta quemadura enferma! " 
		.. cname .. "la piel se siente extranamente fresca." bbold $) +crtext
		cchar statmod{ .fireproof } morale4- badfx
		choice( "ARGH!" )choice
	else
		($ cname .. ", al ser ignifugo, no se ve afectado por esta quemadura en absoluto. " .. cname .. " se siente extremadamente satisfecho con esto." bbold $) +crtext
		goodfx morale4+
		"NO! NO!
		NO! NO!!!!" fbold +crtext
		($ "El doctor huye en intensa verguenza, y " .. cname .. " reacciona saqueando la habitacion inmediatamente." $) +crtext
		deleteme +cr 6 8 rnd trunk-medical+
	then
	clear-cyoa
;

cyoa-bridge: hosptrader1-pick4
	ricon
	($ "Quien va a ir a la " .. "UNIDAD DE QUEMADURAS" .. fbold .. "?" $) cyoa-text!
	' hosptrader1-go cyoa-team-choice
;

cyoa: hosptrader1
	"Unidad de Quemaduras" cyoa-title!
	($ "BIENVENIDOS A LA UNIDAD DE QUEMADURAS	QUIEN VA A SUFRIR QEMADURAS?" fbold $) cyoa-text!
	basic-tone
	choice( "Salvese, no haga contacto ojo a ojo" )choice back-esc
	team-count 3 > if
		' hosptrader1-go 2 #cyoa-team-choice
		choice( "Elige a alguien mas" )choice: hosptrader1-pick4
	else
		' hosptrader1-go cyoa-team-choice
	then
;

0 value ht2-1
0 value ht2-2
0 value ht2-3

cyoa-choice: trade-h2-3
	($ Thegroup .. " 'compra la " .. "PAQUETE MEDICO GIGANTE!" fbold .. " ' Es mucha cantidad de curacion potencial!	" .. "La Spray de Curacion XL incluso funciona en dedos mojados." pbold $) cyoa-text! goodfx
	10 WEAPON_HEALSPRAY trunk.weapon+ 10 trunk-medical+ -15 trunk-food+ someicon ' ht2-3 ++
;
	
cyoa-choice: trade-h2-2
	($ Thegroup .. " 'compra la " .. "CADAVERERO DE ORO" rbold .. ". No se rompe, y golpea a un zombi a la vez extremadamente duro al golpear a maxima velocidad!" $) cyoa-text! goodfx ' ht2-2 ++
	1 WEAPON_GOLDSCALP trunk.weapon+ -15 trunk-food+ picon
;

cyoa-choice: trade-h2-1
	($ Thegroup .. " compra un " .. "SIERRA DE HUESOS" rbold .. "puede darle con una sola vez a dos zombis, y pega fuerte para su velocidad." $) cyoa-text! goodfx ' ht2-1 ++
	1 WEAPON_BONESAW trunk.weapon+ -6 trunk-food+ picon
;

cyoa: hosptrader2
	"Suministros Medicos" cyoa-title!
	($ "Bienvenido a " pbold .. "El Ultimo Hospital!" rainbold .. " 'Estoy vendiendo algunos suministros medicos esenciales, para ayudarte en tu viaje!" pbold $) cyoa-text!
	ht2-3 1 < if
		"Oferta especial hoy: Un solo paquete por grupo de visitantes: Mi Paquete Medico Gigante contiene 10 suministros medicos ademas de una Spray de Cura XL con 10 spray!" pbold +crtext
	then
	basic-tone foodamt
	trunk-food 6 < if
		($ Thegroup .. " ' le falta comida suficiente para comprar algo!" $) +crtext
		($ "El traficante da " .. thegroup .. " 'un complementario gratuito " .. "Bonesaw" rbold .. " ' solo por visitar!" $) +crtext goodfx 1 WEAPON_BONESAW trunk.weapon+ clear-cyoa
	else
		choice( "Deja esto por ahora" )choice back-esc
		trunk-food 14 > ht2-3 1 < and if
			choice( "Paquete Medico Gigante - 15 comida" )choice: trade-h2-3
		then
		trunk-food 14 > ht2-2 1 < and if
			choice( "Cuchillo de Escalpelo Dorado - 15 comida" )choice: trade-h2-2
		then
		trunk-food 5 > ht2-1 3 < and if
			choice( "Bonesaw - 3 comida" )choice: trade-h2-1
		then
	then
;

: issick? statmod{ .sick? } ; // The only time I ever use a sickness check, ever
: hospital-superserum
	' .strength++ foreach-team-char
	' .fitness++ foreach-team-char 
	' .vitality++ foreach-team-char 
	' .dexterity++ foreach-team-char
;

cyoa-choice: hosptrader3-go

	team-damage 0 = if
		($ Thegroup .. " 'se examina, pero no hay nada malo con ellos. El doctor esta visiblemente enojado y ofendido que has desperdiciado su tiempo, y escribe algo en un registro." $) cyoa-text!
		morale++all
	else
		12 groupheal
		($ Thegroup .. " recibe tratamiento para " $) cyoa-text!
		team-damage 1 < if 
			"todo" +text 
		else
			"mas" +text
		then
			" de sus heridas." +text
			"El doctor esta increiblemente decepcionado y aburrido por tus lesiones comunes." +crtext
	then
	
	team-stack ' issick? stack-filter 0 > if 
		"PERO ESPERA! " rbold +crtext
		($ "El doctor genio problematico detecta una enfermedad! Despues de aproximadamente 44 minutos, el doctor llega a una conclusion emocionante: La enfermedad es " .. "ENVENENAMIENTO POR CAFE! " fbold $) +text
		($ "El doctor administra un superserum milagro experimental a " .. thegroup .. "no cura la enfermedad, pero un dia de descanso lo hara, de todos modos!" $) +text +cr hospital-superserum
	then
	
	goodfx -5 trunk-food+ picon clear-cyoa
;

cyoa: hosptrader3
	"Probablemente un doctor genio problematico" cyoa-title!
	($ "He estado tratando nada mas que mordeduras de zombis, quemaduras leves, lesiones por tanques de propano y dedos golpeados durante lo que parecen ser 6 anos. Cuentame que tienes algun tipo de enfermedad. Idealmente una enfermedad misteriosa dramatica, pero incluso algo como intoxicacion alimentaria estaria bien en este punto. Cualquier cosa, pero otra mordedura de zombi!" pbold $) cyoa-text!
	basic-tone +cr foodamt
	trunk-food 5 < if
		($ Thegroup .. " ' a falta de suficiente comida para el tratamiento medico!" $) +crtext
		($ " 'El doctor simpatiza con " .. thegroup .. " ' y entrega suministros medicos." rbold $) +text goodfx 3 4 rnd trunk-medical+ clear-cyoa
	else
		choice( "Deja esto por ahora" )choice back-esc
			solo if
				choice( "Trata las lesiones - 5 comida" )choice: hosptrader3-go
			else
				choice( "Trata las lesiones del grupo - 5 comida" )choice: hosptrader3-go
			then
	then
;

0 value cleaninglady-meals

: cleaninglady-amt cleaninglady-meals 20 * 10 + ;
: cleaninglady-pay? trunk-food cleaninglady-amt 1 - > ;
: cleaninglady-pay cleaninglady-amt -1 * trunk-food+ ;

cyoa-choice: cleaninglady-meal-eat
	cicon
	($ cname .. " 1" $) cyoa-text!
	vitality++ morale4+ cchar .fullheal ' cleaninglady-meals ++
	goodfx
;

cyoa-bridge: cleaninglady-meal
	solo if
		soloer choosechar cleaninglady-meal-eat 
	else
		($ "Quien deberia comer la comida de la cafeteria del hospital?" $) cyoa-text!
		"ESTA COMIDA DE LOS DIOSES CONFERIRA UN BONO A LA SALUD MAXIMA, Y UNA RESTAURACION MASIVA DE MORAL!" rbold +crtext
			' cleaninglady-meal-eat cyoa-team-choice
	then
;

cyoa-bridge: cleaninglady-meal-pay
	cleaninglady-meal
	cleaninglady-pay
;

cyoa-bridge: cleaninglady-recruit
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	"Limpiando" cyoa-title!
	($ rname .. " 'gira su mop rapidamente alrededor de su muneca." .. "Es hora de limpiar este desastre!" pbold .. "El agua de la fregona moja " .. thegroup .. " 'bastante mal." $) cyoa-text!
	goodfx
	
	team-full? if
		($ "El grupo esta lleno. Que persona quieres reemplazar para dejar lugar a " .. rname .. "?" $) +crtext
		recruit-replace?
	else
		recruited
		($ "Eso parecia muy facil." $) +text
		most-charming if
			choosechar ($ cname .. " ' convence " .. rhim .. " a escaquearse de la comida del hospital!" $) +crtext
			choice( "DAME ESA COMIDA DE HOSPITAL" )choice: cleaninglady-meal
		then
	then
	
	// WEAPON_SHOTGUN recruitee .weaponb! 	20 LOOT_SHOTGUN_SHELL recruitee .lootamt!
;
		
cyoa: cleaninglady
	"El Mercado" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	($ "Hay una limpiadora en aqui. Ella no parece estar realmente trabajando. Ella mide " .. thegroup .. " 'me" $) cyoa-text! basic-tone
	cleaninglady-meals 0 > if
		"Si, si. Que terrible! Puedo obtener otro uno, pero va a costarle."
	else
		"Sabia que realmente estabas buscando aqui. Habias venido tan lejos por la comida de la enfermeria.		Puedo llevarte el plato de la cafeteria que deseas, a un precio." 
	then pbold +crtext +cr foodamt
	cleaninglady-pay? if
		choice( "Deja esto por ahora" )choice back-esc
		choice( "Comprar Comida de la Cafeteria - " .. cleaninglady-amt .. " ' comida no de cafeteria" )choice: cleaninglady-meal-pay
		choice( "Negarse a hacer esto por principios" )choice
	else
		choice( "No puedo desprenderme de la comida" )choice
	then
	choice( "Intentar reclutarla" )choice: cleaninglady-recruit
;

0 value hospital-medical+

: hospital-train-amt numhums 2 * hospital-medical+ 1 + * 1 + ; 
: hospital-train-pay? trunk-food hospital-train-amt 1 - > notsolo notsolohuman and and ;
: hospital-train-pay hospital-train-amt -1 * trunk-food+ ;

: hospital-solo-amt hospital-medical+ 2 * 3 + ;
: hospital-solo-pay? trunk-food hospital-solo-amt 1 - > ;
: hospital-solo-pay hospital-solo-amt -1 * trunk-food+ ;

cyoa-choice: hospital-medical-train
	cchar .medical@ 6 < if
		($ cname .. " 'se encuentra en su mayor parte recibiendo una clase magistral de trivia de golf. Alguna manera, esto se aplica a las habilidades medicas." $) goodfx
	else
		($ cname .. " 'ya ha obtenido su grado medico completo de la Universidad Hospitalaria de Nuestra Santisima!		Sin embargo, la Universidad Hospitalaria de Nuestro Santo Bano no otorga reembolsos." $) badfx
	then cyoa-text! 
	hospital-solo-pay medicalREV medical++ ' hospital-medical+ ++
;

cyoa-choice: hospital-medical-group
	($ "El grupo entero obtiene formacion medica! Es casi por completo una representacion teatral de tecnica golfistica adecuada." $) cyoa-text! goodfx
	hospital-train-pay someicon
	regiondef{ ' trade-medical-group! foreach-human-char } numhums ' hospital-medical+ +!
;

cyoa: hospital-medical-give
	solohuman if
		choosehuman hospital-medical-train
	then;
	"Quien deberia obtener este vasto conocimiento medico?" cyoa-text!
	' hospital-medical-train cyoa-human-choice
;

cyoa: hosptrainer
	($ "Hospital Sanitario Bano Universidad" $) cyoa-title!
	"Ah! Doctores! Gran cosa! Puedo ensenarte todo lo que necesitas saber sobre la medicina, aqui en este bano, mucho mejor que en cualquier 'UNIVERSIDAD'." pbold text!
	"Saliva brota de sus bocas cuando dicen 'Universidad'. Pensabas cuanto tiempo han estado en este bano y si se han lavado las manos." +crtext foodamt
		hospital-solo-pay? not if
			"No puede permitirse mas capacitacion." +crtext 
		then;
		choice( "Deja esto por ahora" )choice back-esc
		hospital-solo-pay? if
			choice( "Dame " .. hospital-solo-amt .. " 'comida para un solo entrenamiento" )choice: hospital-medical-give
		then
		hospital-train-pay? if
			choice( "Dame " .. hospital-train-amt .. " ' alimentos para el entrenamiento en grupo" )choice: hospital-medical-group
		then
		
;
	
}

root{

: hosp-magchance
	20 %chance if
		magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! -2 movex drift
	then
;

}

roomgen{


: .ht1 args( ch ) 
	21 <- ch .specialhead! 24 <- ch .specialbody! // Jerk head, Madsci body, Burn Unit
;
: .ht2 args( ch ) 
	5 <- ch .hattype! 25 <- ch .glassestype! // Bandanna, Medical Supplies and Weapons
;
: .ht3 args( ch ) 
	6 <- ch .hattype! 10 <- ch .torsotype! // Beanie, Black Jacket, Group Heal
;

: hosptradercommon
	still-npc regiondef{ ' trader-event lt .events! 1 lt .timer! }
	
;

: hospitaldecomain 1 5 rnd case
	1 of hospitalshelfempty endof
	2 of hospitalbed endof
	3 of hospitalexam endof
	4 of hospitalbedside endof
	5 of wheelchair endof
endcase -10 movey ;

: hospitaldecoside 1 5 rnd case
	1 of ivpole endof
	2 of hospitalstool endof
	3 of hospitalstepper endof
	4 of hospitalcurtain endof
	5 of stretcher endof
endcase ;

: hosptoilets
	bathtoilet -1 movey 0.3 movex
	stall -1 movey 1 movex
	stall -1 movey -0.5 movex
	bathtoilet -1 movey 2 movex
	bathtoilet -1 movey -1.2 movex
	stall -1 movey -2 movex
	sink -1 movey -3 movex
	sink -1 movey -4 movex
	hosptradercommon 2 movey cyoa{ ' hosptrainer cyoa! }
	hosp-magchance
;

: hosptrader1
	hosptradercommon .ht1 cyoa{ ' hosptrader1 cyoa! }
;

: hosptrader2
	hosptradercommon .ht2 cyoa{ ' hosptrader2 cyoa! }
;

: hosptrader3
	hosptradercommon .ht3 cyoa{ ' hosptrader3 cyoa! }
;

: hosptrader4
	hosptradercommon .cleanerify cyoa{ ' cleaninglady cyoa! }
;

		0 stack
			' hosptrader1 shove
			' hosptrader2 shove
			' hosptrader3 shove
			' hosptrader4 shove
		value hosptrader-base
		hosptrader-base shuffles var hosptrader-picker

: hosptrader
	hosptrader-picker depths 0= if hosptrader-base shuffles to hosptrader-picker then pops nip execute ;

: hospbag
	bag-me ;

}

locdef{

location: tradehospital
		max-rooms: 1
		entry-tags: tradehospital
		script: set-theme-hospital
		pre-script: set-theme-hospital
		post-script: nad 0 %zombs
				
		room-choice: tradehospital
			room-weight: 10
			room-count: 1
			filter-only

}
				
map{ regiondef{

trade-camp-reset

: site-setup

	1 no_time!
	12 14 rnd o'clock
	
	mission{ no-edge-spawns }
	
;

	 region: specialcamp-7
		
		trade-mode

		inside
					
		loc-choice: tradehospital
			loc-weight: 99
			loc-count: 1

		post-script: site-setup
} }

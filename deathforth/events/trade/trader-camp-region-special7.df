uses paul/region-layout.df
"events/inner/clinicloc.df" $load

// "../deathforth/rooms/TEMPROOM.txt" $load-rooms
	
" 
:map 1 
:tags tradehospital  
:scriptA hospbag 
:scriptB hosptrader 
:scriptC regiondef{ trader-weapon-buyer-inside } 
:scriptD regiondef{ tnomey-spawn-inside } 
:scriptE hospitaldecomain 
:scriptF hospitaldecoside 
:scriptG hosptoilets 
:scriptH hospitalwall 
:size 45 22 
:off 0 0 
:tiles 
000000000000000000000000000000000000000222222
000002222222222222222222000000000000000233332
000002333332333332333332222222220000000211112
0000021E1EH2HEHE121EHE12333333320000000211112
00000211B11211B11211B1121111G1120000000211112
2222221111121111F2F11112111111120000000222222
233332F111F2F111121111F2111111120000000333333
2EHE12221222221222221222122222220000000000000
21B113331333331333331333133323330000000000000
211111H111H111111H111111111F20000000000000000
211111111111111111111111111120000000000000000
21F1F2222222221122222222222120000000000000000
222222333333321133333333333120000000000000000
333333000000021111111C11111120000000000000000
0000000000000211A1111111111120000000000000000
0000000000000211111111111D1120000000000000000
000000000000021111111111111120000000000000000
000000000000021122222222222220000000000000000
000000000000021123333333333330000000000000000
000000000000021120000000000000000000000000000
000000000000022220000000000000000000000000000
000000000000033330000000000000000000000000000

"
value TRADEHOSPITAL
TRADEHOSPITAL $parse-rooms

cyoa{

cyoa-choice: hosptrader1-go
	"QUEIMADO PARA SEMPRE" cyoa-title!
	($ "O medico " .. cname .. " portugues" .. "voce e um idiota voce cheira a ovo" pbold $) cyoa-text!
	cchar statmod{ .fireproof? } not if
		"OH!
		OH! BURNED! OH!!!!!" fbold +crtext
		($ cname .. " se sente emocionalmente devastado pelos danos causados pelo fogo dessa queimadura doentia " 
		.. cname .. "a pele de ' parece estranhamente fria." bbold $) +crtext
		cchar statmod{ .fireproof } morale4- badfx
		choice( "ARGH!" )choice
	else
		($ cname .. ", sendo a prova de fogo, nao e afetado por esta queimadura " .. cname .. " sente se extremamente presuncoso com isso." bbold $) +crtext
		goodfx morale4+
		"NO! NO!
		NO! NO!!!!" fbold +crtext
		($ "O medico foge com vergonha intensa e " .. cname .. " reage saqueando a sala imediatamente." $) +crtext
		deleteme +cr 6 8 rnd trunk-medical+
	then
	clear-cyoa
;

cyoa-bridge: hosptrader1-pick4
	ricon
	($ "Quem vai ser enviado para o " .. "UNIDADE DE QUEIMADOS" .. fbold .. "?" $) cyoa-text!
	' hosptrader1-go cyoa-team-choice
;

cyoa: hosptrader1
	"Unidade de queimados" cyoa-title!
	($ "BEM VINDO A UNIDADE DE QUEIMADOS QUEM VAI SE QUEIMAR?" fbold $) cyoa-text!
	basic-tone
	choice( "Va embora, nao faca contato visual" )choice back-esc
	team-count 3 > if
		' hosptrader1-go 2 #cyoa-team-choice
		choice( "Escolha outra pessoa" )choice: hosptrader1-pick4
	else
		' hosptrader1-go cyoa-team-choice
	then
;

0 value ht2-1
0 value ht2-2
0 value ht2-3

cyoa-choice: trade-h2-3
	($ Thegroup .. "  compra a " .. "MEGA PACOTE MEDICO!" fbold .. " 'Isso e muita cura em potencial!" .. "O Spray de Cura XL funciona mesmo em dedos torcidos." pbold $) cyoa-text! goodfx
	10 WEAPON_HEALSPRAY trunk.weapon+ 10 trunk-medical+ -15 trunk-food+ someicon ' ht2-3 ++
;
	
cyoa-choice: trade-h2-2
	($ Thegroup .. "  compra a " .. "GOLPETA DE OURO" rbold .. ". Ele nunca quebra, e acerta um zumbi a cada vez muito duro para uma arma que golpeia a velocidade maxima!" $) cyoa-text! goodfx ' ht2-2 ++
	1 WEAPON_GOLDSCALP trunk.weapon+ -15 trunk-food+ picon
;

cyoa-choice: trade-h2-1
	($ Thegroup .. "  compra um " .. "SAW BONE" rbold .. ". Ele pode atingir dois zumbis com uma corte, e atinge forte pela sua velocidade." $) cyoa-text! goodfx ' ht2-1 ++
	1 WEAPON_BONESAW trunk.weapon+ -6 trunk-food+ picon
;

cyoa: hosptrader2
	"Suprimentos Medicos" cyoa-title!
	($ "Bem vindo ao " pbold .. "O Ultimo Hospital!" rainbold .. " ' Estou vendendo algumas suprimentos medicos essenciais, para ajuda lo em sua jornada!" pbold $) cyoa-text!
	ht2-3 1 < if
		"Promocao especial hoje: Um por grupo de visitantes: Meu Pacote Medico Megapack contem 10 suprimentos medicos, mais um Spray de Cura XL com 10 spray!" pbold +crtext
	then
	basic-tone foodamt
	trunk-food 6 < if
		($ Thegroup .. " nao tem comida suficiente para comprar nada!" $) +crtext
		($ "O vendedor da " .. thegroup .. " um presente gratuito " .. "Saw Bone" rbold .. "  apenas para visitar!" $) +crtext goodfx 1 WEAPON_BONESAW trunk.weapon+ clear-cyoa
	else
		choice( "Va embora por enquanto" )choice back-esc
		trunk-food 14 > ht2-3 1 < and if
			choice( "Pacote Medico Gigante   15 alimentos" )choice: trade-h2-3
		then
		trunk-food 14 > ht2-2 1 < and if
			choice( "Bisturi Dourado   15 alimentos" )choice: trade-h2-2
		then
		trunk-food 5 > ht2-1 3 < and if
			choice( "Serrote   6 alimentos" )choice: trade-h2-1
		then
	then
;

: issick? statmod{ .sick? } ; // The only time I ever use a sickness check, ever
: hospital-superserum
	' .strength++ foreach-team-char
	' .fitness++ foreach-team-char 
	' .vitality++ foreach-team-char 
	' .dexterity++ foreach-team-char
;

cyoa-choice: hosptrader3-go

	team-damage 0 = if
		($ Thegroup .. " ' recebe um check up, mas nao ha nada de errado com eles. O medico esta visivelmente zangado e insultado por voce ter perdido seu tempo, e rabisca algo em um grafico." $) cyoa-text!
		morale++all
	else
		12 groupheal
		($ Thegroup .. "  recebe tratamento para " $) cyoa-text!
		team-damage 1 < if 
			"todos" +text 
		else
			"a maioria" +text
		then
			"  de seus ferimentos." +text
			"O medico esta incrivelmente desapontado e entediado com seus ferimentos comuns." +crtext
	then
	
	team-stack ' issick? stack-filter 0 > if 
		"MAS ESPERE! " rbold +crtext
		($ "O medico genio conturbado detecta uma doenca! Depois de cerca de 44 minutos, o medico chega a uma conclusao emocionante: A doenca era " .. "ENVENENAMENTO POR CAFE! " fbold $) +text
		($ "O medico administra um supersoro milagroso experimental para " .. thegroup .. "'! Nao cura a doenca, mas um unico dia de descanso resolvera isso, de qualquer maneira." $) +text +cr hospital-superserum
	then
	
	goodfx -5 trunk-food+ picon clear-cyoa
;

cyoa: hosptrader3
	"Provavelmente um medico genio conturbado" cyoa-title!
	($ "'Tenho tratado apenas mordidas de zumbis, algumas queimaduras leves, ferimentos com tanque de propano e dedos do pe machucados pelo que parecem ser 6 anos. Diga me que voce tem algum tipo de doenca. Idealmente, uma doenca misteriosa dramatica, mas mesmo algo como intoxicacao alimentar seria otimo neste momento. Qualquer coisa, menos outra mordida de zumbi!" pbold $) cyoa-text!
	basic-tone +cr foodamt
	trunk-food 5 < if
		($ Thegroup .. "  nao tem comida suficiente para tratamento medico!" $) +crtext
		($ " O medico tem pena de " .. thegroup .. " e distribui alguns suprimentos medicos." rbold $) +text goodfx 3 4 rnd trunk-medical+ clear-cyoa
	else
		choice( "Va embora por enquanto" )choice back-esc
			solo if
				choice( "Injury treated" )choice: hosptrader3-go
			else
				choice( "5 food" )choice: hosptrader3-go
			then
	then
;

0 value cleaninglady-meals

: cleaninglady-amt cleaninglady-meals 20 * 10 + ;
: cleaninglady-pay? trunk-food cleaninglady-amt 1 - > ;
: cleaninglady-pay cleaninglady-amt -1 * trunk-food+ ;

cyoa-choice: cleaninglady-meal-eat
	cicon
	($ cname .. " " $) cyoa-text!
	vitality++ morale4+ cchar .fullheal ' cleaninglady-meals ++
	goodfx
;

cyoa-bridge: cleaninglady-meal
	solo if
		soloer choosechar cleaninglady-meal-eat 
	else
		($ "Quem deveria comer a comida da cantina do hospital?" $) cyoa-text!
		"Essa comida dos deuses conferira um bonus para a saude maxima, e uma restauracao de moral esmagadora!" rbold +crtext
			' cleaninglady-meal-eat cyoa-team-choice
	then
;

cyoa-bridge: cleaninglady-meal-pay
	cleaninglady-meal
	cleaninglady-pay
;

cyoa-bridge: cleaninglady-recruit
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	"Limpando" cyoa-title!
	($ rname .. "  gira seu esfregao rapidamente ao redor do pulso.	" .. "Hora de limpar essa bagunca!	" pbold .. "Agua de esfregao encharca " .. thegroup .. "  muito ruim." $) cyoa-text!
	goodfx
	
	team-full? if
		($ "O grupo esta cheio. Quem voce gostaria de substituir para abrir espaco para " .. rname .. "?" $) +crtext
		recruit-replace?
	else
		recruited
		($ "Isso pareceu realmente facil." $) +text
		most-charming if
			choosechar ($ cname .. "  convence " .. rhim .. " tentar roubar alguma comida do hospital!" $) +crtext
			choice( "DA ME ESSA COMIDA DO HOSPITAL" )choice: cleaninglady-meal
		then
	then
	
	// WEAPON_SHOTGUN recruitee .weaponb! 	20 LOOT_SHOTGUN_SHELL recruitee .lootamt!
;
		
cyoa: cleaninglady
	"O Mercado Preto da Comida do Cafeteria" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	($ "'Ha uma limpadeira aqui. Ela nao parece estar realmente trabalhando. Ela olha para.. " .. thegroup .. "  para cima." $) cyoa-text! basic-tone
	cleaninglady-meals 0 > if
		"Sim, sim. Voce tem um problema muito grave.		Posso te dar outro, mas isto custara."
	else
		"'Sabia o que realmente voce esta procurando. Voce veio ate aqui por causa da comida do hospital.		Posso te trazer a refeicao da cafeteria que deseja, a um preco." 
	then pbold +crtext +cr foodamt
	cleaninglady-pay? if
		choice( "Va embora por enquanto" )choice back-esc
		choice( "Compre a comida da Cafeteria   " .. cleaninglady-amt .. "  comida nao cafeteria" )choice: cleaninglady-meal-pay
		choice( "Recuse se a fazer isso por principio" )choice
	else
		choice( "Nao posso poupar a comida" )choice
	then
	choice( "Tente recruta la" )choice: cleaninglady-recruit
;

0 value hospital-medical+

: hospital-train-amt numhums 2 * hospital-medical+ 1 + * 1 + ; 
: hospital-train-pay? trunk-food hospital-train-amt 1 - > notsolo notsolohuman and and ;
: hospital-train-pay hospital-train-amt -1 * trunk-food+ ;

: hospital-solo-amt hospital-medical+ 2 * 3 + ;
: hospital-solo-pay? trunk-food hospital-solo-amt 1 - > ;
: hospital-solo-pay hospital-solo-amt -1 * trunk-food+ ;

cyoa-choice: hospital-medical-train
	cchar .medical@ 6 < if
		($ cname .. "  recebe um curso intensivo de curiosidades sobre golfe. De alguma forma, isso parece aplicavel a habilidade medica." $) goodfx
	else
		($ cname .. "  ja obteve seu diploma medico completo da SHBU!		No entanto, a Nossa Senhora da Universidade do Banheiro do Hospital nao emite reembolsos." $) badfx
	then cyoa-text! 
	hospital-solo-pay medicalREV medical++ ' hospital-medical+ ++
;

cyoa-choice: hospital-medical-group
	($ "'O grupo inteiro recebe treinamento medico! E quase inteiramente uma pantomima em grupo da tecnica adequada de golfe." $) cyoa-text! goodfx
	hospital-train-pay someicon
	regiondef{ ' trade-medical-group! foreach-human-char } numhums ' hospital-medical+ +!
;

cyoa: hospital-medical-give
	solohuman if
		choosehuman hospital-medical-train
	then;
	"Quem deve obter este vasto conhecimento medico?" cyoa-text!
	' hospital-medical-train cyoa-human-choice
;

cyoa: hosptrainer
	($ "Universidade do Banheiro do Hospital de Saint" $) cyoa-title!
	"'Feh! Medicos! Grande coisa! Posso ensinar tudo o que voce precisa saber sobre medicina, aqui neste banheiro, muito melhor do que em qualquer citacao desmarque 'UNIVERSIDADE'." pbold text!
	"'Cuspe sai de sua boca quando dizem 'Universidade'. Voce se pergunta ha quanto tempo eles estao neste banheiro e se lavaram as maos." +crtext foodamt
		hospital-solo-pay? not if
			"' Voce nao pode pagar por mais nenhum treinamento." +crtext 
		then;
		choice( "Va embora por enquanto" )choice back-esc
		hospital-solo-pay? if
			choice( "De " .. hospital-solo-amt .. "  comida para um unico treinamento" )choice: hospital-medical-give
		then
		hospital-train-pay? if
			choice( "De " .. hospital-train-amt .. "  alimentos para treinamento em grupo" )choice: hospital-medical-group
		then
		
;
	
}

root{

: hosp-magchance
	20 %chance if
		magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! -2 movex drift
	then
;

}

roomgen{


: .ht1 args( ch ) 
	21 <- ch .specialhead! 24 <- ch .specialbody! // Jerk head, Madsci body, Burn Unit
;
: .ht2 args( ch ) 
	5 <- ch .hattype! 25 <- ch .glassestype! // Bandanna, Medical Supplies and Weapons
;
: .ht3 args( ch ) 
	6 <- ch .hattype! 10 <- ch .torsotype! // Beanie, Black Jacket, Group Heal
;

: hosptradercommon
	still-npc regiondef{ ' trader-event lt .events! 1 lt .timer! }
	
;

: hospitaldecomain 1 5 rnd case
	1 of hospitalshelfempty endof
	2 of hospitalbed endof
	3 of hospitalexam endof
	4 of hospitalbedside endof
	5 of wheelchair endof
endcase -10 movey ;

: hospitaldecoside 1 5 rnd case
	1 of ivpole endof
	2 of hospitalstool endof
	3 of hospitalstepper endof
	4 of hospitalcurtain endof
	5 of stretcher endof
endcase ;

: hosptoilets
	bathtoilet -1 movey 0.3 movex
	stall -1 movey 1 movex
	stall -1 movey -0.5 movex
	bathtoilet -1 movey 2 movex
	bathtoilet -1 movey -1.2 movex
	stall -1 movey -2 movex
	sink -1 movey -3 movex
	sink -1 movey -4 movex
	hosptradercommon 2 movey cyoa{ ' hosptrainer cyoa! }
	hosp-magchance
;

: hosptrader1
	hosptradercommon .ht1 cyoa{ ' hosptrader1 cyoa! }
;

: hosptrader2
	hosptradercommon .ht2 cyoa{ ' hosptrader2 cyoa! }
;

: hosptrader3
	hosptradercommon .ht3 cyoa{ ' hosptrader3 cyoa! }
;

: hosptrader4
	hosptradercommon .cleanerify cyoa{ ' cleaninglady cyoa! }
;

		0 stack
			' hosptrader1 shove
			' hosptrader2 shove
			' hosptrader3 shove
			' hosptrader4 shove
		value hosptrader-base
		hosptrader-base shuffles var hosptrader-picker

: hosptrader
	hosptrader-picker depths 0= if hosptrader-base shuffles to hosptrader-picker then pops nip execute ;

: hospbag
	bag-me ;

}

locdef{

location: tradehospital
		max-rooms: 1
		entry-tags: tradehospital
		script: set-theme-hospital
		pre-script: set-theme-hospital
		post-script: nad 0 %zombs
				
		room-choice: tradehospital
			room-weight: 10
			room-count: 1
			filter-only

}
				
map{ regiondef{

trade-camp-reset

: site-setup

	1 no_time!
	12 14 rnd o'clock
	
	mission{ no-edge-spawns }
	
;

	 region: specialcamp-7
		
		trade-mode

		inside
					
		loc-choice: tradehospital
			loc-weight: 99
			loc-count: 1

		post-script: site-setup
} }

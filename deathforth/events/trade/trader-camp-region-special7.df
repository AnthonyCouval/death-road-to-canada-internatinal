uses paul/region-layout.df
"events/inner/clinicloc.df" $load

// "../deathforth/rooms/TEMPROOM.txt" $load-rooms
	
" 
:map 1 
:tags tradehospital  
:scriptA hospbag 
:scriptB hosptrader 
:scriptC regiondef{ trader-weapon-buyer-inside } 
:scriptD regiondef{ tnomey-spawn-inside } 
:scriptE hospitaldecomain 
:scriptF hospitaldecoside 
:scriptG hosptoilets 
:scriptH hospitalwall 
:size 45 22 
:off 0 0 
:tiles 
000000000000000000000000000000000000000222222
000002222222222222222222000000000000000233332
000002333332333332333332222222220000000211112
0000021E1EH2HEHE121EHE12333333320000000211112
00000211B11211B11211B1121111G1120000000211112
2222221111121111F2F11112111111120000000222222
233332F111F2F111121111F2111111120000000333333
2EHE12221222221222221222122222220000000000000
21B113331333331333331333133323330000000000000
211111H111H111111H111111111F20000000000000000
211111111111111111111111111120000000000000000
21F1F2222222221122222222222120000000000000000
222222333333321133333333333120000000000000000
333333000000021111111C11111120000000000000000
0000000000000211A1111111111120000000000000000
0000000000000211111111111D1120000000000000000
000000000000021111111111111120000000000000000
000000000000021122222222222220000000000000000
000000000000021123333333333330000000000000000
000000000000021120000000000000000000000000000
000000000000022220000000000000000000000000000
000000000000033330000000000000000000000000000

"
value TRADEHOSPITAL
TRADEHOSPITAL $parse-rooms

cyoa{

cyoa-choice: hosptrader1-go
	"BRULE A JAMAIS" cyoa-title!
	($ "Les mains du docteur " .. cname .. " un petit bout de papier. Il est ecrit:	" .. "ur a dingus	u smell like egg" pbold $) cyoa-text!
	cchar statmod{ .fireproof? } not if
		"OH!
		OH! BURNED! OH!!!!!" fbold +crtext
		($ cname .. "  se sent emotionnellement devaste par les degats causes par le feu de cette brulure au troisieme degre! " 
		.. cname .. "' la peau de 's est etrangement fraiche." bbold $) +crtext
		cchar statmod{ .fireproof } morale4- badfx
		choice( "ARGH!" )choice
	else
		($ cname .. ", etant ignifuge, nest pas du tout affecte par cette brulure. " .. cname .. "  se sent extremement suffisant a ce sujet." bbold $) +crtext
		goodfx morale4+
		"NO! NO!
		NO! NO!!!!" fbold +crtext
		($ "Le docteur senfuit dans une honte intense, et " .. cname .. "  reagit en pillant immediatement la piece." $) +crtext
		deleteme +cr 6 8 rnd trunk-medical+
	then
	clear-cyoa
;

cyoa-bridge: hosptrader1-pick4
	ricon
	($ "Qui va etre envoye a l " .. "UNITE DES BRULES" .. fbold .. "?" $) cyoa-text!
	' hosptrader1-go cyoa-team-choice
;

cyoa: hosptrader1
	"Unite des brules" cyoa-title!
	($ "BIENVENUE A LUNITE DES BRULES	QUI VA SE FAIRE BRULER?" fbold $) cyoa-text!
	basic-tone
	choice( "Partez, ne faites aucun contact visuel" )choice back-esc
	team-count 3 > if
		' hosptrader1-go 2 #cyoa-team-choice
		choice( "Choisissez quelquun dautre" )choice: hosptrader1-pick4
	else
		' hosptrader1-go cyoa-team-choice
	then
;

0 value ht2-1
0 value ht2-2
0 value ht2-3

cyoa-choice: trade-h2-3
	($ Thegroup .. "  achete le " .. "MEGAPACK MEDICAL!" fbold .. " C'est beaucoup de guerison potentielle !" .. "Le spray de guerison XL fonctionne meme sur les orteils ecrases." pbold $) cyoa-text! goodfx
	10 WEAPON_HEALSPRAY trunk.weapon+ 10 trunk-medical+ -15 trunk-food+ someicon ' ht2-3 ++
;
	
cyoa-choice: trade-h2-2
	($ Thegroup .. "  achete le " .. "SCALPEL DORE" rbold .. ". Il ne se casse jamais, et frappe un zombie a la fois extremement fort pour une arme qui se balance a vitesse maximale !" $) cyoa-text! goodfx ' ht2-2 ++
	1 WEAPON_GOLDSCALP trunk.weapon+ -15 trunk-food+ picon
;

cyoa-choice: trade-h2-1
	($ Thegroup .. "  achete un " .. "SCIE A OS" rbold .. ". Il peut frapper deux zombies avec une seule tranche, et frappe fort pour sa vitesse." $) cyoa-text! goodfx ' ht2-1 ++
	1 WEAPON_BONESAW trunk.weapon+ -6 trunk-food+ picon
;

cyoa: hosptrader2
	"Fournitures medicales" cyoa-title!
	($ "Bienvenue dans " pbold .. "Lhopital final !" rainbold .. " 'Je vends des fournitures medicales cruciales, pour vous aider dans votre voyage !" pbold $) cyoa-text!
	ht2-3 1 < if
		"Offre speciale aujourdhui, une par groupe de visiteurs : Mon Mega Pack Medical contient 10 fournitures medicales, en plus dun Spray de Soins XL avec 10 pulverisations !" pbold +crtext
	then
	basic-tone foodamt
	trunk-food 6 < if
		($ Thegroup .. "  manque de nourriture pour acheter quoi que ce soit !" $) +crtext
		($ "Le commercant donne " .. thegroup .. "  un " .. "Bonesaw" rbold .. "  rien que pour la visite !" $) +crtext goodfx 1 WEAPON_BONESAW trunk.weapon+ clear-cyoa
	else
		choice( "Partir pour linstant" )choice back-esc
		trunk-food 14 > ht2-3 1 < and if
			choice( "Mega Pack Medical   15 nourriture" )choice: trade-h2-3
		then
		trunk-food 14 > ht2-2 1 < and if
			choice( "Scalpel Dore   15 nourriture" )choice: trade-h2-2
		then
		trunk-food 5 > ht2-1 3 < and if
			choice( "Six centimetres de scie a os   six aliments" )choice: trade-h2-1
		then
	then
;

: issick? statmod{ .sick? } ; // The only time I ever use a sickness check, ever
: hospital-superserum
	' .strength++ foreach-team-char
	' .fitness++ foreach-team-char 
	' .vitality++ foreach-team-char 
	' .dexterity++ foreach-team-char
;

cyoa-choice: hosptrader3-go

	team-damage 0 = if
		($ Thegroup .. " 'vous allez vous faire examiner, mais il ny a rien mal avec eux. Le medecin est visiblement en colere et insulte que vous lavez gaspillee du temps, et trace quelque chose dans un dossier." $) cyoa-text!
		morale++all
	else
		12 groupheal
		($ Thegroup .. "  se fait soigner pour " $) cyoa-text!
		team-damage 1 < if 
			"tout" +text 
		else
			" la plupart" +text
		then
			"  de leurs blessures." +text
			"Le medecin est tres decu et ennuye par vos blessures banales." +crtext
	then
	
	team-stack ' issick? stack-filter 0 > if 
		"MAIS ENATTENDRE ! " rbold +crtext
		($ "Le Medecin Genial Trouble detecte une maladie !		Apres environ quarante quatre minutes, le medecin vient dune conclusion excitante : La maladie etait " .. "Poisonnement a cafe ! " fbold $) +text
		($ " Le medecin administre un superserum miraculeux experimental a " .. thegroup .. "'! Il ny guere guerit la maladie, mais une journee de repos le fera bien, tout de meme." $) +text +cr hospital-superserum
	then
	
	goodfx -5 trunk-food+ picon clear-cyoa
;

cyoa: hosptrader3
	"Peut etre un Medecin Genial Trouble" cyoa-title!
	($ "'Jai traite que des morsures de zombies, quelques brulures legeres, blessures a reservoir de gaz propane et doigts ecrases depuis ce qui semble etre six ans.	Dites moi que vous avez une maladie. Idealement une maladie mysterieuse, mais meme quelque chose comme empoisonnement au lait serait bien a ce point. Nimporte quelle chose que ce ne soit une nouvelle morsure de zombie !" pbold $) cyoa-text!
	basic-tone +cr foodamt
	trunk-food 5 < if
		($ Thegroup .. " manque assez daliments pour traiter une maladie!" $) +crtext
		($ "  Le medecin prend pitie de " .. thegroup .. " et distribue des fournitures medicales." rbold $) +text goodfx 3 4 rnd trunk-medical+ clear-cyoa
	else
		choice( "Partir pour linstant" )choice back-esc
			solo if
				choice( "Health" )choice: hosptrader3-go
			else
				choice( "'Faites soigner les blessures du groupe   5 nourriture" )choice: hosptrader3-go
			then
	then
;

0 value cleaninglady-meals

: cleaninglady-amt cleaninglady-meals 20 * 10 + ;
: cleaninglady-pay? trunk-food cleaninglady-amt 1 - > ;
: cleaninglady-pay cleaninglady-amt -1 * trunk-food+ ;

cyoa-choice: cleaninglady-meal-eat
	cicon
	($ cname .. " ' est inonde de saveurs nostalgiques. Au debut, tout le repas semble avoir un etrange arriere gout de rassis, mais il faudrait etre fou pour penser que ce n'etait pas intentionnel. Tout cela fait partie de son attrait subtil, de son elaboration magistrale non pas comme un simple repas, mais comme une experience alimentaire totale! Il y a meme une gelee supplementaire! Une saveur rouge, une saveur jaune! Incroyable!" $) cyoa-text!
	vitality++ morale4+ cchar .fullheal ' cleaninglady-meals ++
	goodfx
;

cyoa-bridge: cleaninglady-meal
	solo if
		soloer choosechar cleaninglady-meal-eat 
	else
		($ "Qui devrait avoir le droit de manger la nourriture de la cafeteria de lhopital?" $) cyoa-text!
		"CETTE NOURRITURE DES DIEUX CONFERERA UN BONUS A LA SANTE MAXIMALE ET UNE RESTAURATION MASSIVE DU MORAL!" rbold +crtext
			' cleaninglady-meal-eat cyoa-team-choice
	then
;

cyoa-bridge: cleaninglady-meal-pay
	cleaninglady-meal
	cleaninglady-pay
;

cyoa-bridge: cleaninglady-recruit
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	"Nettoyage" cyoa-title!
	($ rname .. "  fait tourner rapidement sa vadrouille autour de son poignet." .. "	Le temps de nettoyer ce gachis!" pbold .. "Eau de serpillere trempee " .. thegroup .. " pas terrible" $) cyoa-text!
	goodfx
	
	team-full? if
		($ "Le groupe est plein. Qui souhaitez vous remplacer pour faire de la place a " .. rname .. "?" $) +crtext
		recruit-replace?
	else
		recruited
		($ "Cela a semble vraiment facile." $) +text
		most-charming if
			choosechar ($ cname .. "  convainc " .. rhim .. "  un pour faufiler de la nourriture dhopital!" $) +crtext
			choice( "DONNEZ MOI CETTE NOURRITURE DHOPITAL" )choice: cleaninglady-meal
		then
	then
	
	// WEAPON_SHOTGUN recruitee .weaponb! 	20 LOOT_SHOTGUN_SHELL recruitee .lootamt!
;
		
cyoa: cleaninglady
	"Le marche noir de la nourriture de la cafeteria" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	($ "'Il y a une femme de menage ici. Elle ne semble pas vraiment travailler. Elle mesure " .. thegroup .. " " $) cyoa-text! basic-tone
	cleaninglady-meals 0 > if
		"Euh huh. Wow, vous avez un serieux probleme. Je peux vous en procurer un autre, mais cela vous coutera cher."
	else
		"'Je sais pourquoi tu es VRAIMENT ici. Tu es venu jusqu'ici pour la nourriture de l'hopital. Je peux te faufiler le repas de la cafeteria dont tu reves, pour un prix." 
	then pbold +crtext +cr foodamt
	cleaninglady-pay? if
		choice( "Partir pour linstant" )choice back-esc
		choice( "Achetez la nourriture de la cafeteria  " .. cleaninglady-amt .. "  nourriture non cafeteria" )choice: cleaninglady-meal-pay
		choice( "Refuser de faire cela par principe" )choice
	else
		choice( "'Ne peut pas se passer de la nourriture" )choice
	then
	choice( "Tenter de la recruter" )choice: cleaninglady-recruit
;

0 value hospital-medical+

: hospital-train-amt numhums 2 * hospital-medical+ 1 + * 1 + ; 
: hospital-train-pay? trunk-food hospital-train-amt 1 - > notsolo notsolohuman and and ;
: hospital-train-pay hospital-train-amt -1 * trunk-food+ ;

: hospital-solo-amt hospital-medical+ 2 * 3 + ;
: hospital-solo-pay? trunk-food hospital-solo-amt 1 - > ;
: hospital-solo-pay hospital-solo-amt -1 * trunk-food+ ;

cyoa-choice: hospital-medical-train
	cchar .medical@ 6 < if
		($ cname .. " la plupart du temps, obtient un cours intensif de golf. Dune maniere ou dune autre, cela semble applicable a la competence medicale." $) goodfx
	else
		($ cname .. " cependant, lUniversite de la Sainte des Salles de Bains de lHopital ne rembourse pas.		However, the Our Saint of the Hospital Bathroom University does not issue refunds." $) badfx
	then cyoa-text! 
	hospital-solo-pay medicalREV medical++ ' hospital-medical+ ++
;

cyoa-choice: hospital-medical-group
	($ "Le groupe entier recoit une formation medicale! C'est presque entierement un pantomime de groupe de la technique de golf appropriee." $) cyoa-text! goodfx
	hospital-train-pay someicon
	regiondef{ ' trade-medical-group! foreach-human-char } numhums ' hospital-medical+ +!
;

cyoa: hospital-medical-give
	solohuman if
		choosehuman hospital-medical-train
	then;
	"Qui devrait obtenir cette vaste connaissance medicale?" cyoa-text!
	' hospital-medical-train cyoa-human-choice
;

cyoa: hosptrainer
	($ "Universite de la salle de bain de lhopital Saint" $) cyoa-title!
	"'Feh! Les medecins! Quelle importance! Je peux vous apprendre tout ce que vous devez savoir sur la medecine, ici dans cette salle de bain, bien mieux que dans n'importe quelle 'UNIVERSITE' entre guillemets." pbold text!
	"'La salive jaillit de leur bouche quand ils disent 'Universite'. Vous vous demandez vaguement depuis combien de temps ils sont dans cette salle de bain et s'ils se sont lave les mains." +crtext foodamt
		hospital-solo-pay? not if
			"Vous ne pouvez plus payer pour une autre formation." +crtext 
		then;
		choice( "Partir pour linstant" )choice back-esc
		hospital-solo-pay? if
			choice( "Donne " .. hospital-solo-amt .. " alimentation pour un entrainement unique" )choice: hospital-medical-give
		then
		hospital-train-pay? if
			choice( "Donne " .. hospital-train-amt .. "  nourriture pour lentrainement de groupe" )choice: hospital-medical-group
		then
		
;
	
}

root{

: hosp-magchance
	20 %chance if
		magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! -2 movex drift
	then
;

}

roomgen{


: .ht1 args( ch ) 
	21 <- ch .specialhead! 24 <- ch .specialbody! // Jerk head, Madsci body, Burn Unit
;
: .ht2 args( ch ) 
	5 <- ch .hattype! 25 <- ch .glassestype! // Bandanna, Medical Supplies and Weapons
;
: .ht3 args( ch ) 
	6 <- ch .hattype! 10 <- ch .torsotype! // Beanie, Black Jacket, Group Heal
;

: hosptradercommon
	still-npc regiondef{ ' trader-event lt .events! 1 lt .timer! }
	
;

: hospitaldecomain 1 5 rnd case
	1 of hospitalshelfempty endof
	2 of hospitalbed endof
	3 of hospitalexam endof
	4 of hospitalbedside endof
	5 of wheelchair endof
endcase -10 movey ;

: hospitaldecoside 1 5 rnd case
	1 of ivpole endof
	2 of hospitalstool endof
	3 of hospitalstepper endof
	4 of hospitalcurtain endof
	5 of stretcher endof
endcase ;

: hosptoilets
	bathtoilet -1 movey 0.3 movex
	stall -1 movey 1 movex
	stall -1 movey -0.5 movex
	bathtoilet -1 movey 2 movex
	bathtoilet -1 movey -1.2 movex
	stall -1 movey -2 movex
	sink -1 movey -3 movex
	sink -1 movey -4 movex
	hosptradercommon 2 movey cyoa{ ' hosptrainer cyoa! }
	hosp-magchance
;

: hosptrader1
	hosptradercommon .ht1 cyoa{ ' hosptrader1 cyoa! }
;

: hosptrader2
	hosptradercommon .ht2 cyoa{ ' hosptrader2 cyoa! }
;

: hosptrader3
	hosptradercommon .ht3 cyoa{ ' hosptrader3 cyoa! }
;

: hosptrader4
	hosptradercommon .cleanerify cyoa{ ' cleaninglady cyoa! }
;

		0 stack
			' hosptrader1 shove
			' hosptrader2 shove
			' hosptrader3 shove
			' hosptrader4 shove
		value hosptrader-base
		hosptrader-base shuffles var hosptrader-picker

: hosptrader
	hosptrader-picker depths 0= if hosptrader-base shuffles to hosptrader-picker then pops nip execute ;

: hospbag
	bag-me ;

}

locdef{

location: tradehospital
		max-rooms: 1
		entry-tags: tradehospital
		script: set-theme-hospital
		pre-script: set-theme-hospital
		post-script: nad 0 %zombs
				
		room-choice: tradehospital
			room-weight: 10
			room-count: 1
			filter-only

}
				
map{ regiondef{

trade-camp-reset

: site-setup

	1 no_time!
	12 14 rnd o'clock
	
	mission{ no-edge-spawns }
	
;

	 region: specialcamp-7
		
		trade-mode

		inside
					
		loc-choice: tradehospital
			loc-weight: 99
			loc-count: 1

		post-script: site-setup
} }

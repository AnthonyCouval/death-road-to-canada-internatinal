uses paul/region-layout.df
"events/inner/clinicloc.df" $load

// "../deathforth/rooms/TEMPROOM.txt" $load-rooms
	
" 
:map 1 
:tags tradehospital  
:scriptA hospbag 
:scriptB hosptrader 
:scriptC regiondef{ trader-weapon-buyer-inside } 
:scriptD regiondef{ tnomey-spawn-inside } 
:scriptE hospitaldecomain 
:scriptF hospitaldecoside 
:scriptG hosptoilets 
:scriptH hospitalwall 
:size 45 22 
:off 0 0 
:tiles 
000000000000000000000000000000000000000222222
000002222222222222222222000000000000000233332
000002333332333332333332222222220000000211112
0000021E1EH2HEHE121EHE12333333320000000211112
00000211B11211B11211B1121111G1120000000211112
2222221111121111F2F11112111111120000000222222
233332F111F2F111121111F2111111120000000333333
2EHE12221222221222221222122222220000000000000
21B113331333331333331333133323330000000000000
211111H111H111111H111111111F20000000000000000
211111111111111111111111111120000000000000000
21F1F2222222221122222222222120000000000000000
222222333333321133333333333120000000000000000
333333000000021111111C11111120000000000000000
0000000000000211A1111111111120000000000000000
0000000000000211111111111D1120000000000000000
000000000000021111111111111120000000000000000
000000000000021122222222222220000000000000000
000000000000021123333333333330000000000000000
000000000000021120000000000000000000000000000
000000000000022220000000000000000000000000000
000000000000033330000000000000000000000000000

"
value TRADEHOSPITAL
TRADEHOSPITAL $parse-rooms

cyoa{

cyoa-choice: hosptrader1-go
	"BRULE POUR TOUJOURS." cyoa-title!
	($ "Le medecin tient quelque chose. " .. cname .. " une petite feuille de papier. c'elle lit:. ." .. "tu parfums comme des oeufs." pbold $) cyoa-text!
	cchar statmod{ .fireproof? } not if
		"OH!
		OH! BURNED! OH!!!!!" fbold +crtext
		($ cname .. " est emotionnellement aneantee par les degats de feu de ce malencontreux brule-moi ! " 
		.. cname .. "la peau de lui est etrangement froide." bbold $) +crtext
		cchar statmod{ .fireproof } morale4- badfx
		choice( "ARRETE DE HURLER, JE NE PE" )choice
	else
		($ cname .. "c'est indemne au feu, donc ce brule-tout n'a aucun effet sur lui. " .. cname .. " se sent extremement satisfait de cela." bbold $) +crtext
		goodfx morale4+
		"NO! NO!
		NO! NO!!!!" fbold +crtext
		($ "Le medecin s'enfuit en profonde honte. " .. cname .. " il reagit en pillant la piece immediatement." $) +crtext
		deleteme +cr 6 8 rnd trunk-medical+
	then
	clear-cyoa
;

cyoa-bridge: hosptrader1-pick4
	ricon
	($ "Qui ira etre envoye ? " .. "TROP EPICE." .. fbold .. "?" $) cyoa-text!
	' hosptrader1-go cyoa-team-choice
;

cyoa: hosptrader1
	"Unite de brulures" cyoa-title!
	($ "Benavez vous souhaitez joindre la presente unite de brulure ? . . . 	quelle personne allera etre brule" fbold $) cyoa-text!
	basic-tone
	choice( "Quittez, ne me regardez pas dans les yeux." )choice back-esc
	team-count 3 > if
		' hosptrader1-go 2 #cyoa-team-choice
		choice( "Choisissez quelqu'un d'autre." )choice: hosptrader1-pick4
	else
		' hosptrader1-go cyoa-team-choice
	then
;

0 value ht2-1
0 value ht2-2
0 value ht2-3

cyoa-choice: trade-h2-3
	($ Thegroup .. " achete " .. "PACK MEDICAL GIGANTQUE !" fbold .. " Cela represente beaucoup de guerison potentielle." .. "La spray de guerison xl fonctionne egalement sur les doigts bourres." pbold $) cyoa-text! goodfx
	10 WEAPON_HEALSPRAY trunk.weapon+ 10 trunk-medical+ -15 trunk-food+ someicon ' ht2-3 ++
;
	
cyoa-choice: trade-h2-2
	($ Thegroup .. " achete " .. "EPEE D'OR" rbold .. "cette arme ne s'est jamais cassee et frappe un zombie a la fois avec une force extreme pour une arme qui fonctionne a plein regime!" $) cyoa-text! goodfx ' ht2-2 ++
	1 WEAPON_GOLDSCALP trunk.weapon+ -15 trunk-food+ picon
;

cyoa-choice: trade-h2-1
	($ Thegroup .. " achete une " .. "DOULEUR, DOULEUR !" rbold .. "cette arme peut toucher deux zombies avec une coupe unique et frappe fort pour sa vitesse." $) cyoa-text! goodfx ' ht2-1 ++
	1 WEAPON_BONESAW trunk.weapon+ -6 trunk-food+ picon
;

cyoa: hosptrader2
	"Rue de la ville a voiture" cyoa-title!
	($ "Approvisionnements... . " pbold .. "L'hopital dernier!" rainbold .. " Je vends des approvisionnements medicaux essentiels pour vous aider sur votre parcours !" pbold $) cyoa-text!
	ht2-3 1 < if
		"Specialement aujourd'hui, une offre exceptionnelle par groupe de visiteurs : mon empaquage medical contient 10 fournitures medicales supplementaires, ainsi qu'un spray guerison xl a 10 jets en bonus !" pbold +crtext
	then
	basic-tone foodamt
	trunk-food 6 < if
		($ Thegroup .. " manque assez d'argent pour acheter quoi que ce soit en raison de la carence alimentaire." $) +crtext
		($ "Mourir a la morsure de chien. " .. thegroup .. " une complementaire. " .. "Autres" rbold .. " seullement pour la visite !" $) +crtext goodfx 1 WEAPON_BONESAW trunk.weapon+ clear-cyoa
	else
		choice( "Laissez-le pour le moment" )choice back-esc
		trunk-food 14 > ht2-3 1 < and if
			choice( "Voici quinze aliments nutritifs pour une bonne sante medicale :" )choice: trade-h2-3
		then
		trunk-food 14 > ht2-2 1 < and if
			choice( "Scalpel d'or - 15 aliments" )choice: trade-h2-2
		then
		trunk-food 5 > ht2-1 3 < and if
			choice( "Scie a os - 6 aliments" )choice: trade-h2-1
		then
	then
;

: issick? statmod{ .sick? } ; // The only time I ever use a sickness check, ever
: hospital-superserum
	' .strength++ foreach-team-char
	' .fitness++ foreach-team-char 
	' .vitality++ foreach-team-char 
	' .dexterity++ foreach-team-char
;

cyoa-choice: hosptrader3-go

	team-damage 0 = if
		($ Thegroup .. " vous avez effectue une consultation medicale, mais rien de grave n'a ete detecte. cependant, le docteur semble etre inquiet et insulte, car vous auriez gaspille son temps. il trace quelque chose dans votre dossier medical." $) cyoa-text!
		morale++all
	else
		12 groupheal
		($ Thegroup .. " obtient traitement pour " $) cyoa-text!
		team-damage 1 < if 
			"toutes les" +text 
		else
			"le plus grand nombre" +text
		then
			" soigner les blessures de chacun d'eux." +text
			"Le docteur est tres decu et ennuye par vos blessures banales." +crtext
	then
	
	team-stack ' issick? stack-filter 0 > if 
		"ATTENDEZ ! " rbold +crtext
		($ "Apres environ 44 minutes, le medecin arrive a une conclusion excitante : l'maladie etait ... 		 " .. "TRES DANGEREUX. " fbold $) +text
		($ "Le medecin administre un serum experimental miraculeux a " .. thegroup .. "une journee de repos ne guerira pas l'affaire, mais elle le fera peut-etre tout de meme." $) +text +cr hospital-superserum
	then
	
	goodfx -5 trunk-food+ picon clear-cyoa
;

cyoa: hosptrader3
	"Un medecin genie, probablement perturbe." cyoa-title!
	($ "Je me suis occupe de rien d'autre que des morsures de zombies, quelques brulures legeres, blessures par gaz propane et doigts gonfles pendant pres de six ans maintenant... . 	avoue-moi avoir quelque maladie. idealement une maladie mysterieuse dramatique, mais meme une simple intoxication alimentaire serait merveilleux a ce stade. n'importe quelle chose que ce ne soit une nouvelle morsure de zombies!" pbold $) cyoa-text!
	basic-tone +cr foodamt
	trunk-food 5 < if
		($ Thegroup .. " manque assez d'aliments pour traiter medicalement !" $) +crtext
		($ " Le medecin eprouve des sentiments misericordieux. " .. thegroup .. " distribue quelques fournitures medicales et met en main quelques-unes d'entre elles." rbold $) +text goodfx 3 4 rnd trunk-medical+ clear-cyoa
	else
		choice( "Laissez-le pour le moment" )choice back-esc
			solo if
				choice( "Traiter des blessures - 5 aliments :" )choice: hosptrader3-go
			else
				choice( "Soignez les blessures du groupe - 5 aliments" )choice: hosptrader3-go
			then
	then
;

0 value cleaninglady-meals

: cleaninglady-amt cleaninglady-meals 20 * 10 + ;
: cleaninglady-pay? trunk-food cleaninglady-amt 1 - > ;
: cleaninglady-pay cleaninglady-amt -1 * trunk-food+ ;

cyoa-choice: cleaninglady-meal-eat
	cicon
	($ cname .. " cette repas est inondee de saveurs nostalgiques. au depart, tout le repas semble avoir un gout etrange et deshabille, mais il faudrait etre un fou pour penser que cela n'etait pas prevu. c'est tout simplement partie integrante de l'appel subtil de cela, de la maitrise savoureuse de cela non pas seulement comme une simple recolte, mais comme une experience alimentaire totale!. . 	il y a meme une gelatine supplementaire ! une saveur rouge, une saveur jaune ! incroyable !" $) cyoa-text!
	vitality++ morale4+ cchar .fullheal ' cleaninglady-meals ++
	goodfx
;

cyoa-bridge: cleaninglady-meal
	solo if
		soloer choosechar cleaninglady-meal-eat 
	else
		($ "Tout le monde est invite a manger dans la cantine hospitaliere." $) cyoa-text!
		"CE ALIMENT DES DIEUX ACCORERA UN BONUS A LA SANTE MAXIMALE ET UNE GROSSE RESTAURATION DE MORAL!" rbold +crtext
			' cleaninglady-meal-eat cyoa-team-choice
	then
;

cyoa-bridge: cleaninglady-meal-pay
	cleaninglady-meal
	cleaninglady-pay
;

cyoa-bridge: cleaninglady-recruit
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	"Nettoyage" cyoa-title!
	($ rname .. " elle tourne rapidement son balai autour de son poignet." .. "Il est temps de nettoyer cette impasse !" pbold .. "L'eau de la souille a nettoyer " .. thegroup .. " tres mauvais." $) cyoa-text!
	goodfx
	
	team-full? if
		($ "Vous devez laisser quelqu'un pour que place soit disponible. quel personne voulez-vous remplacer ? " .. rname .. "?" $) +crtext
		recruit-replace?
	else
		recruited
		($ "Cela semblait vraiment simple." $) +text
		most-charming if
			choosechar ($ cname .. " convainc " .. rhim .. " pour s'echapper avec de la nourriture d'hopital !" $) +crtext
			choice( "DONNE-MOI CE LAIT D'HOSPITALIER" )choice: cleaninglady-meal
		then
	then
	
	// WEAPON_SHOTGUN recruitee .weaponb! 	20 LOOT_SHOTGUN_SHELL recruitee .lootamt!
;
		
cyoa: cleaninglady
	"Mode rares termine" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	($ "Elle est une femme nettoyeuse ici. elle ne semble pas travailler reellement. sa taille est : " .. thegroup .. " haut." $) cyoa-text! basic-tone
	cleaninglady-meals 0 > if
		"C'est un vrai probleme grave... je peux vous en fournir un autre, mais cela coutera cher."
	else
		"Je sais ce que vous venez reellement chercher ici. vous etes venus jusqu'ici pour le repas de l'hopital... . je peux vous en procurer discretement la repas du restaurant cafeteria, a un prix." 
	then pbold +crtext +cr foodamt
	cleaninglady-pay? if
		choice( "Laissez-le pour le moment" )choice back-esc
		choice( "Acheter des aliments de la cantine. " .. cleaninglady-amt .. " aliments non pris en charge par le cafeteria" )choice: cleaninglady-meal-pay
		choice( "N'en fais pas cas, je suis pret pour vous." )choice
	else
		choice( "Desole, je ne peux pas m'en passer de la nourriture." )choice
	then
	choice( "Essayer de la recruter" )choice: cleaninglady-recruit
;

0 value hospital-medical+

: hospital-train-amt numhums 2 * hospital-medical+ 1 + * 1 + ; 
: hospital-train-pay? trunk-food hospital-train-amt 1 - > notsolo notsolohuman and and ;
: hospital-train-pay hospital-train-amt -1 * trunk-food+ ;

: hospital-solo-amt hospital-medical+ 2 * 3 + ;
: hospital-solo-pay? trunk-food hospital-solo-amt 1 - > ;
: hospital-solo-pay hospital-solo-amt -1 * trunk-food+ ;

cyoa-choice: hospital-medical-train
	cchar .medical@ 6 < if
		($ cname .. " principalement, il recoit une formation rapide au sujet de la curiosite du golf. cela ressemble a cela pour les competences medicales." $) goodfx
	else
		($ cname .. " les etudiants ont deja obtenu leur diplome complet en medecine de l'shbu ! cependant, notre saint de l'universite des bains de l'hopital ne rembourse pas." $) badfx
	then cyoa-text! 
	hospital-solo-pay medicalREV medical++ ' hospital-medical+ ++
;

cyoa-choice: hospital-medical-group
	($ "Toute le groupe recoit une formation medicale ! c'est presque entierement un mime de groupe sur la bonne technique de golf." $) cyoa-text! goodfx
	hospital-train-pay someicon
	regiondef{ ' trade-medical-group! foreach-human-char } numhums ' hospital-medical+ +!
;

cyoa: hospital-medical-give
	solohuman if
		choosehuman hospital-medical-train
	then;
	"Ce vaste connaissances medicales devrait etre accordees a ceux qui en ont besoin, c'est-a-dire aux medecins, infirmieres et etudiants de sante." cyoa-text!
	' hospital-medical-train cyoa-human-choice
;

cyoa: hosptrainer
	($ "Salle de bains de l'universite saint-hospital" $) cyoa-title!
	"Feh! medecins! grosse affaire! je peux vous apprendre tout ce que vous devez savoir sur la medecine, ici dans cette salle de bain, bien mieux que dans n'importe quelle « universite » entre guillemets." pbold text!
	"Cela jette de la salive de leur bouche quand ils disent 'universite'. vous pensez vaguement depuis combien de temps ils sont dans ce bathroom et si ils ont lave leurs mains." +crtext foodamt
		hospital-solo-pay? not if
			"Vous ne pouvez plus vous permettre d'autres formations." +crtext 
		then;
		choice( "Laissez-le pour le moment" )choice back-esc
		hospital-solo-pay? if
			choice( "Donner " .. hospital-solo-amt .. " nourriture pour une formation unique" )choice: hospital-medical-give
		then
		hospital-train-pay? if
			choice( "Donner " .. hospital-train-amt .. " vous devez vous entrainer et mettre du muscle !" )choice: hospital-medical-group
		then
		
;
	
}

root{

: hosp-magchance
	20 %chance if
		magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! -2 movex drift
	then
;

}

roomgen{


: .ht1 args( ch ) 
	21 <- ch .specialhead! 24 <- ch .specialbody! // Jerk head, Madsci body, Burn Unit
;
: .ht2 args( ch ) 
	5 <- ch .hattype! 25 <- ch .glassestype! // Bandanna, Medical Supplies and Weapons
;
: .ht3 args( ch ) 
	6 <- ch .hattype! 10 <- ch .torsotype! // Beanie, Black Jacket, Group Heal
;

: hosptradercommon
	still-npc regiondef{ ' trader-event lt .events! 1 lt .timer! }
	
;

: hospitaldecomain 1 5 rnd case
	1 of hospitalshelfempty endof
	2 of hospitalbed endof
	3 of hospitalexam endof
	4 of hospitalbedside endof
	5 of wheelchair endof
endcase -10 movey ;

: hospitaldecoside 1 5 rnd case
	1 of ivpole endof
	2 of hospitalstool endof
	3 of hospitalstepper endof
	4 of hospitalcurtain endof
	5 of stretcher endof
endcase ;

: hosptoilets
	bathtoilet -1 movey 0.3 movex
	stall -1 movey 1 movex
	stall -1 movey -0.5 movex
	bathtoilet -1 movey 2 movex
	bathtoilet -1 movey -1.2 movex
	stall -1 movey -2 movex
	sink -1 movey -3 movex
	sink -1 movey -4 movex
	hosptradercommon 2 movey cyoa{ ' hosptrainer cyoa! }
	hosp-magchance
;

: hosptrader1
	hosptradercommon .ht1 cyoa{ ' hosptrader1 cyoa! }
;

: hosptrader2
	hosptradercommon .ht2 cyoa{ ' hosptrader2 cyoa! }
;

: hosptrader3
	hosptradercommon .ht3 cyoa{ ' hosptrader3 cyoa! }
;

: hosptrader4
	hosptradercommon .cleanerify cyoa{ ' cleaninglady cyoa! }
;

		0 stack
			' hosptrader1 shove
			' hosptrader2 shove
			' hosptrader3 shove
			' hosptrader4 shove
		value hosptrader-base
		hosptrader-base shuffles var hosptrader-picker

: hosptrader
	hosptrader-picker depths 0= if hosptrader-base shuffles to hosptrader-picker then pops nip execute ;

: hospbag
	bag-me ;

}

locdef{

location: tradehospital
		max-rooms: 1
		entry-tags: tradehospital
		script: set-theme-hospital
		pre-script: set-theme-hospital
		post-script: nad 0 %zombs
				
		room-choice: tradehospital
			room-weight: 10
			room-count: 1
			filter-only

}
				
map{ regiondef{

trade-camp-reset

: site-setup

	1 no_time!
	12 14 rnd o'clock
	
	mission{ no-edge-spawns }
	
;

	 region: specialcamp-7
		
		trade-mode

		inside
					
		loc-choice: tradehospital
			loc-weight: 99
			loc-count: 1

		post-script: site-setup
} }

uses paul/region-layout.df
"events/inner/clinicloc.df" $load

// "../deathforth/rooms/TEMPROOM.txt" $load-rooms
	
" 
:map 1 
:tags tradehospital  
:scriptA hospbag 
:scriptB hosptrader 
:scriptC regiondef{ trader-weapon-buyer-inside } 
:scriptD regiondef{ tnomey-spawn-inside } 
:scriptE hospitaldecomain 
:scriptF hospitaldecoside 
:scriptG hosptoilets 
:scriptH hospitalwall 
:size 45 22 
:off 0 0 
:tiles 
000000000000000000000000000000000000000222222
000002222222222222222222000000000000000233332
000002333332333332333332222222220000000211112
0000021E1EH2HEHE121EHE12333333320000000211112
00000211B11211B11211B1121111G1120000000211112
2222221111121111F2F11112111111120000000222222
233332F111F2F111121111F2111111120000000333333
2EHE12221222221222221222122222220000000000000
21B113331333331333331333133323330000000000000
211111H111H111111H111111111F20000000000000000
211111111111111111111111111120000000000000000
21F1F2222222221122222222222120000000000000000
222222333333321133333333333120000000000000000
333333000000021111111C11111120000000000000000
0000000000000211A1111111111120000000000000000
0000000000000211111111111D1120000000000000000
000000000000021111111111111120000000000000000
000000000000021122222222222220000000000000000
000000000000021123333333333330000000000000000
000000000000021120000000000000000000000000000
000000000000022220000000000000000000000000000
000000000000033330000000000000000000000000000

"
value TRADEHOSPITAL
TRADEHOSPITAL $parse-rooms

cyoa{

cyoa-choice: hosptrader1-go
	"QUEMADO PARA SIEMPRE" cyoa-title!
	($ "El medico " .. cname .. "  un pequeno trozo de papel. Dice:	" .. "eres un idiota	hueles a huevo" pbold $) cyoa-text!
	cchar statmod{ .fireproof? } not if
		"OH!
		OH! BURNED! OH!!!!!" fbold +crtext
		($ cname .. "  se siente emocionalmente devastado por el dano causado por el fuego de esta quemadura enfermiza! " 
		.. cname .. "'la piel de 's se siente extranamente fria." bbold $) +crtext
		cchar statmod{ .fireproof } morale4- badfx
		choice( "ARGH!" )choice
	else
		($ cname .. ", al ser ignifugo, no se ve afectado por esta quemadura en absoluto. " .. cname .. "  se siente extremadamente satisfecho con esto." bbold $) +crtext
		goodfx morale4+
		"NO! NO!
		NO! NO!!!!" fbold +crtext
		($ "El medico huye con intensa verguenza y " .. cname .. "  reacciona saqueando la habitacion de inmediato." $) +crtext
		deleteme +cr 6 8 rnd trunk-medical+
	then
	clear-cyoa
;

cyoa-bridge: hosptrader1-pick4
	ricon
	($ "Quien va a ser enviado al " .. "UNIDAD DE QUEMADOS" .. fbold .. "?" $) cyoa-text!
	' hosptrader1-go cyoa-team-choice
;

cyoa: hosptrader1
	"Unidad de Quemados" cyoa-title!
	($ "BIENVENIDOS A LA UNIDAD DE QUEMADOS	QUIEN SE VA A QUEMAR?" fbold $) cyoa-text!
	basic-tone
	choice( "Vete, no hagas contacto visual" )choice back-esc
	team-count 3 > if
		' hosptrader1-go 2 #cyoa-team-choice
		choice( "Escoge a alguien mas" )choice: hosptrader1-pick4
	else
		' hosptrader1-go cyoa-team-choice
	then
;

0 value ht2-1
0 value ht2-2
0 value ht2-3

cyoa-choice: trade-h2-3
	($ Thegroup .. "  compra la " .. "MEGA PAQUETE MEDICO!" fbold .. " ' Eso es mucho potencial de curacion!" .. "El aerosol curativo XL funciona incluso en los dedos gordos del pie." pbold $) cyoa-text! goodfx
	10 WEAPON_HEALSPRAY trunk.weapon+ 10 trunk-medical+ -15 trunk-food+ someicon ' ht2-3 ++
;
	
cyoa-choice: trade-h2-2
	($ Thegroup .. "  compra la " .. "ESCALPELO DORADO" rbold .. ". Nunca se rompe y golpea a un zombi a la vez con mucha fuerza para un arma que se balancea a la velocidad maxima!" $) cyoa-text! goodfx ' ht2-2 ++
	1 WEAPON_GOLDSCALP trunk.weapon+ -15 trunk-food+ picon
;

cyoa-choice: trade-h2-1
	($ Thegroup .. "  compra un " .. "SIERRA PARA HUESOS" rbold .. ". Puede golpear a dos zombis con un corte y golpea fuerte por su velocidad." $) cyoa-text! goodfx ' ht2-1 ++
	1 WEAPON_BONESAW trunk.weapon+ -6 trunk-food+ picon
;

cyoa: hosptrader2
	"Suministros medicos" cyoa-title!
	($ "Bienvenido a " pbold .. "El ultimo hospital!" rainbold .. " ' Estoy vendiendo algunos suministros medicos cruciales para ayudarte en tu viaje!" pbold $) cyoa-text!
	ht2-3 1 < if
		"Oferta especial de hoy, una por grupo de visitantes: Mi Megapaquete medico contiene 10 suministros medicos, ademas de un Aerosol curativo XL con 10 aerosoles!" pbold +crtext
	then
	basic-tone foodamt
	trunk-food 6 < if
		($ Thegroup .. "  no tiene suficiente comida para comprar nada!" $) +crtext
		($ "El comerciante da " .. thegroup .. "  un complementario " .. "Bonesaw" rbold .. "  solo por visitarnos!" $) +crtext goodfx 1 WEAPON_BONESAW trunk.weapon+ clear-cyoa
	else
		choice( "Dejalo por ahora" )choice back-esc
		trunk-food 14 > ht2-3 1 < and if
			choice( "Paquete medico grande 15 alimentos" )choice: trade-h2-3
		then
		trunk-food 14 > ht2-2 1 < and if
			choice( "Bisturi dorado 15 alimentos" )choice: trade-h2-2
		then
		trunk-food 5 > ht2-1 3 < and if
			choice( "Bonesaw   6 alimentos" )choice: trade-h2-1
		then
	then
;

: issick? statmod{ .sick? } ; // The only time I ever use a sickness check, ever
: hospital-superserum
	' .strength++ foreach-team-char
	' .fitness++ foreach-team-char 
	' .vitality++ foreach-team-char 
	' .dexterity++ foreach-team-char
;

cyoa-choice: hosptrader3-go

	team-damage 0 = if
		($ Thegroup .. " ' se revisa, pero no tiene nada malo. El doctor esta visiblemente enojado e insultado por haberle hecho perder el tiempo, y garabatea algo en una grafica." $) cyoa-text!
		morale++all
	else
		12 groupheal
		($ Thegroup .. " recibe tratamiento para " $) cyoa-text!
		team-damage 1 < if 
			"todos" +text 
		else
			"la mayoria" +text
		then
			" de sus heridas." +text
			"El doctor esta increiblemente decepcionado y aburrido por tus lesiones comunes." +crtext
	then
	
	team-stack ' issick? stack-filter 0 > if 
		"PERO ESPERA! " rbold +crtext
		($ "El medico genio problematico detecta una enfermedad! Despues de unos 44 minutos, el medico llega a una emocionante conclusion: La enfermedad fue " .. "ENVENENAMIENTO POR CAFE! " fbold $) +text
		($ "El medico administra un suero milagroso experimental a " .. thegroup .. "'! No cura la enfermedad, pero un solo dia de descanso lo hara, de todos modos." $) +text +cr hospital-superserum
	then
	
	goodfx -5 trunk-food+ picon clear-cyoa
;

cyoa: hosptrader3
	"Probablemente un medico genio problematico" cyoa-title!
	($ "'He hemos estado tratando solo de heridas por mordeduras de zombis, algunos leves quemaduras, lesiones de tanques de propano y dedos enjambrados durante lo que me siente como seis anos ya.	Dime si tienes alguna clase de enfermedad. Idealmente una misteriosa enfermedad dramatica, pero incluso algo como intoxicacion alimentaria hubiera sido genial en este momento. Algo que no sea otra mordedura de zombi!" pbold $) cyoa-text!
	basic-tone +cr foodamt
	trunk-food 5 < if
		($ Thegroup .. " carece de suficiente alimento para tratamiento medico" $) +crtext
		($ " El doctor se compadece de " .. thegroup .. " y entrega algunos suministros medicos." rbold $) +text goodfx 3 4 rnd trunk-medical+ clear-cyoa
	else
		choice( "Dejalo por ahora" )choice back-esc
			solo if
				choice( "Health" )choice: hosptrader3-go
			else
				choice( "5 food" )choice: hosptrader3-go
			then
	then
;

0 value cleaninglady-meals

: cleaninglady-amt cleaninglady-meals 20 * 10 + ;
: cleaninglady-pay? trunk-food cleaninglady-amt 1 - > ;
: cleaninglady-pay cleaninglady-amt -1 * trunk-food+ ;

cyoa-choice: cleaninglady-meal-eat
	cicon
	($ cname .. " " $) cyoa-text!
	vitality++ morale4+ cchar .fullheal ' cleaninglady-meals ++
	goodfx
;

cyoa-bridge: cleaninglady-meal
	solo if
		soloer choosechar cleaninglady-meal-eat 
	else
		($ "Quien deberia comer la comida de la cafeteria del hospital?" $) cyoa-text!
		"ESTA COMIDA DE LOS DIOSES CONFERIRA UN BONO A LA SALUD MAXIMA, Y UNA RESTAURACION MASIVA DE MORAL!" rbold +crtext
			' cleaninglady-meal-eat cyoa-team-choice
	then
;

cyoa-bridge: cleaninglady-meal-pay
	cleaninglady-meal
	cleaninglady-pay
;

cyoa-bridge: cleaninglady-recruit
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	"Limpiando" cyoa-title!
	($ rname .. " gira su escoba rapidamente alrededor de su muneca." .. "Tiempo de limpiar este desorden!	" pbold .. "Agua de escoba moja a " .. thegroup .. "  bastante mal." $) cyoa-text!
	goodfx
	
	team-full? if
		($ "El grupo esta lleno. A quien te gustaria reemplazar para hacerle un lugar a " .. rname .. "?" $) +crtext
		recruit-replace?
	else
		recruited
		($ "Eso parecia muy facil." $) +text
		most-charming if
			choosechar ($ cname .. "  convence " .. rhim .. " a to sneak out some Hospital Food!" $) +crtext
			choice( "DAME ESA COMIDA DE HOSPITAL" )choice: cleaninglady-meal
		then
	then
	
	// WEAPON_SHOTGUN recruitee .weaponb! 	20 LOOT_SHOTGUN_SHELL recruitee .lootamt!
;
		
cyoa: cleaninglady
	"El Mercado" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	($ "'Hay una senora de la limpieza aqui. Parece que en realidad no esta trabajando. Ella mide " .. thegroup .. "  arriba." $) cyoa-text! basic-tone
	cleaninglady-meals 0 > if
		"Aja. Vaya, tienes un problema grave.		Puedo conseguirte otro, pero te costara."
	else
		"'Se por que REALMENTE estas aqui. Has venido hasta aqui por la comida del hospital.		Puedo conseguirte la comida de la cafeteria que tanto deseas, por un precio." 
	then pbold +crtext +cr foodamt
	cleaninglady-pay? if
		choice( "Dejalo por ahora" )choice back-esc
		choice( "Comprar la comida de la cafeteria   " .. cleaninglady-amt .. "  comida que no sea de la cafeteria" )choice: cleaninglady-meal-pay
		choice( "Negarse a hacer esto por principios" )choice
	else
		choice( "'No puedo prescindir de la comida" )choice
	then
	choice( "Tratar de reclutarla" )choice: cleaninglady-recruit
;

0 value hospital-medical+

: hospital-train-amt numhums 2 * hospital-medical+ 1 + * 1 + ; 
: hospital-train-pay? trunk-food hospital-train-amt 1 - > notsolo notsolohuman and and ;
: hospital-train-pay hospital-train-amt -1 * trunk-food+ ;

: hospital-solo-amt hospital-medical+ 2 * 3 + ;
: hospital-solo-pay? trunk-food hospital-solo-amt 1 - > ;
: hospital-solo-pay hospital-solo-amt -1 * trunk-food+ ;

cyoa-choice: hospital-medical-train
	cchar .medical@ 6 < if
		($ cname .. "  mayormente recibe un curso intensivo de trivia de golf. De alguna manera, esto se siente aplicable a la habilidad medica." $) goodfx
	else
		($ cname .. "  ya obtuvo su titulo medico completo de SHBU!		Sin embargo, Our Saint of the Hospital Bathroom University no emite reembolsos." $) badfx
	then cyoa-text! 
	hospital-solo-pay medicalREV medical++ ' hospital-medical+ ++
;

cyoa-choice: hospital-medical-group
	($ "'Todo el grupo recibe capacitacion medica! Es casi en su totalidad una pantomima grupal de la tecnica adecuada de golf." $) cyoa-text! goodfx
	hospital-train-pay someicon
	regiondef{ ' trade-medical-group! foreach-human-char } numhums ' hospital-medical+ +!
;

cyoa: hospital-medical-give
	solohuman if
		choosehuman hospital-medical-train
	then;
	"Quien deberia obtener este vasto conocimiento medico?" cyoa-text!
	' hospital-medical-train cyoa-human-choice
;

cyoa: hosptrainer
	($ "Saint Hospital Bathroom University" $) cyoa-title!
	"'Feh! Doctores! Gran cosa! Puedo ensenarte todo lo que necesitas saber sobre medicina, aqui en este bano, mucho mejor que en cualquier cita entre comillas 'UNIVERSIDAD'." pbold text!
	"'La saliva sale disparada de su boca cuando dicen 'Universidad'. Te preguntas vagamente cuanto tiempo han estado en este bano y si se han lavado las manos." +crtext foodamt
		hospital-solo-pay? not if
			"'No puedes permitirte mas entrenamiento." +crtext 
		then;
		choice( "Dejalo por ahora" )choice back-esc
		hospital-solo-pay? if
			choice( "Dar " .. hospital-solo-amt .. "  comida para entrenamiento individual" )choice: hospital-medical-give
		then
		hospital-train-pay? if
			choice( "Dar " .. hospital-train-amt .. "  comida para la formacion de grupos" )choice: hospital-medical-group
		then
		
;
	
}

root{

: hosp-magchance
	20 %chance if
		magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! -2 movex drift
	then
;

}

roomgen{


: .ht1 args( ch ) 
	21 <- ch .specialhead! 24 <- ch .specialbody! // Jerk head, Madsci body, Burn Unit
;
: .ht2 args( ch ) 
	5 <- ch .hattype! 25 <- ch .glassestype! // Bandanna, Medical Supplies and Weapons
;
: .ht3 args( ch ) 
	6 <- ch .hattype! 10 <- ch .torsotype! // Beanie, Black Jacket, Group Heal
;

: hosptradercommon
	still-npc regiondef{ ' trader-event lt .events! 1 lt .timer! }
	
;

: hospitaldecomain 1 5 rnd case
	1 of hospitalshelfempty endof
	2 of hospitalbed endof
	3 of hospitalexam endof
	4 of hospitalbedside endof
	5 of wheelchair endof
endcase -10 movey ;

: hospitaldecoside 1 5 rnd case
	1 of ivpole endof
	2 of hospitalstool endof
	3 of hospitalstepper endof
	4 of hospitalcurtain endof
	5 of stretcher endof
endcase ;

: hosptoilets
	bathtoilet -1 movey 0.3 movex
	stall -1 movey 1 movex
	stall -1 movey -0.5 movex
	bathtoilet -1 movey 2 movex
	bathtoilet -1 movey -1.2 movex
	stall -1 movey -2 movex
	sink -1 movey -3 movex
	sink -1 movey -4 movex
	hosptradercommon 2 movey cyoa{ ' hosptrainer cyoa! }
	hosp-magchance
;

: hosptrader1
	hosptradercommon .ht1 cyoa{ ' hosptrader1 cyoa! }
;

: hosptrader2
	hosptradercommon .ht2 cyoa{ ' hosptrader2 cyoa! }
;

: hosptrader3
	hosptradercommon .ht3 cyoa{ ' hosptrader3 cyoa! }
;

: hosptrader4
	hosptradercommon .cleanerify cyoa{ ' cleaninglady cyoa! }
;

		0 stack
			' hosptrader1 shove
			' hosptrader2 shove
			' hosptrader3 shove
			' hosptrader4 shove
		value hosptrader-base
		hosptrader-base shuffles var hosptrader-picker

: hosptrader
	hosptrader-picker depths 0= if hosptrader-base shuffles to hosptrader-picker then pops nip execute ;

: hospbag
	bag-me ;

}

locdef{

location: tradehospital
		max-rooms: 1
		entry-tags: tradehospital
		script: set-theme-hospital
		pre-script: set-theme-hospital
		post-script: nad 0 %zombs
				
		room-choice: tradehospital
			room-weight: 10
			room-count: 1
			filter-only

}
				
map{ regiondef{

trade-camp-reset

: site-setup

	1 no_time!
	12 14 rnd o'clock
	
	mission{ no-edge-spawns }
	
;

	 region: specialcamp-7
		
		trade-mode

		inside
					
		loc-choice: tradehospital
			loc-weight: 99
			loc-count: 1

		post-script: site-setup
} }

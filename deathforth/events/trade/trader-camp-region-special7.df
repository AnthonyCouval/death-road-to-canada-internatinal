uses paul/region-layout.df
"events/inner/clinicloc.df" $load

// "../deathforth/rooms/TEMPROOM.txt" $load-rooms
	
" 
:map 1 
:tags tradehospital  
:scriptA hospbag 
:scriptB hosptrader 
:scriptC regiondef{ trader-weapon-buyer-inside } 
:scriptD regiondef{ tnomey-spawn-inside } 
:scriptE hospitaldecomain 
:scriptF hospitaldecoside 
:scriptG hosptoilets 
:scriptH hospitalwall 
:size 45 22 
:off 0 0 
:tiles 
000000000000000000000000000000000000000222222
000002222222222222222222000000000000000233332
000002333332333332333332222222220000000211112
0000021E1EH2HEHE121EHE12333333320000000211112
00000211B11211B11211B1121111G1120000000211112
2222221111121111F2F11112111111120000000222222
233332F111F2F111121111F2111111120000000333333
2EHE12221222221222221222122222220000000000000
21B113331333331333331333133323330000000000000
211111H111H111111H111111111F20000000000000000
211111111111111111111111111120000000000000000
21F1F2222222221122222222222120000000000000000
222222333333321133333333333120000000000000000
333333000000021111111C11111120000000000000000
0000000000000211A1111111111120000000000000000
0000000000000211111111111D1120000000000000000
000000000000021111111111111120000000000000000
000000000000021122222222222220000000000000000
000000000000021123333333333330000000000000000
000000000000021120000000000000000000000000000
000000000000022220000000000000000000000000000
000000000000033330000000000000000000000000000

"
value TRADEHOSPITAL
TRADEHOSPITAL $parse-rooms

cyoa{

cyoa-choice: hosptrader1-go
	"BRUCIATO PER SEMPRE" cyoa-title!
	($ "Il dottore " .. cname .. " un piccolo pezzo di carta. Dice:" .. "sei un idiota puzzi di uovo" pbold $) cyoa-text!
	cchar statmod{ .fireproof? } not if
		"OH!
		OH! BURNED! OH!!!!!" fbold +crtext
		($ cname .. "  si sente emotivamente devastato dai danni del fuoco di questa bruciatura malata! " 
		.. cname .. "'La pelle di 's e stranamente fredda." bbold $) +crtext
		cchar statmod{ .fireproof } morale4- badfx
		choice( "ARGH!" )choice
	else
		($ cname .. ", essendo ignifugo, non e affatto influenzato da questa bruciatura. " .. cname .. "  si sente estremamente compiaciuto di questo." bbold $) +crtext
		goodfx morale4+
		"NO! NO!
		NO! NO!!!!" fbold +crtext
		($ "Il dottore fugge in intensa vergogna, e " .. cname .. " reagisce saccheggiando immediatamente la stanza." $) +crtext
		deleteme +cr 6 8 rnd trunk-medical+
	then
	clear-cyoa
;

cyoa-bridge: hosptrader1-pick4
	ricon
	($ "Chi verra mandato al " .. "UNITA DI GRANDE USTIONATI" .. fbold .. "?" $) cyoa-text!
	' hosptrader1-go cyoa-team-choice
;

cyoa: hosptrader1
	"Burn Unit" cyoa-title!
	($ "BENVENUTI ALLUNITA DI BRUCIATI CHI SI BRUCIA?" fbold $) cyoa-text!
	basic-tone
	choice( "Lascia, non fare contatto visivo" )choice back-esc
	team-count 3 > if
		' hosptrader1-go 2 #cyoa-team-choice
		choice( "Scegli qualcun altro" )choice: hosptrader1-pick4
	else
		' hosptrader1-go cyoa-team-choice
	then
;

0 value ht2-1
0 value ht2-2
0 value ht2-3

cyoa-choice: trade-h2-3
	($ Thegroup .. "  acquista il " .. "MEDICAL MEGAPACK!" fbold .. " 'Questa e un sacco di potenziale guarigione!" .. "Lo Spray di Guarigione XL funziona anche sugli alluci ammaccati." pbold $) cyoa-text! goodfx
	10 WEAPON_HEALSPRAY trunk.weapon+ 10 trunk-medical+ -15 trunk-food+ someicon ' ht2-3 ++
;
	
cyoa-choice: trade-h2-2
	($ Thegroup .. "  acquista il " .. "BISTURI DORO" rbold .. ". Non si rompe mai e colpisce uno zombi alla volta in modo estremamente duro per unarma che oscilla alla massima velocita!" $) cyoa-text! goodfx ' ht2-2 ++
	1 WEAPON_GOLDSCALP trunk.weapon+ -15 trunk-food+ picon
;

cyoa-choice: trade-h2-1
	($ Thegroup .. "  acquista un " .. "SEGHETTO" rbold .. ". Puo colpire due zombi con un colpo e colpisce forte per la sua velocita." $) cyoa-text! goodfx ' ht2-1 ++
	1 WEAPON_BONESAW trunk.weapon+ -6 trunk-food+ picon
;

cyoa: hosptrader2
	"Approvvvviati Medicinali" cyoa-title!
	($ "Benvenuti a " pbold .. "LOspedale Finale!" rainbold .. " 'Vendo qualche approvvvviato approvvvviatico, per aiutarti nel tuo viaggio!" pbold $) cyoa-text!
	ht2-3 1 < if
		"Promozione speciale oggi: Uno per gruppo di visitatori: Il mio Pacchetto Medico Epic contiene 10 approvvvviati approvvvviatici, insieme ad una Spray Guarigione XL con 10 spray!" pbold +crtext
	then
	basic-tone foodamt
	trunk-food 6 < if
		($ Thegroup .. "  non ha abbastanza cibo per comprare qualcosa!" $) +crtext
		($ "Il venditore da " .. thegroup .. " un approvvvviato gratuito " .. "Bonesaw" rbold .. "  solo per aver visitato!" $) +crtext goodfx 1 WEAPON_BONESAW trunk.weapon+ clear-cyoa
	else
		choice( "Lascia per ora" )choice back-esc
		trunk-food 14 > ht2-3 1 < and if
			choice( "Pacchetto Medico Epic   15 cibo" )choice: trade-h2-3
		then
		trunk-food 14 > ht2-2 1 < and if
			choice( "Scalpello doro   15 cibo" )choice: trade-h2-2
		then
		trunk-food 5 > ht2-1 3 < and if
			choice( "Bonesaw   6 cibo" )choice: trade-h2-1
		then
	then
;

: issick? statmod{ .sick? } ; // The only time I ever use a sickness check, ever
: hospital-superserum
	' .strength++ foreach-team-char
	' .fitness++ foreach-team-char 
	' .vitality++ foreach-team-char 
	' .dexterity++ foreach-team-char
;

cyoa-choice: hosptrader3-go

	team-damage 0 = if
		($ Thegroup .. " ' riceve un controllo, ma non c'e nulla sbagliato con loro. Il dottore e visibilmente arrabbiato e offeso che avete sprecato il loro tempo, e scrive qualcosa in un registro." $) cyoa-text!
		morale++all
	else
		12 groupheal
		($ Thegroup .. "  riceve cure per " $) cyoa-text!
		team-damage 1 < if 
			"tutti" +text 
		else
			"maggior parte" +text
		then
			"  delle loro ferite." +text
			"Il dottore e incredibilmente deluso e annoiato dai tuoi comuni infortuni." +crtext
	then
	
	team-stack ' issick? stack-filter 0 > if 
		"MA ASPETTA " rbold +crtext
		($ "Il Geniale Dottore Afflitto rileva una malattia! Dopo circa 44 minuti, il dottore arriva a una conclusione entusiasmante: la malattia era " .. "AVVELENAMENTO DA CAFFE " fbold $) +text
		($ "Il dottore somministra un supersiero miracoloso sperimentale a " .. thegroup .. "'! Non cura la malattia, ma un solo giorno di riposo lo farebbe, comunque." $) +text +cr hospital-superserum
	then
	
	goodfx -5 trunk-food+ picon clear-cyoa
;

cyoa: hosptrader3
	"Probabilmente un Geniale Dottore Afflitto" cyoa-title!
	($ "'Ho curato nient'altro che morsi di zombi, qualche lieve ustione, ferite da bombola di propano e dita dei piedi ammaccate per quelli che sembrano 6 anni ormai. Dimmi che hai una qualche malattia. Idealmente una misteriosa malattia drammatica, ma anche qualcosa come un'intossicazione alimentare sarebbe fantastico a questo punto. Tutto tranne un altro morso di zombi!" pbold $) cyoa-text!
	basic-tone +cr foodamt
	trunk-food 5 < if
		($ Thegroup .. "  non ha abbastanza cibo per le cure mediche!" $) +crtext
		($ "  Il dottore ha pieta di " .. thegroup .. "  e distribuisce alcune forniture mediche." rbold $) +text goodfx 3 4 rnd trunk-medical+ clear-cyoa
	else
		choice( "Lascia per ora" )choice back-esc
			solo if
				choice( "Cura le ferite   5 cibo" )choice: hosptrader3-go
			else
				choice( "'Cura le ferite del gruppo   5 cibo" )choice: hosptrader3-go
			then
	then
;

0 value cleaninglady-meals

: cleaninglady-amt cleaninglady-meals 20 * 10 + ;
: cleaninglady-pay? trunk-food cleaninglady-amt 1 - > ;
: cleaninglady-pay cleaninglady-amt -1 * trunk-food+ ;

cyoa-choice: cleaninglady-meal-eat
	cicon
	($ cname .. " 'e inondata di sapori nostalgici. All'inizio, l'intero pasto sembra avere uno strano retrogusto stantio, ma dovresti essere uno stupido per pensare che non fosse intenzionale. Fa tutto parte del suo sottile fascino, della sua magistrale creazione non come un semplice pasto, ma come una totale esperienza culinaria! C'e persino una gelatina in piu! Un sapore rosso, un sapore giallo! Incredibile!" $) cyoa-text!
	vitality++ morale4+ cchar .fullheal ' cleaninglady-meals ++
	goodfx
;

cyoa-bridge: cleaninglady-meal
	solo if
		soloer choosechar cleaninglady-meal-eat 
	else
		($ "Chi dovrebbe mangiare il cibo della mensa dellospedale?" $) cyoa-text!
		"QUESTO CIBO DEGLI DEI CONFERIRA UN BONUS ALLA SALUTE MASSIMA E UN MASSICCIO RIPRISTINO DEL MORALE!" rbold +crtext
			' cleaninglady-meal-eat cyoa-team-choice
	then
;

cyoa-bridge: cleaninglady-meal-pay
	cleaninglady-meal
	cleaninglady-pay
;

cyoa-bridge: cleaninglady-recruit
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	"Pulizia" cyoa-title!
	($ rname .. "  gira rapidamente il suo mocio intorno al polso.	" .. "E ora di ripulire questo casino!	" pbold .. "Lacqua del mocio inzuppa " .. thegroup .. "  piuttosto male." $) cyoa-text!
	goodfx
	
	team-full? if
		($ "Il gruppo e pieno. Chi vorresti sostituire per fare spazio a " .. rname .. "?" $) +crtext
		recruit-replace?
	else
		recruited
		($ "Sembrava davvero facile." $) +text
		most-charming if
			choosechar ($ cname .. "  convince " .. rhim .. " a per intrufolarsi un po di cibo dellospedale!" $) +crtext
			choice( "DAMMI QUELLO CIBO DELLOSPEDALE" )choice: cleaninglady-meal
		then
	then
	
	// WEAPON_SHOTGUN recruitee .weaponb! 	20 LOOT_SHOTGUN_SHELL recruitee .lootamt!
;
		
cyoa: cleaninglady
	"Il mercato nero del cibo della mensa" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	($ "'C'e una donna delle pulizie qui dentro. Sembra che non stia effettivamente lavorando. Lei valuta " .. thegroup .. "  su." $) cyoa-text! basic-tone
	cleaninglady-meals 0 > if
		"Uh huh. Wow, hai un serio problema.		Posso procurartene un altro, ma ti costera."
	else
		"'So perche sei davvero qui. Sei arrivato fin qui per il cibo dell'ospedale.		Posso intrufolarti il pasto della mensa che desideri, per un prezzo." 
	then pbold +crtext +cr foodamt
	cleaninglady-pay? if
		choice( "Lascia per ora" )choice back-esc
		choice( "Acquista il cibo della mensa  " .. cleaninglady-amt .. "  cibo non della mensa" )choice: cleaninglady-meal-pay
		choice( "Rifiuta di farlo per principio" )choice
	else
		choice( "'Non posso risparmiare il cibo" )choice
	then
	choice( "Prova a reclutarla" )choice: cleaninglady-recruit
;

0 value hospital-medical+

: hospital-train-amt numhums 2 * hospital-medical+ 1 + * 1 + ; 
: hospital-train-pay? trunk-food hospital-train-amt 1 - > notsolo notsolohuman and and ;
: hospital-train-pay hospital-train-amt -1 * trunk-food+ ;

: hospital-solo-amt hospital-medical+ 2 * 3 + ;
: hospital-solo-pay? trunk-food hospital-solo-amt 1 - > ;
: hospital-solo-pay hospital-solo-amt -1 * trunk-food+ ;

cyoa-choice: hospital-medical-train
	cchar .medical@ 6 < if
		($ cname .. " " $) goodfx
	else
		($ cname .. " tuttavia, il nostro Santo dellUniversita del Bagno dellOspedale non rilascia rimborsi." $) badfx
	then cyoa-text! 
	hospital-solo-pay medicalREV medical++ ' hospital-medical+ ++
;

cyoa-choice: hospital-medical-group
	($ "Il gruppo intero riceve addestramento medico! E' quasi interamente un pantomima di gruppo della tecnica di golf corretta." $) cyoa-text! goodfx
	hospital-train-pay someicon
	regiondef{ ' trade-medical-group! foreach-human-char } numhums ' hospital-medical+ +!
;

cyoa: hospital-medical-give
	solohuman if
		choosehuman hospital-medical-train
	then;
	"A chi dovrebbe andare questa vasta conoscenza medica?" cyoa-text!
	' hospital-medical-train cyoa-human-choice
;

cyoa: hosptrainer
	($ "Saint Hospital Bathroom University" $) cyoa-title!
	"'Feh! Medici! Gran cosa! Posso insegnarti tutto quello che devi sapere sulla medicina, qui in questo bagno, molto meglio che in qualsiasi citazione citazione UNIVERSITA." pbold text!
	"Gli sputi escono dalla loro bocca quando dicono 'Universita'. Ti chiedi vagamente da quanto tempo sono in questo bagno e se si sono lavati le mani." +crtext foodamt
		hospital-solo-pay? not if
			"'Non puoi permetterti altro allenamento." +crtext 
		then;
		choice( "Lascia per ora" )choice back-esc
		hospital-solo-pay? if
			choice( "Dai " .. hospital-solo-amt .. "  cibo per allenamento singolo" )choice: hospital-medical-give
		then
		hospital-train-pay? if
			choice( "Dai " .. hospital-train-amt .. "  cibo per laddestramento di gruppo" )choice: hospital-medical-group
		then
		
;
	
}

root{

: hosp-magchance
	20 %chance if
		magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! -2 movex drift
	then
;

}

roomgen{


: .ht1 args( ch ) 
	21 <- ch .specialhead! 24 <- ch .specialbody! // Jerk head, Madsci body, Burn Unit
;
: .ht2 args( ch ) 
	5 <- ch .hattype! 25 <- ch .glassestype! // Bandanna, Medical Supplies and Weapons
;
: .ht3 args( ch ) 
	6 <- ch .hattype! 10 <- ch .torsotype! // Beanie, Black Jacket, Group Heal
;

: hosptradercommon
	still-npc regiondef{ ' trader-event lt .events! 1 lt .timer! }
	
;

: hospitaldecomain 1 5 rnd case
	1 of hospitalshelfempty endof
	2 of hospitalbed endof
	3 of hospitalexam endof
	4 of hospitalbedside endof
	5 of wheelchair endof
endcase -10 movey ;

: hospitaldecoside 1 5 rnd case
	1 of ivpole endof
	2 of hospitalstool endof
	3 of hospitalstepper endof
	4 of hospitalcurtain endof
	5 of stretcher endof
endcase ;

: hosptoilets
	bathtoilet -1 movey 0.3 movex
	stall -1 movey 1 movex
	stall -1 movey -0.5 movex
	bathtoilet -1 movey 2 movex
	bathtoilet -1 movey -1.2 movex
	stall -1 movey -2 movex
	sink -1 movey -3 movex
	sink -1 movey -4 movex
	hosptradercommon 2 movey cyoa{ ' hosptrainer cyoa! }
	hosp-magchance
;

: hosptrader1
	hosptradercommon .ht1 cyoa{ ' hosptrader1 cyoa! }
;

: hosptrader2
	hosptradercommon .ht2 cyoa{ ' hosptrader2 cyoa! }
;

: hosptrader3
	hosptradercommon .ht3 cyoa{ ' hosptrader3 cyoa! }
;

: hosptrader4
	hosptradercommon .cleanerify cyoa{ ' cleaninglady cyoa! }
;

		0 stack
			' hosptrader1 shove
			' hosptrader2 shove
			' hosptrader3 shove
			' hosptrader4 shove
		value hosptrader-base
		hosptrader-base shuffles var hosptrader-picker

: hosptrader
	hosptrader-picker depths 0= if hosptrader-base shuffles to hosptrader-picker then pops nip execute ;

: hospbag
	bag-me ;

}

locdef{

location: tradehospital
		max-rooms: 1
		entry-tags: tradehospital
		script: set-theme-hospital
		pre-script: set-theme-hospital
		post-script: nad 0 %zombs
				
		room-choice: tradehospital
			room-weight: 10
			room-count: 1
			filter-only

}
				
map{ regiondef{

trade-camp-reset

: site-setup

	1 no_time!
	12 14 rnd o'clock
	
	mission{ no-edge-spawns }
	
;

	 region: specialcamp-7
		
		trade-mode

		inside
					
		loc-choice: tradehospital
			loc-weight: 99
			loc-count: 1

		post-script: site-setup
} }

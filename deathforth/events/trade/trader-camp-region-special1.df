uses paul/region-layout.df

"../deathforth/rooms/duodenum.txt" $load-rooms

locdef{

location: traderlab
		max-rooms: 1
		entry-tags: traderlab-entry
		pre-script: set-theme-bunker
		script: set-theme-bunker
		post-script: 
		
		room-choice: traderlab-entry
			room-weight: 10
			room-count: 1
			room-script: ndx 
			room-post-script:
			filter-only

}

cyoa{

0 value tradelab-bluebot-sold

cyoa-choice: tradelab-bluebot-sale
	iicon ($ pname .. "  kauft einen " .. "BLAUEN ROBOTER" bbold .. "!	Wirf es auf den Boden und es wird dir helfen bis die Batterien leer sind."  $) text!
	goodfx 1 WEAPON_BLUEROBOT trunk.weapon+ -3 trunk-food+ ' tradelab-bluebot-sold ++
	tradelab-bluebot-sold 1 > if clear-cyoa then
;

cyoa-choice: tradelab-redbot-sale
	iicon ($ pname .. "  kauft einen " .. "ROTEN ROBOTER" rbold .. "!	Wirf es auf den Boden und es wird dir helfen bis die Batterien leer sind."  $) text!
	goodfx 1 WEAPON_REDROBOT trunk.weapon+ -6 trunk-food+ clear-cyoa
;

cyoa: tradelab-bluebot
	iicon "Hilfreicher Roboter" cyoa-title!
	"'Es ist ein hilfreicher Roboter!	Fur seine Freizeit ist es mit einem Verkaufsgesprach programmiert." text!
	"1. BEEP 2. HONK	3. ICH VERKAUFE ROBOTER	4. BIS ZU ZWEI ROBOTER" bbold +crtext
	choice( "Kein Verkauf!" )choice
	trunk-food 2 > tradelab-bluebot-sold 2 < and if
		choice( "Kaufe Blue Robot   3 Essen" )choice: tradelab-bluebot-sale
	else
		"Er will 3 Essen fur einen." +crtext
	then
;

cyoa: tradelab-redbot
	iicon "Bedrohlicher Roboter" cyoa-title!
	"Dieser Roboter scheint intensiv zu sein.	Seine Krallen sehen bedrohlich aus. Er muss ein Wachter sein." text!
	"1. PIEP 2. HUPEN	3. ICH VERKAUFE EINEN ROBOTER	4. MAXIMALE UBERLEISTUNG" rbold +crtext
	choice( "Geh weg von mir" )choice
	trunk-food 5 > if
		choice( "Kaufe Red Robot   6 Essen" )choice: tradelab-redbot-sale
	else
		"Er will 6 Essen." +crtext
	then
;

0 value labsci-doodad
0 value labsci-mazer

cyoa-choice: tradelab-mazer-sale
	iicon ($ pname .. "  kauft einen " .. "MAZER PISTOLE" rainbold .. "!
	" .. "Jemand mit besserer Zielgenauigkeit als ich konnte dies wirklich effizient nutzen.	Das Durchschlagspotential ist aufgrund der Kraft von " pbold .. "Mazer Energie!" fbold $) text!
	goodfx 200 WEAPON_MAZER trunk.weapon+ -12 trunk-food+ ' labsci-mazer ++
;

cyoa-choice: tradelab-doodad-sale
	iicon ($ pname .. "  kauft einen " .. "WISSENSCHAFTLICHES DING" rainbold .. "!
	" .. "'Dies kann dich kurze Strecken teleportieren. 	Es hat 5 Schusse. Verliere nicht die Zahlung, ich habe vergessen, eine nutzliche Anzeige dafur zu erstellen. Die Anzeige darauf bedeutet jetzt nichts, sie blinkt nur zufallig." pbold $) text!
	goodfx 5 WEAPON_DOODAD trunk.weapon+ -5 trunk-food+ ' labsci-doodad ++
;

cyoa-bridge: tradelab-recruit
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	20 recruit-yes-food "'Ich bin bereit Dinge zu zappen! Ich habe mein Solarladegerat mitgebracht." pbold +crtext 
	recruitee .mechanicREV recruitee .witsREV recruitee .shootingREV
;

cyoa-bridge: tradelab-recruit-warn
	"'Klar, ich kann mich euch anschlieen. Hier wird es mir langweilig, ich muss drauen im Feld etwas recherchieren.	Kaufe alles, was du willst, bevor du mich rekrutierst! Es ist nicht so, dass ich dir nicht vertraue, aber nachdem ich beigetreten bin, sperre ich alle meine Waffen mit meiner Fingerabdruck ID. 	Nur fur den Fall, dass ich auf mysteriose Weise sterbe." pbold text!
		choice( "Spater rekrutieren" )choice
		choice( "Rekrutiere sie!   20 Essen" )choice: tradelab-recruit
;

cyoa: tradelab-scichat
	"Die Wissenschaft des Einkaufens" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"Jeder Handel den du hier machst wird dem Fortschritt der Wissenschaft dienen!" pbold text!
		trunk-food 5 < if
			"Die Wissenschaftlerin hat Mitleid mit deinem Mangel an Nahrung. Sie gibt dir einen kostenlosen Teleporter, leicht gebraucht. Noch 2 Schusse ubrig!" +crtext clear-cyoa 2 WEAPON_DOODAD trunk.weapon+
		then;
		choice( "Lassen" )choice
		trunk-food 4 > labsci-doodad 1 < and if
			choice( "Teleporter kaufen   5 Essen" )choice: tradelab-doodad-sale
		then
		trunk-food 11 > labsci-mazer 1 < and if
			choice( "Mazer Pistole kaufen   12 Essen" )choice: tradelab-mazer-sale
		then
		trunk-food 19 > if
			labsci-doodad 1 < labsci-mazer 1 < and if
				choice( "Rekrutiere sie ins Team   20 Essen" )choice: tradelab-recruit-warn
			else
				choice( "Rekrutiere sie ins Team!   20 Essen" )choice: tradelab-recruit
			then
		then
;

}

roomgen{

: tradelab-bots 
	still-npc .redbotify 2 movey cyoa{ ' tradelab-redbot cyoa! }
	regiondef{ ' trader-event lt .events! 1 lt .timer! }
	still-npc .bluebotify -0.5 movey cyoa{ ' tradelab-bluebot cyoa! }
	regiondef{ ' trader-event lt .events! 1 lt .timer! }
;
: tradelab-scientist still-npc .madscify 
	cyoa{ ' tradelab-scichat cyoa! } 
	regiondef{ ' trader-event lt .events! 1 lt .timer! }
;

}

map{ regiondef{

trade-camp-reset

: bldg tanchor!
	"traderlab" $picklocid addloc prefab{ 7 bunker } 
;

0 value bfx
0 value bfy
0 value snx
0 value sny

: river tile@
	dup LAYER_FLOOR particles32_base_id 0x 10 + .lspriteid!
	dup LAYER_FLOOR 0 0 .tile_layer.off!
	dup LAYER_FLOOR 0 .tile_layer.flipx!

	dup LAYER_FLOORTOP particles32_base_id 0x 11 + .lspriteid!
	dup LAYER_FLOORTOP 0 0 .tile_layer.off!
	dup LAYER_FLOORTOP 0 .tile_layer.flipx!

	TILE_BLOCK over .id!
	drop
;

: riveredge
	2dup river
	tile@ LAYER_FLOORTOP particles32_base_id 0x 12 + .lspriteid!
;

: cliff 2dup blocked tile@
	dup LAYER_FLOORTOP 0 .tile_layer.flipx!
	LAYER_FLOORTOP street_tiles_base_id 0x a9 + 0 3 rnd + .lspriteid!
;

: cliffedge 2dup blocked tile@
	dup LAYER_FLOORTOP 0 .tile_layer.flipx!
	LAYER_FLOORTOP street_tiles_base_id 0x ba + 0 3 rnd + .lspriteid!
;

: bank 2dup tile@ args( x y t )
	<- t LAYER_WALLSIDE street_tiles_base_id 0x 99 + 0 3 rnd + .lspriteid!
	<- t LAYER_FLOORTOP particles32_base_id 0x 12 + .lspriteid!
	<- x <- y riveredge
;

: bushy >r tile@
	// dup LAYER_WALLSIDE rnd5050 .tile_layer.flipx!
	dup LAYER_WALLSIDE street_tiles_base_id 0x 89 + 0 3 rnd + .lspriteid!
	LAYER_WALLSIDE 0 r> .tile_layer.off!
;

' plotgrass >defer ft

: pt
	xyp.xy ft // Plot the current floor by default

	xyp.i char G = if ' plotgrass to ft xyp.xy ft then;
	xyp.i char x = if ' plotgravel to ft then;
	xyp.i char ^ = if xyp.xy river then;
	xyp.i char v = if xyp.xy riveredge then;
	xyp.i char \ = if xyp.xy cliff then;
	xyp.i char / = if xyp.xy cliffedge xyp.xy -4 -5 rnd bushy then;
	xyp.i char | = if xyp.xy bank then;
	xyp.i char o = if xyp.xy -8 -7 rnd bushy then;
	xyp.i char $ = if xyp.xy grassedge then;
	xyp.i char W = if trader-weapon-buyer then;

	xyp.i char t = if pictable1 lt-here 8 rdrift then;		
	xyp.i char @ = if xyp.xy bldg then;
	xyp.i char T = if traderhere then;
	xyp.i char Z = if tnomey-spawn then;
;

"G........................
.........................
.........................
.........................
...t.................t...
.........................
.........................
/////////////////////////
\\\\\\\\\\\\\\\\\\\\\\\\\
|||||||||||||||||||||||||
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
ooooooooooooooooooooooooo
.........................
.........................
....W...Z.@.........T....
........................x
$$$$$$$$$$$$$$$$$$$$$$$$$
.........................
.........................
.........................
.........................
.........................
........................."
constant maplayout

: plotmap
	0 -1 maplayout xyplot: pt
;

: site-setup
	1 no_time!
	7 14 rnd o'clock

	' plotgrass to ft

	30 %chance if 0 0.5 frnd weather! -1 2 frnd wind_force! else -1 1 frnd wind_force! then

	plotmap
	mission{ no-edge-spawns }

	// Put char and chars in middle of map
	9 %mv-up
	24 %chars-up
;

region: specialcamp-1
	w: maplayout layoutw
	h: maplayout layouth

	trade-mode

	post-script: site-setup

} }

uses paul/region-layout.df

"../deathforth/rooms/duodenum.txt" $load-rooms

locdef{

location: traderlab
		max-rooms: 1
		entry-tags: traderlab-entry
		pre-script: set-theme-bunker
		script: set-theme-bunker
		post-script: 
		
		room-choice: traderlab-entry
			room-weight: 10
			room-count: 1
			room-script: ndx 
			room-post-script:
			filter-only

}

cyoa{

0 value tradelab-bluebot-sold

cyoa-choice: tradelab-bluebot-sale
	iicon ($ pname .. " compra un " .. "ROBOTA AZUL ROBIT" bbold .. "lanzaelo en el suelo y le ayudara hasta que se agote las baterias!"  $) text!
	goodfx 1 WEAPON_BLUEROBOT trunk.weapon+ -3 trunk-food+ ' tradelab-bluebot-sold ++
	tradelab-bluebot-sold 1 > if clear-cyoa then
;

cyoa-choice: tradelab-redbot-sale
	iicon ($ pname .. " compra un " .. "ROBOTA ROJA ROBIT" rbold .. "lanzaelo en el suelo y le ayudara hasta que se agote las baterias!"  $) text!
	goodfx 1 WEAPON_REDROBOT trunk.weapon+ -6 trunk-food+ clear-cyoa
;

cyoa: tradelab-bluebot
	iicon "Robot Ayudante" cyoa-title!
	"Es un robot ayudante!Â Para su tiempo libre, esta programado con una presentacion de ventas." text!
	"1. PIERNA 2. TOTEN 3. VENDO ROBITS 4. HASTA DOS ROBITS" bbold +crtext
	choice( "No hay venta!" )choice
	trunk-food 2 > tradelab-bluebot-sold 2 < and if
		choice( "Compra Robota Azul - 3 comida" )choice: tradelab-bluebot-sale
	else
		"Quiere 3 comida por uno." +crtext
	then
;

cyoa: tradelab-redbot
	iicon "Robot Amenazante" cyoa-title!
	"Este robot parece intenso. Sus garras parecen amenazantes. Debe ser un guardia." text!
	"1. BIP 2. BOCINA	3. VENDO UN ROBOT	4. PODER MAXIMO" rbold +crtext
	choice( "Alejate de mi" )choice
	trunk-food 5 > if
		choice( "1 red robot" )choice: tradelab-redbot-sale
	else
		"Quiere 6 comida." +crtext
	then
;

0 value labsci-doodad
0 value labsci-mazer

cyoa-choice: tradelab-mazer-sale
	iicon ($ pname .. " compra un " .. "PISTOLA MAZER" rainbold .. "!
	" .. "1.5x  and the  2x  damage to the next target. " pbold .. "Energia Mazer!" fbold $) text!
	goodfx 200 WEAPON_MAZER trunk.weapon+ -12 trunk-food+ ' labsci-mazer ++
;

cyoa-choice: tradelab-doodad-sale
	iicon ($ pname .. " compra un " .. "ACCESORIO CIENTIFICO" rainbold .. "!
	" .. "Esto puede teletransportarte a distancias cortas. Tiene 5 disparos. No pierdas la cuenta, se me olvido construir una pantalla util para ello. La pantalla que tiene ahora no significa nada, solo parpadea al azar." pbold $) text!
	goodfx 5 WEAPON_DOODAD trunk.weapon+ -5 trunk-food+ ' labsci-doodad ++
;

cyoa-bridge: tradelab-recruit
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	20 recruit-yes-food "Estoy listo para dar zapes! Traje mi cargador solar." pbold +crtext 
	recruitee .mechanicREV recruitee .witsREV recruitee .shootingREV
;

cyoa-bridge: tradelab-recruit-warn
	"Claro, puedo acompanarte. Me estoy aburriendo aqui, necesito hacer alguna investigacion en el campo. Termina de comprar lo que quieras antes de reclutarme! No es que no te tenga confianza, pero una vez que me una, bloqueare todas mis armas con mi huella dactilar. Solo por si acaso muero misteriosamente." pbold text!
		choice( "Recluta a ella despues" )choice
		choice( "Reclutalaa! - 20 comida" )choice: tradelab-recruit
;

cyoa: tradelab-scichat
	"La Ciencia de Comprar" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"Todo el comercio que hagas aqui contribuira al avance de la ciencia!" pbold text!
		trunk-food 5 < if
			"La cientifica se compadece de tu falta de comida. Te da un teletransportador usado, ligeramente, con 2 disparos restantes." +crtext clear-cyoa 2 WEAPON_DOODAD trunk.weapon+
		then;
		choice( "Dejarla por ahora" )choice
		trunk-food 4 > labsci-doodad 1 < and if
			choice( "Compra Teletransportador - 5 comida" )choice: tradelab-doodad-sale
		then
		trunk-food 11 > labsci-mazer 1 < and if
			choice( "Compra Pistola Mazer - 12 comida" )choice: tradelab-mazer-sale
		then
		trunk-food 19 > if
			labsci-doodad 1 < labsci-mazer 1 < and if
				choice( "Recluta a ella para el equipo - 20 comida" )choice: tradelab-recruit-warn
			else
				choice( "Recluta a ella para el equipo! - 20 comida" )choice: tradelab-recruit
			then
		then
;

}

roomgen{

: tradelab-bots 
	still-npc .redbotify 2 movey cyoa{ ' tradelab-redbot cyoa! }
	regiondef{ ' trader-event lt .events! 1 lt .timer! }
	still-npc .bluebotify -0.5 movey cyoa{ ' tradelab-bluebot cyoa! }
	regiondef{ ' trader-event lt .events! 1 lt .timer! }
;
: tradelab-scientist still-npc .madscify 
	cyoa{ ' tradelab-scichat cyoa! } 
	regiondef{ ' trader-event lt .events! 1 lt .timer! }
;

}

map{ regiondef{

trade-camp-reset

: bldg tanchor!
	"traderlab" $picklocid addloc prefab{ 7 bunker } 
;

0 value bfx
0 value bfy
0 value snx
0 value sny

: river tile@
	dup LAYER_FLOOR particles32_base_id 0x 10 + .lspriteid!
	dup LAYER_FLOOR 0 0 .tile_layer.off!
	dup LAYER_FLOOR 0 .tile_layer.flipx!

	dup LAYER_FLOORTOP particles32_base_id 0x 11 + .lspriteid!
	dup LAYER_FLOORTOP 0 0 .tile_layer.off!
	dup LAYER_FLOORTOP 0 .tile_layer.flipx!

	TILE_BLOCK over .id!
	drop
;

: riveredge
	2dup river
	tile@ LAYER_FLOORTOP particles32_base_id 0x 12 + .lspriteid!
;

: cliff 2dup blocked tile@
	dup LAYER_FLOORTOP 0 .tile_layer.flipx!
	LAYER_FLOORTOP street_tiles_base_id 0x a9 + 0 3 rnd + .lspriteid!
;

: cliffedge 2dup blocked tile@
	dup LAYER_FLOORTOP 0 .tile_layer.flipx!
	LAYER_FLOORTOP street_tiles_base_id 0x ba + 0 3 rnd + .lspriteid!
;

: bank 2dup tile@ args( x y t )
	<- t LAYER_WALLSIDE street_tiles_base_id 0x 99 + 0 3 rnd + .lspriteid!
	<- t LAYER_FLOORTOP particles32_base_id 0x 12 + .lspriteid!
	<- x <- y riveredge
;

: bushy >r tile@
	// dup LAYER_WALLSIDE rnd5050 .tile_layer.flipx!
	dup LAYER_WALLSIDE street_tiles_base_id 0x 89 + 0 3 rnd + .lspriteid!
	LAYER_WALLSIDE 0 r> .tile_layer.off!
;

' plotgrass >defer ft

: pt
	xyp.xy ft // Plot the current floor by default

	xyp.i char G = if ' plotgrass to ft xyp.xy ft then;
	xyp.i char x = if ' plotgravel to ft then;
	xyp.i char ^ = if xyp.xy river then;
	xyp.i char v = if xyp.xy riveredge then;
	xyp.i char \ = if xyp.xy cliff then;
	xyp.i char / = if xyp.xy cliffedge xyp.xy -4 -5 rnd bushy then;
	xyp.i char | = if xyp.xy bank then;
	xyp.i char o = if xyp.xy -8 -7 rnd bushy then;
	xyp.i char $ = if xyp.xy grassedge then;
	xyp.i char W = if trader-weapon-buyer then;

	xyp.i char t = if pictable1 lt-here 8 rdrift then;		
	xyp.i char @ = if xyp.xy bldg then;
	xyp.i char T = if traderhere then;
	xyp.i char Z = if tnomey-spawn then;
;

"G........................
.........................
.........................
.........................
...t.................t...
.........................
.........................
/////////////////////////
\\\\\\\\\\\\\\\\\\\\\\\\\
|||||||||||||||||||||||||
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
ooooooooooooooooooooooooo
.........................
.........................
....W...Z.@.........T....
........................x
$$$$$$$$$$$$$$$$$$$$$$$$$
.........................
.........................
.........................
.........................
.........................
........................."
constant maplayout

: plotmap
	0 -1 maplayout xyplot: pt
;

: site-setup
	1 no_time!
	7 14 rnd o'clock

	' plotgrass to ft

	30 %chance if 0 0.5 frnd weather! -1 2 frnd wind_force! else -1 1 frnd wind_force! then

	plotmap
	mission{ no-edge-spawns }

	// Put char and chars in middle of map
	9 %mv-up
	24 %chars-up
;

region: specialcamp-1
	w: maplayout layoutw
	h: maplayout layouth

	trade-mode

	post-script: site-setup

} }

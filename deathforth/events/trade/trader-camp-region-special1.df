uses paul/region-layout.df

"../deathforth/rooms/duodenum.txt" $load-rooms

locdef{

location: traderlab
		max-rooms: 1
		entry-tags: traderlab-entry
		pre-script: set-theme-bunker
		script: set-theme-bunker
		post-script: 
		
		room-choice: traderlab-entry
			room-weight: 10
			room-count: 1
			room-script: ndx 
			room-post-script:
			filter-only

}

cyoa{

0 value tradelab-bluebot-sold

cyoa-choice: tradelab-bluebot-sale
	iicon ($ pname .. " compra um " .. "ROBO AZUL" bbold .. "jogueo no chao e ele te ajudara ate que fique sem baterias."  $) text!
	goodfx 1 WEAPON_BLUEROBOT trunk.weapon+ -3 trunk-food+ ' tradelab-bluebot-sold ++
	tradelab-bluebot-sold 1 > if clear-cyoa then
;

cyoa-choice: tradelab-redbot-sale
	iicon ($ pname .. " compra um " .. "ROBO VERMELHO" rbold .. "jogueo no chao e ele te ajudara ate que fique sem baterias."  $) text!
	goodfx 1 WEAPON_REDROBOT trunk.weapon+ -6 trunk-food+ clear-cyoa
;

cyoa: tradelab-bluebot
	iicon "Robo Auxiliar" cyoa-title!
	"'It's a helpful robot! For its off time, it's programmed with a sales pitch.'" text!
	"1. BEEP	2. TOCAR TOCAR" bbold +crtext
	choice( "Nao vende!" )choice
	trunk-food 2 > tradelab-bluebot-sold 2 < and if
		choice( "1 food" )choice: tradelab-bluebot-sale
	else
		"It wants 3 food for one." +crtext
	then
;

cyoa: tradelab-redbot
	iicon "Robo Ameacador" cyoa-title!
	"Este robo parece intensivo. Suas garras parecem ameacadoras. Deve ser um guarda." text!
	"1. BEEP 2. TOCAR TOTORA	3. VENDO UM ROBO	4. MAXIMA POTENCIA" rbold +crtext
	choice( "Fique longe de mim" )choice
	trunk-food 5 > if
		choice( "Compre Robo Vermelho - 6 alimento" )choice: tradelab-redbot-sale
	else
		"Quer 6 alimento." +crtext
	then
;

0 value labsci-doodad
0 value labsci-mazer

cyoa-choice: tradelab-mazer-sale
	iicon ($ pname .. " compra um " .. "PISTOLA MAZER" rainbold .. "!
	" .. "Alguem com melhor precisao que eu poderia realmente usar isso eficazmente. A potencia de perfuracao e enorme devido a forca do " pbold .. "Energia Mazer!" fbold $) text!
	goodfx 200 WEAPON_MAZER trunk.weapon+ -12 trunk-food+ ' labsci-mazer ++
;

cyoa-choice: tradelab-doodad-sale
	iicon ($ pname .. " compra um " .. "APARATO CIENTIFICO" rainbold .. "!
	" .. "'Isso pode te teleportar distancias curtas. Tem 5 tiros. Nao perca de conta, eu esqueci de construir uma exibicao util para ele. A exibicao nele agora nao significa nada, ela so acende aleatoriamente." pbold $) text!
	goodfx 5 WEAPON_DOODAD trunk.weapon+ -5 trunk-food+ ' labsci-doodad ++
;

cyoa-bridge: tradelab-recruit
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	20 recruit-yes-food "'Estou pronto para zaptar coisas! Traguei meu recharger solar." pbold +crtext 
	recruitee .mechanicREV recruitee .witsREV recruitee .shootingREV
;

cyoa-bridge: tradelab-recruit-warn
	"'Claro, eu posso me juntar a voce. Estou ficando entediada aqui, preciso fazer algumas pesquisas em campo. Termine de comprar tudo que quiser antes de me recrutar! Nao e que eu nao confie em voce, mas depois que eu me juntar, vou trancar todas as minhas armas com minha impressao digital. Apenas no caso de eu morrer misteriosamente." pbold text!
		choice( "Recrutea mais tarde" )choice
		choice( "Recrute-a! - 20 alimentos" )choice: tradelab-recruit
;

cyoa: tradelab-scichat
	"A Ciencia das Compras" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"Todas as negociacoes que voce fizer aqui serao para o avanco da ciencia!" pbold text!
		trunk-food 5 < if
			"A cientista tem pena de sua falta de comida. Ela lhe da um teletransportador gratis, pouco usado. Restam 2 tiros!" +crtext clear-cyoa 2 WEAPON_DOODAD trunk.weapon+
		then;
		choice( "Deixela para la" )choice
		trunk-food 4 > labsci-doodad 1 < and if
			choice( "Comprar Teletransportador - 5 alimentos" )choice: tradelab-doodad-sale
		then
		trunk-food 11 > labsci-mazer 1 < and if
			choice( "Comprar Pistola Mazer - 12 alimentos" )choice: tradelab-mazer-sale
		then
		trunk-food 19 > if
			labsci-doodad 1 < labsci-mazer 1 < and if
				choice( "Recruta-la para a equipe - 20 alimentos" )choice: tradelab-recruit-warn
			else
				choice( "Recruta-la para a equipe! - 20 alimentos" )choice: tradelab-recruit
			then
		then
;

}

roomgen{

: tradelab-bots 
	still-npc .redbotify 2 movey cyoa{ ' tradelab-redbot cyoa! }
	regiondef{ ' trader-event lt .events! 1 lt .timer! }
	still-npc .bluebotify -0.5 movey cyoa{ ' tradelab-bluebot cyoa! }
	regiondef{ ' trader-event lt .events! 1 lt .timer! }
;
: tradelab-scientist still-npc .madscify 
	cyoa{ ' tradelab-scichat cyoa! } 
	regiondef{ ' trader-event lt .events! 1 lt .timer! }
;

}

map{ regiondef{

trade-camp-reset

: bldg tanchor!
	"traderlab" $picklocid addloc prefab{ 7 bunker } 
;

0 value bfx
0 value bfy
0 value snx
0 value sny

: river tile@
	dup LAYER_FLOOR particles32_base_id 0x 10 + .lspriteid!
	dup LAYER_FLOOR 0 0 .tile_layer.off!
	dup LAYER_FLOOR 0 .tile_layer.flipx!

	dup LAYER_FLOORTOP particles32_base_id 0x 11 + .lspriteid!
	dup LAYER_FLOORTOP 0 0 .tile_layer.off!
	dup LAYER_FLOORTOP 0 .tile_layer.flipx!

	TILE_BLOCK over .id!
	drop
;

: riveredge
	2dup river
	tile@ LAYER_FLOORTOP particles32_base_id 0x 12 + .lspriteid!
;

: cliff 2dup blocked tile@
	dup LAYER_FLOORTOP 0 .tile_layer.flipx!
	LAYER_FLOORTOP street_tiles_base_id 0x a9 + 0 3 rnd + .lspriteid!
;

: cliffedge 2dup blocked tile@
	dup LAYER_FLOORTOP 0 .tile_layer.flipx!
	LAYER_FLOORTOP street_tiles_base_id 0x ba + 0 3 rnd + .lspriteid!
;

: bank 2dup tile@ args( x y t )
	<- t LAYER_WALLSIDE street_tiles_base_id 0x 99 + 0 3 rnd + .lspriteid!
	<- t LAYER_FLOORTOP particles32_base_id 0x 12 + .lspriteid!
	<- x <- y riveredge
;

: bushy >r tile@
	// dup LAYER_WALLSIDE rnd5050 .tile_layer.flipx!
	dup LAYER_WALLSIDE street_tiles_base_id 0x 89 + 0 3 rnd + .lspriteid!
	LAYER_WALLSIDE 0 r> .tile_layer.off!
;

' plotgrass >defer ft

: pt
	xyp.xy ft // Plot the current floor by default

	xyp.i char G = if ' plotgrass to ft xyp.xy ft then;
	xyp.i char x = if ' plotgravel to ft then;
	xyp.i char ^ = if xyp.xy river then;
	xyp.i char v = if xyp.xy riveredge then;
	xyp.i char \ = if xyp.xy cliff then;
	xyp.i char / = if xyp.xy cliffedge xyp.xy -4 -5 rnd bushy then;
	xyp.i char | = if xyp.xy bank then;
	xyp.i char o = if xyp.xy -8 -7 rnd bushy then;
	xyp.i char $ = if xyp.xy grassedge then;
	xyp.i char W = if trader-weapon-buyer then;

	xyp.i char t = if pictable1 lt-here 8 rdrift then;		
	xyp.i char @ = if xyp.xy bldg then;
	xyp.i char T = if traderhere then;
	xyp.i char Z = if tnomey-spawn then;
;

"G........................
.........................
.........................
.........................
...t.................t...
.........................
.........................
/////////////////////////
\\\\\\\\\\\\\\\\\\\\\\\\\
|||||||||||||||||||||||||
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
^^^^^^^^^^^^^^^^^^^^^^^^^
ooooooooooooooooooooooooo
.........................
.........................
....W...Z.@.........T....
........................x
$$$$$$$$$$$$$$$$$$$$$$$$$
.........................
.........................
.........................
.........................
.........................
........................."
constant maplayout

: plotmap
	0 -1 maplayout xyplot: pt
;

: site-setup
	1 no_time!
	7 14 rnd o'clock

	' plotgrass to ft

	30 %chance if 0 0.5 frnd weather! -1 2 frnd wind_force! else -1 1 frnd wind_force! then

	plotmap
	mission{ no-edge-spawns }

	// Put char and chars in middle of map
	9 %mv-up
	24 %chars-up
;

region: specialcamp-1
	w: maplayout layoutw
	h: maplayout layouth

	trade-mode

	post-script: site-setup

} }

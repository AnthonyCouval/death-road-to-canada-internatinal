uses paul/region-layout.df
uses paul/region-road-layout.df
// uses events/industrial/indust.df
// "../deathforth/rooms/TEMPROOM.txt" $load-rooms
	
" 
:map 2 
:tags roboshack  
:scriptA roboprof
:scriptB robopunch 
:scriptC robospeed 
:scriptD indwalls 
:scriptE indbluepanel 
:scriptF indgreenpanel 
:scriptG 3indwalls 
:size 11 12 
:off 1 1 
:tiles 
00222222200
00233333200
00211D11200
2221E1F1222
23311A11332
21111111112
21B11111C12
21111111112
22221112222
33321112333
00022222000
00033333000

:map 3 
:tags toolshop  
:scriptA tshopgas 
:scriptB tshopelec 
:scriptC cashcounter flipside 
:scriptD cashcounter 
:scriptE indshelf -1 movey 
:scriptF industsmall 
:size 15 13 
:off 6 0 
:tiles 
222222202222222
233333202333332
21E1E12021E1E12
2F111F202F111F2
222122222221222
332133333331233
00211C111D11200
002A1111111B200
002111111111200
002111111111200
002222111222200
003332222233300
000003333300000

:map 4 
:tags toolschool  
:scriptA schooltrader 
:scriptB industsmall 
:scriptC carwreck 
:scriptD indshelf -1 movey 
:scriptE 3indwalls 
:size 13 11 
:off 6 1 
:tiles 
2222222222222
2333333333332
2B1D1D111E112
2B1111111C112
211111A111112
2111111111BB2
2B111111111B2
2BB111111B112
2222211122222
3333222223333
0000333330000
"
value CAMP
CAMP $parse-rooms

cyoa{

: cyoa-sale-car
	carkey 0 > if cyoa-car then;

	cyoa-start
	"Voiture a vendre." cyoa-title!
		"Cette voiture a l'apparence de fonctionner a plein regime et prete a partir. vous pourriez la racheter aupres du trafiquant automobile voisin !" cyoa-text!
		choice( "Revenir plus tard" )choice
		

;

cyoa-choice: tcarsales-go
	($ Thegroup .. " achete une voiture neuve a nouveau!" .. "Prenez simplement la voiture que vous voulez de l'essence !" pbold $) cyoa-text! goodfx
		1 to carkey clear-cyoa -5 trunk-food+ 50 trunk-gas+ someicon
;

cyoa: tcarsales
	"Chaque voiture doit passer!" cyoa-title!
	($ "Je dois les faire sortir de notre lot ! vous pouvez acheter n'importe quelle voiture, des maintenant, pour la basse, tres basse prix de 5 aliments ! achat immediat ! pas de financement ! pret a l'emploi et pret a fonctionner !. .  et je vous donnerai meme 50 litres de gazole gratuitement !" pbold $) cyoa-text!
	trunk-food 4 > if "C'est bien genereux." +crtext then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " n'arrivant pas a se permettre l'achat de vie changement d'une voiture neuve brillante." $) +crtext
	else
		choice( "Laissez-le pour le moment" )choice back-esc
		trunk-food 4 > if
			choice( "Acheter une nouvelle voiture - 5 aliments" )choice: tcarsales-go
		then
	then
;

cyoa-choice: tgarage-1
	($ Thegroup .. " paie les reparations pleines pour la chassis et le moteur !" .. "Quand vous quitterez ici, il suffit de conduire votre voiture un peu plus loin et nous la reparerons !" pbold $) cyoa-text! goodfx
		clear-cyoa -5 trunk-food+ image-car-garage road{ ' mandible-repairgarage stack-road-action }
;

cyoa-choice: tgarage-2
	($ Thegroup .. " paie des modifications avancees pour un tableau de voitures de luxe, ainsi que les reparations completes du chassis et du moteur !" .. "Quand vous quitterez ici, il suffit de conduire un peu plus loin le long de la route jusqu'au garage. votre voiture recevra un traitment special !" pbold $) cyoa-text! goodfx
		clear-cyoa -35 trunk-food+ image-car-garage road{ ' mandible-megagarage stack-road-action }
;

cyoa: tgarage
	"Atelier d'expertise en garage" cyoa-title!
	($ "Nous avons un garage plein d'experts mecaniciens juste en face, et nous offrons. " pbold .. "LES TRAVAUX" fbold .. "modifications completes de voiture : plaquages blindes, acceleration, chassis renforce, revision moteur a efficacite carburant augmentee. tous pour 35 nourritures... . nous pouvons egalement vous reparer la voiture entierement sans ajouts sucrees, a titre de prix, pour 5 aliments." pbold $) cyoa-text!
	trunk-food 34 > if "La voiture parfaite est possible." +crtext else
		trunk-food 4 > if "Vous pouvez toujours acheter des reparations completes." +crtext then
	then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " n'arrivez pas a payer cette modification automobile etendue et goutee." $) +crtext
	else
		choice( "Laissez-le pour le moment" )choice back-esc
		trunk-food 34 > if
			choice( "Modifications completes de voiture. " .. "LES TRAVAUX" fbold .. " trouvez 35 aliments differents." )choice: tgarage-2
		then
		trunk-food 4 > if
			choice( "Reparations completes - 5 nourritures" )choice: tgarage-1
		then
	then
;

0 value mt1-1
0 value mt1-2
0 value mt1-3
0 value mt1-4

cyoa-choice: trade-m1-4
	($ Thegroup .. " achetez une perforateur a gaz!" .. "Si je parviens a atteindre le canada, je vais y ouvrir une nouvelle laboratoire la-bas." pbold $) cyoa-text! goodfx
	1 WEAPON_JACKHAMMER trunk.weapon+ -20 trunk-food+ someicon ' mt1-4 ++
;

cyoa-choice: trade-m1-3
	($ Thegroup .. " achetez une scie a chaine essence! c'est un classique !" .. "C'est la reponse idealee contre les zombies ! mais attention, ne soyez pas a court de carburant." pbold $) cyoa-text! goodfx
	1 WEAPON_CHAINSAW trunk.weapon+ -10 trunk-food+ someicon ' mt1-3 ++
;

cyoa-choice: trade-m1-2
	($ Thegroup .. " achetez un aspirateur a feuilles a essence !" .. "Fais danser les zombies vers le vent ! comme des feuilles !" pbold $) cyoa-text! goodfx
	1 WEAPON_BLOWER trunk.weapon+ -5 trunk-food+ someicon ' mt1-2 ++
;

cyoa-choice: trade-m1-1
	($ Thegroup .. " achetez un aspirateur a herbe au gazoil !" .. "Tres utile pour eliminer les horde de zombies ! peut etre assez bon pour les mauvaises herbes." pbold $) cyoa-text! goodfx
	1 WEAPON_WHACKER trunk.weapon+ -5 trunk-food+ someicon ' mt1-1 ++
;

cyoa: tshopgas
	"Merci beaucoup." cyoa-title!
	($ "Ne parliez pas a l'autre homme ! il y a quelque chose concernant les armes a gaz, que vous ne pouvez pas obtenir de l'electrique." pbold $) cyoa-text!
	trunk-food 4 > if "Temps de mettre le moteur en marche." +crtext then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " votre compte est figurativement a sec de carburant. le negociant vous accorde une petite consideration et vous remet reellement du gazole." $) +crtext clear-cyoa +30 trunk-gas+ goodfx
	else
		choice( "Laissez-le pour le moment" )choice back-esc
		trunk-food 19 > mt1-4 1 < and if
			choice( "Marteau-piqueur - 20 aliments" )choice: trade-m1-4
		then
		trunk-food 9 > mt1-3 3 < and if
			choice( "Ch chainsaw - dix repas" )choice: trade-m1-3
		then
		trunk-food 4 > mt1-2 3 < and if
			choice( "Camping en detresse" )choice: trade-m1-2
		then
		trunk-food 4 > mt1-1 3 < and if
			choice( "Tondeuse a herbe - 5 aliments" )choice: trade-m1-1
		then
	then
;

0 value mt2-1
0 value mt2-2
0 value mt2-3
0 value mt2-4

: mt2-firstbuy 
	mt2-4 1 < mt2-3 1 < mt2-2 1 < mt2-1 1 < and and and
;

: mt2-firstbuy-done
	($ "Des gobelins mechants ! " pbold .. "CHARGE DOUBLEMENT !" fbold $) +crtext
;

cyoa-choice: trade-m2-4
	($ Thegroup .. " achete une perforateur electrique !" .. "C'est reellement ecologique !" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 255 else 130 then WEAPON_EJHAMMER trunk.weapon+ -20 trunk-food+ someicon ' mt2-4 ++ ;

cyoa-choice: trade-m2-3
	($ Thegroup .. " achete une scie electrique pour ta coupe de bois ! tu te sens un peu gonfle d'orgueil en envisageant l'ecologie sauvegardee dans l'apocalypse post-catastrophe... ." .. "Magnifique pour composter des zombies !" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 160 else 80 then WEAPON_ECHAINSAW trunk.weapon+ -10 trunk-food+ someicon ' mt2-3 ++ ;
	
cyoa-choice: trade-m2-2
	($ Thegroup .. " achete une aspirateur feuilles electrique !" .. "Faire doucement avancer les zombies, une alternative pacifiste a toutes ces eclaboussures !" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 100 else 50 then WEAPON_EBLOWER trunk.weapon+ -5 trunk-food+ picon ' mt2-2 ++ ;

cyoa-choice: trade-m2-1
	($ Thegroup .. " achetez une tondeuse electrique a herbe!" .. "A multiple d'applications efficaces dans la culture de zombies de zomboculture." pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 100 else 50 then WEAPON_EWHACKER trunk.weapon+ -5 trunk-food+ picon ' mt2-1 ++ ;

cyoa: tshopelec
	"Outils electriques" cyoa-title!
	($ "Ne parlez pas a l'autre homme ! les outils electriques sont ecologiques et vous permettent de reduire les emissions de gaz en reduisant en meme temps les zombies a morceaux." pbold $) cyoa-text!
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " manque assez d'argent pour acheter quoi que ce soit en raison de la carence alimentaire." $) +crtext
		($ "Le negociant eprouve une commiseration envers ... " .. thegroup .. " une carte avec des indications et il distribue une carte a chacun. " .. "SE SENT UN PEU MEILLEUR." rbold $) +crtext goodfx 30 WEAPON_EBLOWER trunk.weapon+ clear-cyoa
	else
		mt2-firstbuy if
			($ "Un special : le negociant offrira une demarche privilegiee. " .. "REGARDEZ-LE." rainbold .. " votre premiere achat vous apporte deux fois plus d'energie que de courant." $) +crtext
		then
		choice( "Laissez-le pour le moment" )choice back-esc
		trunk-food 19 > mt2-4 1 < and if
			choice( "Jackhammer electrique - 20 pieds" )choice: trade-m2-4
		then
		trunk-food 9 > mt2-3 2 < and if
			choice( "Scie a chaine electrique - 10 pieds" )choice: trade-m2-3
		then
		trunk-food 4 > mt2-2 2 < and if
			choice( "Souffleur a feuilles electrique - 1,5 metre" )choice: trade-m2-2
		then
		trunk-food 4 > mt2-1 2 < and if
			choice( "Herbe a cigarette electrique - 5 aliments" )choice: trade-m2-1
		then
	then
;

0 value mechtrained+

: mech-train-amt numhums 2 * mechtrained+ 1 + * 1 + ; 
: mech-train-pay? trunk-food mech-train-amt 1 - > notsolo notsolohuman and and ;
: mech-train-pay mech-train-amt -1 * trunk-food+ ;

: mech-solo-amt mechtrained+ 2 * 3 + ;
: mech-solo-pay? trunk-food mech-solo-amt 1 - > ;
: mech-solo-pay mech-solo-amt -1 * trunk-food+ ;

cyoa-choice: mech-school-train
	cchar .mechanical@ 6 < if
		($ cname .. " observez attentivement le professeur qui frappe vraiment a la voiture avec une cle de culasse. ainsi, c'est ca l'ingenierie !" $) goodfx
	else
		($ cname .. " votre derniere paiement est considere comme une contribution pour la construction d'une nouvelle aile pour l'ecole. les ingenieurs ont deja obtenu leur diplome complet a cette prestigieuse academie!" $) badfx
	then cyoa-text! 
	mech-solo-pay mechanicREV mechanic++ ' mechtrained+ ++
;

cyoa-choice: mech-school-group
	($ "Toute le groupe recoit une formation technique mecanique ! chaque membre regarde le professeur frapper la voiture a la cle. c'est tres eclairant." $) cyoa-text! goodfx
	mech-train-pay someicon
	regiondef{ ' trade-mechanic-group! foreach-human-char } numhums ' mechtrained+ +!
;

cyoa: mech-school-give
	solohuman if
		choosehuman mech-school-train
	then;
	"Ce savant connaissances mecaniques devrait etre transmis a qui peut en faire un bon usage." cyoa-text!
	' mech-school-train cyoa-human-choice
;

cyoa: schooltrader
	($ "L'ecole outil" $) cyoa-title!
	"L'ingenierie mecanique est reellement une science. contrairement a la medecine qui est plutot un art. ainsi que l'origami ou peut-etre la danse interpretative." pbold text!
	"Le professeur de mecanique frappe repetement le chassis de la voiture accidentee avec une cle en main, tout en gardant les yeux fixes sur elle : 'ceci est la loi du mecanisme. la force appliquee doit etre egale a la reaction.'" +crtext foodamt
		mech-solo-pay? not if
			"Vous ne pouvez plus vous permettre d'autres formations." +crtext 
		then;
		choice( "Laissez-le pour le moment" )choice back-esc
		mech-solo-pay? if
			choice( "Donner " .. mech-solo-amt .. " nourriture pour une formation unique" )choice: mech-school-give
		then
		mech-train-pay? if
			choice( "Donner " .. mech-train-amt .. " vous devez vous entrainer et mettre du muscle !" )choice: mech-school-group
		then
		
;

cyoa-bridge: robopunch-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	10 recruit-yes-food 
	($ "Une machine qui distribue des coups de baton ? la technologie est assez interessante." $) +crtext
	"pret a frapper #chargement des protocolles de coup#" pbold +crtext
	recruitee .strengthREV recruitee .fitnessREV
;

cyoa: robopunch
	"Nourriture restante pour balles." cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"ROBOT PUNCH : JE SUIS UN ROBOT QUI FRAPPE." pbold cyoa-text! basic-tone
	"Cet robot a ete concu pour une seule raison : frapper." +crtext
	trunk-food 9 > if
		choice( "Laissez-le pour le moment" )choice
		choice( "Acheter punchbot - 10 aliments" )choice: robopunch-go
	else
		"Vous avez besoin de 10 aliments pour acheter ce robot cocktail." +crtext
		choice( "Desole, je ne peux pas m'en passer de la nourriture." )choice
	then
;

cyoa-bridge: robospeed-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	15 recruit-yes-food 
	($ "Le robot effectue rapidement un demi-tour en boule." $) +crtext
	"protocoles de charge : chargingement rapide	#procedures de chargement : chargement rapide#" pbold +crtext
	recruitee .strengthREV recruitee .fitnessREV
;

cyoa: robospeed
	"Tres rapidement" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"JE SUIS LE ROBOT A LA VITESSE ELEVEE." pbold cyoa-text! basic-tone
	"Cet robot mince et equipe de roues d'hubcap miniature semble faire resonner le concept de 'vitesse'...  . 	mots cles : vitesse, robot." +crtext
	trunk-food 14 > if
		choice( "Laissez-le pour le moment" )choice
		choice( "Pense a la curry." )choice: robospeed-go
	else
		"Vous avez besoin de 15 aliments pour acheter ce robot a vitesse elevee." +crtext
		choice( "Desole, je ne peux pas m'en passer de la nourriture." )choice
	then
;

}

roomgen{


: .ct1 args( ch ) // Mech Teacher
	WEAPON_WRENCH <- ch .weapona!
;
: .ct2 args( ch ) // Gas tools
	26 <- ch .glassestype! // Gas Mask
;
: .ct3 args( ch ) // Elec tools
	21 <- ch .glassestype! // Green Goggles
;
: .ct4 args( ch ) // Garage
	1 <- ch .female!
	25 <- ch .hattype!
	6 <- ch .legstype!
	3 <- ch .torsotype!
	0 <- ch .glassestype!
;
: .ct5 args( ch ) 
	7 <- ch .hattype! // Cowboy hat
;
: .ct6 args( ch ) 
	<- ch .speedbotify
;
: .ct7 args( ch ) 
	<- ch .punchbotify
;

: camptradercommon
	still-npc regiondef{ ' trader-event lt .events! 1 lt .timer! }
	
;

: schooltrader
	camptradercommon .ct1 cyoa{ ' schooltrader cyoa! }
;

: robopunch
	camptradercommon .ct7 cyoa{ ' robopunch cyoa! }
;

: robospeed
	camptradercommon .ct6 cyoa{ ' robospeed cyoa! }
;

: roboprof
	// camptradercommon .ct8 cyoa{ ' roboprof cyoa! }
;

: tshopgas
	camptradercommon .ct2 cyoa{ ' tshopgas cyoa! }
;

: tshopelec
	camptradercommon .ct3 cyoa{ ' tshopelec cyoa! }
;

: tgarage
	camptradercommon .ct4 cyoa{ ' tgarage cyoa! }
;

: tcarsales
	camptradercommon .ct5 cyoa{ ' tcarsales cyoa! }
;

		0 stack
			' camptradercommon shove
			' camptradercommon shove
			' camptradercommon shove
			' camptradercommon shove
		value preptrader-base
		preptrader-base shuffles var preptrader-picker

: preppertrader
	preptrader-picker depths 0= if preptrader-base shuffles to preptrader-picker then pops nip execute ;


}

locdef{

location: toolshop
		max-rooms: 1
		entry-tags: toolshop
		script: set-theme-restaurant
		pre-script: set-theme-restaurant
		post-script: nad 0 %zombs
				
		room-choice: toolshop
			room-weight: 10
			room-count: 1
			room-post-script: 
			filter-only
			
location: toolschool
		max-rooms: 1
		entry-tags: toolschool
		script: set-theme-bookstore
		pre-script: set-theme-bookstore
		post-script: nad 0 %zombs
				
		room-choice: toolschool
			room-weight: 10
			room-count: 1
			room-post-script: 
			filter-only
		
location: roboshack
		max-rooms: 1
		entry-tags: roboshack
		script: factory-theme
		pre-script: factory-theme
		post-script: nad 0 %zombs
				
		room-choice: roboshack
			room-weight: 10
			room-count: 1
			room-post-script:
			filter-only
			
}
		
root{

: prepper-magchance
	20 %chance if
		magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! drift
	then
;

: sale-car 
	1 22 rnd spawncar mission{ cyoa{ ' cyoa-sale-car cyoa! } }
	0 lt .action_icon!
;

}
		
map{ regiondef{

trade-camp-reset

0 value gbx
0 value gby
0 value gbx2
0 value gby2
0 value gbx3
0 value gby3
0 value bfx
0 value bfy

: bldg tanchor!
	"tete de chien." $picklocid addloc
	"MAGASIN DE ROBOTIQUE ROBO SHACK" sign-text! prefab{ 1 5 1 shop } 
;
: bldg2 tanchor!
	"atelier d'outillage" $picklocid addloc
	"MAGASIN D'OUTILS" sign-text! prefab{ 2 5 1 shop } 
;
: bldg3 tanchor!
	"la peau est grise." $picklocid addloc
	"ECOLE D'OUTILS" sign-text! prefab{ 3 6 1 shop } 
;


' plotgrass >defer ft

: pt
	xyp.xy ft // Plot the current floor by default
	
	xyp.i char { = if xyp.xy to gby2 to gbx2 then;
	xyp.i char } = if gbx2 1- gby2 xyp.xy swap 1+ swap gravellot then;
	xyp.i char ( = if xyp.xy to gby to gbx then;
	xyp.i char ) = if gbx 1- gby xyp.xy swap 1+ swap gravellot then;
	xyp.i char [ = if xyp.xy to gby3 to gbx3 then;
	xyp.i char ] = if gbx3 1- gby3 xyp.xy swap 1+ swap gravellot then;
	
	xyp.i char 1 = if xyp.x to bfx then;
	xyp.i char 2 = if bfx xyp.y xyp.x barbfence then;

	xyp.i char t = if sale-car lt-here then;
	xyp.i char U = if tnomey-spawn then;
	xyp.i char W = if trader-weapon-buyer then;
	xyp.i char G = if trader-food-for-gas then;
	xyp.i char S = if tgarage lt-here then;
	xyp.i char M = if tcarsales lt-here then;
		
	xyp.i char @ = if xyp.xy bldg then;
	xyp.i char 3 = if xyp.xy bldg2 then;
	xyp.i char 4 = if xyp.xy bldg3 then;
	
	xyp.i char p = if ' plotpave to ft then;
	xyp.i char $ = if xyp.xy grassedge then;
;

"
...........................
1.........................2
...........................
..................[....t...
...........................
...........................
...........................
.......L..........ML...t...
..{........................
.@....3....4...............
...........................
............(...}......t...
..................S........
...........................
..........................]
...........................
1..........2....1.........2
......G..U...........W....p
$$$$$$$$$$$$....$$$$$$$$$$$
...............)...........
...........................
...........................
...........................
...........................
...........................
...........................
..........................."
constant maplayout

: specialcamp-6
	' plotgrass to ft
	0 -1 maplayout xyplot: pt
;


: site-setup

	// Clear the map with grass
	-1 -1 w h grassbox

	pavement
// Plot trader stuff
	specialcamp-6

	-1 -1 w 3 conclot // Top
	-1 h 2 - w h conclot // Bottom

	0 h 6 - w ' plotroadlines hline
	

	1 no_time!
	7 14 rnd o'clock
	// 50 %chance if 1 2 frnd weather! -1 -2 frnd wind_force! else 1 2 frnd wind_force! then
	
	mission{ no-edge-spawns }
	
	20 %mv-up
	25 %chars-up
;

	 region: specialcamp-6
		w: maplayout layoutw
		h: maplayout layouth

		trade-mode

		post-script: site-setup
} }

uses paul/region-layout.df
uses paul/region-road-layout.df
// uses events/industrial/indust.df
// "../deathforth/rooms/TEMPROOM.txt" $load-rooms
	
" 
:map 2 
:tags roboshack  
:scriptA roboprof
:scriptB robopunch 
:scriptC robospeed 
:scriptD indwalls 
:scriptE indbluepanel 
:scriptF indgreenpanel 
:scriptG 3indwalls 
:size 11 12 
:off 1 1 
:tiles 
00222222200
00233333200
00211D11200
2221E1F1222
23311A11332
21111111112
21B11111C12
21111111112
22221112222
33321112333
00022222000
00033333000

:map 3 
:tags toolshop  
:scriptA tshopgas 
:scriptB tshopelec 
:scriptC cashcounter flipside 
:scriptD cashcounter 
:scriptE indshelf -1 movey 
:scriptF industsmall 
:size 15 13 
:off 6 0 
:tiles 
222222202222222
233333202333332
21E1E12021E1E12
2F111F202F111F2
222122222221222
332133333331233
00211C111D11200
002A1111111B200
002111111111200
002111111111200
002222111222200
003332222233300
000003333300000

:map 4 
:tags toolschool  
:scriptA schooltrader 
:scriptB industsmall 
:scriptC carwreck 
:scriptD indshelf -1 movey 
:scriptE 3indwalls 
:size 13 11 
:off 6 1 
:tiles 
2222222222222
2333333333332
2B1D1D111E112
2B1111111C112
211111A111112
2111111111BB2
2B111111111B2
2BB111111B112
2222211122222
3333222223333
0000333330000
"
value CAMP
CAMP $parse-rooms

cyoa{

: cyoa-sale-car
	carkey 0 > if cyoa-car then;

	cyoa-start
	"Wagen angeboten" cyoa-title!
		"'Dieses Auto sieht so aus als ob es sowohl 'schlusselfertig als auch fahrbereit' ist. Sie konnten es von dem schmierigen Autoverkaufer in der Nahe kaufen!" cyoa-text!
		choice( "Komm spater wieder" )choice
		

;

cyoa-choice: tcarsales-go
	($ Thegroup .. "  kauft ein brandneues Auto!	" .. "Nimm einfach jedes Auto vom Grundstuck das du willst!" pbold $) cyoa-text! goodfx
		1 to carkey clear-cyoa -5 trunk-food+ 50 trunk-gas+ someicon
;

cyoa: tcarsales
	"Jedes Auto muss weg!" cyoa-title!
	($ "'Ich muss diese Autos vom Grundstuck bekommen! Sie konnen jedes Auto jetzt sofort zum gunstigen Preis von 5 Lebensmitteln kaufen! Holen Sie es sich jetzt! Keine Finanzierung! Schlusselfertig und fahrbereit!	Ich gebe sogar 50 Gas dazu!" pbold $) cyoa-text!
	trunk-food 4 > if "'Das ist ein ziemliches Angebot." +crtext then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " ' kann sich den lebensverandernden Kauf eines glanzenden neuen Autos nicht leisten." $) +crtext
	else
		choice( "Gehen Sie vorerst" )choice back-esc
		trunk-food 4 > if
			choice( "Kaufen Sie ein neues Auto   5 Lebensmittel" )choice: tcarsales-go
		then
	then
;

cyoa-choice: tgarage-1
	($ Thegroup .. "  zahlt fur komplette Fahrwerks  und Motorreparaturen!	" .. "'Wenn Sie hier wegfahren, fahren Sie einfach ein Stuck die Strae entlang mit Ihrem Auto und wir reparieren es!" pbold $) cyoa-text! goodfx
		clear-cyoa -5 trunk-food+ image-car-garage road{ ' mandible-repairgarage stack-road-action }
;

cyoa-choice: tgarage-2
	($ Thegroup .. "  zahlt fur eine Reihe von fortschrittlichen Automodifikationen sowie fur komplette Fahrwerks  und Motorreparaturen!	" .. "Wenn Sie hier wegfahren, fahren Sie einfach ein Stuck die Strae entlang zur Garage. Ihr Auto wird DAS WERK bekommen!" pbold $) cyoa-text! goodfx
		clear-cyoa -35 trunk-food+ image-car-garage road{ ' mandible-megagarage stack-road-action }
;

cyoa: tgarage
	"Expertenwerkstatt" cyoa-title!
	($ "Wir haben eine Garage voller erfahrener Mechaniker gleich die Strae runter und bieten an " pbold .. "DIE WERKE" fbold .. ". Volle Auto Mods: Panzerung, erhohte Geschwindigkeit, verstarktes Fahrgestell, Motoruberholung mit erhohter Kraftstoffeffizienz. Alles fur 35 Essen. Wir konnen dein Auto auch ohne sue Mods komplett reparieren, fur 5 Essen." pbold $) cyoa-text!
	trunk-food 34 > if "Das perfekte Auto ist moglich." +crtext else
		trunk-food 4 > if "Du konntest immer noch Vollreparaturen kaufen." +crtext then
	then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " ' kann sich diese umfangreiche und dennoch geschmackvolle Automodifikation nicht leisten." $) +crtext
	else
		choice( "Gehen Sie vorerst" )choice back-esc
		trunk-food 34 > if
			choice( "Volle Auto Mods, " .. "DIE WERKE" fbold .. "    35 Essen" )choice: tgarage-2
		then
		trunk-food 4 > if
			choice( "Vollstandige Reparaturen   5 Essen" )choice: tgarage-1
		then
	then
;

0 value mt1-1
0 value mt1-2
0 value mt1-3
0 value mt1-4

cyoa-choice: trade-m1-4
	($ Thegroup .. "  kauft einen gasbetriebenen Presslufthammer!" .. "'Die Wahl des Kenners zum Meieln durch Horden von Zombies!" pbold $) cyoa-text! goodfx
	1 WEAPON_JACKHAMMER trunk.weapon+ -20 trunk-food+ someicon ' mt1-4 ++
;

cyoa-choice: trade-m1-3
	($ Thegroup .. "  kauft eine gasbetriebene Kettensage! Ein Klassiker!" .. "'Jeder wei dass dies DIE Antwort fur Zombies ist! Solange dir das Benzin nicht ausgeht." pbold $) cyoa-text! goodfx
	1 WEAPON_CHAINSAW trunk.weapon+ -10 trunk-food+ someicon ' mt1-3 ++
;

cyoa-choice: trade-m1-2
	($ Thegroup .. "  kauft einen gasbetriebenen Laubblaser!" .. "Blase die Zombies weg! Wie Blatter!" pbold $) cyoa-text! goodfx
	1 WEAPON_BLOWER trunk.weapon+ -5 trunk-food+ someicon ' mt1-2 ++
;

cyoa-choice: trade-m1-1
	($ Thegroup .. "  kauft einen gasbetriebenen Unkrautvernichter!	" .. "Ausgezeichnet zum Trimmen der Zombiehorden! Etwas anstandig fur Unkraut." pbold $) cyoa-text! goodfx
	1 WEAPON_WHACKER trunk.weapon+ -5 trunk-food+ someicon ' mt1-1 ++
;

cyoa: tshopgas
	"Gasbetriebene Werkzeuge" cyoa-title!
	($ "'Sprich nicht mit dem anderen! Es gibt etwas an gasbetriebenen Waffen, das man von elektrischen einfach nicht bekommen kann." pbold $) cyoa-text!
	trunk-food 4 > if "Zeit zum Aufdrehen." +crtext then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. "  ist im ubertragenen Sinne aus dem Benzin. Der Handler hat Mitleid mit dir und verteilt etwas wortliches Benzin." $) +crtext clear-cyoa +30 trunk-gas+ goodfx
	else
		choice( "Gehen Sie vorerst" )choice back-esc
		trunk-food 19 > mt1-4 1 < and if
			choice( "Presslufthammer   20 Lebensmittel" )choice: trade-m1-4
		then
		trunk-food 9 > mt1-3 3 < and if
			choice( "Kettensage   10 Lebensmittel" )choice: trade-m1-3
		then
		trunk-food 4 > mt1-2 3 < and if
			choice( "Laubblaser   5 Lebensmittel" )choice: trade-m1-2
		then
		trunk-food 4 > mt1-1 3 < and if
			choice( "Unkrautvernichter   5 Lebensmittel" )choice: trade-m1-1
		then
	then
;

0 value mt2-1
0 value mt2-2
0 value mt2-3
0 value mt2-4

: mt2-firstbuy 
	mt2-4 1 < mt2-3 1 < mt2-2 1 < mt2-1 1 < and and and
;

: mt2-firstbuy-done
	($ "Viel Spa mit deinem " pbold .. "DOPPELTE LADUNG!" fbold $) +crtext
;

cyoa-choice: trade-m2-4
	($ Thegroup .. "  kauft einen elektrischen Presslufthammer!	" .. "'Es ist einfach so umweltfreundlich!" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 255 else 130 then WEAPON_EJHAMMER trunk.weapon+ -20 trunk-food+ someicon ' mt2-4 ++ ;

cyoa-choice: trade-m2-3
	($ Thegroup .. "  kauft eine elektrische Kettensage! Du fuhlst dich unbehaglich selbstgefallig daruber die Umwelt in der Postapokalypse zu retten.	" .. "Hervorragend zum Kompostieren von Zombies!" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 160 else 80 then WEAPON_ECHAINSAW trunk.weapon+ -10 trunk-food+ someicon ' mt2-3 ++ ;
	
cyoa-choice: trade-m2-2
	($ Thegroup .. "  kauft ein elektrisches Laubgeblase!	" .. "Hirte die Zombies sanft, eine pazifistische Alternative zu all diesem Geplattere!" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 100 else 50 then WEAPON_EBLOWER trunk.weapon+ -5 trunk-food+ picon ' mt2-2 ++ ;

cyoa-choice: trade-m2-1
	($ Thegroup .. "  kauft einen elektrischen Unkrautvernichter!	" .. "Hat mehrere effektive Anwendungen in den Anbaumethoden der Zomboculture." pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 100 else 50 then WEAPON_EWHACKER trunk.weapon+ -5 trunk-food+ picon ' mt2-1 ++ ;

cyoa: tshopelec
	"Elektrowerkzeuge" cyoa-title!
	($ "'Sprich nicht mit dem anderen Typen! Elektrowerkzeuge sind umweltfreundlich und ermoglichen es dir, die Gasemissionen zu reduzieren, wahrend du Zombies niedermahst." pbold $) cyoa-text!
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " hat nicht genugend Nahrung um etwas zu kaufen!" $) +crtext
		($ "Der Handler hat Mitleid mit " .. thegroup .. "  und verteilt a " .. "RABATT LAUBGEBLASE!" rbold $) +crtext goodfx 30 WEAPON_EBLOWER trunk.weapon+ clear-cyoa
	else
		mt2-firstbuy if
			($ "Als Sonderangebot wird der Handler " .. "DOPPELBELASTUNG" rainbold .. "  dein erster Kauf. Es wird doppelt so viel Energie haben wie das was du normalerweise finden wurdest!" $) +crtext
		then
		choice( "Gehen Sie vorerst" )choice back-esc
		trunk-food 19 > mt2-4 1 < and if
			choice( "Elektrischer Presslufthammer   20 Essen" )choice: trade-m2-4
		then
		trunk-food 9 > mt2-3 2 < and if
			choice( "Elektrische Kettensage   10 Essen" )choice: trade-m2-3
		then
		trunk-food 4 > mt2-2 2 < and if
			choice( "Elektrisches Laubgeblase   5 Essen" )choice: trade-m2-2
		then
		trunk-food 4 > mt2-1 2 < and if
			choice( "Elektrischer Freischneider   5 Essen" )choice: trade-m2-1
		then
	then
;

0 value mechtrained+

: mech-train-amt numhums 2 * mechtrained+ 1 + * 1 + ; 
: mech-train-pay? trunk-food mech-train-amt 1 - > notsolo notsolohuman and and ;
: mech-train-pay mech-train-amt -1 * trunk-food+ ;

: mech-solo-amt mechtrained+ 2 * 3 + ;
: mech-solo-pay? trunk-food mech-solo-amt 1 - > ;
: mech-solo-pay mech-solo-amt -1 * trunk-food+ ;

cyoa-choice: mech-school-train
	cchar .mechanical@ 6 < if
		($ cname .. " ' beobachtet aufmerksam wie der Professor mit einem Schraubenschlussel auf das Auto einprugelt. DAS ist also Ingenieurwesen!" $) goodfx
	else
		($ cname .. "  hat bereits seinen vollstandigen Ingenieurabschluss von dieser renommierten Akademie! Ihre letzte Zahlung wird als Spende fur den Bau eines neuen Flugels fur die Schule entgegengenommen." $) badfx
	then cyoa-text! 
	mech-solo-pay mechanicREV mechanic++ ' mechtrained+ ++
;

cyoa-choice: mech-school-group
	($ "'Die gesamte Gruppe erhalt eine mechanische Ausbildung! Alle beobachten wie der Professor mit einem Schraubenschlussel auf das Auto einschlagt. Es ist sehr aufschlussreich." $) cyoa-text! goodfx
	mech-train-pay someicon
	regiondef{ ' trade-mechanic-group! foreach-human-char } numhums ' mechtrained+ +!
;

cyoa: mech-school-give
	solohuman if
		choosehuman mech-school-train
	then;
	"Wer sollte dieses umfangreiche mechanische Wissen erhalten?" cyoa-text!
	' mech-school-train cyoa-human-choice
;

cyoa: schooltrader
	($ "Die Werkzeugschule" $) cyoa-title!
	"Maschinenbau ist eine echte Wissenschaft. Im Gegensatz zur Medizin, die eher eine Kunst ist. Ahnlich wie Origami oder vielleicht Ausdruckstanz." pbold text!
	"Der Professor fur Mechanik schlagt wahrend dieser Aussage wiederholt mit einem Schraubenschlussel auf das Chassis des zerstorten Autos ein, ohne dabei den Augenkontakt zu verlieren." +crtext foodamt
		mech-solo-pay? not if
			"'Sie konnen sich kein weiteres Training leisten." +crtext 
		then;
		choice( "Gehen Sie vorerst" )choice back-esc
		mech-solo-pay? if
			choice( "Gib " .. mech-solo-amt .. "  essen fur Einzeltraining" )choice: mech-school-give
		then
		mech-train-pay? if
			choice( "Gib " .. mech-train-amt .. "  essen fur Gruppentraining" )choice: mech-school-group
		then
		
;

cyoa-bridge: robopunch-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	10 recruit-yes-food 
	($ "Ein Roboter der Schlage austeilt? Technologie ist ziemlich cool." $) +crtext
	"#SCHLAGPROTOKOLLE LADEN#	#BEREIT ZUM SCHLAGEN#" pbold +crtext
	recruitee .strengthREV recruitee .fitnessREV
;

cyoa: robopunch
	"Der perfekte mechanische Schlag" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"#ICH BIN PUNCH BOT#" pbold cyoa-text! basic-tone
	"'Jetzt kommen wir der Sache naher! Dieser Roboter ist fur einen Zweck konzipiert: Schlagen." +crtext
	trunk-food 9 > if
		choice( "Lass es erstmal so" )choice
		choice( "Kaufe PUNCHBOT   10 Essen" )choice: robopunch-go
	else
		"Du brauchst 10 Essen um diesen Punchbot zu kaufen." +crtext
		choice( "'Kann das Essen nicht entbehren" )choice
	then
;

cyoa-bridge: robospeed-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	15 recruit-yes-food 
	($ "Der Roboter macht schnell einen Donut. So schnell." $) +crtext
	"#LADE GESCHWINDIGKEITSPROTOKOLLE#	#MUSS SCHNELL GEHEN#" pbold +crtext
	recruitee .strengthREV recruitee .fitnessREV
;

cyoa: robospeed
	"So schnell" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"#ICH BIN SPEED BOT#" pbold cyoa-text! basic-tone
	"'Mit seinen schlanken Linien und winzigen Spinner Radkappen strahlt dieser Roboter selbst im Stillstand das Konzept von Geschwindigkeit aus.	Auerdem hat er gerade das Wort Geschwindigkeit gesagt." +crtext
	trunk-food 14 > if
		choice( "Lass es erstmal so" )choice
		choice( "Kaufe SPEEDBOT   15 Essen" )choice: robospeed-go
	else
		"Du brauchst 15 Essen um diesen Speedbot zu kaufen." +crtext
		choice( "'Kann das Essen nicht entbehren" )choice
	then
;

}

roomgen{


: .ct1 args( ch ) // Mech Teacher
	WEAPON_WRENCH <- ch .weapona!
;
: .ct2 args( ch ) // Gas tools
	26 <- ch .glassestype! // Gas Mask
;
: .ct3 args( ch ) // Elec tools
	21 <- ch .glassestype! // Green Goggles
;
: .ct4 args( ch ) // Garage
	1 <- ch .female!
	25 <- ch .hattype!
	6 <- ch .legstype!
	3 <- ch .torsotype!
	0 <- ch .glassestype!
;
: .ct5 args( ch ) 
	7 <- ch .hattype! // Cowboy hat
;
: .ct6 args( ch ) 
	<- ch .speedbotify
;
: .ct7 args( ch ) 
	<- ch .punchbotify
;

: camptradercommon
	still-npc regiondef{ ' trader-event lt .events! 1 lt .timer! }
	
;

: schooltrader
	camptradercommon .ct1 cyoa{ ' schooltrader cyoa! }
;

: robopunch
	camptradercommon .ct7 cyoa{ ' robopunch cyoa! }
;

: robospeed
	camptradercommon .ct6 cyoa{ ' robospeed cyoa! }
;

: roboprof
	// camptradercommon .ct8 cyoa{ ' roboprof cyoa! }
;

: tshopgas
	camptradercommon .ct2 cyoa{ ' tshopgas cyoa! }
;

: tshopelec
	camptradercommon .ct3 cyoa{ ' tshopelec cyoa! }
;

: tgarage
	camptradercommon .ct4 cyoa{ ' tgarage cyoa! }
;

: tcarsales
	camptradercommon .ct5 cyoa{ ' tcarsales cyoa! }
;

		0 stack
			' camptradercommon shove
			' camptradercommon shove
			' camptradercommon shove
			' camptradercommon shove
		value preptrader-base
		preptrader-base shuffles var preptrader-picker

: preppertrader
	preptrader-picker depths 0= if preptrader-base shuffles to preptrader-picker then pops nip execute ;


}

locdef{

location: toolshop
		max-rooms: 1
		entry-tags: toolshop
		script: set-theme-restaurant
		pre-script: set-theme-restaurant
		post-script: nad 0 %zombs
				
		room-choice: toolshop
			room-weight: 10
			room-count: 1
			room-post-script: 
			filter-only
			
location: toolschool
		max-rooms: 1
		entry-tags: toolschool
		script: set-theme-bookstore
		pre-script: set-theme-bookstore
		post-script: nad 0 %zombs
				
		room-choice: toolschool
			room-weight: 10
			room-count: 1
			room-post-script: 
			filter-only
		
location: roboshack
		max-rooms: 1
		entry-tags: roboshack
		script: factory-theme
		pre-script: factory-theme
		post-script: nad 0 %zombs
				
		room-choice: roboshack
			room-weight: 10
			room-count: 1
			room-post-script:
			filter-only
			
}
		
root{

: prepper-magchance
	20 %chance if
		magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! drift
	then
;

: sale-car 
	1 22 rnd spawncar mission{ cyoa{ ' cyoa-sale-car cyoa! } }
	0 lt .action_icon!
;

}
		
map{ regiondef{

trade-camp-reset

0 value gbx
0 value gby
0 value gbx2
0 value gby2
0 value gbx3
0 value gby3
0 value bfx
0 value bfy

: bldg tanchor!
	"roboshack" $picklocid addloc
	"ROBO SHACK" sign-text! prefab{ 1 5 1 shop } 
;
: bldg2 tanchor!
	"toolshop" $picklocid addloc
	"WERKZEUG SHOP" sign-text! prefab{ 2 5 1 shop } 
;
: bldg3 tanchor!
	"werkzeugschule" $picklocid addloc
	"WERKZEUGSCHULE" sign-text! prefab{ 3 6 1 shop } 
;


' plotgrass >defer ft

: pt
	xyp.xy ft // Plot the current floor by default
	
	xyp.i char { = if xyp.xy to gby2 to gbx2 then;
	xyp.i char } = if gbx2 1- gby2 xyp.xy swap 1+ swap gravellot then;
	xyp.i char ( = if xyp.xy to gby to gbx then;
	xyp.i char ) = if gbx 1- gby xyp.xy swap 1+ swap gravellot then;
	xyp.i char [ = if xyp.xy to gby3 to gbx3 then;
	xyp.i char ] = if gbx3 1- gby3 xyp.xy swap 1+ swap gravellot then;
	
	xyp.i char 1 = if xyp.x to bfx then;
	xyp.i char 2 = if bfx xyp.y xyp.x barbfence then;

	xyp.i char t = if sale-car lt-here then;
	xyp.i char U = if tnomey-spawn then;
	xyp.i char W = if trader-weapon-buyer then;
	xyp.i char G = if trader-food-for-gas then;
	xyp.i char S = if tgarage lt-here then;
	xyp.i char M = if tcarsales lt-here then;
		
	xyp.i char @ = if xyp.xy bldg then;
	xyp.i char 3 = if xyp.xy bldg2 then;
	xyp.i char 4 = if xyp.xy bldg3 then;
	
	xyp.i char p = if ' plotpave to ft then;
	xyp.i char $ = if xyp.xy grassedge then;
;

"
...........................
1.........................2
...........................
..................[....t...
...........................
...........................
...........................
.......L..........ML...t...
..{........................
.@....3....4...............
...........................
............(...}......t...
..................S........
...........................
..........................]
...........................
1..........2....1.........2
......G..U...........W....p
$$$$$$$$$$$$....$$$$$$$$$$$
...............)...........
...........................
...........................
...........................
...........................
...........................
...........................
..........................."
constant maplayout

: specialcamp-6
	' plotgrass to ft
	0 -1 maplayout xyplot: pt
;


: site-setup

	// Clear the map with grass
	-1 -1 w h grassbox

	pavement
// Plot trader stuff
	specialcamp-6

	-1 -1 w 3 conclot // Top
	-1 h 2 - w h conclot // Bottom

	0 h 6 - w ' plotroadlines hline
	

	1 no_time!
	7 14 rnd o'clock
	// 50 %chance if 1 2 frnd weather! -1 -2 frnd wind_force! else 1 2 frnd wind_force! then
	
	mission{ no-edge-spawns }
	
	20 %mv-up
	25 %chars-up
;

	 region: specialcamp-6
		w: maplayout layoutw
		h: maplayout layouth

		trade-mode

		post-script: site-setup
} }

uses paul/region-layout.df
uses paul/region-road-layout.df
// uses events/industrial/indust.df
// "../deathforth/rooms/TEMPROOM.txt" $load-rooms
	
" 
:map 2 
:tags roboshack  
:scriptA roboprof
:scriptB robopunch 
:scriptC robospeed 
:scriptD indwalls 
:scriptE indbluepanel 
:scriptF indgreenpanel 
:scriptG 3indwalls 
:size 11 12 
:off 1 1 
:tiles 
00222222200
00233333200
00211D11200
2221E1F1222
23311A11332
21111111112
21B11111C12
21111111112
22221112222
33321112333
00022222000
00033333000

:map 3 
:tags toolshop  
:scriptA tshopgas 
:scriptB tshopelec 
:scriptC cashcounter flipside 
:scriptD cashcounter 
:scriptE indshelf -1 movey 
:scriptF industsmall 
:size 15 13 
:off 6 0 
:tiles 
222222202222222
233333202333332
21E1E12021E1E12
2F111F202F111F2
222122222221222
332133333331233
00211C111D11200
002A1111111B200
002111111111200
002111111111200
002222111222200
003332222233300
000003333300000

:map 4 
:tags toolschool  
:scriptA schooltrader 
:scriptB industsmall 
:scriptC carwreck 
:scriptD indshelf -1 movey 
:scriptE 3indwalls 
:size 13 11 
:off 6 1 
:tiles 
2222222222222
2333333333332
2B1D1D111E112
2B1111111C112
211111A111112
2111111111BB2
2B111111111B2
2BB111111B112
2222211122222
3333222223333
0000333330000
"
value CAMP
CAMP $parse-rooms

cyoa{

: cyoa-sale-car
	carkey 0 > if cyoa-car then;

	cyoa-start
	"Carro a Venda" cyoa-title!
		"'Este carro parece que esta sendo um completo pronto para funcionar e ser vendido.'		Voce poderia comprar dele o vizinho revendedor sujo!" cyoa-text!
		choice( "Volte mais tarde" )choice
		

;

cyoa-choice: tcarsales-go
	($ Thegroup .. " compra um carro novo!	" .. "So pegue qualquer carro da loja que desejar!" pbold $) cyoa-text! goodfx
		1 to carkey clear-cyoa -5 trunk-food+ 50 trunk-gas+ someicon
;

cyoa: tcarsales
	"Todos os Carros Precisam Sair!" cyoa-title!
	($ "'Tenho que tirar esses carros da loja! Voce pode comprar qualquer carro, agora, por apenas 5 alimentos! Faca a compra agora! Nenhum financiamento! Pronto para funcionar e ir!	Eu te dar 50 litros de gasolina gratis!" pbold $) cyoa-text!
	trunk-food 4 > if "'E uma boa oferta." +crtext then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " 'nao pode permitir-se a compra vital de um carro novo." $) +crtext
	else
		choice( "Sai agora" )choice back-esc
		trunk-food 4 > if
			choice( "Compre um novo carro - 5 alimentos" )choice: tcarsales-go
		then
	then
;

cyoa-choice: tgarage-1
	($ Thegroup .. " paga por reparos de chassis e motor completos!	" .. "'Quando sair deste lugar, basta dirigir seu carro um pouco pela estrada e ele sera reparado!" pbold $) cyoa-text! goodfx
		clear-cyoa -5 trunk-food+ image-car-garage road{ ' mandible-repairgarage stack-road-action }
;

cyoa-choice: tgarage-2
	($ Thegroup .. " paga por uma variedade de modificacoes avancadas de carro, alem de reparos de chassis e motor completos!	" .. "Quando sair deste lugar, basta dirigir seu carro um pouco pela estrada ate o garagem. Seu carro recebera TRABALHO DE ALTA QUALIDADE!" pbold $) cyoa-text! goodfx
		clear-cyoa -35 trunk-food+ image-car-garage road{ ' mandible-megagarage stack-road-action }
;

cyoa: tgarage
	"Garagem de Expertos" cyoa-title!
	($ "Temos uma garagem cheia de mecanicos especialistas logo abaixo da estrada, e oferecemos " pbold .. "AS OBRAS" fbold .. ". Mods completos para carros: blindagem, aumento de velocidade, chassi reforcado, revisao do motor com maior eficiencia de combustivel. Tudo por 35 alimentos.	Tambem podemos consertar completamente seu carro sem mods doces, por 5 alimentos." pbold $) cyoa-text!
	trunk-food 34 > if "O carro perfeito e possivel." +crtext else
		trunk-food 4 > if "Voce ainda pode comprar reparos completos." +crtext then
	then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " ' nao pode pagar por esta extensa porem elegante modificacao de carro." $) +crtext
	else
		choice( "Sai agora" )choice back-esc
		trunk-food 34 > if
			choice( "Modificacoes completas para carros, " .. "AS OBRAS" fbold .. "  - 35 alimentos" )choice: tgarage-2
		then
		trunk-food 4 > if
			choice( "Reparos completos - 5 alimentos" )choice: tgarage-1
		then
	then
;

0 value mt1-1
0 value mt1-2
0 value mt1-3
0 value mt1-4

cyoa-choice: trade-m1-4
	($ Thegroup .. "  compra um martelo pneumatico a gasolina!	" .. "'A escolha do conhecedor para esculpir hordas de zumbis!" pbold $) cyoa-text! goodfx
	1 WEAPON_JACKHAMMER trunk.weapon+ -20 trunk-food+ someicon ' mt1-4 ++
;

cyoa-choice: trade-m1-3
	($ Thegroup .. "  compra uma motosserra a gasolina! Um classico!	" .. "'Todo mundo sabe que esta e a resposta para os zumbis! Contanto que voce nao fique sem gasolina." pbold $) cyoa-text! goodfx
	1 WEAPON_CHAINSAW trunk.weapon+ -10 trunk-food+ someicon ' mt1-3 ++
;

cyoa-choice: trade-m1-2
	($ Thegroup .. "  compra um soprador de folhas a gasolina!" .. "Derrube os zumbis! Como folhas!" pbold $) cyoa-text! goodfx
	1 WEAPON_BLOWER trunk.weapon+ -5 trunk-food+ someicon ' mt1-2 ++
;

cyoa-choice: trade-m1-1
	($ Thegroup .. " compra uma motosserra a gasolina!	" .. "Excelente para cortar as multidoes de zumbis! Decente para ervas." pbold $) cyoa-text! goodfx
	1 WEAPON_WHACKER trunk.weapon+ -5 trunk-food+ someicon ' mt1-1 ++
;

cyoa: tshopgas
	"Ferramentas a Gasolina" cyoa-title!
	($ "'Nao tenha palavras com o outro homem! Ha algo sobre armas a gas, que voce simplesmente nao pode obter de eletricas." pbold $) cyoa-text!
	trunk-food 4 > if "E hora de acender." +crtext then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " esta figurativamente sem gas. O vendedor te da algumas litros de gas literais como um favor." $) +crtext clear-cyoa +30 trunk-gas+ goodfx
	else
		choice( "Sai agora" )choice back-esc
		trunk-food 19 > mt1-4 1 < and if
			choice( "Jackhammer - 20 comida" )choice: trade-m1-4
		then
		trunk-food 9 > mt1-3 3 < and if
			choice( "Motosserra - 10 comida" )choice: trade-m1-3
		then
		trunk-food 4 > mt1-2 3 < and if
			choice( "Folheada - 5 comida" )choice: trade-m1-2
		then
		trunk-food 4 > mt1-1 3 < and if
			choice( "Motosserra de Folhas - 5 comida" )choice: trade-m1-1
		then
	then
;

0 value mt2-1
0 value mt2-2
0 value mt2-3
0 value mt2-4

: mt2-firstbuy 
	mt2-4 1 < mt2-3 1 < mt2-2 1 < mt2-1 1 < and and and
;

: mt2-firstbuy-done
	($ "Aproveite o seu " pbold .. "DOBRA A COBRANCA!" fbold $) +crtext
;

cyoa-choice: trade-m2-4
	($ Thegroup .. "  comprou um martelo eletrico!" .. "'E tao ecologicamente correto!" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 255 else 130 then WEAPON_EJHAMMER trunk.weapon+ -20 trunk-food+ someicon ' mt2-4 ++ ;

cyoa-choice: trade-m2-3
	($ Thegroup .. "  compra uma motosserra eletrica! Voce se sente desconfortavelmente presuncoso sobre salvar o meio ambiente no pos-apocalipse.	" .. "Excelente para compostar zumbis!" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 160 else 80 then WEAPON_ECHAINSAW trunk.weapon+ -10 trunk-food+ someicon ' mt2-3 ++ ;
	
cyoa-choice: trade-m2-2
	($ Thegroup .. "  compra um soprador de folhas eletrico!	" .. "Pastoreie suavemente os zumbis, uma alternativa pacifista a todo esse respingo!" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 100 else 50 then WEAPON_EBLOWER trunk.weapon+ -5 trunk-food+ picon ' mt2-2 ++ ;

cyoa-choice: trade-m2-1
	($ Thegroup .. "  compra um cortador de ervas daninhas eletrico!	" .. "Tem varios usos eficazes nos metodos de cultivo da Zombocultura." pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 100 else 50 then WEAPON_EWHACKER trunk.weapon+ -5 trunk-food+ picon ' mt2-1 ++ ;

cyoa: tshopelec
	"Ferramentas eletricas" cyoa-title!
	($ "'Nao fale com o outro cara! As ferramentas eletricas sao ecologicas, permitindo que voce reduza as emissoes de gas ao derrubar zumbis." pbold $) cyoa-text!
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. "  carece de suficiente comida para comprar nada!" $) +crtext
		($ "O comerciante tem pena de " .. thegroup .. "  e distribui um " .. "DESCONTO NO SOPRADOR DE FOLHAS!" rbold $) +crtext goodfx 30 WEAPON_EBLOWER trunk.weapon+ clear-cyoa
	else
		mt2-firstbuy if
			($ "Como uma oferta especial, o comerciante " .. "DUPLICA A CARGA" rainbold .. " 'Sua primeira compra. Ela tera o dobro de energia do que voce normalmente encontraria!'" $) +crtext
		then
		choice( "Sai agora" )choice back-esc
		trunk-food 19 > mt2-4 1 < and if
			choice( "20 energy" )choice: trade-m2-4
		then
		trunk-food 9 > mt2-3 2 < and if
			choice( "1 wood" )choice: trade-m2-3
		then
		trunk-food 4 > mt2-2 2 < and if
			choice( "1 energy" )choice: trade-m2-2
		then
		trunk-food 4 > mt2-1 2 < and if
			choice( "1 energy" )choice: trade-m2-1
		then
	then
;

0 value mechtrained+

: mech-train-amt numhums 2 * mechtrained+ 1 + * 1 + ; 
: mech-train-pay? trunk-food mech-train-amt 1 - > notsolo notsolohuman and and ;
: mech-train-pay mech-train-amt -1 * trunk-food+ ;

: mech-solo-amt mechtrained+ 2 * 3 + ;
: mech-solo-pay? trunk-food mech-solo-amt 1 - > ;
: mech-solo-pay mech-solo-amt -1 * trunk-food+ ;

cyoa-choice: mech-school-train
	cchar .mechanical@ 6 < if
		($ cname .. " 'observa atentamente enquanto o Professor realmente bate no carro com uma chave inglesa. Entao E ISSO que e engenharia!'" $) goodfx
	else
		($ cname .. " 1" $) badfx
	then cyoa-text! 
	mech-solo-pay mechanicREV mechanic++ ' mechtrained+ ++
;

cyoa-choice: mech-school-group
	($ "'O grupo inteiro recebe treinamento mecanico! Todos assistem o Professor bater no carro com uma chave inglesa. E muito esclarecedor.'" $) cyoa-text! goodfx
	mech-train-pay someicon
	regiondef{ ' trade-mechanic-group! foreach-human-char } numhums ' mechtrained+ +!
;

cyoa: mech-school-give
	solohuman if
		choosehuman mech-school-train
	then;
	"Quem deveria obter esse vasto conhecimento mecanico?" cyoa-text!
	' mech-school-train cyoa-human-choice
;

cyoa: schooltrader
	($ "A Escola da Ferramenta" $) cyoa-title!
	"Mecanica e uma ciencia verdadeira. Diferente da medicina, que e mais uma arte. Similar ao Origami, ou talvez danca interpretativa." pbold text!
	"O Professor de Mecanica bate repetidamente no chassi do carro destruido com uma chave de boca enquanto diz isso, nunca quebrando contato visual." +crtext foodamt
		mech-solo-pay? not if
			"'Voce nao pode permitirse mais treinamento." +crtext 
		then;
		choice( "Sai agora" )choice back-esc
		mech-solo-pay? if
			choice( "De " .. mech-solo-amt .. "  comida para treinamento de solteiro" )choice: mech-school-give
		then
		mech-train-pay? if
			choice( "De " .. mech-train-amt .. " alimento para treinamento em grupo" )choice: mech-school-group
		then
		
;

cyoa-bridge: robopunch-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	10 recruit-yes-food 
	($ "Um robo que distribui surras? A tecnologia e muito legal." $) +crtext
	"#CARREGANDO PROTOCOLOS DE SOCOS#	#PRONTO PARA SOCOS#" pbold +crtext
	recruitee .strengthREV recruitee .fitnessREV
;

cyoa: robopunch
	"O Perfeito Soco Mecanico" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"#EU SOU O ROBO SOQUE#" pbold cyoa-text! basic-tone
	"'Agora estamos chegando a algum lugar! Este robo foi projetado para um proposito: Socos." +crtext
	trunk-food 9 > if
		choice( "Deixe isso para agora" )choice
		choice( "Compre PUNCHBOT - 10 alimentos" )choice: robopunch-go
	else
		"Voce precisa de 10 alimentos para comprar este robo de soco." +crtext
		choice( "Nao posso poupar a comida" )choice
	then
;

cyoa-bridge: robospeed-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	15 recruit-yes-food 
	($ "O robo faz um donut muito rapido. Tao rapido." $) +crtext
	"#CARREGANDO PROTOCOLOS DE VELOCIDADE#	#TENHO QUE IR RAPIDO#" pbold +crtext
	recruitee .strengthREV recruitee .fitnessREV
;

cyoa: robospeed
	"Tao veloz" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"#EU SOU O ROBO DE VELOCIDADE#" pbold cyoa-text! basic-tone
	"'Com suas linhas elegantes e minusculas calotas giratorias, mesmo parado, este robo exala o conceito de 'velocidade'.	Alem disso, ele acabou de dizer a palavra 'velocidade'." +crtext
	trunk-food 14 > if
		choice( "Deixe isso para agora" )choice
		choice( "Compre SPEEDBOT - 15 alimentos" )choice: robospeed-go
	else
		"Voce precisa de 15 alimentos para comprar este robo de velocidade." +crtext
		choice( "Nao posso poupar a comida" )choice
	then
;

}

roomgen{


: .ct1 args( ch ) // Mech Teacher
	WEAPON_WRENCH <- ch .weapona!
;
: .ct2 args( ch ) // Gas tools
	26 <- ch .glassestype! // Gas Mask
;
: .ct3 args( ch ) // Elec tools
	21 <- ch .glassestype! // Green Goggles
;
: .ct4 args( ch ) // Garage
	1 <- ch .female!
	25 <- ch .hattype!
	6 <- ch .legstype!
	3 <- ch .torsotype!
	0 <- ch .glassestype!
;
: .ct5 args( ch ) 
	7 <- ch .hattype! // Cowboy hat
;
: .ct6 args( ch ) 
	<- ch .speedbotify
;
: .ct7 args( ch ) 
	<- ch .punchbotify
;

: camptradercommon
	still-npc regiondef{ ' trader-event lt .events! 1 lt .timer! }
	
;

: schooltrader
	camptradercommon .ct1 cyoa{ ' schooltrader cyoa! }
;

: robopunch
	camptradercommon .ct7 cyoa{ ' robopunch cyoa! }
;

: robospeed
	camptradercommon .ct6 cyoa{ ' robospeed cyoa! }
;

: roboprof
	// camptradercommon .ct8 cyoa{ ' roboprof cyoa! }
;

: tshopgas
	camptradercommon .ct2 cyoa{ ' tshopgas cyoa! }
;

: tshopelec
	camptradercommon .ct3 cyoa{ ' tshopelec cyoa! }
;

: tgarage
	camptradercommon .ct4 cyoa{ ' tgarage cyoa! }
;

: tcarsales
	camptradercommon .ct5 cyoa{ ' tcarsales cyoa! }
;

		0 stack
			' camptradercommon shove
			' camptradercommon shove
			' camptradercommon shove
			' camptradercommon shove
		value preptrader-base
		preptrader-base shuffles var preptrader-picker

: preppertrader
	preptrader-picker depths 0= if preptrader-base shuffles to preptrader-picker then pops nip execute ;


}

locdef{

location: toolshop
		max-rooms: 1
		entry-tags: toolshop
		script: set-theme-restaurant
		pre-script: set-theme-restaurant
		post-script: nad 0 %zombs
				
		room-choice: toolshop
			room-weight: 10
			room-count: 1
			room-post-script: 
			filter-only
			
location: toolschool
		max-rooms: 1
		entry-tags: toolschool
		script: set-theme-bookstore
		pre-script: set-theme-bookstore
		post-script: nad 0 %zombs
				
		room-choice: toolschool
			room-weight: 10
			room-count: 1
			room-post-script: 
			filter-only
		
location: roboshack
		max-rooms: 1
		entry-tags: roboshack
		script: factory-theme
		pre-script: factory-theme
		post-script: nad 0 %zombs
				
		room-choice: roboshack
			room-weight: 10
			room-count: 1
			room-post-script:
			filter-only
			
}
		
root{

: prepper-magchance
	20 %chance if
		magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! drift
	then
;

: sale-car 
	1 22 rnd spawncar mission{ cyoa{ ' cyoa-sale-car cyoa! } }
	0 lt .action_icon!
;

}
		
map{ regiondef{

trade-camp-reset

0 value gbx
0 value gby
0 value gbx2
0 value gby2
0 value gbx3
0 value gby3
0 value bfx
0 value bfy

: bldg tanchor!
	"roboshack" $picklocid addloc
	"ROBO SHACK" sign-text! prefab{ 1 5 1 shop } 
;
: bldg2 tanchor!
	"toolshop" $picklocid addloc
	"LOJA DE FERRAMENTAS" sign-text! prefab{ 2 5 1 shop } 
;
: bldg3 tanchor!
	"escola de ferramentas" $picklocid addloc
	"ESCOLA DE FERRAMENTAS" sign-text! prefab{ 3 6 1 shop } 
;


' plotgrass >defer ft

: pt
	xyp.xy ft // Plot the current floor by default
	
	xyp.i char { = if xyp.xy to gby2 to gbx2 then;
	xyp.i char } = if gbx2 1- gby2 xyp.xy swap 1+ swap gravellot then;
	xyp.i char ( = if xyp.xy to gby to gbx then;
	xyp.i char ) = if gbx 1- gby xyp.xy swap 1+ swap gravellot then;
	xyp.i char [ = if xyp.xy to gby3 to gbx3 then;
	xyp.i char ] = if gbx3 1- gby3 xyp.xy swap 1+ swap gravellot then;
	
	xyp.i char 1 = if xyp.x to bfx then;
	xyp.i char 2 = if bfx xyp.y xyp.x barbfence then;

	xyp.i char t = if sale-car lt-here then;
	xyp.i char U = if tnomey-spawn then;
	xyp.i char W = if trader-weapon-buyer then;
	xyp.i char G = if trader-food-for-gas then;
	xyp.i char S = if tgarage lt-here then;
	xyp.i char M = if tcarsales lt-here then;
		
	xyp.i char @ = if xyp.xy bldg then;
	xyp.i char 3 = if xyp.xy bldg2 then;
	xyp.i char 4 = if xyp.xy bldg3 then;
	
	xyp.i char p = if ' plotpave to ft then;
	xyp.i char $ = if xyp.xy grassedge then;
;

"
...........................
1.........................2
...........................
..................[....t...
...........................
...........................
...........................
.......L..........ML...t...
..{........................
.@....3....4...............
...........................
............(...}......t...
..................S........
...........................
..........................]
...........................
1..........2....1.........2
......G..U...........W....p
$$$$$$$$$$$$....$$$$$$$$$$$
...............)...........
...........................
...........................
...........................
...........................
...........................
...........................
..........................."
constant maplayout

: specialcamp-6
	' plotgrass to ft
	0 -1 maplayout xyplot: pt
;


: site-setup

	// Clear the map with grass
	-1 -1 w h grassbox

	pavement
// Plot trader stuff
	specialcamp-6

	-1 -1 w 3 conclot // Top
	-1 h 2 - w h conclot // Bottom

	0 h 6 - w ' plotroadlines hline
	

	1 no_time!
	7 14 rnd o'clock
	// 50 %chance if 1 2 frnd weather! -1 -2 frnd wind_force! else 1 2 frnd wind_force! then
	
	mission{ no-edge-spawns }
	
	20 %mv-up
	25 %chars-up
;

	 region: specialcamp-6
		w: maplayout layoutw
		h: maplayout layouth

		trade-mode

		post-script: site-setup
} }

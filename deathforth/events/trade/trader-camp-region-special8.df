uses paul/region-layout.df
uses paul/region-road-layout.df
// uses events/industrial/indust.df
// "../deathforth/rooms/TEMPROOM.txt" $load-rooms
	
" 
:map 2 
:tags roboshack  
:scriptA roboprof
:scriptB robopunch 
:scriptC robospeed 
:scriptD indwalls 
:scriptE indbluepanel 
:scriptF indgreenpanel 
:scriptG 3indwalls 
:size 11 12 
:off 1 1 
:tiles 
00222222200
00233333200
00211D11200
2221E1F1222
23311A11332
21111111112
21B11111C12
21111111112
22221112222
33321112333
00022222000
00033333000

:map 3 
:tags toolshop  
:scriptA tshopgas 
:scriptB tshopelec 
:scriptC cashcounter flipside 
:scriptD cashcounter 
:scriptE indshelf -1 movey 
:scriptF industsmall 
:size 15 13 
:off 6 0 
:tiles 
222222202222222
233333202333332
21E1E12021E1E12
2F111F202F111F2
222122222221222
332133333331233
00211C111D11200
002A1111111B200
002111111111200
002111111111200
002222111222200
003332222233300
000003333300000

:map 4 
:tags toolschool  
:scriptA schooltrader 
:scriptB industsmall 
:scriptC carwreck 
:scriptD indshelf -1 movey 
:scriptE 3indwalls 
:size 13 11 
:off 6 1 
:tiles 
2222222222222
2333333333332
2B1D1D111E112
2B1111111C112
211111A111112
2111111111BB2
2B111111111B2
2BB111111B112
2222211122222
3333222223333
0000333330000
"
value CAMP
CAMP $parse-rooms

cyoa{

: cyoa-sale-car
	carkey 0 > if cyoa-car then;

	cyoa-start
	"Auto en venta" cyoa-title!
		"'Este coche parece que este listo para la venta inmediata y en perfectas condiciones.'		Puedes comprarlo del vendedor sucioso de cerca!" cyoa-text!
		choice( "Vuelve mas tarde" )choice
		

;

cyoa-choice: tcarsales-go
	($ Thegroup .. " compra un coche nuevo!" .. "Solo tome cualquier coche que quiera de la concesion" pbold $) cyoa-text! goodfx
		1 to carkey clear-cyoa -5 trunk-food+ 50 trunk-gas+ someicon
;

cyoa: tcarsales
	"Todos los coches deben irse!" cyoa-title!
	($ "'Tengo que deshacerme de estos coches! Puedes comprar cualquier coche, ahora mismo, por la baja, baja precio de 5 alimentos! Andate rapido! Sin financiacion! Listo para funcionar y listo para ir!	Tambien te dare 50 litros de gasolina gratis!" pbold $) cyoa-text!
	trunk-food 4 > if "'Eso es una buena oferta." +crtext then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " 'no puede permitirse la compra vital de un coche nuevo." $) +crtext
	else
		choice( "Dejalo por ahora" )choice back-esc
		trunk-food 4 > if
			choice( "Compra un coche nuevo   5 alimentos" )choice: tcarsales-go
		then
	then
;

cyoa-choice: tgarage-1
	($ Thegroup .. " paga por reparaciones de chasis y motor completas!" .. "'Cuando salgas de aqui, solo conduce tu coche un poco por la carretera y vamos a repararlo alli" pbold $) cyoa-text! goodfx
		clear-cyoa -5 trunk-food+ image-car-garage road{ ' mandible-repairgarage stack-road-action }
;

cyoa-choice: tgarage-2
	($ Thegroup .. " paga por una variedad de modificaciones avanzadas de automovil, ademas de reparaciones de chasis y motor completas!" .. "Cuando salgas de aqui, solo conduce un poco por la carretera hasta el taller. Tu coche recibira TRABAJOS DE CARA!" pbold $) cyoa-text! goodfx
		clear-cyoa -35 trunk-food+ image-car-garage road{ ' mandible-megagarage stack-road-action }
;

cyoa: tgarage
	"Garaje Experto" cyoa-title!
	($ "Tenemos un garaje lleno de mecanicos expertos justo en la calle de al lado, y ofrecemos " pbold .. "LAS OBRAS" fbold .. "5 food" pbold $) cyoa-text!
	trunk-food 34 > if "El auto perfecto es posible." +crtext else
		trunk-food 4 > if "Aun podrias comprar reparaciones completas." +crtext then
	then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " ' no puedo permitirme esta extensa pero elegante modificacion de autos." $) +crtext
	else
		choice( "Dejalo por ahora" )choice back-esc
		trunk-food 34 > if
			choice( "Modificaciones de auto completas, " .. "LAS OBRAS" fbold .. "    35 comida" )choice: tgarage-2
		then
		trunk-food 4 > if
			choice( "Reparaciones completas   5 comida" )choice: tgarage-1
		then
	then
;

0 value mt1-1
0 value mt1-2
0 value mt1-3
0 value mt1-4

cyoa-choice: trade-m1-4
	($ Thegroup .. "  compra un martillo neumatico a gas!	" .. "'La eleccion del conocedor para cincelar a traves de hordas de zombis!" pbold $) cyoa-text! goodfx
	1 WEAPON_JACKHAMMER trunk.weapon+ -20 trunk-food+ someicon ' mt1-4 ++
;

cyoa-choice: trade-m1-3
	($ Thegroup .. "  compra una motosierra a gas! Un clasico!	" .. "'Todos saben que esta es LA respuesta para los zombis! Siempre y cuando no te quedes sin gasolina." pbold $) cyoa-text! goodfx
	1 WEAPON_CHAINSAW trunk.weapon+ -10 trunk-food+ someicon ' mt1-3 ++
;

cyoa-choice: trade-m1-2
	($ Thegroup .. "  compra un soplador de hojas a gas!	" .. "Vuela a los zombis! Como hojas!" pbold $) cyoa-text! goodfx
	1 WEAPON_BLOWER trunk.weapon+ -5 trunk-food+ someicon ' mt1-2 ++
;

cyoa-choice: trade-m1-1
	($ Thegroup .. "  compra una desbrozadora a gas!	" .. "Excelente para recortar las hordas de zombis! Algo decente para las malas hierbas." pbold $) cyoa-text! goodfx
	1 WEAPON_WHACKER trunk.weapon+ -5 trunk-food+ someicon ' mt1-1 ++
;

cyoa: tshopgas
	"Herramientas de gas" cyoa-title!
	($ "'No hables con el otro tipo! Hay algo acerca de las armas de gas, que simplemente no se pueden obtener de las electricas." pbold $) cyoa-text!
	trunk-food 4 > if "Hora de acelerar." +crtext then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. "  esta figurativamente sin gasolina. El comerciante se compadece de ti y te da algo de gasolina literal." $) +crtext clear-cyoa +30 trunk-gas+ goodfx
	else
		choice( "Dejalo por ahora" )choice back-esc
		trunk-food 19 > mt1-4 1 < and if
			choice( "Martillo neumatico   20 alimentos" )choice: trade-m1-4
		then
		trunk-food 9 > mt1-3 3 < and if
			choice( "Motosierra   10 alimentos" )choice: trade-m1-3
		then
		trunk-food 4 > mt1-2 3 < and if
			choice( "Soplador de hojas   5 alimentos" )choice: trade-m1-2
		then
		trunk-food 4 > mt1-1 3 < and if
			choice( "Cortadora de malezas   5 alimentos" )choice: trade-m1-1
		then
	then
;

0 value mt2-1
0 value mt2-2
0 value mt2-3
0 value mt2-4

: mt2-firstbuy 
	mt2-4 1 < mt2-3 1 < mt2-2 1 < mt2-1 1 < and and and
;

: mt2-firstbuy-done
	($ "Disfruta tu " pbold .. "DOBLE CARGA!" fbold $) +crtext
;

cyoa-choice: trade-m2-4
	($ Thegroup .. " compra un martillo neumatico electrico!	" .. "'Es tan ecologico!" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 255 else 130 then WEAPON_EJHAMMER trunk.weapon+ -20 trunk-food+ someicon ' mt2-4 ++ ;

cyoa-choice: trade-m2-3
	($ Thegroup .. " compra una motosierra electrica! Te sientes incomodamente engreido por salvar el medio ambiente en el post apocalipsis.	" .. "Excelente para compostar zombis!" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 160 else 80 then WEAPON_ECHAINSAW trunk.weapon+ -10 trunk-food+ someicon ' mt2-3 ++ ;
	
cyoa-choice: trade-m2-2
	($ Thegroup .. " compra una sopladora de hojas electrica!" .. "Guia suavemente a los zombies, una alternativa pacifista a todo este chapoteo!" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 100 else 50 then WEAPON_EBLOWER trunk.weapon+ -5 trunk-food+ picon ' mt2-2 ++ ;

cyoa-choice: trade-m2-1
	($ Thegroup .. " 	compra una recortadora de cesped electrica!" .. "Tiene multiples usos efectivos en los metodos de cultivo de Zombocultura." pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 100 else 50 then WEAPON_EWHACKER trunk.weapon+ -5 trunk-food+ picon ' mt2-1 ++ ;

cyoa: tshopelec
	"Herramientas electricas" cyoa-title!
	($ "'No hables con el otro tipo! Las herramientas electricas son ecologicas, lo que te permite reducir las emisiones de gas mientras cortas zombis." pbold $) cyoa-text!
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. "  no tiene suficiente comida para comprar nada!" $) +crtext
		($ "El comerciante se compadece de " .. thegroup .. "  y reparte un " .. "SOPLADOR DE HOJAS CON DESCUENTO!" rbold $) +crtext goodfx 30 WEAPON_EBLOWER trunk.weapon+ clear-cyoa
	else
		mt2-firstbuy if
			($ "Como oferta especial, el comerciante " .. "CARGARA EL DOBLE" rainbold .. "  de tu primera compra. Tendra el doble de energia que la que normalmente encontrarias!" $) +crtext
		then
		choice( "Dejalo por ahora" )choice back-esc
		trunk-food 19 > mt2-4 1 < and if
			choice( "Martillo neumatico electrico   20 alimentos" )choice: trade-m2-4
		then
		trunk-food 9 > mt2-3 2 < and if
			choice( "Motosierra electrica   10 alimentos" )choice: trade-m2-3
		then
		trunk-food 4 > mt2-2 2 < and if
			choice( "Soplador de hojas electrico   5 alimentos" )choice: trade-m2-2
		then
		trunk-food 4 > mt2-1 2 < and if
			choice( "Desbrozadora electrica   5 alimentos" )choice: trade-m2-1
		then
	then
;

0 value mechtrained+

: mech-train-amt numhums 2 * mechtrained+ 1 + * 1 + ; 
: mech-train-pay? trunk-food mech-train-amt 1 - > notsolo notsolohuman and and ;
: mech-train-pay mech-train-amt -1 * trunk-food+ ;

: mech-solo-amt mechtrained+ 2 * 3 + ;
: mech-solo-pay? trunk-food mech-solo-amt 1 - > ;
: mech-solo-pay mech-solo-amt -1 * trunk-food+ ;

cyoa-choice: mech-school-train
	cchar .mechanical@ 6 < if
		($ cname .. " ' observa atentamente como el Profesor realmente golpea el auto con una llave inglesa. Asi que DE ESO se trata la ingenieria!" $) goodfx
	else
		($ cname .. "  ya obtuvo su titulo completo de ingenieria de esta prestigiosa academia! Su ultimo pago se toma como una donacion para construir un nuevo ala para la escuela." $) badfx
	then cyoa-text! 
	mech-solo-pay mechanicREV mechanic++ ' mechtrained+ ++
;

cyoa-choice: mech-school-group
	($ "Todo el grupo recibe entrenamiento mecanico! Todos observan al Profesor golpear el coche con una llave inglesa. Es muy ilustrativo." $) cyoa-text! goodfx
	mech-train-pay someicon
	regiondef{ ' trade-mechanic-group! foreach-human-char } numhums ' mechtrained+ +!
;

cyoa: mech-school-give
	solohuman if
		choosehuman mech-school-train
	then;
	"Quien deberia obtener este vasto conocimiento mecanico?" cyoa-text!
	' mech-school-train cyoa-human-choice
;

cyoa: schooltrader
	($ "La Escuela de Herramientas" $) cyoa-title!
	"La ingenieria mecanica es una verdadera ciencia. A diferencia de la medicina, que es mas un arte. Similar al origami, o quizas a la danza interpretativa." pbold text!
	"El Profesor de Mecanica golpea repetidamente el chasis del coche destrozado con una llave inglesa mientras dice esto, sin romper nunca el contacto visual." +crtext foodamt
		mech-solo-pay? not if
			"'No puedes permitirte mas entrenamiento." +crtext 
		then;
		choice( "Dejalo por ahora" )choice back-esc
		mech-solo-pay? if
			choice( "Dar " .. mech-solo-amt .. "  comida para entrenamiento individual" )choice: mech-school-give
		then
		mech-train-pay? if
			choice( "Dar " .. mech-train-amt .. "  comida para la formacion de grupos" )choice: mech-school-group
		then
		
;

cyoa-bridge: robopunch-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	10 recruit-yes-food 
	($ "Un robot que reparte palizas? La tecnologia es genial." $) +crtext
	"#CARGANDO PROTOCOLOS DE PUNZON#	#LISTO PARA GOLPEAR#" pbold +crtext
	recruitee .strengthREV recruitee .fitnessREV
;

cyoa: robopunch
	"El punetazo mecanico perfecto" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"#SOY PUNCH BOT#" pbold cyoa-text! basic-tone
	"'Ahora estamos llegando a alguna parte! Este robot esta disenado para un proposito: golpear." +crtext
	trunk-food 9 > if
		choice( "Dejalo por ahora" )choice
		choice( "Comprar PUNCHBOT   10 alimentos" )choice: robopunch-go
	else
		"Necesitas 10 alimentos para comprar este robot de punetazos." +crtext
		choice( "'No puedo prescindir de la comida" )choice
	then
;

cyoa-bridge: robospeed-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	15 recruit-yes-food 
	($ "El robot hace una rosquilla muy rapido. Tan rapido." $) +crtext
	"#CARGANDO PROTOCOLOS DE VELOCIDAD#	#TENGO QUE IR RAPIDO#" pbold +crtext
	recruitee .strengthREV recruitee .fitnessREV
;

cyoa: robospeed
	"Tan veloz" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"#SOY SPEED BOT#" pbold cyoa-text! basic-tone
	"'Con sus elegantes lineas y sus diminutos tapacubos giratorios, incluso estando parado, este robot irradia el concepto de velocidad. Ademas, acaba de decir la palabra velocidad." +crtext
	trunk-food 14 > if
		choice( "Dejalo por ahora" )choice
		choice( "Comprar SPEEDBOT   15 alimentos" )choice: robospeed-go
	else
		"Necesitas 15 comida para comprar este bot de velocidad." +crtext
		choice( "'No puedo prescindir de la comida" )choice
	then
;

}

roomgen{


: .ct1 args( ch ) // Mech Teacher
	WEAPON_WRENCH <- ch .weapona!
;
: .ct2 args( ch ) // Gas tools
	26 <- ch .glassestype! // Gas Mask
;
: .ct3 args( ch ) // Elec tools
	21 <- ch .glassestype! // Green Goggles
;
: .ct4 args( ch ) // Garage
	1 <- ch .female!
	25 <- ch .hattype!
	6 <- ch .legstype!
	3 <- ch .torsotype!
	0 <- ch .glassestype!
;
: .ct5 args( ch ) 
	7 <- ch .hattype! // Cowboy hat
;
: .ct6 args( ch ) 
	<- ch .speedbotify
;
: .ct7 args( ch ) 
	<- ch .punchbotify
;

: camptradercommon
	still-npc regiondef{ ' trader-event lt .events! 1 lt .timer! }
	
;

: schooltrader
	camptradercommon .ct1 cyoa{ ' schooltrader cyoa! }
;

: robopunch
	camptradercommon .ct7 cyoa{ ' robopunch cyoa! }
;

: robospeed
	camptradercommon .ct6 cyoa{ ' robospeed cyoa! }
;

: roboprof
	// camptradercommon .ct8 cyoa{ ' roboprof cyoa! }
;

: tshopgas
	camptradercommon .ct2 cyoa{ ' tshopgas cyoa! }
;

: tshopelec
	camptradercommon .ct3 cyoa{ ' tshopelec cyoa! }
;

: tgarage
	camptradercommon .ct4 cyoa{ ' tgarage cyoa! }
;

: tcarsales
	camptradercommon .ct5 cyoa{ ' tcarsales cyoa! }
;

		0 stack
			' camptradercommon shove
			' camptradercommon shove
			' camptradercommon shove
			' camptradercommon shove
		value preptrader-base
		preptrader-base shuffles var preptrader-picker

: preppertrader
	preptrader-picker depths 0= if preptrader-base shuffles to preptrader-picker then pops nip execute ;


}

locdef{

location: toolshop
		max-rooms: 1
		entry-tags: toolshop
		script: set-theme-restaurant
		pre-script: set-theme-restaurant
		post-script: nad 0 %zombs
				
		room-choice: toolshop
			room-weight: 10
			room-count: 1
			room-post-script: 
			filter-only
			
location: toolschool
		max-rooms: 1
		entry-tags: toolschool
		script: set-theme-bookstore
		pre-script: set-theme-bookstore
		post-script: nad 0 %zombs
				
		room-choice: toolschool
			room-weight: 10
			room-count: 1
			room-post-script: 
			filter-only
		
location: roboshack
		max-rooms: 1
		entry-tags: roboshack
		script: factory-theme
		pre-script: factory-theme
		post-script: nad 0 %zombs
				
		room-choice: roboshack
			room-weight: 10
			room-count: 1
			room-post-script:
			filter-only
			
}
		
root{

: prepper-magchance
	20 %chance if
		magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! drift
	then
;

: sale-car 
	1 22 rnd spawncar mission{ cyoa{ ' cyoa-sale-car cyoa! } }
	0 lt .action_icon!
;

}
		
map{ regiondef{

trade-camp-reset

0 value gbx
0 value gby
0 value gbx2
0 value gby2
0 value gbx3
0 value gby3
0 value bfx
0 value bfy

: bldg tanchor!
	"rOBOSHACK" $picklocid addloc
	"ROBOSHACK" sign-text! prefab{ 1 5 1 shop } 
;
: bldg2 tanchor!
	"tIENDA DE HERRAMIENTAS" $picklocid addloc
	"TIENDA DE HERRAMIENTAS" sign-text! prefab{ 2 5 1 shop } 
;
: bldg3 tanchor!
	"eSCUELA DE HERRAMIENTAS" $picklocid addloc
	"ESCUELA DE HERRAMIENTAS" sign-text! prefab{ 3 6 1 shop } 
;


' plotgrass >defer ft

: pt
	xyp.xy ft // Plot the current floor by default
	
	xyp.i char { = if xyp.xy to gby2 to gbx2 then;
	xyp.i char } = if gbx2 1- gby2 xyp.xy swap 1+ swap gravellot then;
	xyp.i char ( = if xyp.xy to gby to gbx then;
	xyp.i char ) = if gbx 1- gby xyp.xy swap 1+ swap gravellot then;
	xyp.i char [ = if xyp.xy to gby3 to gbx3 then;
	xyp.i char ] = if gbx3 1- gby3 xyp.xy swap 1+ swap gravellot then;
	
	xyp.i char 1 = if xyp.x to bfx then;
	xyp.i char 2 = if bfx xyp.y xyp.x barbfence then;

	xyp.i char t = if sale-car lt-here then;
	xyp.i char U = if tnomey-spawn then;
	xyp.i char W = if trader-weapon-buyer then;
	xyp.i char G = if trader-food-for-gas then;
	xyp.i char S = if tgarage lt-here then;
	xyp.i char M = if tcarsales lt-here then;
		
	xyp.i char @ = if xyp.xy bldg then;
	xyp.i char 3 = if xyp.xy bldg2 then;
	xyp.i char 4 = if xyp.xy bldg3 then;
	
	xyp.i char p = if ' plotpave to ft then;
	xyp.i char $ = if xyp.xy grassedge then;
;

"
...........................
1.........................2
...........................
..................[....t...
...........................
...........................
...........................
.......L..........ML...t...
..{........................
.@....3....4...............
...........................
............(...}......t...
..................S........
...........................
..........................]
...........................
1..........2....1.........2
......G..U...........W....p
$$$$$$$$$$$$....$$$$$$$$$$$
...............)...........
...........................
...........................
...........................
...........................
...........................
...........................
..........................."
constant maplayout

: specialcamp-6
	' plotgrass to ft
	0 -1 maplayout xyplot: pt
;


: site-setup

	// Clear the map with grass
	-1 -1 w h grassbox

	pavement
// Plot trader stuff
	specialcamp-6

	-1 -1 w 3 conclot // Top
	-1 h 2 - w h conclot // Bottom

	0 h 6 - w ' plotroadlines hline
	

	1 no_time!
	7 14 rnd o'clock
	// 50 %chance if 1 2 frnd weather! -1 -2 frnd wind_force! else 1 2 frnd wind_force! then
	
	mission{ no-edge-spawns }
	
	20 %mv-up
	25 %chars-up
;

	 region: specialcamp-6
		w: maplayout layoutw
		h: maplayout layouth

		trade-mode

		post-script: site-setup
} }

uses paul/region-layout.df
uses paul/region-road-layout.df
// uses events/industrial/indust.df
// "../deathforth/rooms/TEMPROOM.txt" $load-rooms
	
" 
:map 2 
:tags roboshack  
:scriptA roboprof
:scriptB robopunch 
:scriptC robospeed 
:scriptD indwalls 
:scriptE indbluepanel 
:scriptF indgreenpanel 
:scriptG 3indwalls 
:size 11 12 
:off 1 1 
:tiles 
00222222200
00233333200
00211D11200
2221E1F1222
23311A11332
21111111112
21B11111C12
21111111112
22221112222
33321112333
00022222000
00033333000

:map 3 
:tags toolshop  
:scriptA tshopgas 
:scriptB tshopelec 
:scriptC cashcounter flipside 
:scriptD cashcounter 
:scriptE indshelf -1 movey 
:scriptF industsmall 
:size 15 13 
:off 6 0 
:tiles 
222222202222222
233333202333332
21E1E12021E1E12
2F111F202F111F2
222122222221222
332133333331233
00211C111D11200
002A1111111B200
002111111111200
002111111111200
002222111222200
003332222233300
000003333300000

:map 4 
:tags toolschool  
:scriptA schooltrader 
:scriptB industsmall 
:scriptC carwreck 
:scriptD indshelf -1 movey 
:scriptE 3indwalls 
:size 13 11 
:off 6 1 
:tiles 
2222222222222
2333333333332
2B1D1D111E112
2B1111111C112
211111A111112
2111111111BB2
2B111111111B2
2BB111111B112
2222211122222
3333222223333
0000333330000
"
value CAMP
CAMP $parse-rooms

cyoa{

: cyoa-sale-car
	carkey 0 > if cyoa-car then;

	cyoa-start
	"Coche a la venta" cyoa-title!
		"Este coche parece que este listo para la venta desde el trastejero de coches sucio!" cyoa-text!
		choice( "Vuelva mas tarde" )choice
		

;

cyoa-choice: tcarsales-go
	($ Thegroup .. " 'compra un coche nuevo!" .. "Solo tome cualquier coche de la linea que quiera!" pbold $) cyoa-text! goodfx
		1 to carkey clear-cyoa -5 trunk-food+ 50 trunk-gas+ someicon
;

cyoa: tcarsales
	"Todos los coches deben irse!" cyoa-title!
	($ "Tengo que sacar estos coches de la linea! Puedes comprar cualquier coche, ahora mismo, por el precio bajo de 5 libras de comida! Sin financiacion! Listo para usarse y salir! Le dare incluso 50 litros de gasolina!" pbold $) cyoa-text!
	trunk-food 4 > if "Es una buena oferta." +crtext then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " 'no puede permitirse la compra vital de un coche nuevo." $) +crtext
	else
		choice( "Deja esto por ahora" )choice back-esc
		trunk-food 4 > if
			choice( "Compra un coche nuevo - 5 comida" )choice: tcarsales-go
		then
	then
;

cyoa-choice: tgarage-1
	($ Thegroup .. " 'paga por reparaciones completas del chasis y del motor!'	" .. "Cuando te vayas de aqui, simplemente conduce tu coche un poco por la carretera y lo repararemos!" pbold $) cyoa-text! goodfx
		clear-cyoa -5 trunk-food+ image-car-garage road{ ' mandible-repairgarage stack-road-action }
;

cyoa-choice: tgarage-2
	($ Thegroup .. " 'paga por una variedad de modificaciones avanzadas de automoviles, asi como por reparaciones completas del chasis y del motor!'	" .. "Cuando te vayas de aqui, simplemente conduce un poco por la carretera hasta el taller. Tu coche recibira EL TRABAJO!" pbold $) cyoa-text! goodfx
		clear-cyoa -35 trunk-food+ image-car-garage road{ ' mandible-megagarage stack-road-action }
;

cyoa: tgarage
	"Expert Garage" cyoa-title!
	($ "Tenemos un taller lleno de mecanicos expertos, solo tienes que conducir un poco por la carretera, y ofrecemos " pbold .. "EL TRABAJO" fbold .. ". Modificaciones completas de automoviles: Revestimiento de proteccion, aumento de velocidad, chasis reforzado, revision del motor con aumento de eficiencia de combustible. Todo por 35 comida.	Podemos reparar completamente tu coche sin modificaciones dulces, por 5 comida." pbold $) cyoa-text!
	trunk-food 34 > if "El coche perfecto es posible." +crtext else
		trunk-food 4 > if "Todavia puedes comprar reparaciones completas." +crtext then
	then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " 'no puede permitirse esta modificacion de automovil extensa pero elegante." $) +crtext
	else
		choice( "Deja esto por ahora" )choice back-esc
		trunk-food 34 > if
			choice( "Espanol " .. "EL TRABAJO" fbold .. " 1.5â‚¬" )choice: tgarage-2
		then
		trunk-food 4 > if
			choice( "5 food" )choice: tgarage-1
		then
	then
;

0 value mt1-1
0 value mt1-2
0 value mt1-3
0 value mt1-4

cyoa-choice: trade-m1-4
	($ Thegroup .. "  compra un martillo neumatico de gasolina!" .. "La eleccion del conocedor para cincelar a traves de hordas de zombies!" pbold $) cyoa-text! goodfx
	1 WEAPON_JACKHAMMER trunk.weapon+ -20 trunk-food+ someicon ' mt1-4 ++
;

cyoa-choice: trade-m1-3
	($ Thegroup .. "  compra una motosierra de gasolina! Un clasico!" .. "Todos saben que esta es LA respuesta para los zombies! Mientras no te quedes sin gasolina." pbold $) cyoa-text! goodfx
	1 WEAPON_CHAINSAW trunk.weapon+ -10 trunk-food+ someicon ' mt1-3 ++
;

cyoa-choice: trade-m1-2
	($ Thegroup .. "  compra un soplador de hojas a gasolina!" .. "Soplarlos a los zombis! Como hojas!" pbold $) cyoa-text! goodfx
	1 WEAPON_BLOWER trunk.weapon+ -5 trunk-food+ someicon ' mt1-2 ++
;

cyoa-choice: trade-m1-1
	($ Thegroup .. " ' compra una motosierras electrica!	" .. "Excelente para cortar las multitudes de zombis! Decentemente bien para hierbas." pbold $) cyoa-text! goodfx
	1 WEAPON_WHACKER trunk.weapon+ -5 trunk-food+ someicon ' mt1-1 ++
;

cyoa: tshopgas
	"Herramientas a Gasolina" cyoa-title!
	($ "No hables con la otra persona! Hay algo sobre armas a gasolina, que simplemente no puedes obtener de electricas." pbold $) cyoa-text!
	trunk-food 4 > if "Tiempo de encender." +crtext then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " 'esta figurativamente sin gas. El comerciante te da alguna pena y te entrega algunas literas de gasolina literalmente." $) +crtext clear-cyoa +30 trunk-gas+ goodfx
	else
		choice( "Deja esto por ahora" )choice back-esc
		trunk-food 19 > mt1-4 1 < and if
			choice( "Martillo Perforador - 20 comida" )choice: trade-m1-4
		then
		trunk-food 9 > mt1-3 3 < and if
			choice( "Sierra de Cadena - 10 comida" )choice: trade-m1-3
		then
		trunk-food 4 > mt1-2 3 < and if
			choice( "Aspiradora de Hojas - 5 comida" )choice: trade-m1-2
		then
		trunk-food 4 > mt1-1 3 < and if
			choice( "Motosierras - 5 comida" )choice: trade-m1-1
		then
	then
;

0 value mt2-1
0 value mt2-2
0 value mt2-3
0 value mt2-4

: mt2-firstbuy 
	mt2-4 1 < mt2-3 1 < mt2-2 1 < mt2-1 1 < and and and
;

: mt2-firstbuy-done
	($ "Disfruta tuyo de " pbold .. "CARGA DOUBLE!" fbold $) +crtext
;

cyoa-choice: trade-m2-4
	($ Thegroup .. " 'compra una martilla electrica de jackhammer!	" .. "Es tan eco-friendly!" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 255 else 130 then WEAPON_EJHAMMER trunk.weapon+ -20 trunk-food+ someicon ' mt2-4 ++ ;

cyoa-choice: trade-m2-3
	($ Thegroup .. " 'compra una cadena electrica de sierras! Tu se siente desagradablemente orgulloso de salvar el medio ambiente en la post-apocalipsis!	" .. "Excelente para compostar zombis!" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 160 else 80 then WEAPON_ECHAINSAW trunk.weapon+ -10 trunk-food+ someicon ' mt2-3 ++ ;
	
cyoa-choice: trade-m2-2
	($ Thegroup .. " 'compra una aspiradora electrica de hojas!	" .. "Asegura tranquilamente a los zombis, una opcion pacifista en lugar de este esplat." pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 100 else 50 then WEAPON_EBLOWER trunk.weapon+ -5 trunk-food+ picon ' mt2-2 ++ ;

cyoa-choice: trade-m2-1
	($ Thegroup .. " 'compra una podadora electrica!	" .. "Tiene multiples usos efectivos en metodos agricolas zombicultura." pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 100 else 50 then WEAPON_EWHACKER trunk.weapon+ -5 trunk-food+ picon ' mt2-1 ++ ;

cyoa: tshopelec
	"Herramientas Electricas" cyoa-title!
	($ "No hables con el otro tipo! Las herramientas electricas son ecologicas, lo que te permite reducir las emisiones de gas mientras cortas zombis." pbold $) cyoa-text!
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " ' le falta comida suficiente para comprar algo!" $) +crtext
		($ "El comerciante se apiada de " .. thegroup .. "  y reparte un " .. "SOPLADOR DE HOJAS EN DESCUENTO!" rbold $) +crtext goodfx 30 WEAPON_EBLOWER trunk.weapon+ clear-cyoa
	else
		mt2-firstbuy if
			($ "Como una oferta especial, el comerciante " .. "DOBLE CARGO" rainbold .. " 'Tu primera compra. Tendra el doble de energia de lo que normalmente encontrarias!" $) +crtext
		then
		choice( "Deja esto por ahora" )choice back-esc
		trunk-food 19 > mt2-4 1 < and if
			choice( "Martillo electrico - 20 pies" )choice: trade-m2-4
		then
		trunk-food 9 > mt2-3 2 < and if
			choice( "Sierra electrica - 10 pies" )choice: trade-m2-3
		then
		trunk-food 4 > mt2-2 2 < and if
			choice( "Aspiradora electrica de hojas - 5 pies" )choice: trade-m2-2
		then
		trunk-food 4 > mt2-1 2 < and if
			choice( "Herramienta electrica para cortar malezas - 5 pies" )choice: trade-m2-1
		then
	then
;

0 value mechtrained+

: mech-train-amt numhums 2 * mechtrained+ 1 + * 1 + ; 
: mech-train-pay? trunk-food mech-train-amt 1 - > notsolo notsolohuman and and ;
: mech-train-pay mech-train-amt -1 * trunk-food+ ;

: mech-solo-amt mechtrained+ 2 * 3 + ;
: mech-solo-pay? trunk-food mech-solo-amt 1 - > ;
: mech-solo-pay mech-solo-amt -1 * trunk-food+ ;

cyoa-choice: mech-school-train
	cchar .mechanical@ 6 < if
		($ cname .. " ' observa atentamente a como el Profesor realmente golpea el coche con un clavo. Asi que es lo que la ingenieria esta sobre." $) goodfx
	else
		($ cname .. " 'ya han obtenido su grado completo de ingenieria mecanica en esta prestigiosa academia!		Su ultimo pago se recoge como una donacion para construir una nueva ala para la escuela." $) badfx
	then cyoa-text! 
	mech-solo-pay mechanicREV mechanic++ ' mechtrained+ ++
;

cyoa-choice: mech-school-group
	($ "El grupo entero obtiene entrenamiento mecanico! Todos miran a como el Profesor golpea el coche con un clavo. Es muy iluminador." $) cyoa-text! goodfx
	mech-train-pay someicon
	regiondef{ ' trade-mechanic-group! foreach-human-char } numhums ' mechtrained+ +!
;

cyoa: mech-school-give
	solohuman if
		choosehuman mech-school-train
	then;
	"Quien deberia obtener este vasto conocimiento mecanico?" cyoa-text!
	' mech-school-train cyoa-human-choice
;

cyoa: schooltrader
	($ "La Escuela de Herramientas" $) cyoa-title!
	"La ingenieria mecanica es una verdadera ciencia. Diferente a la medicina, que es mas de un arte. Similar a Origami o quizas a la danza interpretativa." pbold text!
	"El Profesor de Ingenieria Mecanica golpea repetidamente el chasis del coche destrozado con un clavo mientras dice esto, sin interrumpir la mirada." +crtext foodamt
		mech-solo-pay? not if
			"No puede permitirse mas capacitacion." +crtext 
		then;
		choice( "Deja esto por ahora" )choice back-esc
		mech-solo-pay? if
			choice( "Dame " .. mech-solo-amt .. " 'comida para un solo entrenamiento" )choice: mech-school-give
		then
		mech-train-pay? if
			choice( "Dame " .. mech-train-amt .. " ' alimentos para el entrenamiento en grupo" )choice: mech-school-group
		then
		
;

cyoa-bridge: robopunch-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	10 recruit-yes-food 
	($ "Un robot que reparte palizas? La tecnologia es muy buena." $) +crtext
	"#CARGANDO PROTOCOLOS DE GOLPES#	#LISTO PARA GOLPEAR#" pbold +crtext
	recruitee .strengthREV recruitee .fitnessREV
;

cyoa: robopunch
	"El Golpe Mecanico Perfecto" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"#SOY PUNCH BOT#" pbold cyoa-text! basic-tone
	"Ahora vamos por buen camino! Este robot esta disenado para un solo proposito: Dar golpes." +crtext
	trunk-food 9 > if
		choice( "Dejarlo para otro momento" )choice
		choice( "Comprar PUNCHBOT - 10 comida" )choice: robopunch-go
	else
		"Necesitas 10 comida para comprar este punch bot." +crtext
		choice( "No puedo desprenderme de la comida" )choice
	then
;

cyoa-bridge: robospeed-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	15 recruit-yes-food 
	($ "El robot hace un roscon en un abrir y cerrar de ojos. Tan rapido!" $) +crtext
	"#CARGANDO PROTOCOLOS DE VELOCIDAD#	#TENGO QUE IR RAPIDO#" pbold +crtext
	recruitee .strengthREV recruitee .fitnessREV
;

cyoa: robospeed
	"Que rapido!" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"#SOY BOT DE VELOCIDAD#" pbold cyoa-text! basic-tone
	"Con sus lineas elegantes y sus minusculas llantas de rueda libre, incluso parado, este robot irradia el concepto de 'velocidad'.	Ademas, acaba de decir la palabra 'velocidad'." +crtext
	trunk-food 14 > if
		choice( "Dejarlo para otro momento" )choice
		choice( "Comprar SPEEDBOT - 15 comida" )choice: robospeed-go
	else
		"Necesitas 15 alimentos para comprar este robot de velocidad." +crtext
		choice( "No puedo desprenderme de la comida" )choice
	then
;

}

roomgen{


: .ct1 args( ch ) // Mech Teacher
	WEAPON_WRENCH <- ch .weapona!
;
: .ct2 args( ch ) // Gas tools
	26 <- ch .glassestype! // Gas Mask
;
: .ct3 args( ch ) // Elec tools
	21 <- ch .glassestype! // Green Goggles
;
: .ct4 args( ch ) // Garage
	1 <- ch .female!
	25 <- ch .hattype!
	6 <- ch .legstype!
	3 <- ch .torsotype!
	0 <- ch .glassestype!
;
: .ct5 args( ch ) 
	7 <- ch .hattype! // Cowboy hat
;
: .ct6 args( ch ) 
	<- ch .speedbotify
;
: .ct7 args( ch ) 
	<- ch .punchbotify
;

: camptradercommon
	still-npc regiondef{ ' trader-event lt .events! 1 lt .timer! }
	
;

: schooltrader
	camptradercommon .ct1 cyoa{ ' schooltrader cyoa! }
;

: robopunch
	camptradercommon .ct7 cyoa{ ' robopunch cyoa! }
;

: robospeed
	camptradercommon .ct6 cyoa{ ' robospeed cyoa! }
;

: roboprof
	// camptradercommon .ct8 cyoa{ ' roboprof cyoa! }
;

: tshopgas
	camptradercommon .ct2 cyoa{ ' tshopgas cyoa! }
;

: tshopelec
	camptradercommon .ct3 cyoa{ ' tshopelec cyoa! }
;

: tgarage
	camptradercommon .ct4 cyoa{ ' tgarage cyoa! }
;

: tcarsales
	camptradercommon .ct5 cyoa{ ' tcarsales cyoa! }
;

		0 stack
			' camptradercommon shove
			' camptradercommon shove
			' camptradercommon shove
			' camptradercommon shove
		value preptrader-base
		preptrader-base shuffles var preptrader-picker

: preppertrader
	preptrader-picker depths 0= if preptrader-base shuffles to preptrader-picker then pops nip execute ;


}

locdef{

location: toolshop
		max-rooms: 1
		entry-tags: toolshop
		script: set-theme-restaurant
		pre-script: set-theme-restaurant
		post-script: nad 0 %zombs
				
		room-choice: toolshop
			room-weight: 10
			room-count: 1
			room-post-script: 
			filter-only
			
location: toolschool
		max-rooms: 1
		entry-tags: toolschool
		script: set-theme-bookstore
		pre-script: set-theme-bookstore
		post-script: nad 0 %zombs
				
		room-choice: toolschool
			room-weight: 10
			room-count: 1
			room-post-script: 
			filter-only
		
location: roboshack
		max-rooms: 1
		entry-tags: roboshack
		script: factory-theme
		pre-script: factory-theme
		post-script: nad 0 %zombs
				
		room-choice: roboshack
			room-weight: 10
			room-count: 1
			room-post-script:
			filter-only
			
}
		
root{

: prepper-magchance
	20 %chance if
		magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! drift
	then
;

: sale-car 
	1 22 rnd spawncar mission{ cyoa{ ' cyoa-sale-car cyoa! } }
	0 lt .action_icon!
;

}
		
map{ regiondef{

trade-camp-reset

0 value gbx
0 value gby
0 value gbx2
0 value gby2
0 value gbx3
0 value gby3
0 value bfx
0 value bfy

: bldg tanchor!
	"roboshack" $picklocid addloc
	"ROBO TIENDA" sign-text! prefab{ 1 5 1 shop } 
;
: bldg2 tanchor!
	"ferreteria" $picklocid addloc
	"TIENDA DE HERRAMIENTAS" sign-text! prefab{ 2 5 1 shop } 
;
: bldg3 tanchor!
	"eSCUELA DE HERRAMIENTAS" $picklocid addloc
	"ESCUELA DE HERRAMIENTAS" sign-text! prefab{ 3 6 1 shop } 
;


' plotgrass >defer ft

: pt
	xyp.xy ft // Plot the current floor by default
	
	xyp.i char { = if xyp.xy to gby2 to gbx2 then;
	xyp.i char } = if gbx2 1- gby2 xyp.xy swap 1+ swap gravellot then;
	xyp.i char ( = if xyp.xy to gby to gbx then;
	xyp.i char ) = if gbx 1- gby xyp.xy swap 1+ swap gravellot then;
	xyp.i char [ = if xyp.xy to gby3 to gbx3 then;
	xyp.i char ] = if gbx3 1- gby3 xyp.xy swap 1+ swap gravellot then;
	
	xyp.i char 1 = if xyp.x to bfx then;
	xyp.i char 2 = if bfx xyp.y xyp.x barbfence then;

	xyp.i char t = if sale-car lt-here then;
	xyp.i char U = if tnomey-spawn then;
	xyp.i char W = if trader-weapon-buyer then;
	xyp.i char G = if trader-food-for-gas then;
	xyp.i char S = if tgarage lt-here then;
	xyp.i char M = if tcarsales lt-here then;
		
	xyp.i char @ = if xyp.xy bldg then;
	xyp.i char 3 = if xyp.xy bldg2 then;
	xyp.i char 4 = if xyp.xy bldg3 then;
	
	xyp.i char p = if ' plotpave to ft then;
	xyp.i char $ = if xyp.xy grassedge then;
;

"
...........................
1.........................2
...........................
..................[....t...
...........................
...........................
...........................
.......L..........ML...t...
..{........................
.@....3....4...............
...........................
............(...}......t...
..................S........
...........................
..........................]
...........................
1..........2....1.........2
......G..U...........W....p
$$$$$$$$$$$$....$$$$$$$$$$$
...............)...........
...........................
...........................
...........................
...........................
...........................
...........................
..........................."
constant maplayout

: specialcamp-6
	' plotgrass to ft
	0 -1 maplayout xyplot: pt
;


: site-setup

	// Clear the map with grass
	-1 -1 w h grassbox

	pavement
// Plot trader stuff
	specialcamp-6

	-1 -1 w 3 conclot // Top
	-1 h 2 - w h conclot // Bottom

	0 h 6 - w ' plotroadlines hline
	

	1 no_time!
	7 14 rnd o'clock
	// 50 %chance if 1 2 frnd weather! -1 -2 frnd wind_force! else 1 2 frnd wind_force! then
	
	mission{ no-edge-spawns }
	
	20 %mv-up
	25 %chars-up
;

	 region: specialcamp-6
		w: maplayout layoutw
		h: maplayout layouth

		trade-mode

		post-script: site-setup
} }

uses paul/region-layout.df
uses paul/region-road-layout.df
// uses events/industrial/indust.df
// "../deathforth/rooms/TEMPROOM.txt" $load-rooms
	
" 
:map 2 
:tags roboshack  
:scriptA roboprof
:scriptB robopunch 
:scriptC robospeed 
:scriptD indwalls 
:scriptE indbluepanel 
:scriptF indgreenpanel 
:scriptG 3indwalls 
:size 11 12 
:off 1 1 
:tiles 
00222222200
00233333200
00211D11200
2221E1F1222
23311A11332
21111111112
21B11111C12
21111111112
22221112222
33321112333
00022222000
00033333000

:map 3 
:tags toolshop  
:scriptA tshopgas 
:scriptB tshopelec 
:scriptC cashcounter flipside 
:scriptD cashcounter 
:scriptE indshelf -1 movey 
:scriptF industsmall 
:size 15 13 
:off 6 0 
:tiles 
222222202222222
233333202333332
21E1E12021E1E12
2F111F202F111F2
222122222221222
332133333331233
00211C111D11200
002A1111111B200
002111111111200
002111111111200
002222111222200
003332222233300
000003333300000

:map 4 
:tags toolschool  
:scriptA schooltrader 
:scriptB industsmall 
:scriptC carwreck 
:scriptD indshelf -1 movey 
:scriptE 3indwalls 
:size 13 11 
:off 6 1 
:tiles 
2222222222222
2333333333332
2B1D1D111E112
2B1111111C112
211111A111112
2111111111BB2
2B111111111B2
2BB111111B112
2222211122222
3333222223333
0000333330000
"
value CAMP
CAMP $parse-rooms

cyoa{

: cyoa-sale-car
	carkey 0 > if cyoa-car then;

	cyoa-start
	"Voiture a vendre" cyoa-title!
		"'Cette voiture a l'air a la fois 'cle en main' et 'prete a partir'. Vous pouvez l'acheter au vendeur de voitures louches a proximite!" cyoa-text!
		choice( "Revenez plus tard" )choice
		

;

cyoa-choice: tcarsales-go
	($ Thegroup .. "  achete une toute nouvelle voiture!	" .. "Prenez simplement nimporte quelle voiture du lot que vous voulez!" pbold $) cyoa-text! goodfx
		1 to carkey clear-cyoa -5 trunk-food+ 50 trunk-gas+ someicon
;

cyoa: tcarsales
	"Toutes les voitures doivent partir!" cyoa-title!
	($ "'Je dois debarrasser ces voitures du lot! Vous pouvez acheter n'importe quelle voiture, des maintenant, pour le bas, bas prix de 5aliments! Obtenez le maintenant! Aucun financement! Cle en main et pret a partir! Je vais meme ajouter 50d'essence!" pbold $) cyoa-text!
	trunk-food 4 > if "'C'est une sacree affaire." +crtext then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " ' ne peut pas se permettre l'achat qui changera sa vie d'une nouvelle voiture brillante." $) +crtext
	else
		choice( "Partir pour linstant" )choice back-esc
		trunk-food 4 > if
			choice( "Achetez une nouvelle voiture   5aliments" )choice: tcarsales-go
		then
	then
;

cyoa-choice: tgarage-1
	($ Thegroup .. "  paie pour des reparations completes du chassis et du moteur!	" .. "Quand vous partirez d'ici, descendez la route un peu et nous reparerons votre voiture!" pbold $) cyoa-text! goodfx
		clear-cyoa -5 trunk-food+ image-car-garage road{ ' mandible-repairgarage stack-road-action }
;

cyoa-choice: tgarage-2
	($ Thegroup .. " 	pays pour un tableau de modifications de voiture avancees, plus des reparations completes du chassis et du moteur!" .. "Quand vous quitterez ici, allez simplement le long de la route un peu vers le garage. Votre voiture va recevoir LE TRAVAIL!" pbold $) cyoa-text! goodfx
		clear-cyoa -35 trunk-food+ image-car-garage road{ ' mandible-megagarage stack-road-action }
;

cyoa: tgarage
	"Garage Expert" cyoa-title!
	($ "Nous avons une garager plein dexperts mecaniciens juste en bas de la route, et nous offrons " pbold .. "LE TRAVAIL" fbold .. ". Modifications completes de voiture : plaquage blinde, acceleration, chassis renforce, revision moteur avec augmentation de lefficacite carburant. Tout cela coute 35 nourriture. Nous pouvons egalement effectuer des reparations completes de voiture sans aucune modification agreable, a 5 nourriture." pbold $) cyoa-text!
	trunk-food 34 > if "Le parfait automobile est possible." +crtext else
		trunk-food 4 > if "Vous pourriez encore acheter des reparations completes." +crtext then
	then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " ne peut pas se permettre ces modifications de car extensives et goutees." $) +crtext
	else
		choice( "Partir pour linstant" )choice back-esc
		trunk-food 34 > if
			choice( "Modifications completes de voiture, " .. "LE TRAVAIL" fbold .. "   35 nourriture" )choice: tgarage-2
		then
		trunk-food 4 > if
			choice( "Reparations completes de voiture   5 nourriture" )choice: tgarage-1
		then
	then
;

0 value mt1-1
0 value mt1-2
0 value mt1-3
0 value mt1-4

cyoa-choice: trade-m1-4
	($ Thegroup .. " achete un marteau piqueur a gazole!" .. "'Le choix du connaisseur pour ciseler a travers des hordes de zombies !" pbold $) cyoa-text! goodfx
	1 WEAPON_JACKHAMMER trunk.weapon+ -20 trunk-food+ someicon ' mt1-4 ++
;

cyoa-choice: trade-m1-3
	($ Thegroup .. "  achete une tronconneuse a essence ! Un classique !" .. "'Tout le monde sait que c'est LA reponse pour les zombies ! Tant que vous ne tombez pas en panne d'essence." pbold $) cyoa-text! goodfx
	1 WEAPON_CHAINSAW trunk.weapon+ -10 trunk-food+ someicon ' mt1-3 ++
;

cyoa-choice: trade-m1-2
	($ Thegroup .. "  achete un souffleur a feuilles a essence !" .. "Faites sauter les zombies ! Comme des feuilles !" pbold $) cyoa-text! goodfx
	1 WEAPON_BLOWER trunk.weapon+ -5 trunk-food+ someicon ' mt1-2 ++
;

cyoa-choice: trade-m1-1
	($ Thegroup .. "  achete un coupe herbe a essence !" .. "Excellent pour tailler les hordes de zombies ! Un peu decent pour les mauvaises herbes." pbold $) cyoa-text! goodfx
	1 WEAPON_WHACKER trunk.weapon+ -5 trunk-food+ someicon ' mt1-1 ++
;

cyoa: tshopgas
	"Outils a essence" cyoa-title!
	($ "'Ne parlez pas a l'autre gars ! Il y a quelque chose a propos des armes a essence que vous ne pouvez tout simplement pas obtenir de l'electricite." pbold $) cyoa-text!
	trunk-food 4 > if "Il est temps daccelerer." +crtext then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. "  est au figure a court dessence. Le commercant a pitie de vous et vous distribue de lessence litterale." $) +crtext clear-cyoa +30 trunk-gas+ goodfx
	else
		choice( "Partir pour linstant" )choice back-esc
		trunk-food 19 > mt1-4 1 < and if
			choice( "Marteau piqueur   20 nourriture" )choice: trade-m1-4
		then
		trunk-food 9 > mt1-3 3 < and if
			choice( "Tronconneuse   10 nourriture" )choice: trade-m1-3
		then
		trunk-food 4 > mt1-2 3 < and if
			choice( "Souffleur a feuilles   5 nourriture" )choice: trade-m1-2
		then
		trunk-food 4 > mt1-1 3 < and if
			choice( "Coupe herbe   5 nourriture" )choice: trade-m1-1
		then
	then
;

0 value mt2-1
0 value mt2-2
0 value mt2-3
0 value mt2-4

: mt2-firstbuy 
	mt2-4 1 < mt2-3 1 < mt2-2 1 < mt2-1 1 < and and and
;

: mt2-firstbuy-done
	($ "Profitez de votre " pbold .. "DOUBLE CHARGE!" fbold $) +crtext
;

cyoa-choice: trade-m2-4
	($ Thegroup .. " achete un marteau piqueur electrique!" .. "C'est tellement ecolo!" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 255 else 130 then WEAPON_EJHAMMER trunk.weapon+ -20 trunk-food+ someicon ' mt2-4 ++ ;

cyoa-choice: trade-m2-3
	($ Thegroup .. " 	achete une tronconneuse electrique ! Vous vous sentez etrangement satisfait davoir sauve lenvironnement apres lapocalypse." .. "Excellent pour composter des zombies !" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 160 else 80 then WEAPON_ECHAINSAW trunk.weapon+ -10 trunk-food+ someicon ' mt2-3 ++ ;
	
cyoa-choice: trade-m2-2
	($ Thegroup .. "  achete un souffleur de feuilles electrique !" .. "Menez doucement les zombies, une alternative pacifiste a tous ces eclaboussures !" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 100 else 50 then WEAPON_EBLOWER trunk.weapon+ -5 trunk-food+ picon ' mt2-2 ++ ;

cyoa-choice: trade-m2-1
	($ Thegroup .. "  achete une debroussailleuse electrique !" .. "A de multiples utilisations efficaces dans les methodes de culture Zomboculture." pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 100 else 50 then WEAPON_EWHACKER trunk.weapon+ -5 trunk-food+ picon ' mt2-1 ++ ;

cyoa: tshopelec
	"Outils electriques" cyoa-title!
	($ "'Ne parlez pas a lautre gars! Les outils electriques sont ecologiques, vous permettant de reduire les emissions de gaz en tondant les zombies." pbold $) cyoa-text!
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. "  manque de nourriture pour acheter quoi que ce soit !" $) +crtext
		($ "Le commercant prend pitie de " .. thegroup .. "  et distribue un " .. "SOUFFLEUSE A FEUILLES A RABAIS!" rbold $) +crtext goodfx 30 WEAPON_EBLOWER trunk.weapon+ clear-cyoa
	else
		mt2-firstbuy if
			($ "Dans le cadre dune offre speciale, le commercant " .. "DOUBLERA LA CHARGE" rainbold .. "  de votre premier achat. Il aura deux fois plus denergie que ce que vous trouveriez normalement!" $) +crtext
		then
		choice( "Partir pour linstant" )choice back-esc
		trunk-food 19 > mt2-4 1 < and if
			choice( "Marteau piqueur electrique   20aliments" )choice: trade-m2-4
		then
		trunk-food 9 > mt2-3 2 < and if
			choice( "Saw a Chaine Electrique   10 pieds" )choice: trade-m2-3
		then
		trunk-food 4 > mt2-2 2 < and if
			choice( "Aspirateur a Feuilles Electrique   5 pieds" )choice: trade-m2-2
		then
		trunk-food 4 > mt2-1 2 < and if
			choice( "Coupe Herbe Electrique   5 pieds" )choice: trade-m2-1
		then
	then
;

0 value mechtrained+

: mech-train-amt numhums 2 * mechtrained+ 1 + * 1 + ; 
: mech-train-pay? trunk-food mech-train-amt 1 - > notsolo notsolohuman and and ;
: mech-train-pay mech-train-amt -1 * trunk-food+ ;

: mech-solo-amt mechtrained+ 2 * 3 + ;
: mech-solo-pay? trunk-food mech-solo-amt 1 - > ;
: mech-solo-pay mech-solo-amt -1 * trunk-food+ ;

cyoa-choice: mech-school-train
	cchar .mechanical@ 6 < if
		($ cname .. " 'regarde attentivement tandis que le Professeur frappe vraiment dur sur la voiture avec une cle en fer. Ainsi c'est ce qu'il est de l'ingenierie !" $) goodfx
	else
		($ cname .. " a deja obtenu leur diplome complet dingenierie mecanique de cette prestigieuse ecole !	Votre derniere paiement est pris en tant que don pour la construction dune nouvelle aile pour lecole." $) badfx
	then cyoa-text! 
	mech-solo-pay mechanicREV mechanic++ ' mechtrained+ ++
;

cyoa-choice: mech-school-group
	($ "'Le groupe entier recoit une formation mecanique ! Tous regardent le Professeur frapper dur sur la voiture avec une cle. Cela est tres eclairant." $) cyoa-text! goodfx
	mech-train-pay someicon
	regiondef{ ' trade-mechanic-group! foreach-human-char } numhums ' mechtrained+ +!
;

cyoa: mech-school-give
	solohuman if
		choosehuman mech-school-train
	then;
	"Qui devrait recevoir cette connaissance mecanique vaste ?" cyoa-text!
	' mech-school-train cyoa-human-choice
;

cyoa: schooltrader
	($ "LEcole des Outils" $) cyoa-title!
	"Lingenierie mecanique est une vraie science. Contrairement a la medecine, qui est plutot de lart. Semblable a lOrigami ou peut etre a la danse interpretative." pbold text!
	"Le Professeur dingenierie mecanique frappe repeteement le chassis de la voiture detruite avec une cle en fer en disant cela, sans jamais rompre le contact visuel." +crtext foodamt
		mech-solo-pay? not if
			"Vous ne pouvez plus payer pour une autre formation." +crtext 
		then;
		choice( "Partir pour linstant" )choice back-esc
		mech-solo-pay? if
			choice( "Donne " .. mech-solo-amt .. " alimentation pour un entrainement unique" )choice: mech-school-give
		then
		mech-train-pay? if
			choice( "Donne " .. mech-train-amt .. "  nourriture pour lentrainement de groupe" )choice: mech-school-group
		then
		
;

cyoa-bridge: robopunch-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	10 recruit-yes-food 
	($ "Un robot qui distribue des coups ? La technologie est assez interessante." $) +crtext
	"#CHARGEMENT PROTOCOLES DE COUP DE POING#	#PRET A COUPER#" pbold +crtext
	recruitee .strengthREV recruitee .fitnessREV
;

cyoa: robopunch
	"Le parfait coup mecanique" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"#JE SUIS LE BOT DE POING#" pbold cyoa-text! basic-tone
	"'Maintenant on y arrive ! Ce robot est concu dans un seul but : frapper." +crtext
	trunk-food 9 > if
		choice( "Laisser pour le moment" )choice
		choice( "Achetez PUNCHBOT   10 aliments" )choice: robopunch-go
	else
		"Vous avez besoin de 10 aliments pour acheter ce bot de frappe." +crtext
		choice( "'Ne peut pas se passer de la nourriture" )choice
	then
;

cyoa-bridge: robospeed-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	15 recruit-yes-food 
	($ "Le robot fait un beignet tres rapidement. Si vite." $) +crtext
	"#CHARGEMENT DES PROTOCOLES DE VITESSE#	#JE DOIS ALLER VITE#" pbold +crtext
	recruitee .strengthREV recruitee .fitnessREV
;

cyoa: robospeed
	"Si rapide" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"#JE SUIS LE BOT DE VITESSE#" pbold cyoa-text! basic-tone
	"'Avec ses lignes epurees et ses minuscules enjoliveurs rotatifs, meme a l'arret, ce robot respire le concept de vitesse.	De plus, il vient de prononcer le mot vitesse." +crtext
	trunk-food 14 > if
		choice( "Laisser pour le moment" )choice
		choice( "Achetez SPEEDBOT   15 aliments" )choice: robospeed-go
	else
		"Vous avez besoin de 15 aliments pour acheter ce bot de vitesse." +crtext
		choice( "'Ne peut pas se passer de la nourriture" )choice
	then
;

}

roomgen{


: .ct1 args( ch ) // Mech Teacher
	WEAPON_WRENCH <- ch .weapona!
;
: .ct2 args( ch ) // Gas tools
	26 <- ch .glassestype! // Gas Mask
;
: .ct3 args( ch ) // Elec tools
	21 <- ch .glassestype! // Green Goggles
;
: .ct4 args( ch ) // Garage
	1 <- ch .female!
	25 <- ch .hattype!
	6 <- ch .legstype!
	3 <- ch .torsotype!
	0 <- ch .glassestype!
;
: .ct5 args( ch ) 
	7 <- ch .hattype! // Cowboy hat
;
: .ct6 args( ch ) 
	<- ch .speedbotify
;
: .ct7 args( ch ) 
	<- ch .punchbotify
;

: camptradercommon
	still-npc regiondef{ ' trader-event lt .events! 1 lt .timer! }
	
;

: schooltrader
	camptradercommon .ct1 cyoa{ ' schooltrader cyoa! }
;

: robopunch
	camptradercommon .ct7 cyoa{ ' robopunch cyoa! }
;

: robospeed
	camptradercommon .ct6 cyoa{ ' robospeed cyoa! }
;

: roboprof
	// camptradercommon .ct8 cyoa{ ' roboprof cyoa! }
;

: tshopgas
	camptradercommon .ct2 cyoa{ ' tshopgas cyoa! }
;

: tshopelec
	camptradercommon .ct3 cyoa{ ' tshopelec cyoa! }
;

: tgarage
	camptradercommon .ct4 cyoa{ ' tgarage cyoa! }
;

: tcarsales
	camptradercommon .ct5 cyoa{ ' tcarsales cyoa! }
;

		0 stack
			' camptradercommon shove
			' camptradercommon shove
			' camptradercommon shove
			' camptradercommon shove
		value preptrader-base
		preptrader-base shuffles var preptrader-picker

: preppertrader
	preptrader-picker depths 0= if preptrader-base shuffles to preptrader-picker then pops nip execute ;


}

locdef{

location: toolshop
		max-rooms: 1
		entry-tags: toolshop
		script: set-theme-restaurant
		pre-script: set-theme-restaurant
		post-script: nad 0 %zombs
				
		room-choice: toolshop
			room-weight: 10
			room-count: 1
			room-post-script: 
			filter-only
			
location: toolschool
		max-rooms: 1
		entry-tags: toolschool
		script: set-theme-bookstore
		pre-script: set-theme-bookstore
		post-script: nad 0 %zombs
				
		room-choice: toolschool
			room-weight: 10
			room-count: 1
			room-post-script: 
			filter-only
		
location: roboshack
		max-rooms: 1
		entry-tags: roboshack
		script: factory-theme
		pre-script: factory-theme
		post-script: nad 0 %zombs
				
		room-choice: roboshack
			room-weight: 10
			room-count: 1
			room-post-script:
			filter-only
			
}
		
root{

: prepper-magchance
	20 %chance if
		magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! drift
	then
;

: sale-car 
	1 22 rnd spawncar mission{ cyoa{ ' cyoa-sale-car cyoa! } }
	0 lt .action_icon!
;

}
		
map{ regiondef{

trade-camp-reset

0 value gbx
0 value gby
0 value gbx2
0 value gby2
0 value gbx3
0 value gby3
0 value bfx
0 value bfy

: bldg tanchor!
	"roboshack" $picklocid addloc
	"ROBO SHACK" sign-text! prefab{ 1 5 1 shop } 
;
: bldg2 tanchor!
	"atelier doutillage" $picklocid addloc
	"ATELIER DOUTILLAGE" sign-text! prefab{ 2 5 1 shop } 
;
: bldg3 tanchor!
	"ecole doutillage" $picklocid addloc
	"ECOLE DOUTILLAGE" sign-text! prefab{ 3 6 1 shop } 
;


' plotgrass >defer ft

: pt
	xyp.xy ft // Plot the current floor by default
	
	xyp.i char { = if xyp.xy to gby2 to gbx2 then;
	xyp.i char } = if gbx2 1- gby2 xyp.xy swap 1+ swap gravellot then;
	xyp.i char ( = if xyp.xy to gby to gbx then;
	xyp.i char ) = if gbx 1- gby xyp.xy swap 1+ swap gravellot then;
	xyp.i char [ = if xyp.xy to gby3 to gbx3 then;
	xyp.i char ] = if gbx3 1- gby3 xyp.xy swap 1+ swap gravellot then;
	
	xyp.i char 1 = if xyp.x to bfx then;
	xyp.i char 2 = if bfx xyp.y xyp.x barbfence then;

	xyp.i char t = if sale-car lt-here then;
	xyp.i char U = if tnomey-spawn then;
	xyp.i char W = if trader-weapon-buyer then;
	xyp.i char G = if trader-food-for-gas then;
	xyp.i char S = if tgarage lt-here then;
	xyp.i char M = if tcarsales lt-here then;
		
	xyp.i char @ = if xyp.xy bldg then;
	xyp.i char 3 = if xyp.xy bldg2 then;
	xyp.i char 4 = if xyp.xy bldg3 then;
	
	xyp.i char p = if ' plotpave to ft then;
	xyp.i char $ = if xyp.xy grassedge then;
;

"
...........................
1.........................2
...........................
..................[....t...
...........................
...........................
...........................
.......L..........ML...t...
..{........................
.@....3....4...............
...........................
............(...}......t...
..................S........
...........................
..........................]
...........................
1..........2....1.........2
......G..U...........W....p
$$$$$$$$$$$$....$$$$$$$$$$$
...............)...........
...........................
...........................
...........................
...........................
...........................
...........................
..........................."
constant maplayout

: specialcamp-6
	' plotgrass to ft
	0 -1 maplayout xyplot: pt
;


: site-setup

	// Clear the map with grass
	-1 -1 w h grassbox

	pavement
// Plot trader stuff
	specialcamp-6

	-1 -1 w 3 conclot // Top
	-1 h 2 - w h conclot // Bottom

	0 h 6 - w ' plotroadlines hline
	

	1 no_time!
	7 14 rnd o'clock
	// 50 %chance if 1 2 frnd weather! -1 -2 frnd wind_force! else 1 2 frnd wind_force! then
	
	mission{ no-edge-spawns }
	
	20 %mv-up
	25 %chars-up
;

	 region: specialcamp-6
		w: maplayout layoutw
		h: maplayout layouth

		trade-mode

		post-script: site-setup
} }

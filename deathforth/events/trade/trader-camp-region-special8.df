uses paul/region-layout.df
uses paul/region-road-layout.df
// uses events/industrial/indust.df
// "../deathforth/rooms/TEMPROOM.txt" $load-rooms
	
" 
:map 2 
:tags roboshack  
:scriptA roboprof
:scriptB robopunch 
:scriptC robospeed 
:scriptD indwalls 
:scriptE indbluepanel 
:scriptF indgreenpanel 
:scriptG 3indwalls 
:size 11 12 
:off 1 1 
:tiles 
00222222200
00233333200
00211D11200
2221E1F1222
23311A11332
21111111112
21B11111C12
21111111112
22221112222
33321112333
00022222000
00033333000

:map 3 
:tags toolshop  
:scriptA tshopgas 
:scriptB tshopelec 
:scriptC cashcounter flipside 
:scriptD cashcounter 
:scriptE indshelf -1 movey 
:scriptF industsmall 
:size 15 13 
:off 6 0 
:tiles 
222222202222222
233333202333332
21E1E12021E1E12
2F111F202F111F2
222122222221222
332133333331233
00211C111D11200
002A1111111B200
002111111111200
002111111111200
002222111222200
003332222233300
000003333300000

:map 4 
:tags toolschool  
:scriptA schooltrader 
:scriptB industsmall 
:scriptC carwreck 
:scriptD indshelf -1 movey 
:scriptE 3indwalls 
:size 13 11 
:off 6 1 
:tiles 
2222222222222
2333333333332
2B1D1D111E112
2B1111111C112
211111A111112
2111111111BB2
2B111111111B2
2BB111111B112
2222211122222
3333222223333
0000333330000
"
value CAMP
CAMP $parse-rooms

cyoa{

: cyoa-sale-car
	carkey 0 > if cyoa-car then;

	cyoa-start
	"Auto in vendita" cyoa-title!
		"'Questa macchina sembra sia chiavi in mano e pronta a partire. Potresti comprarla dal losco venditore di auto qui vicino!" cyoa-text!
		choice( "Torna piu tardi" )choice
		

;

cyoa-choice: tcarsales-go
	($ Thegroup .. "  acquista unauto nuova di zecca!	" .. "Prendi qualsiasi macchina dal lotto che vuoi!" pbold $) cyoa-text! goodfx
		1 to carkey clear-cyoa -5 trunk-food+ 50 trunk-gas+ someicon
;

cyoa: tcarsales
	"Ogni macchina deve andare!" cyoa-title!
	($ "'Devo togliere queste macchine dal lotto! Puoi acquistare qualsiasi macchina, proprio ora, al prezzo basso di 5 cibi! Prendilo ora! Nessun finanziamento! Chiavi in mano e pronto a partire! Ci aggiungero anche 50 benzina!" pbold $) cyoa-text!
	trunk-food 4 > if "'Questo e un vero affare." +crtext then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " ' non puo permettersi l'acquisto di un'auto nuova di zecca che cambia la vita." $) +crtext
	else
		choice( "Lascia per ora" )choice back-esc
		trunk-food 4 > if
			choice( "Acquista unauto nuova   5 cibi" )choice: tcarsales-go
		then
	then
;

cyoa-choice: tgarage-1
	($ Thegroup .. "  paga per riparazioni complete del telaio e del motore!	" .. "'Quando esci di qui, guida la tua auto un po' piu avanti e la ripareremo!" pbold $) cyoa-text! goodfx
		clear-cyoa -5 trunk-food+ image-car-garage road{ ' mandible-repairgarage stack-road-action }
;

cyoa-choice: tgarage-2
	($ Thegroup .. " xDADB," .. "Quando lasci qui, basta guidare giu per la strada un po al garage. La tua auto otterra IL LAVORO!" pbold $) cyoa-text! goodfx
		clear-cyoa -35 trunk-food+ image-car-garage road{ ' mandible-megagarage stack-road-action }
;

cyoa: tgarage
	"Garage Esperto" cyoa-title!
	($ "Abbiamo un garage pieno di meccanici esperti proprio dietro langolo, e offriamo " pbold .. "LE OPERE" fbold .. ". Mod complete per lauto: corazzatura, aumento della velocita, telaio rinforzato, revisione del motore con maggiore efficienza nei consumi. Tutto per 35 cibo.	Possiamo anche riparare completamente la tua auto senza mod dolci, per 5 cibo." pbold $) cyoa-text!
	trunk-food 34 > if "La macchina perfetta e possibile." +crtext else
		trunk-food 4 > if "Potresti ancora acquistare riparazioni complete." +crtext then
	then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. " ' non puo permettersi questa mod per auto cosi ampia ma di buon gusto." $) +crtext
	else
		choice( "Lascia per ora" )choice back-esc
		trunk-food 34 > if
			choice( "Mod complete per auto, " .. "LE OPERE" fbold .. "    35 cibo" )choice: tgarage-2
		then
		trunk-food 4 > if
			choice( "Riparazioni complete   5 cibo" )choice: tgarage-1
		then
	then
;

0 value mt1-1
0 value mt1-2
0 value mt1-3
0 value mt1-4

cyoa-choice: trade-m1-4
	($ Thegroup .. "  acquista un martello pneumatico a benzina!	" .. "'La scelta del conoscitore per scalpellare orde di zombi!" pbold $) cyoa-text! goodfx
	1 WEAPON_JACKHAMMER trunk.weapon+ -20 trunk-food+ someicon ' mt1-4 ++
;

cyoa-choice: trade-m1-3
	($ Thegroup .. "  acquista una motosega a benzina! Un classico!	" .. "'Tutti sanno che questa e LA risposta per gli zombi! Finche non finisci la benzina." pbold $) cyoa-text! goodfx
	1 WEAPON_CHAINSAW trunk.weapon+ -10 trunk-food+ someicon ' mt1-3 ++
;

cyoa-choice: trade-m1-2
	($ Thegroup .. "  acquista un soffiatore a benzina!	" .. "Fai saltare in aria gli zombi! Come le foglie!" pbold $) cyoa-text! goodfx
	1 WEAPON_BLOWER trunk.weapon+ -5 trunk-food+ someicon ' mt1-2 ++
;

cyoa-choice: trade-m1-1
	($ Thegroup .. "  acquista un tagliaerba a gas!	" .. "Eccellente per tagliare le orde di zombi! Abbastanza decente per le erbacce." pbold $) cyoa-text! goodfx
	1 WEAPON_WHACKER trunk.weapon+ -5 trunk-food+ someicon ' mt1-1 ++
;

cyoa: tshopgas
	"Strumenti a gas" cyoa-title!
	($ "'Non parlare con l'altro ragazzo! C'e qualcosa nelle armi a gas che semplicemente non puoi ottenere dall'elettricita." pbold $) cyoa-text!
	trunk-food 4 > if "E ora di accelerare." +crtext then
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. "  e figurativamente senza benzina. Il commerciante ha un po di pieta di te e ti distribuisce un po di benzina letterale." $) +crtext clear-cyoa +30 trunk-gas+ goodfx
	else
		choice( "Lascia per ora" )choice back-esc
		trunk-food 19 > mt1-4 1 < and if
			choice( "Jackhammer" )choice: trade-m1-4
		then
		trunk-food 9 > mt1-3 3 < and if
			choice( "10 wood" )choice: trade-m1-3
		then
		trunk-food 4 > mt1-2 3 < and if
			choice( "Soffiatore di foglie   5 cibo" )choice: trade-m1-2
		then
		trunk-food 4 > mt1-1 3 < and if
			choice( "Tagliaerba   5 cibo" )choice: trade-m1-1
		then
	then
;

0 value mt2-1
0 value mt2-2
0 value mt2-3
0 value mt2-4

: mt2-firstbuy 
	mt2-4 1 < mt2-3 1 < mt2-2 1 < mt2-1 1 < and and and
;

: mt2-firstbuy-done
	($ "Goditi il tuo " pbold .. "DOPPIO CARICO!" fbold $) +crtext
;

cyoa-choice: trade-m2-4
	($ Thegroup .. " acquista un martello pneumatico elettrico!" .. "'E semplicemente cosi ecologico!" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 255 else 130 then WEAPON_EJHAMMER trunk.weapon+ -20 trunk-food+ someicon ' mt2-4 ++ ;

cyoa-choice: trade-m2-3
	($ Thegroup .. " acquista una motosega elettrica! Ti sentirai a disagio e compiaciuto nel salvare lambiente nellapocalisse." .. "Eccellente per il compostaggio degli zombi!" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 160 else 80 then WEAPON_ECHAINSAW trunk.weapon+ -10 trunk-food+ someicon ' mt2-3 ++ ;
	
cyoa-choice: trade-m2-2
	($ Thegroup .. " acquista un soffiatore elettrico per foglie!" .. "Guida delicatamente gli zombi, unalternativa pacifista a tutto questo schianto!" pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 100 else 50 then WEAPON_EBLOWER trunk.weapon+ -5 trunk-food+ picon ' mt2-2 ++ ;

cyoa-choice: trade-m2-1
	($ Thegroup .. " acquista un tagliaerba elettrico!" .. "Ha molteplici usi efficaci nei metodi di coltivazione di Zomboculture." pbold $) cyoa-text! goodfx
	mt2-firstbuy if mt2-firstbuy-done 100 else 50 then WEAPON_EWHACKER trunk.weapon+ -5 trunk-food+ picon ' mt2-1 ++ ;

cyoa: tshopelec
	"Attrezzi elettrici" cyoa-title!
	($ "'Non parlare con l'altro ragazzo! Gli utensili elettrici sono ecologici, consentendoti di ridurre le emissioni di gas mentre falci gli zombi." pbold $) cyoa-text!
	basic-tone foodamt
	trunk-food 5 < if
		($ Thegroup .. "  non ha abbastanza cibo per comprare qualcosa!" $) +crtext
		($ "Il mercante si impietosisce di " .. thegroup .. " e distribuisce un " .. "SOFFIATORE PER FOGLIE SCONTATO!" rbold $) +crtext goodfx 30 WEAPON_EBLOWER trunk.weapon+ clear-cyoa
	else
		mt2-firstbuy if
			($ "Come offerta speciale, il commerciante " .. "DOPPIO ADDEBITO" rainbold .. "  il tuo primo acquisto. Avra il doppio dellenergia di quella che normalmente troveresti!" $) +crtext
		then
		choice( "Lascia per ora" )choice back-esc
		trunk-food 19 > mt2-4 1 < and if
			choice( "Martello pneumatico elettrico   20 cibo" )choice: trade-m2-4
		then
		trunk-food 9 > mt2-3 2 < and if
			choice( "Motosega elettrica   10 cibo" )choice: trade-m2-3
		then
		trunk-food 4 > mt2-2 2 < and if
			choice( "Soffiafoglie elettrico   5 cibo" )choice: trade-m2-2
		then
		trunk-food 4 > mt2-1 2 < and if
			choice( "Tagliaerba elettrico   5 cibo" )choice: trade-m2-1
		then
	then
;

0 value mechtrained+

: mech-train-amt numhums 2 * mechtrained+ 1 + * 1 + ; 
: mech-train-pay? trunk-food mech-train-amt 1 - > notsolo notsolohuman and and ;
: mech-train-pay mech-train-amt -1 * trunk-food+ ;

: mech-solo-amt mechtrained+ 2 * 3 + ;
: mech-solo-pay? trunk-food mech-solo-amt 1 - > ;
: mech-solo-pay mech-solo-amt -1 * trunk-food+ ;

cyoa-choice: mech-school-train
	cchar .mechanical@ 6 < if
		($ cname .. " ' osserva attentamente mentre il Professore colpisce davvero l'auto con una chiave inglese. Quindi e questo l'ingegneria!" $) goodfx
	else
		($ cname .. "  ha gia ottenuto la laurea in ingegneria da questa prestigiosa accademia! Il tuo ultimo pagamento viene preso come donazione per costruire una nuova ala per la scuola." $) badfx
	then cyoa-text! 
	mech-solo-pay mechanicREV mechanic++ ' mechtrained+ ++
;

cyoa-choice: mech-school-group
	($ "'L'intero gruppo riceve formazione meccanica! Tutti osservano il Professore colpire l'auto con una chiave inglese. E molto illuminante." $) cyoa-text! goodfx
	mech-train-pay someicon
	regiondef{ ' trade-mechanic-group! foreach-human-char } numhums ' mechtrained+ +!
;

cyoa: mech-school-give
	solohuman if
		choosehuman mech-school-train
	then;
	"Chi dovrebbe ottenere questa vasta conoscenza meccanica?" cyoa-text!
	' mech-school-train cyoa-human-choice
;

cyoa: schooltrader
	($ "La scuola degli attrezzi" $) cyoa-title!
	"Lingegneria meccanica e una vera scienza. A differenza della medicina, che e piu unarte. Simile allorigami, o forse alla danza interpretativa." pbold text!
	"Il professore di meccanica colpisce ripetutamente il telaio dellauto distrutta con una chiave inglese mentre dice questo, senza mai staccare lo sguardo." +crtext foodamt
		mech-solo-pay? not if
			"'Non puoi permetterti altro allenamento." +crtext 
		then;
		choice( "Lascia per ora" )choice back-esc
		mech-solo-pay? if
			choice( "Dai " .. mech-solo-amt .. "  cibo per allenamento singolo" )choice: mech-school-give
		then
		mech-train-pay? if
			choice( "Dai " .. mech-train-amt .. "  cibo per laddestramento di gruppo" )choice: mech-school-group
		then
		
;

cyoa-bridge: robopunch-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	10 recruit-yes-food 
	($ "Un robot che distribuisce botte? La tecnologia e piuttosto interessante." $) +crtext
	"#CARICAMENTO PROTOCOLLI DI PUGNO#	#PRONTO A PUGNO#" pbold +crtext
	recruitee .strengthREV recruitee .fitnessREV
;

cyoa: robopunch
	"Il pugno meccanico perfetto" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"#IO SONO PUGNO BOT#" pbold cyoa-text! basic-tone
	"'Ora stiamo arrivando da qualche parte! Questo robot e progettato per uno scopo: pugni." +crtext
	trunk-food 9 > if
		choice( "Lascialo per ora" )choice
		choice( "Acquista PUNCHBOT   10 cibo" )choice: robopunch-go
	else
		"Hai bisogno di 10 cibo per acquistare questo bot." +crtext
		choice( "'Non posso risparmiare il cibo" )choice
	then
;

cyoa-bridge: robospeed-go
	ricon 4 recruitee .morale! clear-cyoa 0.25 recruitee .ai.wander_chance! 0 nearest .unseen!
	15 recruit-yes-food 
	($ "Il robot fa una ciambella molto velocemente. Cosi veloce." $) +crtext
	"#CARICAMENTO PROTOCOLLI DI VELOCITA#	#HO BISOGNO DI ANDARE VELOCE#" pbold +crtext
	recruitee .strengthREV recruitee .fitnessREV
;

cyoa: robospeed
	"Cosi veloce" cyoa-title!
	nearest .character@ choosechar cchar to recruitee ricon
	"#IO SONO VELOCITA BOT#" pbold cyoa-text! basic-tone
	"'Con le sue linee eleganti e i minuscoli coprimozzi rotanti, anche da fermo, questo robot trasuda il concetto di velocita.	Inoltre, ha appena pronunciato la parola velocita." +crtext
	trunk-food 14 > if
		choice( "Lascialo per ora" )choice
		choice( "Acquista SPEEDBOT   15 cibo" )choice: robospeed-go
	else
		"Hai bisogno di 15 cibo per comprare questo speed bot." +crtext
		choice( "'Non posso risparmiare il cibo" )choice
	then
;

}

roomgen{


: .ct1 args( ch ) // Mech Teacher
	WEAPON_WRENCH <- ch .weapona!
;
: .ct2 args( ch ) // Gas tools
	26 <- ch .glassestype! // Gas Mask
;
: .ct3 args( ch ) // Elec tools
	21 <- ch .glassestype! // Green Goggles
;
: .ct4 args( ch ) // Garage
	1 <- ch .female!
	25 <- ch .hattype!
	6 <- ch .legstype!
	3 <- ch .torsotype!
	0 <- ch .glassestype!
;
: .ct5 args( ch ) 
	7 <- ch .hattype! // Cowboy hat
;
: .ct6 args( ch ) 
	<- ch .speedbotify
;
: .ct7 args( ch ) 
	<- ch .punchbotify
;

: camptradercommon
	still-npc regiondef{ ' trader-event lt .events! 1 lt .timer! }
	
;

: schooltrader
	camptradercommon .ct1 cyoa{ ' schooltrader cyoa! }
;

: robopunch
	camptradercommon .ct7 cyoa{ ' robopunch cyoa! }
;

: robospeed
	camptradercommon .ct6 cyoa{ ' robospeed cyoa! }
;

: roboprof
	// camptradercommon .ct8 cyoa{ ' roboprof cyoa! }
;

: tshopgas
	camptradercommon .ct2 cyoa{ ' tshopgas cyoa! }
;

: tshopelec
	camptradercommon .ct3 cyoa{ ' tshopelec cyoa! }
;

: tgarage
	camptradercommon .ct4 cyoa{ ' tgarage cyoa! }
;

: tcarsales
	camptradercommon .ct5 cyoa{ ' tcarsales cyoa! }
;

		0 stack
			' camptradercommon shove
			' camptradercommon shove
			' camptradercommon shove
			' camptradercommon shove
		value preptrader-base
		preptrader-base shuffles var preptrader-picker

: preppertrader
	preptrader-picker depths 0= if preptrader-base shuffles to preptrader-picker then pops nip execute ;


}

locdef{

location: toolshop
		max-rooms: 1
		entry-tags: toolshop
		script: set-theme-restaurant
		pre-script: set-theme-restaurant
		post-script: nad 0 %zombs
				
		room-choice: toolshop
			room-weight: 10
			room-count: 1
			room-post-script: 
			filter-only
			
location: toolschool
		max-rooms: 1
		entry-tags: toolschool
		script: set-theme-bookstore
		pre-script: set-theme-bookstore
		post-script: nad 0 %zombs
				
		room-choice: toolschool
			room-weight: 10
			room-count: 1
			room-post-script: 
			filter-only
		
location: roboshack
		max-rooms: 1
		entry-tags: roboshack
		script: factory-theme
		pre-script: factory-theme
		post-script: nad 0 %zombs
				
		room-choice: roboshack
			room-weight: 10
			room-count: 1
			room-post-script:
			filter-only
			
}
		
root{

: prepper-magchance
	20 %chance if
		magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! drift
	then
;

: sale-car 
	1 22 rnd spawncar mission{ cyoa{ ' cyoa-sale-car cyoa! } }
	0 lt .action_icon!
;

}
		
map{ regiondef{

trade-camp-reset

0 value gbx
0 value gby
0 value gbx2
0 value gby2
0 value gbx3
0 value gby3
0 value bfx
0 value bfy

: bldg tanchor!
	"roboshack" $picklocid addloc
	"ROBO SHACK" sign-text! prefab{ 1 5 1 shop } 
;
: bldg2 tanchor!
	"toolshop" $picklocid addloc
	"TOOL SHOP" sign-text! prefab{ 2 5 1 shop } 
;
: bldg3 tanchor!
	"toolschool" $picklocid addloc
	"TOOL SCHOOL" sign-text! prefab{ 3 6 1 shop } 
;


' plotgrass >defer ft

: pt
	xyp.xy ft // Plot the current floor by default
	
	xyp.i char { = if xyp.xy to gby2 to gbx2 then;
	xyp.i char } = if gbx2 1- gby2 xyp.xy swap 1+ swap gravellot then;
	xyp.i char ( = if xyp.xy to gby to gbx then;
	xyp.i char ) = if gbx 1- gby xyp.xy swap 1+ swap gravellot then;
	xyp.i char [ = if xyp.xy to gby3 to gbx3 then;
	xyp.i char ] = if gbx3 1- gby3 xyp.xy swap 1+ swap gravellot then;
	
	xyp.i char 1 = if xyp.x to bfx then;
	xyp.i char 2 = if bfx xyp.y xyp.x barbfence then;

	xyp.i char t = if sale-car lt-here then;
	xyp.i char U = if tnomey-spawn then;
	xyp.i char W = if trader-weapon-buyer then;
	xyp.i char G = if trader-food-for-gas then;
	xyp.i char S = if tgarage lt-here then;
	xyp.i char M = if tcarsales lt-here then;
		
	xyp.i char @ = if xyp.xy bldg then;
	xyp.i char 3 = if xyp.xy bldg2 then;
	xyp.i char 4 = if xyp.xy bldg3 then;
	
	xyp.i char p = if ' plotpave to ft then;
	xyp.i char $ = if xyp.xy grassedge then;
;

"
...........................
1.........................2
...........................
..................[....t...
...........................
...........................
...........................
.......L..........ML...t...
..{........................
.@....3....4...............
...........................
............(...}......t...
..................S........
...........................
..........................]
...........................
1..........2....1.........2
......G..U...........W....p
$$$$$$$$$$$$....$$$$$$$$$$$
...............)...........
...........................
...........................
...........................
...........................
...........................
...........................
..........................."
constant maplayout

: specialcamp-6
	' plotgrass to ft
	0 -1 maplayout xyplot: pt
;


: site-setup

	// Clear the map with grass
	-1 -1 w h grassbox

	pavement
// Plot trader stuff
	specialcamp-6

	-1 -1 w 3 conclot // Top
	-1 h 2 - w h conclot // Bottom

	0 h 6 - w ' plotroadlines hline
	

	1 no_time!
	7 14 rnd o'clock
	// 50 %chance if 1 2 frnd weather! -1 -2 frnd wind_force! else 1 2 frnd wind_force! then
	
	mission{ no-edge-spawns }
	
	20 %mv-up
	25 %chars-up
;

	 region: specialcamp-6
		w: maplayout layoutw
		h: maplayout layouth

		trade-mode

		post-script: site-setup
} }

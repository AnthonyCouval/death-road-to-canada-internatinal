uses paul/region-layout.df

"events/inner/arcadeloc.df" $load

"
:map 1
:tags traderarcade  
:scriptA arcademachine -1 movey 
:scriptB cashcounter -1 movey 0.2 movex
:scriptC traderarcade-npc -0.5 movey 0.4 movex
:scriptD traderarcade-shoot -1 movey 
:scriptE traderarcade-rp -1 movey 
:scriptF clawmachine -1 movey
:scriptG traderarcade-fire -1 movey
:scriptH traderarcade-axe -1 movey
:size 14 9 
:off 8 0 
:tiles 
22222222222000
23333333332222
2AEADFGAHA3332
21111111111112
2111111111BC12
21111111111112
21111111111112
22222222222222
33333333333333
"
value TRADERARCADE
TRADERARCADE $parse-rooms

locdef{

location: traderarcade
		max-rooms: 1
		entry-tags: traderarcade
		pre-script: yall-theme
		script: yall-theme
		post-script: 
		
		room-choice: traderarcade
			room-weight: 10
			room-count: 1
			room-script: ndx 
			room-post-script:
			filter-only

}

cyoa{

// Shooting Arcade Machine

0 value traderarcade-shoot+

: traderarcade-amt traderarcade-shoot+ 2 * 2 + ;
: traderarcade-pay? trunk-food traderarcade-amt 1 - > ;
: traderarcade-pay traderarcade-amt -1 * trunk-food+ ;

cyoa-choice: traderarcade-mech
	($ "Ei! Deixe a maquina de fliperama em paz, seu nerd!" bbold $) text!
	"O trabalhador do fliperama nao esta feliz." +crtext
	($ cname .. " recua." $) +crtext 
	mechanicREV badfx
;

cyoa-choice: traderarcade-shoot-go
	($ cname .. " ' continua o jogo de fliperama. A tela esta cheia de formas em movimento. E realmente impossivel dizer o que esta acontecendo e, ainda assim, " .. che .. "  se sente completamente absorto. O jogo termina em um flash, mas e gravado em " .. cname .. "''s mente. Ainda nao ha pista sobre como o jogo e chamado." $) cyoa-text!
		shootingREV shooting++ goodfx traderarcade-pay ' traderarcade-shoot+ ++
;

cyoa: traderarcade-shoot
	"Jogo de Fliperama Misterioso" cyoa-title!
	picon!
	($ "Esta maquina de fliperama tem seu slot de moeda substituido por uma grande lixeira que diz " .. "COLOQUE COMIDA AQUI" rbold .. " ' acima dele. Nem mesmo esta claro que jogo deveria ser, a unica coisa na tela e uma mensagem que diz: " $) cyoa-text!
	($ "CONTINUAR: " fbold .. traderarcade-amt rainbold .. "  COMIDA" rainbold $) +text
	+cr foodamt
	chumany? not if
		($ pname .. " ' olha para a maquina de fliperama em confusao. Os animais geralmente nao jogam videogames, com algumas excecoes. Por exemplo, vermes achatados podem jogar simuladores de caminhada, mesmo que nao consigam entender o genio sublime da narracao." $) +crtext
	then;
		traderarcade-pay? not if
			"'Voce nao pode pagar por isso." +crtext 
		then;
	choice( "Deixe para depois" )choice
	pchar charchoice( "  joga por " .. traderarcade-amt .. "  comida" )choice: traderarcade-shoot-go
	cmechanic+ if
		pchar charchoice( "  desmonta a maquina de fliperama" )choice: traderarcade-mech
	then
;

// Road Punchers Machine

0 value rp-saguts
0 value rp-bryu

cyoa-choice: traderarcade-rp-done2
	rp-saguts 0 > rp-bryu 0 > and if
		clear-cyoa
		"Esta maquina de fliperama de repente da um curto circuito!" text!
		"NO! WHAT DID YOU DOOOOOOOOOO?" bbold +crtext
		"O funcionario do fliperama esta menos feliz do que o possivel." +crtext badfx
	then 
;

: traderarcade-rp-done
	 // -10 add-food pchar .loot-to-trunk 
	 -10 trunk-food+ rp-saguts 0 > rp-bryu 0 > and if traderarcade-rp-done2 then 
;

cyoa-bridge: rp_recruit
	nearest .character@ choosechar cchar to recruitee ricon clear-cyoa
	($ cname .. "  esta pronto para testar sua forca na estrada de socos." $) text!
	recruitee .strengthREV recruitee .fitnessREV
	cchar .name@ "BRYU" $= if "'HADOOK! HADOOK! KADOOSHIN'!" else "I GER! I GER! I GER UPPERCUT!" then fbold +crtext recruit-yesno
	0.25 cchar .ai.wander_chance! 0 nearest .unseen!	
;

: rp_pop_share
	lt mission{ .smokepoof }
	0.01 lt .character@ .ai.wander_chance! 0 lt .character@ .ai.loot_chance! 1 lt .unseen!
	-30 lt .fade!
	90 lt .stun!
	180 lt .aim_angle! goodfx
	' rp_recruit cyoa!
;

: bryu_pop
	new-npc lt .character@ .bryuify rp_pop_share
;

: saguts_pop
	new-npc lt .character@ .sagutsify rp_pop_share
;

: traderarcade-bryu	bryu_pop ' rp-bryu ++ traderarcade-rp-done ;
: traderarcade-saguts saguts_pop ' rp-saguts ++ traderarcade-rp-done ;

cyoa: traderarcade-rp
	"Road Punchers 2: Os Novos Socadores" cyoa-title!
	picon!
	($ "Esta e uma antiga maquina de fliperama do famoso jogo de luta classico: " .. "ROAD PUNCHERS" bbold .. "
	This machine has been converted to run on food instead of quarters. It's 10 food to pick a character. For reasons unknown, it looks like almost all the characters are grayed out..." $) cyoa-text!
	+cr foodamt
		trunk-food 10 < if
			"'Voce nao tem comida suficiente para jogar!" +crtext 
		then;
	choice( "Deixe para depois" )choice
	rp-saguts 1 < if
		choice( "Escolha SAGUTS   10 comida" )choice: traderarcade-saguts
	then
	rp-bryu 1 < if
		choice( "Escolha BRYU   10 comida" )choice: traderarcade-bryu
	then
;
	
// Fireproofing Arcade

cyoa-choice: traderarcade-fire-go
	"Forjado no Fogo" cyoa-title!
	($ "Ao colocar a comida e tocar no botao, a maquina de fliperama " .. "EXPLODE IMEDIATAMENTE!" fbold .. "  Realmente Maduro!" $) cyoa-text!
	cchar statmod{ .fireproof? } not if
		($ cname .. "  e queimada em uma grande bola de fogo! No entanto, " .. che .. " esta vivo e apenas levemente irritado." .. cname .. "a pele de ' parece estranhamente fria." bbold $) +crtext
		cchar statmod{ .fireproof } sting-- bitefx
		choice( "AI! DROGA!" )choice
	else
		($ cname .. " ' fica envolto em uma bola de fogo! Claro, isso nao faz nada.		" .. cname .. " se sente muito satisfeito com isso." bbold $) +crtext
		goodfx morale2+
	then
	-10 trunk-food+ clear-cyoa deleteme
;

cyoa: traderarcade-fire
	"Maquina de fliperama estranha" cyoa-title!
	picon!
	($ "Esta e uma nova maquina de fliperama. Ha letras gigantes na tela que formam: " .. "FIRE EXPLODERS" rainbold .. "
	This machine has been converted to run on food, so the coin slot is enormous. There's only one button. Instead of any sort of gameplay, the machine displays a roaring fire behind the title..." $) cyoa-text!
	+cr foodamt
		trunk-food 10 < if
			"'Voce nao tem comida suficiente para jogar!" +crtext 
		then;
	choice( "Deixe para depois" )choice
		pchar charchoice( "  joga por 10 comidas" )choice: traderarcade-fire-go
;

cyoa-choice: traderarcade-axe-go
	($ "Um machado voador gira cada vez mais perto e entao " .. "VOA PELA TELA!" fbold .. " ' Eles nao fazem mais jogos como este!" $) cyoa-text!
	cfitness+ cstrength+ or if
		($ cname .. "  esta fisicamente apto o suficiente para desviar o machado. " .. cHe .. "  sente se muito feliz porque o machado foi segurado pelo cabo, nao pela lamina." $) +crtext morale++ goodfx fitnessREV strengthREV
	else
		($ cname .. " ' e atingido bem no rosto pelo machado, cabo primeiro! O funcionario do fliperama fica muito feliz com isso, pois e menos limpeza do que o outro lado teria exigido." $) +crtext ouch-- bitefx
		alive? if morale-- then fitnessREV strengthREV
	then
	"A tela falha quando o machado cai no chao." +crtext
	clear-cyoa
;

: traderarcade-axe-hard goldenaxe2 drop southofcyoa drop traderarcade-axe-go ;
: traderarcade-axe-normal goldenaxe1 drop southofcyoa drop traderarcade-axe-go ;

cyoa: traderarcade-axe
	"SELECAO DE DIFICULDADE" cyoa-title!
	picon!
	($ "'Esta maquina da uma vibe dos anos 80. Ha letras douradas na tela que dizem " .. "MACHADO*BRILHANTE" rainbold .. "	A maquina de fliperama parece funcionar com fichas de outra rede de fliperamas " .. pname .. " notices this and turns to look at the arcade worker, who just gives a listless shrug and looks away." $) cyoa-text!
	($ "Alguem deve ter colocado um token antes de voce chegar aqui, a tela diz " .. "CREDITO RESTANTE!" fbold .. "  e " .. "SELECIONE UMA DIFICULDADE:" rbold $) +crtext
	choice( "Deixe para depois" )choice
	pchar charchoice( "  joga no modo NORMAL" )choice: traderarcade-axe-normal
	pchar charchoice( "  joga no modo DIFICIL" )choice: traderarcade-axe-hard
;


// NPC

cyoa: traderarcade
	"Funcionario de fliperama" cyoa-title!
	"'Estou trazendo os fliperamas de volta! Eles vao ficar muito populares agora que a internet foi destruida. Nenhum reembolso." bbold text!
;

}

roomgen{

: tradercade-box
	gasarcademachine
	' arcade-events events!
	5 lt .timer!
;

: traderarcade-shoot
	tradercade-box cyoa{ ' traderarcade-shoot cyoa! }
;

: traderarcade-rp
	tradercade-box cyoa{ ' traderarcade-rp cyoa! }
;

: traderarcade-fire
	tradercade-box cyoa{ ' traderarcade-fire cyoa! }
;

: traderarcade-axe
	tradercade-box cyoa{ ' traderarcade-axe cyoa! }
;

: .arcadify args( chara )
	"Arcader" <- chara .name!
;

: traderarcade-npc
	still-npc .arcadify cyoa{ ' traderarcade cyoa! } ;
	// regiondef{ ' trader-event lt .events! 1 lt .timer! }
}

map{ regiondef{

trade-camp-reset

: bldg tanchor!
	"traderarcade" $picklocid addloc 
		prefab{
		BLINKY "O ULTIMO " .. FLASHY .. "ARCADE" .. sign-text!
		0 0 0 sign-bg!
		0 1 0 sign-border!
		1 1 1 sign-fg!
		1 sign-glow!
		0 3 rnd 8 4 shop }
	
;
	
0 value gbx
0 value gby
0 value gbx2
0 value gby2

' plotpave >defer ft

: pt
	xyp.xy ft // Plot the current floor by default

	xyp.i char < = if xyp.xy to gby to gbx then;
	xyp.i char > = if gbx 1- gby xyp.xy swap 1+ swap gravellot then;
	xyp.i char ( = if xyp.xy to gby2 to gbx2 then;
	xyp.i char ) = if gbx2 1- gby2 xyp.xy swap 1+ swap gravellot then;
		
	xyp.i char P = if ' plotpave to ft xyp.xy ft then;
	
	xyp.i char x = if ' plotdirt to ft then;
	xyp.i char G = if ' plotgrass to ft xyp.xy ft then;
	xyp.i char $ = if xyp.xy grassedge then;
	xyp.i char { = if dirtstart then;
	xyp.i char } = if dirtend then;

	xyp.i char t = if pictable2 lt-here 8 rdrift then;		
	xyp.i char w = if pine lt-here drift then;
	xyp.i char V = if tradercarhere then;
	xyp.i char f = if campfirehere then;
		
	xyp.i char W = if trader-weapon-buyer then;
	xyp.i char T = if traderhere then;
	xyp.i char Z = if tnomey-spawn then;
	xyp.i char g = if trader-food-for-gas then;
	
	xyp.i char @ = if xyp.xy bldg then;
;

"
P........................
(........................
.........................
.........................
.........................
.........................
.........................
....W....@.........g..Z.)
.........................
.........................
.........................
.........................
.........................
.........................
........................."
constant maplayout

: plotmap
	0 -1 maplayout xyplot: pt
;

: site-setup
	1 no_time!
	17 19 rnd o'clock

	' plotdirt to ft

	30 %chance if 0 0.2 frnd weather! -1 2 frnd wind_force! else -1 1 frnd wind_force! then

	plotmap
	mission{ no-edge-spawns }

	// Put char and chars in middle of map
	25 %mv-up
	45 %chars-up
	
	map{ street-junk }
;

region: specialcamp-3
	w: maplayout layoutw
	h: maplayout layouth

	trade-mode

	post-script: site-setup

} }

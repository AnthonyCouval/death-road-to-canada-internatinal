uses paul/region-layout.df

"events/inner/arcadeloc.df" $load

"
:map 1
:tags traderarcade  
:scriptA arcademachine -1 movey 
:scriptB cashcounter -1 movey 0.2 movex
:scriptC traderarcade-npc -0.5 movey 0.4 movex
:scriptD traderarcade-shoot -1 movey 
:scriptE traderarcade-rp -1 movey 
:scriptF clawmachine -1 movey
:scriptG traderarcade-fire -1 movey
:scriptH traderarcade-axe -1 movey
:size 14 9 
:off 8 0 
:tiles 
22222222222000
23333333332222
2AEADFGAHA3332
21111111111112
2111111111BC12
21111111111112
21111111111112
22222222222222
33333333333333
"
value TRADERARCADE
TRADERARCADE $parse-rooms

locdef{

location: traderarcade
		max-rooms: 1
		entry-tags: traderarcade
		pre-script: yall-theme
		script: yall-theme
		post-script: 
		
		room-choice: traderarcade
			room-weight: 10
			room-count: 1
			room-script: ndx 
			room-post-script:
			filter-only

}

cyoa{

// Shooting Arcade Machine

0 value traderarcade-shoot+

: traderarcade-amt traderarcade-shoot+ 2 * 2 + ;
: traderarcade-pay? trunk-food traderarcade-amt 1 - > ;
: traderarcade-pay traderarcade-amt -1 * trunk-food+ ;

cyoa-choice: traderarcade-mech
	($ "EHI! LASCIA STARE LA MACCHINA ARCADE, CRETINO!" bbold $) text!
	"Il lavoratore dellarcade non e felice." +crtext
	($ cname .. "  sussulta." $) +crtext 
	mechanicREV badfx
;

cyoa-choice: traderarcade-shoot-go
	($ cname .. " ' continua il gioco arcade. Lo schermo e pieno di forme in movimento. E davvero impossibile capire cosa stia succedendo, eppure " .. che .. "  e completamente assorto. Il gioco finisce in un lampo, ma e impresso nella mente di " .. cname .. "''. Non c'e ancora alcun indizio su come si chiami il gioco." $) cyoa-text!
		shootingREV shooting++ goodfx traderarcade-pay ' traderarcade-shoot+ ++
;

cyoa: traderarcade-shoot
	"Gioco arcade misterioso" cyoa-title!
	picon!
	($ "Italiano " .. "METTI IL CIBO QUI" rbold .. " ' sopra. Non e neanche chiaro a quale gioco si riferisca, l'unica cosa sullo schermo e un messaggio che dice: " $) cyoa-text!
	($ "CONTINUA: " fbold .. traderarcade-amt rainbold .. "  CIBO" rainbold $) +text
	+cr foodamt
	chumany? not if
		($ pname .. " ' guarda la macchina arcade confuso. Gli animali per lo piu non giocano ai videogiochi, con alcune eccezioni. Ad esempio, i vermi piatti possono giocare a simulatori di camminata, anche se non riescono a comprendere il sublime genio della narrazione." $) +crtext
	then;
		traderarcade-pay? not if
			"'Non puoi permettertelo." +crtext 
		then;
	choice( "Lascialo per ora" )choice
	pchar charchoice( "  gioca per " .. traderarcade-amt .. " cibo" )choice: traderarcade-shoot-go
	cmechanic+ if
		pchar charchoice( "  smonta la macchina arcade" )choice: traderarcade-mech
	then
;

// Road Punchers Machine

0 value rp-saguts
0 value rp-bryu

cyoa-choice: traderarcade-rp-done2
	rp-saguts 0 > rp-bryu 0 > and if
		clear-cyoa
		"Questa macchina arcade improvvisamente va in cortocircuito!" text!
		"NO! WHAT DID YOU DOOOOOOOOOO?" bbold +crtext
		"Laddetto allarcade e meno felice di quanto si pensasse possibile." +crtext badfx
	then 
;

: traderarcade-rp-done
	 // -10 add-food pchar .loot-to-trunk 
	 -10 trunk-food+ rp-saguts 0 > rp-bryu 0 > and if traderarcade-rp-done2 then 
;

cyoa-bridge: rp_recruit
	nearest .character@ choosechar cchar to recruitee ricon clear-cyoa
	($ cname .. "  e pronto a testare la sua forza sulla strada del pugno." $) text!
	recruitee .strengthREV recruitee .fitnessREV
	cchar .name@ "BRYU" $= if "'HADOOK! HADOOK! KADOOSHIN'!" else "I GER! I GER! I GER UPPERCUT!" then fbold +crtext recruit-yesno
	0.25 cchar .ai.wander_chance! 0 nearest .unseen!	
;

: rp_pop_share
	lt mission{ .smokepoof }
	0.01 lt .character@ .ai.wander_chance! 0 lt .character@ .ai.loot_chance! 1 lt .unseen!
	-30 lt .fade!
	90 lt .stun!
	180 lt .aim_angle! goodfx
	' rp_recruit cyoa!
;

: bryu_pop
	new-npc lt .character@ .bryuify rp_pop_share
;

: saguts_pop
	new-npc lt .character@ .sagutsify rp_pop_share
;

: traderarcade-bryu	bryu_pop ' rp-bryu ++ traderarcade-rp-done ;
: traderarcade-saguts saguts_pop ' rp-saguts ++ traderarcade-rp-done ;

cyoa: traderarcade-rp
	"Road Punchers 2: The New Punchers" cyoa-title!
	picon!
	($ "Questa e una vecchia macchina arcade per il famoso classico gioco di combattimento: " .. "PUGNI DA STRADA" bbold .. "
	This machine has been converted to run on food instead of quarters. It's 10 food to pick a character. For reasons unknown, it looks like almost all the characters are grayed out..." $) cyoa-text!
	+cr foodamt
		trunk-food 10 < if
			"'Non hai il cibo necessario per giocare!" +crtext 
		then;
	choice( "Lascialo per ora" )choice
	rp-saguts 1 < if
		choice( "Scegli SAGUTS   10 cibo" )choice: traderarcade-saguts
	then
	rp-bryu 1 < if
		choice( "Scegli BRYU   10 cibo" )choice: traderarcade-bryu
	then
;
	
// Fireproofing Arcade

cyoa-choice: traderarcade-fire-go
	"Forgiato nel fuoco" cyoa-title!
	($ "Dopo aver messo il cibo e premuto il pulsante, la macchina arcade " .. "ESPLODE IMMEDIATAMENTE!" fbold .. "  Davvero maturo!" $) cyoa-text!
	cchar statmod{ .fireproof? } not if
		($ cname .. "  viene bruciato in una grande palla di fuoco! Tuttavia, " .. che .. "  e vivo e solo leggermente irritato.		" .. cname .. "'La pelle di 's e stranamente fredda." bbold $) +crtext
		cchar statmod{ .fireproof } sting-- bitefx
		choice( "AIUTO! DANNAZIONE!" )choice
	else
		($ cname .. " ' viene avvolto da una palla di fuoco! Ovviamente, questo non fa nulla.		" .. cname .. "  si sente piuttosto compiaciuto di questo." bbold $) +crtext
		goodfx morale2+
	then
	-10 trunk-food+ clear-cyoa deleteme
;

cyoa: traderarcade-fire
	"Macchina arcade strana" cyoa-title!
	picon!
	($ "Questo e una macchina da gioco nuova e affascinante. Ci sono lettere giganti in schermo che scrivono: " .. "ESPLODONO IL Fuoco" rainbold .. "
	This machine has been converted to run on food, so the coin slot is enormous. There's only one button. Instead of any sort of gameplay, the machine displays a roaring fire behind the title..." $) cyoa-text!
	+cr foodamt
		trunk-food 10 < if
			"'Non hai il cibo necessario per giocare!" +crtext 
		then;
	choice( "Lascialo per ora" )choice
		pchar charchoice( " gioca per 10 cibi" )choice: traderarcade-fire-go
;

cyoa-choice: traderarcade-axe-go
	($ "Un martello volante si avvicina e si avvicina, e poi " .. "VOLA INTRAPRENDERE IL TELLO SCHERMATA!" fbold .. " ' Questi giochi non si producono piu!" $) cyoa-text!
	cfitness+ cstrength+ or if
		($ cname .. "  e abbastanza fit per riflettere lascia. " .. cHe .. "  si sente molto felice di aver manovrato la testata prima, non la punta." $) +crtext morale++ goodfx fitnessREV strengthREV
	else
		($ cname .. " ' viene schiacciato direttamente al volto dall'ascia, testata in primo! Il lavoratore dell'arcade e abbastanza felice per questo, poiche meno pulizia di quello che sarebbe richiesto l'altra parte." $) +crtext ouch-- bitefx
		alive? if morale-- then fitnessREV strengthREV
	then
	"Lo schermo ha un guasto quando lascia cade a terra." +crtext
	clear-cyoa
;

: traderarcade-axe-hard goldenaxe2 drop southofcyoa drop traderarcade-axe-go ;
: traderarcade-axe-normal goldenaxe1 drop southofcyoa drop traderarcade-axe-go ;

cyoa: traderarcade-axe
	"SELEZIONE DI GRAVEZZA" cyoa-title!
	picon!
	($ "'Questa macchina da una sensazione degli anni 80. Ci sono lettere dorate in schermo che scrivono: " .. "ASCIA*LUCIDA" rainbold .. "	La macchina arcade sembra funzionare con gettoni di unaltra catena di sale giochi " .. pname .. " si accorge di questo e si gira per guardare laddetto allarcade, che si limita a scrollare le spalle e a guardare altrove." $) cyoa-text!
	($ "Qualcuno deve aver messo un gettone prima che tu arrivassi qui, lo schermo dice " .. "UN CREDITO RIMASTO!" fbold .. "  e " .. "SELEZIONA UNA DIFFICOLTA:" rbold $) +crtext
	choice( "Lascialo per ora" )choice
	pchar charchoice( "  gioca in modalita NORMALE" )choice: traderarcade-axe-normal
	pchar charchoice( "  gioca in modalita DIFFICILE" )choice: traderarcade-axe-hard
;


// NPC

cyoa: traderarcade
	"Arcade Worker" cyoa-title!
	"'Sto riportando indietro le sale giochi! Diventeranno molto popolari ora che Internet e stato distrutto.		Nessun rimborso." bbold text!
;

}

roomgen{

: tradercade-box
	gasarcademachine
	' arcade-events events!
	5 lt .timer!
;

: traderarcade-shoot
	tradercade-box cyoa{ ' traderarcade-shoot cyoa! }
;

: traderarcade-rp
	tradercade-box cyoa{ ' traderarcade-rp cyoa! }
;

: traderarcade-fire
	tradercade-box cyoa{ ' traderarcade-fire cyoa! }
;

: traderarcade-axe
	tradercade-box cyoa{ ' traderarcade-axe cyoa! }
;

: .arcadify args( chara )
	"Arcader" <- chara .name!
;

: traderarcade-npc
	still-npc .arcadify cyoa{ ' traderarcade cyoa! } ;
	// regiondef{ ' trader-event lt .events! 1 lt .timer! }
}

map{ regiondef{

trade-camp-reset

: bldg tanchor!
	"traderarcade" $picklocid addloc 
		prefab{
		BLINKY "LULTIMA " .. FLASHY .. "ARCADE" .. sign-text!
		0 0 0 sign-bg!
		0 1 0 sign-border!
		1 1 1 sign-fg!
		1 sign-glow!
		0 3 rnd 8 4 shop }
	
;
	
0 value gbx
0 value gby
0 value gbx2
0 value gby2

' plotpave >defer ft

: pt
	xyp.xy ft // Plot the current floor by default

	xyp.i char < = if xyp.xy to gby to gbx then;
	xyp.i char > = if gbx 1- gby xyp.xy swap 1+ swap gravellot then;
	xyp.i char ( = if xyp.xy to gby2 to gbx2 then;
	xyp.i char ) = if gbx2 1- gby2 xyp.xy swap 1+ swap gravellot then;
		
	xyp.i char P = if ' plotpave to ft xyp.xy ft then;
	
	xyp.i char x = if ' plotdirt to ft then;
	xyp.i char G = if ' plotgrass to ft xyp.xy ft then;
	xyp.i char $ = if xyp.xy grassedge then;
	xyp.i char { = if dirtstart then;
	xyp.i char } = if dirtend then;

	xyp.i char t = if pictable2 lt-here 8 rdrift then;		
	xyp.i char w = if pine lt-here drift then;
	xyp.i char V = if tradercarhere then;
	xyp.i char f = if campfirehere then;
		
	xyp.i char W = if trader-weapon-buyer then;
	xyp.i char T = if traderhere then;
	xyp.i char Z = if tnomey-spawn then;
	xyp.i char g = if trader-food-for-gas then;
	
	xyp.i char @ = if xyp.xy bldg then;
;

"
P........................
(........................
.........................
.........................
.........................
.........................
.........................
....W....@.........g..Z.)
.........................
.........................
.........................
.........................
.........................
.........................
........................."
constant maplayout

: plotmap
	0 -1 maplayout xyplot: pt
;

: site-setup
	1 no_time!
	17 19 rnd o'clock

	' plotdirt to ft

	30 %chance if 0 0.2 frnd weather! -1 2 frnd wind_force! else -1 1 frnd wind_force! then

	plotmap
	mission{ no-edge-spawns }

	// Put char and chars in middle of map
	25 %mv-up
	45 %chars-up
	
	map{ street-junk }
;

region: specialcamp-3
	w: maplayout layoutw
	h: maplayout layouth

	trade-mode

	post-script: site-setup

} }

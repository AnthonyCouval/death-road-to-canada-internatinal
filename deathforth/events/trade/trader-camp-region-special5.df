uses paul/region-layout.df

"events/inner/arcadeloc.df" $load

"
:map 1
:tags traderarcade  
:scriptA arcademachine -1 movey 
:scriptB cashcounter -1 movey 0.2 movex
:scriptC traderarcade-npc -0.5 movey 0.4 movex
:scriptD traderarcade-shoot -1 movey 
:scriptE traderarcade-rp -1 movey 
:scriptF clawmachine -1 movey
:scriptG traderarcade-fire -1 movey
:scriptH traderarcade-axe -1 movey
:size 14 9 
:off 8 0 
:tiles 
22222222222000
23333333332222
2AEADFGAHA3332
21111111111112
2111111111BC12
21111111111112
21111111111112
22222222222222
33333333333333
"
value TRADERARCADE
TRADERARCADE $parse-rooms

locdef{

location: traderarcade
		max-rooms: 1
		entry-tags: traderarcade
		pre-script: yall-theme
		script: yall-theme
		post-script: 
		
		room-choice: traderarcade
			room-weight: 10
			room-count: 1
			room-script: ndx 
			room-post-script:
			filter-only

}

cyoa{

// Shooting Arcade Machine

0 value traderarcade-shoot+

: traderarcade-amt traderarcade-shoot+ 2 * 2 + ;
: traderarcade-pay? trunk-food traderarcade-amt 1 - > ;
: traderarcade-pay traderarcade-amt -1 * trunk-food+ ;

cyoa-choice: traderarcade-mech
	($ "HEY! DEJA LA MAQUINA DE ARCADE SOLA, NERD!" bbold $) text!
	"El trabajador de la sala de juegos no esta contento." +crtext
	($ cname .. "  se estremece." $) +crtext 
	mechanicREV badfx
;

cyoa-choice: traderarcade-shoot-go
	($ cname .. " ' continua el juego de arcade. La pantalla esta llena de formas en movimiento. Es realmente imposible decir que esta pasando y, sin embargo, " .. che .. "  se siente completamente absorto. El juego termina en un instante, pero esta grabado en " .. cname .. "''s mente. Todavia no hay ninguna pista sobre como se llama el juego." $) cyoa-text!
		shootingREV shooting++ goodfx traderarcade-pay ' traderarcade-shoot+ ++
;

cyoa: traderarcade-shoot
	"Juego Arcade Misterioso" cyoa-title!
	picon!
	($ "Esta maquina recreativa tiene su ranura para monedas reemplazada por un gran contenedor de caida que dice " .. "PON COMIDA AQUI" rbold .. " ' encima de el. Ni siquiera esta claro que juego se supone que es, lo unico en pantalla es un mensaje que dice: " $) cyoa-text!
	($ "CONTINUAR: " fbold .. traderarcade-amt rainbold .. "  COMIDA" rainbold $) +text
	+cr foodamt
	chumany? not if
		($ pname .. " ' mira la maquina recreativa confundido. Los animales en su mayoria no juegan videojuegos, con algunas excepciones. Por ejemplo, los gusanos planos pueden jugar simuladores de caminar, aunque no pueden entender el sublime genio de la narracion." $) +crtext
	then;
		traderarcade-pay? not if
			"'No puedes permitirte esto." +crtext 
		then;
	choice( "Dejalo por ahora" )choice
	pchar charchoice( "  juega por " .. traderarcade-amt .. "  comida" )choice: traderarcade-shoot-go
	cmechanic+ if
		pchar charchoice( "  desmonta la maquina recreativa" )choice: traderarcade-mech
	then
;

// Road Punchers Machine

0 value rp-saguts
0 value rp-bryu

cyoa-choice: traderarcade-rp-done2
	rp-saguts 0 > rp-bryu 0 > and if
		clear-cyoa
		"Esta maquina recreativa de repente se cortocircuita!" text!
		"NO! WHAT DID YOU DOOOOOOOOOO?" bbold +crtext
		"El trabajador de la sala de juegos esta menos feliz de lo que se creia posible." +crtext badfx
	then 
;

: traderarcade-rp-done
	 // -10 add-food pchar .loot-to-trunk 
	 -10 trunk-food+ rp-saguts 0 > rp-bryu 0 > and if traderarcade-rp-done2 then 
;

cyoa-bridge: rp_recruit
	nearest .character@ choosechar cchar to recruitee ricon clear-cyoa
	($ cname .. "  esta listo para poner a prueba su fuerza en el camino de los punetazos." $) text!
	recruitee .strengthREV recruitee .fitnessREV
	cchar .name@ "BRYU" $= if "'HADOOK! HADOOK! KADOOSHIN'!" else "I GER! I GER! I GER UPPERCUT!" then fbold +crtext recruit-yesno
	0.25 cchar .ai.wander_chance! 0 nearest .unseen!	
;

: rp_pop_share
	lt mission{ .smokepoof }
	0.01 lt .character@ .ai.wander_chance! 0 lt .character@ .ai.loot_chance! 1 lt .unseen!
	-30 lt .fade!
	90 lt .stun!
	180 lt .aim_angle! goodfx
	' rp_recruit cyoa!
;

: bryu_pop
	new-npc lt .character@ .bryuify rp_pop_share
;

: saguts_pop
	new-npc lt .character@ .sagutsify rp_pop_share
;

: traderarcade-bryu	bryu_pop ' rp-bryu ++ traderarcade-rp-done ;
: traderarcade-saguts saguts_pop ' rp-saguts ++ traderarcade-rp-done ;

cyoa: traderarcade-rp
	"Road Punchers 2: Los nuevos pegadores" cyoa-title!
	picon!
	($ "Esta es una vieja maquina recreativa del famoso juego de lucha clasico: " .. "ROAD PUNCHERS" bbold .. "
	This machine has been converted to run on food instead of quarters. It's 10 food to pick a character. For reasons unknown, it looks like almost all the characters are grayed out..." $) cyoa-text!
	+cr foodamt
		trunk-food 10 < if
			"'No tienes la comida necesaria para jugar!" +crtext 
		then;
	choice( "Dejalo por ahora" )choice
	rp-saguts 1 < if
		choice( "Elige SAGUTS   10 alimentos" )choice: traderarcade-saguts
	then
	rp-bryu 1 < if
		choice( "Elige BRYU   10 alimentos" )choice: traderarcade-bryu
	then
;
	
// Fireproofing Arcade

cyoa-choice: traderarcade-fire-go
	"Forjado en el fuego" cyoa-title!
	($ "Al poner la comida y tocar el boton, la maquina recreativa " .. "EXPLOTA INMEDIATAMENTE!" fbold .. "  Realmente maduro!" $) cyoa-text!
	cchar statmod{ .fireproof? } not if
		($ cname .. "  se quema en una gran bola de fuego! Sin embargo, " .. che .. "  esta vivo y solo ligeramente irritado.		" .. cname .. "'la piel de 's se siente extranamente fria." bbold $) +crtext
		cchar statmod{ .fireproof } sting-- bitefx
		choice( "AY! MALDITA SEA!" )choice
	else
		($ cname .. " ' se ve envuelto en una bola de fuego! Por supuesto, esto no hace nada.		" .. cname .. "  se siente bastante satisfecho por esto." bbold $) +crtext
		goodfx morale2+
	then
	-10 trunk-food+ clear-cyoa deleteme
;

cyoa: traderarcade-fire
	"Extrana maquina de arcade" cyoa-title!
	picon!
	($ "Esta es una maquina de arcade de aspecto nuevo. Hay letras gigantes en la pantalla que deletrean: " .. "FIRE EXPLODERS" rainbold .. "
	This machine has been converted to run on food, so the coin slot is enormous. There's only one button. Instead of any sort of gameplay, the machine displays a roaring fire behind the title..." $) cyoa-text!
	+cr foodamt
		trunk-food 10 < if
			"'No tienes la comida necesaria para jugar!" +crtext 
		then;
	choice( "Dejalo por ahora" )choice
		pchar charchoice( "  juega por 10 alimentos" )choice: traderarcade-fire-go
;

cyoa-choice: traderarcade-axe-go
	($ "Un hacha voladora gira mas y mas cerca, y luego " .. "VUELA A TRAVES DE LA PANTALLA!" fbold .. " ' Ya no hacen juegos como este!" $) cyoa-text!
	cfitness+ cstrength+ or if
		($ cname .. "  esta lo suficientemente en forma fisicamente como para desviar el hacha. " .. cHe .. "  se siente muy feliz de que el hacha haya sido mango primero, no hoja primero." $) +crtext morale++ goodfx fitnessREV strengthREV
	else
		($ cname .. " ' recibe un golpe directo en la cara con el hacha, por el mango primero! El empleado del arcade esta bastante contento con esto, ya que es menos limpieza de la que habria requerido el otro extremo." $) +crtext ouch-- bitefx
		alive? if morale-- then fitnessREV strengthREV
	then
	"La pantalla falla cuando el hacha cae al suelo." +crtext
	clear-cyoa
;

: traderarcade-axe-hard goldenaxe2 drop southofcyoa drop traderarcade-axe-go ;
: traderarcade-axe-normal goldenaxe1 drop southofcyoa drop traderarcade-axe-go ;

cyoa: traderarcade-axe
	"SELECCION DE DIFICULTAD" cyoa-title!
	picon!
	($ "'Esta maquina emite un ambiente de los 80. Hay letras doradas en pantalla que deletrean: " .. "HACHA*BRILLANTE" rainbold .. "la maquina recreativa parece funcionar con fichas de una cadena de recreativas diferente. " .. pname .. "  se da cuenta de esto y se gira para mirar al empleado del arcade, que simplemente se encoge de hombros con indiferencia y mira hacia otro lado." $) cyoa-text!
	($ "Alguien debe haber puesto una ficha antes de que llegaras, la pantalla dice " .. "UN CREDITO RESTANTE!" fbold .. "  y " .. "SELECCIONA UNA DIFICULTAD:" rbold $) +crtext
	choice( "Dejalo por ahora" )choice
	pchar charchoice( "  juega en modo NORMAL" )choice: traderarcade-axe-normal
	pchar charchoice( "  juega en modo DIFICIL" )choice: traderarcade-axe-hard
;


// NPC

cyoa: traderarcade
	"Empleado del arcade" cyoa-title!
	"'Traigo de vuelta las recreativas! Se van a volver muy populares ahora que Internet ha sido destruida.		No hay reembolsos." bbold text!
;

}

roomgen{

: tradercade-box
	gasarcademachine
	' arcade-events events!
	5 lt .timer!
;

: traderarcade-shoot
	tradercade-box cyoa{ ' traderarcade-shoot cyoa! }
;

: traderarcade-rp
	tradercade-box cyoa{ ' traderarcade-rp cyoa! }
;

: traderarcade-fire
	tradercade-box cyoa{ ' traderarcade-fire cyoa! }
;

: traderarcade-axe
	tradercade-box cyoa{ ' traderarcade-axe cyoa! }
;

: .arcadify args( chara )
	"Arcader" <- chara .name!
;

: traderarcade-npc
	still-npc .arcadify cyoa{ ' traderarcade cyoa! } ;
	// regiondef{ ' trader-event lt .events! 1 lt .timer! }
}

map{ regiondef{

trade-camp-reset

: bldg tanchor!
	"traderarcade" $picklocid addloc 
		prefab{
		BLINKY "EL ULTIMO " .. FLASHY .. "ARCADE" .. sign-text!
		0 0 0 sign-bg!
		0 1 0 sign-border!
		1 1 1 sign-fg!
		1 sign-glow!
		0 3 rnd 8 4 shop }
	
;
	
0 value gbx
0 value gby
0 value gbx2
0 value gby2

' plotpave >defer ft

: pt
	xyp.xy ft // Plot the current floor by default

	xyp.i char < = if xyp.xy to gby to gbx then;
	xyp.i char > = if gbx 1- gby xyp.xy swap 1+ swap gravellot then;
	xyp.i char ( = if xyp.xy to gby2 to gbx2 then;
	xyp.i char ) = if gbx2 1- gby2 xyp.xy swap 1+ swap gravellot then;
		
	xyp.i char P = if ' plotpave to ft xyp.xy ft then;
	
	xyp.i char x = if ' plotdirt to ft then;
	xyp.i char G = if ' plotgrass to ft xyp.xy ft then;
	xyp.i char $ = if xyp.xy grassedge then;
	xyp.i char { = if dirtstart then;
	xyp.i char } = if dirtend then;

	xyp.i char t = if pictable2 lt-here 8 rdrift then;		
	xyp.i char w = if pine lt-here drift then;
	xyp.i char V = if tradercarhere then;
	xyp.i char f = if campfirehere then;
		
	xyp.i char W = if trader-weapon-buyer then;
	xyp.i char T = if traderhere then;
	xyp.i char Z = if tnomey-spawn then;
	xyp.i char g = if trader-food-for-gas then;
	
	xyp.i char @ = if xyp.xy bldg then;
;

"
P........................
(........................
.........................
.........................
.........................
.........................
.........................
....W....@.........g..Z.)
.........................
.........................
.........................
.........................
.........................
.........................
........................."
constant maplayout

: plotmap
	0 -1 maplayout xyplot: pt
;

: site-setup
	1 no_time!
	17 19 rnd o'clock

	' plotdirt to ft

	30 %chance if 0 0.2 frnd weather! -1 2 frnd wind_force! else -1 1 frnd wind_force! then

	plotmap
	mission{ no-edge-spawns }

	// Put char and chars in middle of map
	25 %mv-up
	45 %chars-up
	
	map{ street-junk }
;

region: specialcamp-3
	w: maplayout layoutw
	h: maplayout layouth

	trade-mode

	post-script: site-setup

} }

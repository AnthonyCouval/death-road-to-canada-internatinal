		
cyoa{		

cyoa-choice: arcade-vidcon2
	($ cname .. " " .. che .. " descobre a razao. Acontece que todas essas antigas maquinas de fliperama sao movidas a gas! " .. cname .. "  cuidadosamente drena o gas para fora de seu compartimento." $) cyoa-text!
	goodfx 40 70 rnd LOOT_GAS spawnloot clear-cyoa
;

: arcade-names
	1 6 rnd case
		1 of "ESTRADA DA MORTE PARA O CANADA" endof
		2 of "DEATH SPLATTERS" endof
		3 of "TOUCH OF DEATH" endof
		4 of "POLYBIUS" endof
		5 of "DEATHSPLOSIONAL" endof
		6 of "U*L*T*R*A*D*E*A*T*H" endof
	endcase
;
		
cyoa-choice: arcade-vidcon1
	($ cname .. "  joga um velho jogo de arcade chamado " .. arcade-names fbold .. "  e realmente entra nele. Como todos os videogames ja feitos, joga lo aumenta suas habilidades com armas. Os criticos estavam certos. Antes de " .. cname .. "  sabia que, 2 horas haviam passado." $) cyoa-text!
		shootingREV shooting++ morale++ goodfx
		clear-cyoa
		o'clock@ 2 + o'clock 
		team-nearby? not if	
			($ "Nao havia ninguem por perto para defender " .. pname .. "  durante isso, entao " .. phe .. "  teve que se defender de zumbis enquanto jogava. Cuidado!" $) +crtext
			7 13 rnd roomgen{ .zombspawn }
		then
;

cyoa: arcade-vidcon
	"Intervalo de Arcade" cyoa-title!
	picon!
	"'Esta maquina de arcade parece estar funcionando! Nao esta muito claro como ela pode funcionar sem eletricidade." cyoa-text!
	chumany? not if
		($ pname .. " ' olha para a maquina de fliperama confuso. O que separa as pessoas dos animais e que os animais nao jogam videogames. Pelo menos, na maioria das vezes." $) +crtext
	then; 
	team-nearby? not if
		($ "Sem ninguem de confianca por perto para cuidar dos zumbis, jogar pode ser perigoso." $) +crtext
	else
		($ pname .. "  considera fazer uma pausa no videogame enquanto " .. phe .. "  esta protegido." $) +crtext
	then
		pchar charchoice( "  joga o jogo de fliperama" )choice: arcade-vidcon1
		cmechanic+ if
			pchar charchoice( "  desmonta a maquina de fliperama" )choice: arcade-vidcon2
		then
	choice( "Deixe para depois" )choice
;

}
			
map{ roomgen{
	
: gachapon
	spawn special_objects32_base_id 0x 5
	+ lt .spriteid! rndflip
	25 health! bbox openable swolebolt- as-metal
;
: gachaempty gachapon opened ;
	
: arcademachine
	spawn special_objects32_base_id 0x 8 + lt .spriteid!
	30 health! bbox swolebolt+ as-metal
;
: gasarcademachine
	spawn special_objects32_base_id 0x 27 + lt .spriteid!
	 30 health! bbox swolebolt+ as-metal
;


: arcade-events args( me eventid )
	<- eventid THING_EVENT_TIMER <> if then;

	// CRT scanlines
	<- me .pos particle{ new pos!
		special_objects32_base_id 0x 98 + <- me .anim.state@ + spriteid!
		<- me .anim.state@ not <- me .anim.state!
		5 ticks!
		0.3 clr.a!
		0.5 clr.r!
		0.5 clr.g!
		1 clr.b!
		0 global_colour!
	}

	// blink PLAY <ALIEN>
	<- me .anim.substate@ 1+ dup <- me .anim.substate!
	4 & if
		<- me .pos particle{ new pos!
			special_objects32_base_id 0x 9a + <- me .anim.substate@ 3 >> 1 & + spriteid!
			5 ticks!

			<- me .anim.substate@ 45 * angle-colour rgb!

			0 global_colour!
		}
	then

	5 <- me .timer!
;

: arcade-on
	gasarcademachine cyoa{ ' arcade-vidcon cyoa! }
	' arcade-events events!
	5 lt .timer!
;

: smallarcade
	spawn special_objects32_base_id 0x c + lt .spriteid!
	15 health! bbox 5bolt as-metal
;

: clawmachine
	spawn special_objects32_base_id 0x d 
	80 %chance if 1 + then
	+ lt .spriteid! rndflip
	15 health! bbox 4bolt as-metal
;
	
} }

locdef{ 
		
location: arcade
		max-rooms: 2
		entry-tags: arcade
		script: set-theme-arcade

		room-choice: arcade-play
			room-weight: 10
			room-count: 1
			room-script: 
			room-post-script: 10diff %zombs map{ room-garbage }
			filter-only		

		room-choice: restroom
			room-weight: 10
			room-count: 1
			room-script: floor-bath
			
}

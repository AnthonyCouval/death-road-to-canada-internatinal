		
cyoa{		

cyoa-choice: arcade-vidcon2
	($ cname .. "  empieza a desmontar la maquina recreativa para ver como funciona. Despues de hurgar un poco, " .. che .. "  descubre el motivo. Resulta que todas estas viejas maquinas recreativas funcionan con gas " .. cname .. "  drena con cuidado el gas de su compartimento." $) cyoa-text!
	goodfx 40 70 rnd LOOT_GAS spawnloot clear-cyoa
;

: arcade-names
	1 6 rnd case
		1 of "CAMINO DE LA MUERTE A CANADA" endof
		2 of "DEATH SPLATTERS" endof
		3 of "TOUCH OF DEATH" endof
		4 of "POLYBIUS" endof
		5 of "DEATHSPLOSIONAL" endof
		6 of "U*L*T*R*A*D*E*A*T*H" endof
	endcase
;
		
cyoa-choice: arcade-vidcon1
	($ cname .. "  juega a un viejo juego de arcade llamado " .. arcade-names fbold .. "  y se mete mucho en el. Como todos los videojuegos jamas creados, jugarlo aumenta tus habilidades con las armas. Los criticos tenian razon. Antes " .. cname .. "  lo sabia, habian pasado 2 horas." $) cyoa-text!
		shootingREV shooting++ morale++ goodfx
		clear-cyoa
		o'clock@ 2 + o'clock 
		team-nearby? not if	
			($ "No habia nadie cerca para defender " .. pname .. "  durante esto, asi que " .. phe .. "  tuvo que defenderse de los zombis mientras jugaba. Cuidado!" $) +crtext
			7 13 rnd roomgen{ .zombspawn }
		then
;

cyoa: arcade-vidcon
	"Arcade Break" cyoa-title!
	picon!
	"'Esta maquina recreativa parece funcionar! No esta muy claro como puede funcionar sin electricidad." cyoa-text!
	chumany? not if
		($ pname .. " ' mira la maquina recreativa confundido. Lo que separa a las personas de los animales es principalmente que los animales no juegan videojuegos. Al menos, en su mayor parte." $) +crtext
	then; 
	team-nearby? not if
		($ "Sin nadie de confianza cerca para vigilar a los zombis, jugar podria ser peligroso." $) +crtext
	else
		($ pname .. "  considera tomarse un descanso de videojuegos mientras " .. phe .. "  esta protegido." $) +crtext
	then
		pchar charchoice( "  juega al juego de arcade" )choice: arcade-vidcon1
		cmechanic+ if
			pchar charchoice( "  desmonta la maquina recreativa" )choice: arcade-vidcon2
		then
	choice( "Dejalo por ahora" )choice
;

}
			
map{ roomgen{
	
: gachapon
	spawn special_objects32_base_id 0x 5
	+ lt .spriteid! rndflip
	25 health! bbox openable swolebolt- as-metal
;
: gachaempty gachapon opened ;
	
: arcademachine
	spawn special_objects32_base_id 0x 8 + lt .spriteid!
	30 health! bbox swolebolt+ as-metal
;
: gasarcademachine
	spawn special_objects32_base_id 0x 27 + lt .spriteid!
	 30 health! bbox swolebolt+ as-metal
;


: arcade-events args( me eventid )
	<- eventid THING_EVENT_TIMER <> if then;

	// CRT scanlines
	<- me .pos particle{ new pos!
		special_objects32_base_id 0x 98 + <- me .anim.state@ + spriteid!
		<- me .anim.state@ not <- me .anim.state!
		5 ticks!
		0.3 clr.a!
		0.5 clr.r!
		0.5 clr.g!
		1 clr.b!
		0 global_colour!
	}

	// blink PLAY <ALIEN>
	<- me .anim.substate@ 1+ dup <- me .anim.substate!
	4 & if
		<- me .pos particle{ new pos!
			special_objects32_base_id 0x 9a + <- me .anim.substate@ 3 >> 1 & + spriteid!
			5 ticks!

			<- me .anim.substate@ 45 * angle-colour rgb!

			0 global_colour!
		}
	then

	5 <- me .timer!
;

: arcade-on
	gasarcademachine cyoa{ ' arcade-vidcon cyoa! }
	' arcade-events events!
	5 lt .timer!
;

: smallarcade
	spawn special_objects32_base_id 0x c + lt .spriteid!
	15 health! bbox 5bolt as-metal
;

: clawmachine
	spawn special_objects32_base_id 0x d 
	80 %chance if 1 + then
	+ lt .spriteid! rndflip
	15 health! bbox 4bolt as-metal
;
	
} }

locdef{ 
		
location: arcade
		max-rooms: 2
		entry-tags: arcade
		script: set-theme-arcade

		room-choice: arcade-play
			room-weight: 10
			room-count: 1
			room-script: 
			room-post-script: 10diff %zombs map{ room-garbage }
			filter-only		

		room-choice: restroom
			room-weight: 10
			room-count: 1
			room-script: floor-bath
			
}

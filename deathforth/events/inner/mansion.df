"../deathforth/rooms/duodenum.txt" $load-rooms
"../deathforth/rooms/city.txt" $add-rooms

cyoa{ 


: ghosty-sound
	sfx{ fm_sweep 1 j! 50 a! 4 freq! 500 ramp_start! 400 ramp_end! 1500 d! 0.15 value! 0.5 1 frnd pitch! 0.5 scale!
	40 delay!
	}
;

: mghost-spooky 
	picon
	($ "El fantasma es " .. "MUY ATERRADOR" fbold .. " 'a " .. pname .. "! Desaparece!" $) +crtext
	morale2- ghosty-sound deleteme
;

: mghost-warn 
	text! "Hablar con un fantasma es MUY TERRORIFICO y seguro sera una baja moral, ademas de lo que suceda." rbold +crtext
		choice( "Ignora al fantasma por ahora" )choice
		cheevo: ghostfound
;

cyoa-choice: mghost-shy-talk
	"El Fantasma se escapa! Me refiero a que flota muy rapido." text!
	($ pname .. " ' se rie de " .. phe .. " ' asusto a un fantasma." $) +crtext
	morale++ goodfx deleteme
;

cyoa-choice: mghost-shy-kiss
	($ "El Fantasma emite vapor por los lados de su cabeza durante un segundo, y luego " .. "EXPLOTA!" fbold $) text!
	($ pname .. " ' se lastima, pero la explosion infunde a " .. phim .. " 'con energia fantasmal misteriosa. " .. "ENERGIA DE FANTASMA!" rainbold $) +crtext
	morale3+ sting2- fitness++ bitefx deleteme
	// Make character glow?
;

cyoa-blank: mghost-shy
	($ "Este fantasma alterna rapidamente entre intentar asustar a " .. pname .. ", y sentirse abrumado por la timidez." $) mghost-warn
		pchar charchoice( " 'habla con el fantasma sobre la timidez" )choice: mghost-shy-talk
		pchar charchoice( " 'besa al fantasma" )choice: mghost-shy-kiss
;

cyoa-choice: mghost-bite-go
	"CHOMP CHOMP CHOMP TODO EL DIA" rainbold text!
	($ "El fantasma muerde " .. pname .. " 'con sus dientes fantasmales! La dolorosa paranormal endurece " .. phim .. " 'se fuerza!" $) +crtext
	sting-- vitality++ mghost-spooky bitefx
;

cyoa-blank: mghost-bite
	"Es un fantasma! Tiene dientes hermosos." mghost-warn
		pchar charchoice( " 'habla con el fantasma acerca de sus dientes" )choice: mghost-bite-go
;

cyoa-choice: mghost-sass-sass
	"QUE, ESTAS BROMANDO AHI! BROMA GRANDE!" rainbold text!
	($ "El Fantasma bromea " .. pname .. ", de manera considerable!" $) +crtext
	morale-- badfx
;

cyoa-choice: mghost-sass-flex
	"QUE, ESTO SOY FLEXANDO! ME GUSTA SER FUERTE!" rainbold text!
	($ "Maldice " .. pname .. " ... with strength!" $) +crtext
	strength++ mghost-spooky
;

cyoa-blank: mghost-sass
	"Maldicion de la Broma" cyoa-title!
	"Este fantasma parece que se esta flexionando, o quizas esta haciendo una gestion muy sarcastica? Es dificil decirlo debido a que el fantasma carece de forma corporal." mghost-warn
		pchar charchoice( " 'pregunta al fantasma si esta flexionando" )choice: mghost-sass-flex
		pchar charchoice( "  esta sasando? " .. phim )choice: mghost-sass-sass
;

cyoa-choice: mghost-lick-coolit
	cpet? if
		($ cname .. " 'fartea a un fantasma." $)
	else
		($ cname .. " 'Vete de aqui, espiritu, y no vuelvas a molestar a nadie mas." .. " 'TRanquILIDAD!" pbold $)
	then cyoa-text!
	cool-it-7 if
		char-gen to recruitee "Fantasma Molesto" recruitee .name!
		"NUNCA HE OIDO TANTA PERFECCION!		HAS LEVANTADO MI MALDICION DE FANTASMA! AHORA ELIGE UNA RECOMPENSA FANTASMAL!" rainbold +crtext
		morale3+all rescue-trainitem cooled-it goodfx
	else cool-it++
		"WHAT? YOU CAN'T TELL ME TO COOL IT!
		Y... YOU'RE A NERD!" rainbold +crtext
		($ "El fantasma desaparece en un pequeno y enfadado puff de humo fantasmal. " .. cname .. " 'se siente realmente engreido por esto." $) +crtext
		goodfx morale2+
	then witsattREV deleteme
;

cyoa-choice: mghost-lick-go
	"UH, TAL VEZ DEBERIAS CALMARTE" rainbold text!
	($ "Maldice " .. pname .. " ' con el " .. "Maldicion del Calma!" fbold $) +crtext
	($ pname .. "'la personalidad de se vuelve muy molesta!" $) +crtext
	0 pchar .wits! 0 pchar .attitude! witsattREV mghost-spooky cool-it++
;

cyoa-blank: mghost-lick
	"El fantasma sigue moviendo su lengua. Ya parece un ser molesto." mghost-warn
		pchar grating? if
			pchar charchoice( " 'le dice al fantasma que se Calme!" fcoolit )choice: mghost-lick-coolit
		else
			pchar charchoice( " 'grita al fantasma" )choice: mghost-lick-go
		then
;

} 

roomgen{

: ghost-events args( me eventid )
	
	<- eventid THING_EVENT_TIMER <> if then;

	rnd5050 if <- me .anim.flop@ 1+ 1 & <- me .anim.flop! then

	special_objects32_base_id 0x a8 + <- me .anim.hint@ + <- me .anim.flop@ + <- me .spriteid!

	<- me .overlap dup if <- me .turn-toward else drop then

	8 <- me .timer!
;


: ghostcommon args( sprite )
	spawn scatter

	<- sprite 2 * lt .anim.hint!

	special_objects32_base_id 0x a8 + lt .anim.hint@ + lt .spriteid!

	1 lt .nocollide!
	1 lt .uncentered!
	16 lt .rad.x!
	0 lt .flammability!
	0 lt .spreadability!

	1 lt .no_hit!

	1 lt .glow!
	1 lt .no_lighting!

	0.3 lt .clr.a!
	0.5 lt .clr.r!
	0.5 lt .clr.g!

	3 lt .anim.floatval!
	10 lt .anim.floatheight!

	' ghost-events events!
	10 lt .timer! 50 health! low-profile
;


: mghost-shy 0 ghostcommon cyoa{ ' mghost-shy cyoa! } ;
: mghost-bite 1 ghostcommon cyoa{ ' mghost-bite cyoa! } ;
: mghost-sass 2 ghostcommon cyoa{ ' mghost-sass cyoa! } ;
: mghost-lick 3 ghostcommon cyoa{ ' mghost-lick cyoa! } ;

0 stack
	' mghost-bite shove
	' mghost-shy shove
	' mghost-sass shove
	' mghost-lick shove
value ghost-cyoa-picker-base
ghost-cyoa-picker-base shuffles var ghost-cyoa-picker

: ghost-cyoa-pick ghost-cyoa-picker depths 0= if ghost-cyoa-picker-base shuffles to ghost-cyoa-picker then pops nip execute ;

: ghostcheck mansiontype 0 > if ghost-cyoa-pick then ;

}

// mansion house doors

0 stack
	"bano" shove
	"open1" shove	
	"open1" shove
	"open1" shove
	"viviria" shove
	"cocina" shove
	"mansionpantry" shove
value mansionhall-doors-base
mansionhall-doors-base shuffles var mansionhall-doors

: mansionhall-doors-pick mansionhall-doors depths 0= if drop "open1" then; pops nip ;

: mansionhalldoors
	2 for random-door mansionhall-doors-pick only-with-door next
;

: mansionhouseloot
	mansionloot-pick
	2 4 rnd for skelscat next 10diff %zombs
;

0 stack
	"bano" shove
	"habitacion de cama" shove
	"habitacion de cama" shove
	"habitacion de cama" shove
	"habitacion de cama" shove
	"habitacion de cama" shove
	"habitacion de cama" shove
	"mansionend" shove
	"habitacion de cama" shove
value mansionend-doors-base
mansionend-doors-base shuffles var mansionend-doors

: mansionend-doors-pick mansionend-doors depths 0= if then; pops nip ;

: mansionenddoors
	3 for random-door mansionend-doors-pick only-with-door next
;
		
		: haunted-chair-chance
			60 %chance if haunted-chair lt .scatter then
		;
		: haunted-fridge-chance
			50 %chance if haunted-fridge lt .drift then
		;
		: haunted-drawers-chance
			40 %chance if haunted-drawers lt .drift then
		;
		: haunted-living-chance	70 %chance if haunted-chair-chance else haunted-drawers-chance then ;
		
		: mansionbathloot 50 %chance if looseloot then 10diff %zombs ;
		: mansionkitchenloot 2 4 rnd sfood 10diff %zombs 10 %chance if hauntedware then haunted-chair-chance ;

		: mansion-loc-events args( eventid )
			<- eventid MAP_EVENT_VIEW_ENTER = if
				1 light_mode! toodark 0.01 gloom_lighting! // Maybe 0.02 instead?
			then;
		;

		: set-mansion-events ' mansion-loc-events loc_here_events! ;
					
location: darkmansion

		max-rooms: 28
		entry-tags: mansionstart
		// VVV script is executed once before any rooms are created. Location id is not known yet.
		script: set-theme-museum

		// VVV pre-script is executed before each room is created
		pre-script: set-theme-museum set-mansion-events
		// post-script: toodark
		
		: mansionopendoors
			random-door "mansionhall" only-with-door
			// random-door mansionhall-doors-pick only-with-door
			random-door "bano" only-with-door
			random-door "habitacion de cama" only-with-door			
		;
		
		: mansionstartdoors
			2 for random-door "pasillo" only-with-door next
			random-door "viviria" only-with-door
		;
						
		room-choice: open1
			room-weight: 10
			room-count: 3
			room-script: nad
			room-post-script: 10diff %zombs mansionopendoors
			filter-only
	
		room-choice: kitchen
			room-weight: 1
			room-count: 1
			room-script: nad floor-kitchen
			room-post-script: mansionkitchenloot
			filter-only
	
		room-choice: bathroom
			room-weight: 1
			room-count: 1
			room-script: nad floor-bath
			room-post-script: mansionbathloot
			filter-only
			
		room-choice: bathroom
			room-weight: 1
			room-count: 3
			room-script: nad floor-bath
			room-post-script: 5diff %zombs
			filter-only
		
		room-choice: hall
			room-weight: 10
			room-count: 2
			room-script: nad
			room-post-script: 10diff %zombs hallset mansionhalldoors
			filter-only
			
		room-choice: mansionhall
			room-weight: 10
			room-count: 3
			room-script: nad
			room-post-script: 10diff %zombs mansionenddoors
			filter-only
					
		room-choice: bedroom
			room-weight: 10
			room-count: 5
			room-script: nad		
			room-post-script: looseloot 10diff %zombs
			
		room-choice: bedroom
			room-weight: 10
			room-count: 5
			room-script: nad		
			room-post-script: 5diff %zombs haunted-living-chance
			filter-only
			
		room-choice: living
			room-weight: 10
			room-count: 2
			room-script: nad		
			room-post-script: looseloot mansionhalldoors 10diff %zombs haunted-living-chance
			filter-only
			
		room-choice: mansionend
			room-weight: 10
			room-count: 1
			room-script: nad		
			room-post-script: 10diff %zombs
			filter-only
			
		room-choice: mansionstart
			room-weight: 10
			room-count: 1
			room-script: nad
			room-post-script: 5diff %zombs mansionstartdoors
			filter-only
			
		room-choice: mansionpantry
			room-weight: 10
			room-count: 1
			room-script: nad
			room-post-script: 2 4 rnd sfood 10diff %zombs haunted-fridge-chance
			filter-only
			
regiondef{			

: dig-diff
	12 1 difficulty / >int 3 8 clamp * // Multiplied directly to timer
	// 1 difficulty / - when it was 120 to 360 rnd, lower diff increased the timer
;

: dodigadd

	// zomb-count maxzombs < if
		2 map{ w 3 - } rnd 2 map{ h 3 - } rnd tile>pos xyzombdig
	// then

	40 dig-diff 100 dig-diff rnd timer_dur! timer_retrigger

;

: dodigcheck mansiontype 0 > if 60 6 * ' dodigadd timer_add then ;
	
	region: darkmansion
		script: dodigcheck
		
		inside spooky-music
		
		loc-choice: darkmansion
			loc-weight: 99
			loc-count: 1

		post-script: 1 no_time! inspawn // timeofday 10diff %zombs
	
}

root{ : ght } roomgen{ ghost-cyoa-pick } ;

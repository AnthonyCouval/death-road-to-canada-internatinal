// Useful words for assigning CYOA roles
0 value victim
0 value bully
0 value sc-camper


: recruited 
	+cr ($ rname .. "  se junta a equipe! Avancando para o Canada " $) +text
	recruitee .morale@ 2 < if
		2 recruitee .morale!
	then
	recruitee .recruit
	gstats{ ugnome-extraZP 2 > if 1 zombop+! then }

	// if the character has a related THING, clear what it's following so it will follow the player
	recruitee .thing@ if 0 recruitee .thing@ .ai.followid! then

;

// Word replacements that alter pronouns and such to the situation

: xhis .female@ if "dela" else "dele" then ;
: xHis .female@ if "Dela" else "Dele" then ;
: xhim .female@ if "dela" else "ele" then ;
: xhe .female@ if "ela" else "ele" then ;
: xHe .female@ if "Ela" else "Ele" then ;

: ghis solo if soloer xhis else "deles" then ;
: gHis solo if soloer xHis else "Deles" then ;
: ghim solo if soloer xhim else "eles" then ;
: ghe solo if soloer xhe else "o grupo" then ;
: gHe solo if soloer xHe else "O grupo" then ;
: gthey solo if soloer xhe else "eles" then ;
: gThey solo if soloer xHe else "Eles" then ;

// Below words for putting in an extra s depending on group

: g1s solo if "s" else "" then ;
: g2s team-count 2 = if "s" else "" then ;
: gs solo if "" else "s" then ;
: gS solo if "" else "S" then ;

: gare solo if "e" else "sao" then ;
: gwere solo if "foi" else "foram" then ;
: gtheywere gthey .. " " .. gwere ;
: gtheyare gthey .. " " .. gare ;
: gTheyare gThey .. " " .. gare ;

// Below example: "traseiro" aVSs = you're a butt versus you're butts, depending 2 or 3 people.
: g2a team-count-alive 2 = if "  um" else "" then ;
: g1a team-count-alive 1 = if "  um" else "" then ;
: g1an team-count-alive 1 = if "  uma" else "" then ;
: g3s team-count-alive 2 > if "s" else "" then ;
: aVSs ($ g2a .. swap .. g3s $) ;
: gaVSs ($ g1a .. swap .. gs $) ;
: ganVSs ($ g1an .. swap .. gs $) ;

: I-or-we team-count-alive 1 = if "  Eu " else "  nos " then ;
: I-or-WE team-count-alive 1 = if "  Eu " else "  NOS " then ;

: cname-he solo if soloer xhe else cname then ;
: cname-He solo if soloer xHe else cname then ;
: thegroup-him solo if soloer xhim else "o grupo" then ;
: chis cchar xhis ;
: cHis cchar xHis ;
: chim cchar xhim ;
: che cchar xhe ;
: cHe cchar xHe ;

: phis pchar xhis ;
: pHis pchar xHis ;
: phim pchar xhim ;
: phe pchar xhe ;
: pHe pchar xHe ;


: rhis recruitee xhis ;
: rhim recruitee xhim ;
: rhe recruitee xhe ;
: rHe recruitee xHe ;

: vhis victim xhis ;
: vhim victim xhim ;
: vhe victim xhe ;
: vHe victim xHe ;

: bhim bully xhim ;
: bhe bully xhe ;
: bHe bully xHe ;


: Thegroup solo if soloer .colourname else "O grupo" then ;
: thegroup solo if soloer .colourname else "o grupo" then ;
: yourteam solo if soloer .colourname else "sua equipe" then ;

: Theothers team-count 2 = if notcname else "Os outros membros" then ;
: theothers team-count 2 = if notcname else "os outros membros" then ;
: Theothersarename team-count 2 = if ($ notcname .. "  e" $) else "Os outros sao" then ;

: everyone team-count 2 = if notcname else "todos" then ;
: Everyone team-count 2 = if notcname else "Todos" then ;

: Theothersare team-count 2 = if "O outro membro e" else "Os outros membros sao" then ;
: theothersare team-count 2 = if "o outro membro e" else "os outros membros sao" then ;

: attitudeREV STAT_ATTITUDE cchar .reveal ;
: temperREV STAT_COMPOSURE cchar .reveal ;
: loyaltyREV STAT_LOYALTY cchar .reveal ;
: witsREV STAT_WITS cchar .reveal ;
: witsattREV witsREV attitudeREV ;

: aholeREV loyaltyREV temperREV ;

: strengthREV STAT_STRENGTH cchar .reveal ;
: medicalREV STAT_MEDICAL cchar .reveal ;
: mechanicREV STAT_MECHANICAL cchar .reveal ;
: shootingREV STAT_SHOOTING cchar .reveal ;
: fitnessREV STAT_FITNESS cchar .reveal ;

: rageREV temperREV strengthREV ;

: allREV strengthREV shootingREV fitnessREV mechanicREV medicalREV witsattREV temperREV loyaltyREV ;

: morale-- STAT_MORALE cchar .stat-- ;
: morale2- STAT_MORALE cchar .stat2- ;
: morale3- STAT_MORALE cchar .stat3- ;
: morale4- STAT_MORALE cchar .stat4- ;

: attitude-- STAT_ATTITUDE cchar .stat-- ;
: temper-- STAT_COMPOSURE cchar .stat-- ;
: loyalty-- STAT_LOYALTY cchar .stat-- ;
: wits-- STAT_WITS cchar .stat-- ;

: strength-- STAT_STRENGTH cchar .stat-- ;
: medical-- STAT_MEDICAL cchar .stat-- ;
: mechanic-- STAT_MECHANICAL cchar .stat-- ;
: shooting-- STAT_SHOOTING cchar .stat-- ;
: fitness-- STAT_FITNESS cchar .stat-- ;

: morale++ STAT_MORALE cchar .stat++ ;
: morale2+ STAT_MORALE cchar .stat2+ ;
: morale3+ STAT_MORALE cchar .stat3+ ;
: morale4+ STAT_MORALE cchar .stat4+ ;

: vitality++ STAT_VITALITY cchar .stat++ ;
: dexterity++ STAT_DEXTERITY cchar .stat++ ;

: attitude++ STAT_ATTITUDE cchar .stat++ ;
: temper++ STAT_COMPOSURE cchar .stat++ ;
: loyalty++ STAT_LOYALTY cchar .stat++ ;
: wits++ STAT_WITS cchar .stat++ ;

: strength++ STAT_STRENGTH cchar .stat++ gstats{ ' total-str+ ++ } stat-cheevs ;
: medical++ STAT_MEDICAL cchar fastlearn-stat gstats{ ' total-med+ ++ } stat-cheevs ;
: mechanic++ STAT_MECHANICAL cchar fastlearn-stat gstats{ ' total-mech+ ++ } stat-cheevs ;
: shooting++ STAT_SHOOTING cchar fastlearn-stat gstats{ ' total-shoot+ ++ } stat-cheevs ;
: fitness++ STAT_FITNESS cchar .stat++ gstats{ ' total-fit+ ++ } stat-cheevs ;

: ricon recruitee character# cyoa-char-icon! ;
: dead? cchar .health@ 1 < ;

0 value post-mission-cyoa

: ouch-- cchar team-attacked ;
: ouch2- 2 for ouch-- next ;
: nonfatal cchar .health@ 1 > ;
: alive? cchar .alive? ;
: health++ cchar .health++ ;
: despairbite cchar team-attacked 50 %chance if cchar team-attacked then ;
: sting-- nonfatal if ouch-- then ;
: sting2- 2 for sting-- next ;

cyoa{

: rewardevent-random
	0 stack
		' .fitness++ shove
		' .strength++ shove
		' .shooting++ shove
		' .mechanic++ shove
		' .medical++ shove
	shuffles pops nip execute
;

cyoa-choice: toiletwishA
	"Riquezas e Ouro" cyoa-title!
		($ "'Latas de comida comecam a chover do teto. Finalmente. Voce e rico! Entao o teto desmorona em cima de " .. pname .. "... " $) cyoa-text! goodfx clear-cyoa
	15 20 rnd sfood
	' xyzombceil 10 40 90 rnd zwave-ex	
;

cyoa-choice: toiletwishB
		($ pname .. "  olha horrorizado enquanto " .. phis .. "  a pele se transforma em pedra, prendendo " .. phim .. "  no lugar para sempre como uma estatua. Nao espere, ele simplesmente se transformou " .. phis .. "  pele cinza." $) text! goodfx deleteme
		pchar .vitality++ pchar .fullheal pchar .stoneclr
;

cyoa-choice: toiletwishC
	"A resposta chocante" cyoa-title!
	($ "SIM, QUASE ISSO diz o genio. Entao ele apenas paira por ai, olhando para " .. pname .. "  por um tempo antes de desaparecer." $) cyoa-text! badfx clear-cyoa deleteme
	nearest .tpos anchor! 20 zombs
;

cyoa: toiletwishgenie // Added an underscore so that "toiletwish" isn't redefined, as that would mess up the next part
	"O banheiro dos desejos" cyoa-title!
	picon!
	($ "Quando " .. pname .. "  abriu o banheiro, um genio magico apareceu.	" .. "OBRIGADO POR ME LIBERTAR DO BANHEIRO," pbold .. "  ele diz. " .. "AGORA FACA UM DESEJO.	" pbold .. pname .. "  faz um desejo:" $) text!
		choice( "'Nao faca um desejo ainda" )choice
		choice( "Eu quero... ser rico" )choice: toiletwishA
		choice( "Imortalidade" )choice: toiletwishB
		choice( "Voce e um desses genios malignos?" )choice: toiletwishC
;

: toiletwish_
	new-npc lt .character@ .genieify goodfx lt mission{ .smokepoof }
	0.01 lt .character@ .ai.wander_chance! 0 lt .character@ .ai.loot_chance! 1 lt .unseen!
	-30 lt .fade!
	90 lt .stun!
	180 lt .aim_angle!
	cyoa{ ' toiletwishgenie cyoa! }
;

// Assign to the previously-defined toiletwish deferred word
' toiletwish_ to toiletwish

: foodamt
	($ "  Voce tem " .. LOOT_FOOD looticon .. "" .. trunk-food .. "  comida sobrando. " $) +text
;

: gasamt
	($ "  Voce tem " .. LOOT_GAS looticon .. "" .. trunk-gas .. "  gas sobrando. " $) +text
;

cyoa: just-testing beep "TESTANDO POS MISSAO ROAD STACKED CYOA" cyoa-text! ;

// Special events called later on stack-road-action

: comicbookgain shooting++ strength++ fitness++ morale3+ ;

cyoa-choice: comicbook-who
	chumany? if
		($ cname .. "  consegue ler " .. "A LIGA SPANDEX" fbold .. ".
		" .. cHe .. "  absorve o conteudo! Infelizmente, " .. che .. "  estraga a historia em quadrinhos para os outros devido a " .. chis .. "  sujo " .. cfingspaws .. "." $) cyoa-text!
		comicbookgain goodfx
	else
		($ cname .. " ' estraga completamente a historia em quadrinhos. Parece que alguem nao gosta de historias em quadrinhos." $) cyoa-text! badfx
	then 
;

cyoa: comicbook-2
	"'E uma Historia em Quadrinhos NAO uma Revista em Quadrinhos" cyoa-title!
	picon!
	dead? if
		($ "A historia em quadrinhos foi rasgada durante a luta." $) cyoa-text! badfx
	then;
	($ Thegroup .. " ' saiu do local com um quadrinho! Esta cheio de historias sobre pessoas robustas que se batem uns com os outros e disparam uns uns raios olimpicos." $) cyoa-text! goodfx
	solohuman if
		choosehuman cicon
		($ cname-He .. " ' le e absorve a sabedoria contida nele! E informativo e divertido!" $) +crtext cchar comicbookgain
	then;  
		"Quem deveria ler isso?" +crtext
		' comicbook-who cyoa-team-choice
;

// More common events

cyoa-choice: bookmag-who
	chumany? if
		($ cname .. "  consegue ler " .. bookmagtitle .. ".
		" .. cHe .. "  absorve o conteudo! Infelizmente, " .. che .. "  acaba por destruir o quadrinho para outros devido a " .. chis .. "  sujo " .. cfingspaws .. "." $) cyoa-text!
		cchar bookmaggain goodfx
	else
		($ cname .. " ' acaba por destruir completamente o quadrinho.		Parece que alguem nao aprecia a literatura fina." $) cyoa-text! badfx
	then 
;

cyoa: bookmag-get2
	"Atualize suas Leituras" cyoa-title!
	picon!
	dead? if
		($ "O " .. bookormag .. "  foi perdido na confusao." $) cyoa-text! badfx
	then;
	($ Thegroup .. "  agora tem o " .. bookormag .. ", " .. bookmagtitle .. "." $) cyoa-text! goodfx
	solohuman if
		choosehuman cicon
		($ cname-He .. "  le e absorve a conhecimento!" $) +crtext cchar bookmaggain goodfx
	then;  
	bookmagtype 4 > if
		"Todos tem uma chance de ler e aprender coisas novas." +crtext ' bookmaggain foreach-human-char 
	else
		"Quem deveria ler isso?" +crtext
		' bookmag-who cyoa-team-choice
	then
;

cyoa: bookmag-get
	"Material de Leitura" cyoa-title!
	picon!
	($ cname .. "  encontra um " .. bookormag .. "  em bom estado, entre as revistas e livros arruinados. 	O " .. bookormag .. "''s titulo e: " .. bookmagtitle .. ".
	" .. cHe .. "  coloca " .. chis .. "  pacote." $) cyoa-text!
	deleteme
	clear-cyoa goodfx mission{ ' bookmag-get2 to post-mission-cyoa }
;

cyoa-choice: recruit-replace
	ricon
	($ cname .. "  e expulso do grupo para abrir espaco para " .. rname .. "." $) cyoa-text!
	ctakeweapons TEAM_STATUS_BAD_LEFT cchar .decruit 0.5 600 tone
	recruited
;

cyoa-bridge: recruit-replace4
	ricon
	($ "Quem voce gostaria de substituir para abrir espaco para " .. rname .. "?" $) cyoa-text!
	' recruit-replace cyoa-team-choice
;

: recruit-replace?
	// ' recruit-replace cyoa-team-choice
	// Look into maybe removing this: jerk-get
	// Doesn't use this: recruit-dog-feed recruit-familiarreplace? and the camp-recruits
	team-count 3 > if
		' recruit-replace 2 #cyoa-team-choice
		choice( "Escolha outra pessoa" )choice: recruit-replace4
	else
		' recruit-replace cyoa-team-choice
	then
	choice( "Deixe " .. rname .. "  para tras" )choice
;

(* : recruit-replace?-3 // The old recruit-replace?, currently used twice for annoying recruits in cyoa-k-recruit ... but I got rid of it because I added the pick someone else options to recruit-replace?
	// This was formerly so you could avoid the pushy person with a full party, and so you could avoid the Dinkdoor guy if you used a cool-it but NOT if you recruited him without a cool-it.
	// ' recruit-replace 3 #cyoa-team-choice choice( "Deixe " .. rname .. "  para tras" )choice
; *)

cyoa: recruit-yes
	team-full? if
		($ "O grupo esta cheio. Quem voce gostaria de substituir para abrir espaco para " .. rname .. "?" $) cyoa-text!
		recruit-replace?
	then;
	ricon
	($ Thegroup .. "  aceita " .. rname .. "  para o time!" $) cyoa-text! 2 300 tone
		recruited
;

cyoa: recruit-yes-food args( food )
	team-full? if
		($ "O grupo esta cheio. Quem voce gostaria de substituir para abrir espaco para " .. rname .. "?" $) cyoa-text!
			<- food -1 * trunk-food+
			' recruit-replace cyoa-team-choice 
	then;
	ricon
	($ Thegroup .. "  recruta " .. rname .. "  para o time!" $) cyoa-text! 2 300 tone
		<- food -1 * trunk-food+ recruited
;

: recruit-yesno
	choice( "Pergunte " .. rname .. " se juntar" )choice: recruit-yes
	choice( "Deixe " .. rname .. "  para tras" )choice
;

: npc-died 
	"Perdeu se na Estrada da Morte" cyoa-title! 
	($ pname .. " ' chega a tarde. Nao ha nada que possa fazer por " .. chim .. "  agora." $) cyoa-text!
	clear-cyoa badfx
; 

0 value rescskilltype

: rescskillup
	rescskilltype case
		// 0 of dup .strengthREV .strength++ endof - Got rid of the reveal
		0 of .strength++ endof
		1 of .fitness++ endof
		2 of .medical++ endof
		3 of .mechanic++ endof
		4 of .shooting++ endof
	endcase
;
: rescskillname
	rescskilltype case
		0 of "fortaleza" endof
		1 of "fisicalidade" endof
		2 of "medico" endof
		3 of "mecanico" endof
		4 of "tiro" endof
	endcase
;

: rescskill-got
	($ Thegroup .. "  recebe todos os tipos de dicas secretas de " .. rescskillname .. "  ele " .. rname .. "!	O treinamento termina, e " .. rname .. "  sai." $) cyoa-text!
	// formerly cchar
	' rescskillup foreach-human-char goodfx
;

 cyoa-choice: rescskill-who
	choosehuman cicon
	chumany? if
		rescskill-got
	else
		($ cname .. " ' nao entende " .. rname .. "''Quatrocentas e oitenta e quatro tentativas de treinamento, em todo.''," $) cyoa-text! badfx
	then 
;

cyoa: rescskill-choose
 	solohuman if
		choosehuman cicon
		rescskill-got
	else  
		($ "Quem deveria receber o " .. rescskillname .. " " $) cyoa-text!
		' rescskill-who cyoa-team-choice
	then
;
 
: rescskill-pick to rescskilltype 
	// formerly rescskill-choose 
	rescskill-who
;

: groupadd team-count 1 > if "Grupo " .. then ;

: rescskill1-get 0 rescskill-pick ;
: rescskill1 choice( groupadd "Treinamento de Forca" )choice: rescskill1-get 
	4 recruitee .strength!
;
: rescskill2-get 1 rescskill-pick ;
: rescskill2 choice( groupadd "Treinamento de Fitness" )choice: rescskill2-get 
	4 recruitee .fitness!
;
: rescskill3-get 4 rescskill-pick ;
: rescskill3 choice( groupadd "Treinamento de Tiro" )choice: rescskill3-get 
	4 recruitee .shooting!
;
: rescskill4-get 3 rescskill-pick ;
: rescskill4 choice( groupadd "Treinamento Mecanico" )choice: rescskill4-get 
	4 recruitee .mechanical!
;
: rescskill5-get 2 rescskill-pick ;
: rescskill5 choice( groupadd "Armazenamento de setenta e cinco balas" )choice: rescskill5-get 
	4 recruitee .medical!
;
 
// Old way of doing it
0 var rescue-skill-deck
	deck-new
	card: rescskill1
	card: rescskill2
	card: rescskill3
	card: rescskill4
	card: rescskill5
constant RESCUE-SKILL
RESCUE-SKILL shuffles to rescue-skill-deck

: rescreward1-get 75 trunk-bullet+ ; 
: rescreward1 choice( "Municao de 75 balas" )choice: rescreward1-get ;
: rescreward2-get 60 trunk-rifle-shell+ ;
: rescreward2 choice( "Armazenamento de sessenta amunicao de rifle" )choice: rescreward2-get ;
: rescreward3-get 45 trunk-shotgun-shell+ ;
: rescreward3 choice( "quarenta e cinco cartuchos de espingarda" )choice: rescreward3-get ;
: rescreward4-get 15 trunk-food+ ;
: rescreward4 choice( "Reserva de 15 alimentos" )choice: rescreward4-get ;
: rescreward5-get 80 trunk-gas+ ;
: rescreward5 choice( "Tanque de 80 de gasolina" )choice: rescreward5-get ;
: rescreward6-get 10 trunk-medical+ ;
: rescreward6 choice( "Reserva de 10 de medicamentos" )choice: rescreward6-get ;
: rescreward7-get morale2+all goodfx ;
: rescreward8-get 5 groupheal goodfx ; // trader is 7
: rescreward7 // Two for the price of one
	choice( "Conversa para animar a moral" )choice: rescreward7-get
	choice( groupadd "Primeiros socorros" )choice: rescreward8-get 
;
 
0 var rescue-reward-deck
	deck-new
	card: rescreward1
	card: rescreward2
	card: rescreward3
	card: rescreward4
	card: rescreward5
	card: rescreward6
	card: rescreward7
	// Training added to resc reward deck
	card: rescskill1
	card: rescskill2
	card: rescskill3
	card: rescskill4
	card: rescskill5
constant RESCUE-REWARD
RESCUE-REWARD shuffles to rescue-reward-deck

: rescue-reward-draw rescue-reward-deck drawcard ;
: rescue-reward-shuffle
	rescue-reward-deck depths 0= if RESCUE-REWARD shuffles to rescue-reward-deck then
	rescue-reward-draw
;
: rescue-skill-shuffle
	rescue-skill-deck depths 0= if RESCUE-SKILL shuffles to rescue-skill-deck then
	rescue-skill-deck drawcard
;

: rescue-trainitem
	// rescue-skill-shuffle
	rescue-reward-shuffle
	rescue-reward-draw
;


: recruit-choice
	choice( "Recrutar " .. rname .. "  para o time" )choice: recruit-yes
;

: rescue-reward
	gstats{ ugnome-extraZP 0 > if 1 zombop+! then }
	recruit-choice
	rescue-trainitem
;

: judgerecruit
	most-paranoid if choosechar
		50 %chance if 
			($ cname .. "  avalia " .. rhis .. " skills..." $) +crtext
			recruitee dup .medicalREV dup .mechanicREV dup .shootingREV dup .strengthREV dup .fitnessREV
		else
			($ cname .. "  julga " .. rhis .. " character..." $) +crtext
			recruitee dup .attitudeREV dup .temperREV dup .witsREV dup .loyaltyREV
		then goodfx witsattREV
	then
;

cyoa: rescuedme
	// rescue choosechar cicon
	// Maybe need to do a check here to see if the rescue team was left alive
	rescue to recruitee ricon
	0 rescue .party!
	recruitee .alive? if
		"Resgate bem sucedido!" cyoa-title!
		($ rname .. "  encontra " .. thegroup .. "  em um local seguro.		" .. rHe .. "  oferece uma recompensa em agradecimento por salvar " .. rhim .. "!" $) cyoa-text!
		rescue-reward
		judgerecruit
	else
		"'Sim bem ninguem e perfeito" cyoa-title!
		($ Thegroup .. "  se sente muito culpado por " .. rname .. "  ser comido.		Assim e a vida, suponho." $) cyoa-text!
		morale--all
	then
;

cyoa: rescueddog
	// rescue choosechar cicon
	rescue to recruitee ricon
	0 rescue .party!
	recruitee .alive? if
		"Resgate bem sucedido!" cyoa-title!
		gstats{ ugnome-extraZP 0 > if 1 zombop+! then }
		($ rname .. "  encontra " .. thegroup .. "  em um local seguro.		" .. rHe .. "  parece " .. rhe .. "  quer ficar por perto!" $) cyoa-text!
		recruit-choice
		choice( "Definir " .. rname .. "  livre" )choice: morale2+all
		judgerecruit
	else
		"Adocao de cachorro falhou" cyoa-title!
		($ Thegroup .. "  se sente culpado por " .. rname .. "  ser comido.		Um cachorro poderia ter sido muito util." $) cyoa-text!
		morale2-all
	then
;

cyoa-choice: chicken-dinner
	"O frango e devolvido as suas pepitas componentes." text!
	"Cinzas as cinzas, nugs aos nugs" pbold +crtext
	"O belo ciclo da vida continua." +crtext
	3 4 rnd trunk-food+ goodfx
;

cyoa-choice: chicken-free
	"O frango deixa para tras um ovo!	Isso e um sinal de boa sorte! Provavelmente." text!
	morale++all goodfx
	1 weapondef{ WEAPON_EGG } trunk.weapon+
;

root{ defer freechicken-cheevo }

cyoa: rescuedchicken
	rescue choosechar cicon 0 rescue .party!
	rescue .alive? if
		"Frango resgatado!" cyoa-title!
		gstats{ ugnome-extraZP 0 > if 1 zombop+! then }
		($ Thegroup .. " ' conseguiu tirar uma galinha daquela fazenda!		A galinha nao seria uma boa recruta.		Ela pode ser libertada ou abatida para servir de alimento. Ela nao tem muita carne, mas pode ser boa para alguns nuggets de frango." $) cyoa-text!
		choice( "Libertar a galinha" )choice: chicken-free
		choice( "Nuggetificar a galinha" )choice: chicken-dinner
		freechicken-cheevo
	else
		"Falha no resgate da galinha" cyoa-title!
		($ Thegroup .. "  sente se vagamente culpado pela galinha ter sido comida.		Foi um desperdicio de uma boa galinha." $) cyoa-text!
		morale--all badfx
	then
; 

0 value gothic-ma
0 value gothic-pa

cyoa-choice: farmrescue-1
	($ Thegroup .. "  obtem alguma sabedoria caseira sobre como reparar montes de lixo enferrujados de todas as formas e tamanhos." $) text! picon
	' .mechanicREV foreach-team-char ' .mechanic++ foreach-team-char goodfx ;
cyoa-choice: farmrescue-2
	($ Thegroup .. "  aprende alguns remedios caseiros antigos para fazendeiros. Ervas medicinais locais, pomadas, tecnicas de cirurgia caseira." $) text! picon
	' .medicalREV foreach-team-char ' .medical++ foreach-team-char goodfx ;
cyoa-choice: farmrescue-3 
	($ Thegroup .. " ' recebe uma foice afiada como presente. E leve, nao quebra e pode causar algum dano." $) text! picon 1 WEAPON_SICKLE trunk.weapon+ goodfx ;
cyoa-choice: farmrescue-4 
	($ Thegroup .. "  e levado a um estoque de frutas em conserva, vegetais e carne estranha! Comida em pote e uma boa mudanca em relacao a comida enlatada." $) text! picon 10 trunk-food+ morale++all goodfx ;

cyoa: farmrescue
	"Resgate da Fazenda Gotica" cyoa-title! picon
	($ Thegroup .. "  escapa da fazenda!" $) text!
	gothic-ma 0= not if
		0 gothic-ma .party!
		gothic-ma .alive? if
			($ "Mae" pbold .. "  escapa da fazenda, gracas a voce!" $) +crtext
		else
			($ "Mae" pbold .. "  nao escapou com voce." $) +crtext
			morale--all
		then
	then
	gothic-pa 0= not if
		0 gothic-pa .party!
		gothic-pa .alive? if
			($ "Pa" pbold .. "  sobrevive gracas a sua ajuda!" $) +crtext
		else
			($ "Pa" pbold .. " ' nao conseguiu." $) +crtext
			morale--all
		then
	then
	gothic-ma 0= gothic-pa 0= and if "Infelizmente, nenhum dos fazendeiros conseguiu." +crtext badfx morale--all then;
	gothic-pa 0= not gothic-ma 0= not and if
		gothic-pa .alive? gothic-ma .alive? and if
			"Gratos por estarem vivos, eles lhe oferecem um presente:" +crtext
			choice( "Reserva de 10 alimentos" )choice: farmrescue-4
			choice( "'Dicas de Conserto do Pa" )choice: farmrescue-1
			choice( "'Remedios Caseiros da Ma" )choice: farmrescue-2
			choice( "Uma Foice Robusta" )choice: farmrescue-3
		then
	then
;

: drunkchat-up
	rescue choosechar cicon
	($ "'OK, eu acordei!" pbold .. "
	" .. cHe .. "  diz " .. che .. "''vou seguir voce por um tempo. Tambem que voce deveria encontrar " .. chim .. "  fora da cidade, se voces dois conseguirem." $) +crtext
		1 rescue .party-familiar 0 nearest .unseen!
		rescue .wakeup
		mission{ ' rescuedme to post-mission-cyoa }
		clear-cyoa
;

cyoa-choice: drunkchat-med
	rescue choosechar
	($ pname .. "  faz um diagnostico cuidadoso e, em seguida, comeca a chutar " .. cname .. "  no traseiro." $) cyoa-text!
	drunkchat-up
;

cyoa-choice: drunkchat-food
	($ pname .. "  comeca a esmagar um pouco de comida em " .. cname .. "''boca como " .. che .. "  dorme." $) cyoa-text!
	-1 add-food
	drunkchat-up
;

cyoa-choice: drunkchat-poke
	($ pname .. "  cutuca " .. cname .. "  para acordar " .. chim .. "  do sono. Sao necessarias quatro horas consecutivas de cutucadas." $) cyoa-text!
	drunkchat-up
	o'clock@ 4 + o'clock
	4 7 rnd nearest .tpos anchor! zombs
	// 6 10 rnd roomgen{ .zombspawn }
;

cyoa-choice: drunkchat-pet
	($ pname .. "  comeca a lamber " .. cname .. "''rosto como " .. che .. "  dorme." $) cyoa-text!
	drunkchat-up
;

cyoa: drunkchat
	// rescue to recruitee ricon
	rescue choosechar cicon
	dead? if npc-died then;
	"Desmaiado" cyoa-title!
	($ pname .. "  ja viu esse tipo de coisa antes.	Esta pessoa claramente bebeu muito refrigerante a julgar pelas garrafas espalhadas.	Agora " .. che .. "'' precisa de comida para equilibrar " .. chis .. "  acucar no sangue." $) cyoa-text!
	pchar notpet? if
		pchar .cmedical+ if
			pchar charchoice( "  revive usando habilidades medicas" )choice: drunkchat-med
		then
		LOOT_FOOD pchar .lootamt@ 0 > if
			choice( "Alimente " .. chim .. "  1 alimento" )choice: drunkchat-food
		else
			"There might be some food in a nearby building..." +crtext
		then
	else
		pchar charchoice( "  revive " .. chim .. "  atraves de lambidas na cara" )choice: drunkchat-pet
	then
	choice( "Deixe " .. chim .. "  sozinho" )choice
	LOOT_FOOD pchar .lootamt@ 1 < if
		choice( "Cutucar " .. chim .. "  por 4 horas" )choice: drunkchat-poke
	then
;

cyoa-choice: rescueme-2
	($ cname .. "  comeca a seguir " .. pname .. "!
	" .. cHe .. "  pode ser util, se voce puder obter " .. chim .. " " $) cyoa-text!
	rescueget mission{ ' rescuedme to post-mission-cyoa }
;

cyoa-choice: rescueme-d
	($ cname .. "  comeca a seguir " .. pname .. "!
	" .. cHe .. "  pode ser util, se voce puder obter " .. chim .. " " $) cyoa-text!
	rescueget mission{ ' rescueddog to post-mission-cyoa }
;

cyoa: rescueme
	rescue choosechar cicon
	dead? if npc-died then;
	"Todo mundo precisa de ajuda as vezes" cyoa-title!
	($ cname .. " diz que " .. che .. " 	quer mesmo sair deste lugar." .. cHe .. " " .. che .. "  pode te seguir por ai." $) cyoa-text!
		choice( "Deixe " .. chim .. "  te seguir"  )choice: rescueme-2
		choice( "Deixe " .. chim .. "  por enquanto" )choice
;

cyoa: dogrescue
	rescue choosechar cicon
	dead? if npc-died then;
	
	cchar .bodytype@ 20 = if
	"O Ultra Wiener" cyoa-title!
		($ "'Voce nunca viu um cachorro salsicha mais impressionante. Este deve ter sido um cachorro de exposicao, com pedigree. O cachorro tambem esta empunhando uma espingarda.		A coleira dele diz:" .. " ' LIL' SAM BYOOL" rainbold $) text!
		cchar .shootingREV cchar .witsREV		
	else
		"'O que um cachorro esta fazendo aqui?" cyoa-title!
		($ "O cachorro parece extraordinariamente manso! A maioria dos cachorros que ainda estao vivos se tornaram selvagens a essa altura. Este cachorro parece ansioso para ver humanos." $) text!
	then
		choice( "Deixe o cachorro te seguir"  )choice: rescueme-d
		choice( "Deixe o cachorro em paz por enquanto" )choice
;

cyoa-bridge: feral-recruit
	cchar to recruitee goodfx clear-cyoa
	($ "O cachorro quer seguir " .. pname .. "!" $) text!
	pchar .morale++
	0.25 cchar .ai.wander_chance! 0 nearest .unseen!
	recruit-yesno
;

cyoa: feralrescue
	interactee .character@ choosechar cicon
	dead? if npc-died then;
	"Matilha de Cachorros" cyoa-title!
	($ "'O cachorro e selvagem! Ele nao e mais capaz de confiar em humanos. " $) cyoa-text!
		mission-team-stack ' notpet? stack-filter nip 1 < if
			choice( "Deixe o cachorro em paz" )choice
			pchar dog? if 
				choice( "Recrute o cachorro" )choice: feral-recruit
				"Isso nao e problema, ja que nao ha humanos por perto."				
			else
				($ pname .. " ' nao e um cachorro. O cachorro selvagem rosna em suspeita." $)
			then +crtext
		then;
		pchar isdogpal? if
			($ "Claro, " .. pname .. " is different! The dog starts to remember some long forgotten memories of when dogs and humans worked together... " $) +crtext
			choice( "Recrute o cachorro" )choice: feral-recruit
		else
			"This dog's eyes glimmer with recognition for a brief moment..." +text
		then
		choice( "Deixe o cachorro em paz" )choice
;

: southofshared
	interactee .pos lt .pos! // Put it right on the CYOA target we are interacting with
	lt .pos interactee .rad.y@ + lt .rad.x@ 2 * + lt .pos.y! // Move it down by the interactee's rectangular y radius and ours (circular) x2 (no guessing)
	-16 lt .pos.z! // Start above ground
	-1 lt .vel.z! // With a little bounce up
;

: southofcyoa
	southofshared
	lt .sproink // AND NEW SPECIAL FX
;

: southofaltar
	southofshared
	lt .poofmescary
;

: safe1-get 20 add-shell shotgun southofcyoa ;
: safe1 choice( "Escopeta com 20 cartuchos" )choice: safe1-get ;
: safe2-get 36 add-bullet dolt southofcyoa ;
: safe2 choice( "Magnum e 36 balas" )choice: safe2-get ;
: safe3-get 75 add-bullet ;
: safe3 choice( "Caixa com 75 balas" )choice: safe3-get ;
: safe4-get 80 add-gas ;
: safe4 choice( "Recipiente com 80 de gas" )choice: safe4-get ;
: safe5-get 45 add-shell ;
: safe5 choice( "Caixa com 45 cartuchos" )choice: safe5-get ;
: safe6-get 60 add-rifle ;
: safe6 choice( "Caixa com 60 municoes de rifle" )choice: safe6-get ;
: safe7-get 10 add-medical ;
: safe7 choice( "Pacote com 10 medicos" )choice: safe7-get ;
: safe8-get 15 add-food ;
: safe8 choice( "Estoque de 15 alimentos" )choice: safe8-get ;
: safe9-get uzi southofcyoa ;
: safe9 choice( "Submetralhadora" )choice: safe9-get ;
: safe10-get hunting southofcyoa ;
: safe10 choice( "Rifle de caca" )choice: safe10-get ;
: safe11-get 30 add-rifle rifle southofcyoa ;
: safe11 choice( "Rifle de cowboy e 30 municoes" )choice: safe11-get ;
: safe12-geta knight southofcyoa ;
: safe12-getb ak47 southofcyoa ;
: safe12-getc autoshot southofcyoa ;
: safe12-getd slug southofcyoa ;
: safe12
	20 %chance if
		choice( "Espada de cavaleiro" )choice: safe12-geta
	else
		25 %chance if
			choice( "Espingarda de bala raiado" )choice: safe12-getd
		else
			50 %chance if
				choice( "Espingarda automatica" )choice: safe12-getc
			else
				choice( "Rifle de assalto" )choice: safe12-getb
			then
		then
	then
 ;

0 var safe-reward
	deck-new
	card: safe1
	card: safe2
	card: safe3
	card: safe4
	card: safe5
	card: safe6
	card: safe7
	card: safe8
	card: safe9
	card: safe10
	card: safe11
	card: safe12
constant SAFE-REWARD
SAFE-REWARD shuffles to safe-reward

: safe-reward-get
	1 nearest .open!
	safe-reward depths 0= if SAFE-REWARD shuffles to safe-reward then
	4 for safe-reward drawcard next	
	clear-cyoa resetsafe
;
: combo? safecombo 0 > ;

cyoa: safe-crack
	"Cofre arrombado!" cyoa-title!
	($ "Demora algumas horas, mas " .. pname .. " 'descobre a combinacao do cofre! Junto com algum dinheiro agora inutil, ele tinha:" $) cyoa-text!
	safe-reward-get
	o'clock@ 2 + o'clock 6 10 rnd nearest .tpos anchor! zombs 
;
cyoa: safe
	pchar humany? not if
	"Cofre contra animais de estimacao" cyoa-title!
		($ pname .. " 'nao possui o cerebro necessario para abrir um cofre. So mais um exemplo do mundo humano anti" .. cdogcat .. "  preconceito." $) cyoa-text!
	then;
	combo? if 
	"Cofre aberto!" cyoa-title!
		($ pname .. "  desbloqueia e abre o seguro!		Acompanhado de algumas papeis agora inuteis, ele tinha:" $) cyoa-text!
		safe-reward-get
	else
	"Seguro Fechado" cyoa-title!
	"Este seguro esta fechado e muito solido.	Com sorte, o numero de combinacao ou chave estara por perto, algunde." cyoa-text!
		pchar .cmechanic+ if
			choice( "Deixe estar por enquanto" )choice
			pchar charchoice( "  quebra o seguro!" )choice: safe-crack
		then
	then 
	
;

cyoa: comboskel
	"Numero de Combinacao" cyoa-title!
	($ "Este esqueleto esta agarrando um pequeno pedaco de papel.	O papel tem uma serie de numeros nele que parecem ser um codigo de combinacao de seguro!	" .. pname .. " pega o." $) cyoa-text! goodfx
	1 to safecombo clear-cyoa
;

cyoa: combokey
	"Chave Grande" cyoa-title!
	($ "Este grande chave parece abrir um seguro!" .. pname .. " pega o." $) cyoa-text! goodfx
	1 to safecombo deleteme
;

cyoa: combonote
	"Numero de Combinacao" cyoa-title!
	($ "Este pedaco de papel tem uma serie de numeros nele que parecem ser um codigo de combinacao de seguro!	" .. pname .. " pega o." $) cyoa-text! goodfx
	1 to safecombo deleteme
;

: lab1 
	cchar .hairtype@ 0 = if 
		($ cname .. " cresce novas cabelos! Legal!" $) +crtext morale3+ goodfx
		1 25 rnd cchar .hairtype!
	else
		($ cname .. " perde " .. chis .. "  cabelos!" $) +crtext morale3- badfx
		0 cchar .hairtype!
	then
;
: lab2 
	cchar .cwits+ if
		($ cname .. "'s eyes and mind are a bit more blurry..." $) +crtext wits-- shooting-- badfx
		0.8 cchar .scale.head.x! 0.8 cchar .scale.head.y! 0.5 pchar .headoff.y!
	else
		($ cname .. "  sente se mais perceptivo!" $) +crtext wits++ shooting++ goodfx
		1.2 cchar .scale.head.x! 1.2 cchar .scale.head.y! 0.5 pchar .headoff.y!
	then	
;
: lab3
	cchar .dexterity@ 0 > if
		($ cname .. "  sente se como " .. che .. " is slowing down..." $) +crtext 0 cchar .dexterity! badfx
		0.7 pchar .scale.body.y!
	else
		($ cname .. "  sente se mais rapido!" $) +crtext dexterity++ goodfx
		1.3 pchar .scale.body.y!
	then	
;
: lab4
	($ cname .. "  sente se como uma nova pessoa!" $) +crtext 6 cchar .morale! goodfx
		0 3 rnd cchar .bodytype!
		0 25 rnd cchar .hairtype!
		0 15 rnd cchar .torsotype!
		0 15 rnd cchar .legstype!	
		0 11 rnd cchar .headtype!
		cchar .female@ 0 = if 
			1
		else
			0
		then cchar .female!
;
: lab5
	cpet? if
		($ cname .. "  sente se mais humano!" $) +crtext 1 cchar .loyalty! loyalty-- goodfx
		// 0 cchar .pet! cchar statmod{ .undog } 0 3 rnd cchar .bodytype!
		cchar .dogtosuperdog	
	else
		($ cname .. "  sente se mais leal!" $) +crtext 5 cchar .loyalty! loyalty++ goodfx
		cchar .doggify cchar .dogtosuperdog	
	then loyaltyREV
;
: lab6
	50 %chance if
		($ cname .. "  sente se como se o mundo tivesse ficado maior!" $) +crtext badfx
		0.8 cchar .scale.head.x! 0.8 cchar .scale.head.y! 0.5 cchar .headoff.y!
		0.7 cchar .scale.body.y! 0.7 cchar .scale.body.x!
	else
		($ cname .. "  sente se grande e no comando!" $) +crtext vitality++ goodfx
		1.2 cchar .scale.head.x! 1.2 cchar .scale.head.y! 0.5 cchar .headoff.y!
		1.2 cchar .scale.body.y! 1.2 cchar .scale.body.x!
	then	
;
: lab7
	cchar .vitality@ 3 > if
		($ cname .. "  sente se fragil!" $) +crtext 2 cchar .vitality!  2 cchar .health! badfx
		0.6 cchar .scale.body.x!
	else
		($ cname .. "  sente se robusto!" $) +crtext vitality++ goodfx
		1.5 cchar .scale.body.x!
	then	
;
: lab8
	cchar .specialbody@ 73 = cchar .specialhead@ 73 = and if
		($ cname .. " turns into a dogdogdogtaur! It's unclear what this actually means..." $) +crtext
	then;
	cpet? if
		($ cname .. "  se transforma em um dogman: um cachorro com corpo de humano!" $) +crtext goodfx
		0 cchar .pet! cchar statmod{ .undog } 0 3 rnd cchar .bodytype! 73 cchar .specialhead!
	then;
	cchar .specialbody@ 73 = cchar .specialhead@ 73 = or if
		($ cname .. "  se transforma em um dogdogtaur! Um cachorro com bracos de humano e cabeca e pernas de cachorro!" $) +crtext goodfx 73 cchar .specialhead! cchar .dogtaur
	else
		($ cname .. "  se transforma em um dogtaur: um cachorro com tronco e bracos de humano!" $) +crtext goodfx
		cchar .dogtaur
	then	
;
: lab9
	cchar .specialtype@ 74 = if
		($ "'Uma mosca entra na maquina! Parece nao incomodar " .. cname .. "..." $) +crtext
	then;
	cpet? if
		($ cname .. "  transforma se numa mosca cao! Que horror!" $) +crtext goodfx
		0 cchar .pet! cchar statmod{ .undog } 
		50 %chance if 73 cchar .specialhead! cchar .flybody else 73 cchar .specialbody! cchar .flyhead then
	then;
	($ "Uma mosca entra na maquina quando ela liga! " .. cname .. " gets spliced up with fly DNA! Bonus!!!" $) +crtext goodfx cchar
	cchar .specialbody@ 74 = cchar .specialhead@ 74 = or if
		.fly
	else
		rnd5050 if .flyhead else .flybody then
	then	
;


0 var lab-effect
	deck-new
	card: lab1
	card: lab2
	card: lab3
	card: lab4
	card: lab5
	card: lab6
	card: lab7
	card: lab8
	card: lab9
constant LAB-EFFECT
LAB-EFFECT shuffles to lab-effect

cyoa-choice: lab-go
	"Resultados do Experimento" cyoa-title!
	($ cname .. "  e atingido por misteriosos raios cientificos ate a maquina parar de funcionar. " .. cHe .. "  sente se energizado!" $) text!
	strength++ fitness++ cchar .fullheal
	lab-effect depths 0= if LAB-EFFECT shuffles to lab-effect then
	drawcard clear-cyoa
;

cyoa: lab
	"Dispositivo Misterioso" cyoa-title!
	($ "'Parece uma especie de coisa cientifica maluca. Nao da para dizer o que isso pode fazer se for mexido." $) text!
		choice( "Deixe isso quieto por enquanto" )choice
		pchar charchoice( "  entra na maquina!" )choice: lab-go
;

: altarpush lt .poofmescary ;

cyoa-choice: altar-1
	($ cname .. "  toca o altar e e instantaneamente atingido por energias estranhas!" $) text!
	ouch2- bitefx 300 tint_trigger 0 light_mode!
	alive? if 
		($ cHe .. "  consegue sobreviver e se sente diferente do que antes." $) +crtext goodfx
		strength++ fitness++ shooting++
	else
		($ "Para " .. chis .. "  sacrificio, o altar concede um presente!		A distancia, um " .. "duende assustador" fbold .. " ri de forma semelhante a um goblin." $) +crtext
		33 %chance if ' .strength++ else 50 %chance if ' .fitness++ else ' .shooting++ then then foreach-team-char
		goblin-laugh
		0 6 rnd case
		0 of 30 sshot altarpush shotgun southofaltar endof
		1 of 50 sbullet altarpush dolt southofaltar endof
		2 of 10 for 1 LOOT_FOOD spawnscat altarpush next endof
		3 of 40 srifle altarpush rifle southofaltar endof
		4 of 60 sgas altarpush endof
		5 of 7 for 1 LOOT_MEDICAL spawnscat altarpush next endof
		6 of 
			70 %chance if 
				33 %chance if aluminum else 50 %chance if machete2 else fire then then
			else 
				60 %chance if hunting else 70 %chance if uzi else autoshot then then
			then southofaltar
		endof endcase
	then
;

cyoa: altar
	"Altar de Sacrificio" cyoa-title!
	picon!
	($ cname .. " esta proximo de um altar de pedra. Deve ser a fonte do sentimento ameacador que vem deste lugar inteiro.	O altar parece " .. "extremamente perigoso!" rbold .. " 'E um pouco estranho que alguem tenha isso em casa.	Provavelmente nao deve ser mexido!" $) text!
	"AVISO: IRA MATAR QUALQUER UM QUE NAO ESTIVER COM A SAUDE COMPLETA!" rbold +crtext
		choice( "Tem " .. cname .. " tocar no altar" )choice: altar-1
		choice( "Deixar o altar em paz por enquanto" )choice
;

}

: randommag
	magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! scatter
;

: randombook
	book 5 9 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health!
;

: randombookmag
	50 %chance if
	book 5 9
	else
		magazine 0 4
	then rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health!
;

: altar altarstand cyoa{ ' altar cyoa! } ;
: laboratory labmachine cyoa{ ' lab cyoa! } ;
: metalsafe safe cyoa{ ' safe cyoa! } ;
: combokey goldkey cyoa{ ' combokey cyoa! } delete-covering ;
: comboskel combokey skelscat ;
// skelscat cyoa{ ' comboskel cyoa! } 12 health! ;
: combonote wrotenote scatter cyoa{ ' combonote cyoa! } delete-covering ;

: new-drunk-npc new-npc lt .character@ dup .liedown 1 lt .unseen! 
	0 over .ai.wander_chance! 0 over .ai.attack_chance! drop
	// lying on random side
	rndsign 90 * lt .angle!
	160 190 frnd lt .aim_angle! 
	npc@rescue
	lt cyoa{ ' drunkchat cyoa! }
	lt .character@ .notake
;

: new-rescue-npc new-npc@ rescue-weapon over .weapona! 1 lt .unseen! 0 lt .leaveok! npc@rescue cyoa{ ' rescueme cyoa! } drop ;
: new-feraldog map{ selected } . cr new-npc@ .doggify 1 lt .unseen! 0 lt .leaveok! npc@rescue lt cyoa{ ' feralrescue cyoa! } drop ;
: new-rescue-dog 
	map{ selected } . cr new-npc@ 
	1 %chance kspet-checker 1 < and if
		.sambyoolify
	else
		.doggify
	then
	1 lt .unseen! 0 lt .leaveok! 
	npc@rescue lt cyoa{ ' dogrescue cyoa! } drop 
;

// FIXME: Things like these NPC words should be somewhere else
: inside-npc new-npc 0 lt .leaveok! 1 lt .unseen! lt .character@ ;
: still-npc new-npc 0 lt .leaveok! 1 lt .unseen! 0 lt .character@ .ai.wander_chance! 185 rnd5050 + lt .aim_angle! lt .character@ ;
: nomove-npc new-npc lt .character@ args( ch )
	185 rnd5050 + lt .aim_angle!
	0 <- ch .ai.wander_chance!
	50 <- ch .ai.safety_leash!
;


// Mansion loot

: hauntedwareset hauntspoon .drift hauntfork .drift hauntknife .drift ;

0 value mansiontype

: mansionloot1
	mansiontype 0 > if // If a GHOST MANSION
		goldspear
	else // If a normal dark mansion
		goldknife
	then .drift
;
: mansionloot2 hauntedwareset 3 4 rnd sfood ;
// Used to be mansiontype 0 > if turretred else turretgreen then .drift
: mansionloot3 mansiontype 0 > if 6 else 3 then for pukeyball .drift next ;
: mansionloot4 mansiontype 0 > if dragonslayer else megaknight then .drift ;
: mansionloot5 cavalry .drift ;

0 stack
	' mansionloot1 shove
	' mansionloot2 shove
	' mansionloot3 shove
	' mansionloot4 shove
	' mansionloot5 shove
value mansionloot-picker-base
mansionloot-picker-base shuffles var mansionloot-picker

: mansionloot-pick mansionloot-picker depths 0= if mansionloot-picker-base shuffles to mansionloot-picker then pops nip execute ;

// Effective stats used to be here, was moved to charfilt.df due to weird dependencies

// =================
// Quick stat checks
// =================

: statroll 2 skillcheck? ;

: moraleroll cchar .morale statroll ;
: attituderoll cchar .attitude statroll ;
: temperroll cchar .composure statroll ;
: loyaltyroll cchar .loyalty statroll ;
: witsroll cchar .wits statroll ;

: charm-sum dup .attitude swap .wits + ;
: charmroll cchar charm-sum 2 / statroll ;

: strengthroll cchar .strength statroll ;
: medicalroll cchar .medical statroll ;
: mechanicroll cchar .mechanical statroll ;
: shootingroll cchar .shooting statroll ;
: fitnessroll cchar .fitness statroll ;

// checks for the choicechar to have over 4 or under 2 in a stat

: cmorale- cchar .morale low ;
: cattitude- cchar .attitude low ;
: ctemper- cchar .composure low ;
: cloyalty- cchar .loyalty low ;
: cwits- cchar .wits low ;

: cstrength- cchar .strength low ;
: cmedical- cchar .medical low ;
: cmechanic- cchar .mechanical low ;
: cshooting- cchar .shooting low ;
: cfitness- cchar .fitness low ;

: cmorale+ cchar .morale high ;
: cattitude+ cchar .attitude high ;
: ctemper+ cchar .composure high ;
: cloyalty+ cchar .loyalty high ;
: cwits+ cchar .wits high ;

: cstrength+ cchar .strength high ;
: cmedical+ cchar .medical high ;
: cmechanic+ cchar .mechanical high ;
: cshooting+ cchar .shooting high ;
: cfitness+ cchar .fitness high ;

: morale+roll cmorale+ moraleroll or ;
: attitude+roll cattitude+ attituderoll or ;
: temper+roll ctemper+ temperroll or ;
: loyalty+roll cloyalty+ loyaltyroll or ;
: wits+roll cwits+ witsroll or ;
: strength+roll cstrength+ strengthroll or ;
: medical+roll cmedical+ medicalroll or ;
: mechanic+roll cmechanic+ mechanicroll or ;
: shooting+roll cshooting+ shootingroll or ;
: fitness+roll cfitness+ fitnessroll or ;

: decruited
	+cr ($ cname .. " deixou o time!" $) +text
;

: screcruit recruitee .specialtype! ;
: decruit-bad-silent TEAM_STATUS_BAD_LEFT cchar .decruit ;
: decruit-bad decruited decruit-bad-silent badfx ;
: decruit-bad-takeweapons ctakeweapons decruit-bad ;
: decruit-bad-silent-takeweapons ctakeweapons decruit-bad-silent ;
: decruit-good-silent ctakeweapons TEAM_STATUS_GOOD_LEFT cchar .decruit ;
: decruit-good decruited decruit-good-silent goodfx ;


// pain words moved upwards

: 0weapona
	cchar .weapona@ 0 > if
		($ cHe .. " perde " .. chis .. " arma primaria!" $) +crtext
		0 cchar .weapona!
	then
;
: 0weaponb
	cchar .weaponb@ 0 > if
		($ cHe .. " perde " .. chis .. " arma secundaria!" $) +crtext
		0 cchar .weaponb!
	then
;
: 0weaponc
	cchar .weaponb@ 0 > if
		($ cHe .. " perde " .. chis .. " arma terciaria!" $) +crtext
		0 cchar .weaponc!
	then
;
: 0weapons
	cchar .weaponc@ 0 > cchar .weaponb@ 0 > cchar .weapona@ 0 > or or if
		($ cHe .. " perde " .. chis .. " armas!" $) +crtext
		0 cchar .weaponb! 0 cchar .weapona! 0 cchar .weaponc!
	then
;

: trunk.killer 
	trunk.count 0 > if
		($ trunk.count rbold .. " ARMAS PERDIDAS!" rbold $) +crtext
		trunk.clear
	then
;

: morale--gourmandall ' .morale--gourmand foreach-team-char ;

: chowtime
	fullmeal if
		($ Thegroup .. " faz uma refeicao decente." $)
		cyoa-text-append
		chowday
		goodfx
		' .cmorale-recover foreach-team-char
	then;
	halfmeal if
		($ Thegroup .. "  esta com pouca comida e come menos." $)
		cyoa-text-append
		chowday
		badfx
		' .morale-- foreach-team-char morale--gourmandall
	then;
	trunk-food 0 = if
		($ Thegroup .. "  descobre que nao ha mais comida. O desespero se instala." $)
		cyoa-text-append
		team-count 1 > if
			' .morale3- foreach-team-char
		else
			soloer .morale2-
		then morale--gourmandall badfx
	then;
	($ Thegroup .. " ' nao tinha comida suficiente para comer. A duvida aumenta." $)
		cyoa-text-append
	chowday
	badfx
	' .morale2- foreach-team-char morale--gourmandall
;

// Sleeping

: tired statmod{ .tired } ;
: untired statmod{ .untired } ;

: nosleep ' tired foreach-team-char ;
: sleep	' untired foreach-team-char ;
: sleep-except cchar tired cchar ' untired foreach-team-char-except ;

: 50nosleep 50 %chance if tired then ;
: 5050nosleep ' 50nosleep foreach-team-char ;

: tiredcamp chowtime nosleep ;
: fullcamp chowtime sleep ;

// Words for trading
: charm-text choosechar 
	($ "O comerciante se apaixona por " .. cname .. "' e da mais!" $) 
	+crtext +cr witsREV attitudeREV cicon
;

: charm-gas	most-charming if charm-text 40 trunk-gas+ then ;
: charmhalf-gas	most-charming if charm-text 20 trunk-gas+ then ;

: charm-bullet most-charming if charm-text 30 trunk-bullet+ then ;
: charmhalf-bullet most-charming if charm-text 15 trunk-bullet+ then ;

: charm-shotgun-shell most-charming if charm-text 20 trunk-shotgun-shell+ then ;
: charmhalf-shotgun-shell most-charming if charm-text 10 trunk-shotgun-shell+ then ;

: charm-rifle-shell	most-charming if charm-text 25 trunk-rifle-shell+ then ;
: charmhalf-rifle-shell	most-charming if charm-text 12 trunk-rifle-shell+ then ;

: charm-medical	most-charming if charm-text 5 trunk-medical+ then ;
: charmhalf-medical	most-charming if charm-text 2 trunk-medical+ then ;

: charm-allammo	most-charming if charm-text 20 trunk-bullet+ 10 trunk-shotgun-shell+ 15 trunk-rifle-shell+ then ;
: charmhalf-allammo	most-charming if charm-text 10 trunk-bullet+ 5 trunk-shotgun-shell+ 7 trunk-rifle-shell+ then ;

: food-all-to-gas trunk-food 10 * trunk-gas+ eat-all ;
: food-15-to-gas 150 trunk-gas+ -15 trunk-food+ charm-gas ;
: food-10-to-gas 100 trunk-gas+ -10 trunk-food+ charmhalf-gas ;
: food-5-to-gas 50 trunk-gas+ -5 trunk-food+ ;

: food-all-to-bullet trunk-food 8 * trunk-bullet+ eat-all ;
: food-15-to-bullet 120 trunk-bullet+ -15 trunk-food+ charm-bullet ;
: food-10-to-bullet 80 trunk-bullet+ -10 trunk-food+ charmhalf-bullet ;
: food-5-to-bullet 40 trunk-bullet+ -5 trunk-food+ ;

: food-all-to-shotgun-shell trunk-food 5 * trunk-shotgun-shell+ eat-all ;
: food-15-to-shotgun-shell 75 trunk-shotgun-shell+ -15 trunk-food+ charm-shotgun-shell ;
: food-10-to-shotgun-shell 50 trunk-shotgun-shell+ -10 trunk-food+ charmhalf-shotgun-shell ;
: food-5-to-shotgun-shell 25 trunk-shotgun-shell+ -5 trunk-food+ ;

: food-all-to-rifle-shell trunk-food 6 * trunk-rifle-shell+ eat-all ;
: food-15-to-rifle-shell 90 trunk-rifle-shell+ -15 trunk-food+ charm-rifle-shell ;
: food-10-to-rifle-shell 60 trunk-rifle-shell+ -10 trunk-food+ charmhalf-rifle-shell ;
: food-5-to-rifle-shell 30 trunk-rifle-shell+ -5 trunk-food+ ;

: food-all-to-medical trunk-food 1 * trunk-medical+ eat-all ;
: food-15-to-medical 15 trunk-medical+ -15 trunk-food+ charm-medical ;
: food-10-to-medical 10 trunk-medical+ -10 trunk-food+ charmhalf-medical ;
: food-5-to-medical 5 trunk-medical+ -5 trunk-food+ ;

: food-all-to-allammo 
	trunk-food 5 * trunk-bullet+ 
	trunk-food 4 * trunk-rifle-shell+ 
	trunk-food 3 * trunk-shotgun-shell+ 
	eat-all 
;
: food-15-to-allammo 75 trunk-bullet+ 45 trunk-shotgun-shell+ 60 trunk-rifle-shell+ -15 trunk-food+ charm-allammo ;
: food-10-to-allammo 50 trunk-bullet+ 30 trunk-shotgun-shell+ 40 trunk-rifle-shell+ -10 trunk-food+ charmhalf-allammo ;
: food-5-to-allammo 25 trunk-bullet+ 15 trunk-shotgun-shell+ 20 trunk-rifle-shell+ -5 trunk-food+ ;

: rob-guiltloyal
	dup .cloyalty+ if
		dup .loyaltyREV
		dup .morale2-
	then;
	dup .loyaltyREV
	dup .morale--
;
: rob-guilt?
	dup .cloyalty- if
		dup .loyaltyREV
		dup .morale++
	then;
	rob-guiltloyal
;
: rob-guilt2?
	dup .cloyalty- if dup .loyaltyREV then;
	rob-guiltloyal
;
: rob-guilt	' rob-guilt? foreach-team-char ;
: rob-guilt2 ' rob-guilt2? foreach-team-char ;

: rob-fail?
	70 %chance if .ouch-- else .morale2- then
;
: rob-fail ouch-- ' rob-fail? foreach-team-char ;
: rob-megafail?
	dup .ouch-- dup .morale3-
;
: rob-megafail ' rob-megafail? foreach-team-char ouch-- ;

// Words for dealing with bandits

: ezbandit? road{ easybandit-mode } 0 > ;
: ezbandit2/ ezbandit? if 2 / then ;

: anyammo? trunk-bullet 1 > trunk-rifle-shell 1 > trunk-shotgun-shell 1 > or or ;

: ishaggler? statmod{ .haggler? } ;
: numhaggler team-stack ' ishaggler? stack-filter ;
: haggler? numhaggler 0 > ;

: bandit-toll team-count 
	ezbandit? if // EZ mode
		haggler? gstats{ perk-haggler } 2 >= and if drop
			gstats{ perk-haggler } 3 >= if
			2
			else
			3
			then * 1
		else drop // No haggler
			3 * 3
		then
	else // Deadlier and beyond
		haggler? gstats{ perk-haggler } 2 >= and if drop
			gstats{ perk-haggler } 3 >= if
			3			
			else
			4
			then * 2
		else drop // No haggler
			4 * 4 
		then
	then +
;
// 5 * 5 + ;
: bandit-toll2 bandit-toll 1.3 / >int ;

: bandit-toll? trunk-food bandit-toll 1 - > ;
: bandit-toll-pay bandit-toll -1 * trunk-food+ badfx ;
: bandit-toll-pay2 bandit-toll2 -1 * trunk-food+ badfx ;
: bandit-ammo
	trunk-bullet 0 > if trunk-bullet ezbandit2/ -1 * trunk-bullet+ then
	trunk-rifle-shell 0 > if trunk-rifle-shell ezbandit2/ -1 * trunk-rifle-shell+ then
	trunk-shotgun-shell 0 > if trunk-shotgun-shell ezbandit2/ -1 * trunk-shotgun-shell+ then
;
: bandit-med
	trunk-medical 0 > if trunk-medical ezbandit2/ -1 * trunk-medical+ then
;
: bandit-gas
	trunk-gas 0 > if trunk-gas -1 * trunk-gas+ then
;
: bandit-gastank
	trunk-gas 100 > if trunk-gas 100 - -1 * >int trunk-gas+ then
;
: bandit-ammomed bandit-ammo bandit-med ;
: bandit-food trunk-food 0 > if eat-all then ;
: bandit-ammofood bandit-food bandit-ammo ;
: bandit-most bandit-ammofood bandit-med ;
: bandit-mostgas bandit-most bandit-gastank ;
: bandit-all bandit-most bandit-gas ;

: bandit-ammo2
	trunk-bullet 1 > if trunk-bullet 2 / ezbandit2/ -1 * >int trunk-bullet+ then
	trunk-rifle-shell 1 > if trunk-rifle-shell 2 / ezbandit2/ -1 * >int trunk-rifle-shell+ then
	trunk-shotgun-shell 1 > if trunk-shotgun-shell 2 / ezbandit2/ -1 * >int trunk-shotgun-shell+ then
;
: bandit-med2
	trunk-medical 1 > if trunk-medical 2 / ezbandit2/ -1 * >int trunk-medical+ then
;
: bandit-ammomed2
	bandit-ammo2 bandit-med2
;
: bandit-food2
	trunk-food 1 > if trunk-food 2 / ezbandit2/ -1 * >int trunk-food+ then
;
: bandit-supplies2
	bandit-ammomed2 bandit-food2 bandit-gastank
;

: vname victim .colourname ;
: bname bully .colourname ;
: bully-st bully if bully .specialtype@ then; 0 ;
: camper-st sc-camper if sc-camper .specialtype@ then; 0 ;


// Useful words for assigning CYOA roles
0 value victim
0 value bully
0 value sc-camper


: recruited 
	+cr ($ rname .. " se une al equipo! 	Adelante a Canada " $) +text
	recruitee .morale@ 2 < if
		2 recruitee .morale!
	then
	recruitee .recruit
	gstats{ ugnome-extraZP 2 > if 1 zombop+! then }

	// if the character has a related THING, clear what it's following so it will follow the player
	recruitee .thing@ if 0 recruitee .thing@ .ai.followid! then

;

// Word replacements that alter pronouns and such to the situation

: xhis .female@ if "ella" else "el" then ;
: xHis .female@ if "Su" else "Su" then ;
: xhim .female@ if "ella" else "le" then ;
: xhe .female@ if "ella" else "el" then ;
: xHe .female@ if "Ella" else "El" then ;

: ghis solo if soloer xhis else "su" then ;
: gHis solo if soloer xHis else "Su" then ;
: ghim solo if soloer xhim else "los" then ;
: ghe solo if soloer xhe else "el grupo" then ;
: gHe solo if soloer xHe else "El grupo" then ;
: gthey solo if soloer xhe else "ellos" then ;
: gThey solo if soloer xHe else "Ellos" then ;

// Below words for putting in an extra s depending on group

: g1s solo if "s" else "" then ;
: g2s team-count 2 = if "s" else "" then ;
: gs solo if "" else "s" then ;
: gS solo if "" else "S" then ;

: gare solo if "es" else "son" then ;
: gwere solo if "era" else "eran" then ;
: gtheywere gthey .. " " .. gwere ;
: gtheyare gthey .. " " .. gare ;
: gTheyare gThey .. " " .. gare ;

// Below example: "trasero" aVSs = you're a butt versus you're butts, depending 2 or 3 people.
: g2a team-count-alive 2 = if "  un" else "" then ;
: g1a team-count-alive 1 = if "  un" else "" then ;
: g1an team-count-alive 1 = if "  una" else "" then ;
: g3s team-count-alive 2 > if "s" else "" then ;
: aVSs ($ g2a .. swap .. g3s $) ;
: gaVSs ($ g1a .. swap .. gs $) ;
: ganVSs ($ g1an .. swap .. gs $) ;

: I-or-we team-count-alive 1 = if "  Yo " else "  nosotros " then ;
: I-or-WE team-count-alive 1 = if "  Yo " else "  NOSOTROS " then ;

: cname-he solo if soloer xhe else cname then ;
: cname-He solo if soloer xHe else cname then ;
: thegroup-him solo if soloer xhim else "el grupo" then ;
: chis cchar xhis ;
: cHis cchar xHis ;
: chim cchar xhim ;
: che cchar xhe ;
: cHe cchar xHe ;

: phis pchar xhis ;
: pHis pchar xHis ;
: phim pchar xhim ;
: phe pchar xhe ;
: pHe pchar xHe ;


: rhis recruitee xhis ;
: rhim recruitee xhim ;
: rhe recruitee xhe ;
: rHe recruitee xHe ;

: vhis victim xhis ;
: vhim victim xhim ;
: vhe victim xhe ;
: vHe victim xHe ;

: bhim bully xhim ;
: bhe bully xhe ;
: bHe bully xHe ;


: Thegroup solo if soloer .colourname else "El grupo" then ;
: thegroup solo if soloer .colourname else "el grupo" then ;
: yourteam solo if soloer .colourname else "tu equipo" then ;

: Theothers team-count 2 = if notcname else "Los otros miembros" then ;
: theothers team-count 2 = if notcname else "los otros miembros" then ;
: Theothersarename team-count 2 = if ($ notcname .. "  es" $) else "Los demas son" then ;

: everyone team-count 2 = if notcname else "todos" then ;
: Everyone team-count 2 = if notcname else "Todos" then ;

: Theothersare team-count 2 = if "El otro miembro es" else "Los otros miembros son" then ;
: theothersare team-count 2 = if "el otro miembro es" else "los otros miembros son" then ;

: attitudeREV STAT_ATTITUDE cchar .reveal ;
: temperREV STAT_COMPOSURE cchar .reveal ;
: loyaltyREV STAT_LOYALTY cchar .reveal ;
: witsREV STAT_WITS cchar .reveal ;
: witsattREV witsREV attitudeREV ;

: aholeREV loyaltyREV temperREV ;

: strengthREV STAT_STRENGTH cchar .reveal ;
: medicalREV STAT_MEDICAL cchar .reveal ;
: mechanicREV STAT_MECHANICAL cchar .reveal ;
: shootingREV STAT_SHOOTING cchar .reveal ;
: fitnessREV STAT_FITNESS cchar .reveal ;

: rageREV temperREV strengthREV ;

: allREV strengthREV shootingREV fitnessREV mechanicREV medicalREV witsattREV temperREV loyaltyREV ;

: morale-- STAT_MORALE cchar .stat-- ;
: morale2- STAT_MORALE cchar .stat2- ;
: morale3- STAT_MORALE cchar .stat3- ;
: morale4- STAT_MORALE cchar .stat4- ;

: attitude-- STAT_ATTITUDE cchar .stat-- ;
: temper-- STAT_COMPOSURE cchar .stat-- ;
: loyalty-- STAT_LOYALTY cchar .stat-- ;
: wits-- STAT_WITS cchar .stat-- ;

: strength-- STAT_STRENGTH cchar .stat-- ;
: medical-- STAT_MEDICAL cchar .stat-- ;
: mechanic-- STAT_MECHANICAL cchar .stat-- ;
: shooting-- STAT_SHOOTING cchar .stat-- ;
: fitness-- STAT_FITNESS cchar .stat-- ;

: morale++ STAT_MORALE cchar .stat++ ;
: morale2+ STAT_MORALE cchar .stat2+ ;
: morale3+ STAT_MORALE cchar .stat3+ ;
: morale4+ STAT_MORALE cchar .stat4+ ;

: vitality++ STAT_VITALITY cchar .stat++ ;
: dexterity++ STAT_DEXTERITY cchar .stat++ ;

: attitude++ STAT_ATTITUDE cchar .stat++ ;
: temper++ STAT_COMPOSURE cchar .stat++ ;
: loyalty++ STAT_LOYALTY cchar .stat++ ;
: wits++ STAT_WITS cchar .stat++ ;

: strength++ STAT_STRENGTH cchar .stat++ gstats{ ' total-str+ ++ } stat-cheevs ;
: medical++ STAT_MEDICAL cchar fastlearn-stat gstats{ ' total-med+ ++ } stat-cheevs ;
: mechanic++ STAT_MECHANICAL cchar fastlearn-stat gstats{ ' total-mech+ ++ } stat-cheevs ;
: shooting++ STAT_SHOOTING cchar fastlearn-stat gstats{ ' total-shoot+ ++ } stat-cheevs ;
: fitness++ STAT_FITNESS cchar .stat++ gstats{ ' total-fit+ ++ } stat-cheevs ;

: ricon recruitee character# cyoa-char-icon! ;
: dead? cchar .health@ 1 < ;

0 value post-mission-cyoa

: ouch-- cchar team-attacked ;
: ouch2- 2 for ouch-- next ;
: nonfatal cchar .health@ 1 > ;
: alive? cchar .alive? ;
: health++ cchar .health++ ;
: despairbite cchar team-attacked 50 %chance if cchar team-attacked then ;
: sting-- nonfatal if ouch-- then ;
: sting2- 2 for sting-- next ;

cyoa{

: rewardevent-random
	0 stack
		' .fitness++ shove
		' .strength++ shove
		' .shooting++ shove
		' .mechanic++ shove
		' .medical++ shove
	shuffles pops nip execute
;

cyoa-choice: toiletwishA
	"Riquezas y Oro" cyoa-title!
		($ "'Latas de comida empiezan a llover desde el techo.		Por fin. Eres rico!		Entonces el techo se derrumba sobre " .. pname .. "... " $) cyoa-text! goodfx clear-cyoa
	15 20 rnd sfood
	' xyzombceil 10 40 90 rnd zwave-ex	
;

cyoa-choice: toiletwishB
		($ pname .. "  mira horrorizado como " .. phis .. "  la piel se convierte en piedra, atrapando " .. phim .. "  en su lugar para siempre como una estatua.		No, espera, solo se volvio un poco " .. phis .. "  la piel gris." $) text! goodfx deleteme
		pchar .vitality++ pchar .fullheal pchar .stoneclr
;

cyoa-choice: toiletwishC
	"La respuesta impactante" cyoa-title!
	($ ". " .. pname .. " por un rato antes de que desaparezca." $) cyoa-text! badfx clear-cyoa deleteme
	nearest .tpos anchor! 20 zombs
;

cyoa: toiletwishgenie // Added an underscore so that "toiletwish" isn't redefined, as that would mess up the next part
	"El inodoro de los deseos" cyoa-title!
	picon!
	($ "Cuando " .. pname .. "  abrio el inodoro, aparecio un genio magico.	" .. "GRACIAS POR LIBERARME DEL INODORO" pbold .. "  dice. " .. "AHORA PIDE UN DESEO.	" pbold .. pname .. "  pide un deseo:" $) text!
		choice( "'No pidas un deseo todavia" )choice
		choice( "Quiero... ser rico" )choice: toiletwishA
		choice( "Inmortalidad" )choice: toiletwishB
		choice( "Eres uno de esos genios malvados?" )choice: toiletwishC
;

: toiletwish_
	new-npc lt .character@ .genieify goodfx lt mission{ .smokepoof }
	0.01 lt .character@ .ai.wander_chance! 0 lt .character@ .ai.loot_chance! 1 lt .unseen!
	-30 lt .fade!
	90 lt .stun!
	180 lt .aim_angle!
	cyoa{ ' toiletwishgenie cyoa! }
;

// Assign to the previously-defined toiletwish deferred word
' toiletwish_ to toiletwish

: foodamt
	($ "  Tienes " .. LOOT_FOOD looticon .. "" .. trunk-food .. " comida sobrante " $) +text
;

: gasamt
	($ "  Tienes " .. LOOT_GAS looticon .. "" .. trunk-gas .. " gas left " $) +text
;

cyoa: just-testing beep "PRUEBA DE PILA DE CARRETERA POST MISION CYOA" cyoa-text! ;

// Special events called later on stack-road-action

: comicbookgain shooting++ strength++ fitness++ morale3+ ;

cyoa-choice: comicbook-who
	chumany? if
		($ cname .. " consigue leer " .. "LA LIGA SPANDEX" fbold .. ".
		" .. cHe .. "  absorbe los contenidos! Lamentablemente, " .. che .. "  arruina el comic para otros debido a " .. chis .. "  mugriento " .. cfingspaws .. "." $) cyoa-text!
		comicbookgain goodfx
	else
		($ cname .. " ' arruina completamente el comic. Parece que a alguien no le gustan las novelas graficas." $) cyoa-text! badfx
	then 
;

cyoa: comicbook-2
	"'Es una novela grafica NO un comic" cyoa-title!
	picon!
	dead? if
		($ "El comic se rompio durante la pelea." $) cyoa-text! badfx
	then;
	($ Thegroup .. " ' salio con un comic! Esta lleno de historias de gente corpulenta que se golpea entre si y se dispara rayos oculares." $) cyoa-text! goodfx
	solohuman if
		choosehuman cicon
		($ cname-He .. " ' lo lee y absorbe la sabiduria que contiene! Es informativo y entretenido!" $) +crtext cchar comicbookgain
	then;  
		"Quien deberia leerlo?" +crtext
		' comicbook-who cyoa-team-choice
;

// More common events

cyoa-choice: bookmag-who
	chumany? if
		($ cname .. " consigue leer " .. bookmagtitle .. ".
		" .. cHe .. "  absorbe los contenidos! Lamentablemente, " .. che .. "  arruina la revista para otros debido a " .. chis .. "  mugriento " .. cfingspaws .. "." $) cyoa-text!
		cchar bookmaggain goodfx
	else
		($ cname .. " ' arruina completamente la revista. Parece que alguien no aprecia la buena literatura." $) cyoa-text! badfx
	then 
;

cyoa: bookmag-get2
	"Ponte al dia con la lectura" cyoa-title!
	picon!
	dead? if
		($ "El " .. bookormag .. "  se perdio en la carniceria." $) cyoa-text! badfx
	then;
	($ Thegroup .. "  ahora tiene el " .. bookormag .. ", " .. bookmagtitle .. "." $) cyoa-text! goodfx
	solohuman if
		choosehuman cicon
		($ cname-He .. "  lo lee y absorbe el conocimiento!" $) +crtext cchar bookmaggain goodfx
	then;  
	bookmagtype 4 > if
		"Todos tienen la oportunidad de leerlo y aprender cosas nuevas." +crtext ' bookmaggain foreach-human-char 
	else
		"Quien deberia leerlo?" +crtext
		' bookmag-who cyoa-team-choice
	then
;

cyoa: bookmag-get
	"Material de lectura" cyoa-title!
	picon!
	($ cname .. "  encuentra un " .. bookormag .. "  en buenas condiciones, entre las revistas y libros arruinados. El " .. bookormag .. "' el titulo es: " .. bookmagtitle .. ".
	" .. cHe .. "  lo mete en " .. chis .. "  mochila." $) cyoa-text!
	deleteme
	clear-cyoa goodfx mission{ ' bookmag-get2 to post-mission-cyoa }
;

cyoa-choice: recruit-replace
	ricon
	($ cname .. "  es expulsado del grupo para dejar espacio a " .. rname .. "." $) cyoa-text!
	ctakeweapons TEAM_STATUS_BAD_LEFT cchar .decruit 0.5 600 tone
	recruited
;

cyoa-bridge: recruit-replace4
	ricon
	($ "A quien te gustaria reemplazar para dejar espacio a " .. rname .. "?" $) cyoa-text!
	' recruit-replace cyoa-team-choice
;

: recruit-replace?
	// ' recruit-replace cyoa-team-choice
	// Look into maybe removing this: jerk-get
	// Doesn't use this: recruit-dog-feed recruit-familiarreplace? and the camp-recruits
	team-count 3 > if
		' recruit-replace 2 #cyoa-team-choice
		choice( "Escoge a alguien mas" )choice: recruit-replace4
	else
		' recruit-replace cyoa-team-choice
	then
	choice( "Salir " .. rname .. "  detras" )choice
;

(* : recruit-replace?-3 // The old recruit-replace?, currently used twice for annoying recruits in cyoa-k-recruit ... but I got rid of it because I added the pick someone else options to recruit-replace?
	// This was formerly so you could avoid the pushy person with a full party, and so you could avoid the Dinkdoor guy if you used a cool-it but NOT if you recruited him without a cool-it.
	// ' recruit-replace 3 #cyoa-team-choice choice( "Salir " .. rname .. "  detras" )choice
; *)

cyoa: recruit-yes
	team-full? if
		($ "El grupo esta lleno. A quien te gustaria reemplazar para hacerle un lugar a " .. rname .. "?" $) cyoa-text!
		recruit-replace?
	then;
	ricon
	($ Thegroup .. "  acepta " .. rname .. "  al equipo!" $) cyoa-text! 2 300 tone
		recruited
;

cyoa: recruit-yes-food args( food )
	team-full? if
		($ "El grupo esta lleno. A quien te gustaria reemplazar para hacerle un lugar a " .. rname .. "?" $) cyoa-text!
			<- food -1 * trunk-food+
			' recruit-replace cyoa-team-choice 
	then;
	ricon
	($ Thegroup .. "  recluta " .. rname .. "  al equipo!" $) cyoa-text! 2 300 tone
		<- food -1 * trunk-food+ recruited
;

: recruit-yesno
	choice( "Pidele a " .. rname .. "  que se una" )choice: recruit-yes
	choice( "Salir " .. rname .. "  detras" )choice
;

: npc-died 
	"Perdido en el Camino de la Muerte" cyoa-title! 
	($ pname .. " ' llega demasiado tarde. No hay nada que puedas hacer por " .. chim .. "  ahora." $) cyoa-text!
	clear-cyoa badfx
; 

0 value rescskilltype

: rescskillup
	rescskilltype case
		// 0 of dup .strengthREV .strength++ endof - Got rid of the reveal
		0 of .strength++ endof
		1 of .fitness++ endof
		2 of .medical++ endof
		3 of .mechanic++ endof
		4 of .shooting++ endof
	endcase
;
: rescskillname
	rescskilltype case
		0 of "fuerza" endof
		1 of "aptitud fisica" endof
		2 of "medico" endof
		3 of "mecanico" endof
		4 of "disparo" endof
	endcase
;

: rescskill-got
	($ Thegroup .. " obtiene todo tipo de secreto " .. rescskillname .. " consejos de " .. rname .. "the training begins " .. rname .. " sale" $) cyoa-text!
	// formerly cchar
	' rescskillup foreach-human-char goodfx
;

 cyoa-choice: rescskill-who
	choosehuman cicon
	chumany? if
		rescskill-got
	else
		($ cname .. " no entiende " .. rname .. "intentos de entrenamiento, en absoluto.'" $) cyoa-text! badfx
	then 
;

cyoa: rescskill-choose
 	solohuman if
		choosehuman cicon
		rescskill-got
	else  
		($ "Quien deberia obtener el " .. rescskillname .. " entrenamiento?" $) cyoa-text!
		' rescskill-who cyoa-team-choice
	then
;
 
: rescskill-pick to rescskilltype 
	// formerly rescskill-choose 
	rescskill-who
;

: groupadd team-count 1 > if "Grupo " .. then ;

: rescskill1-get 0 rescskill-pick ;
: rescskill1 choice( groupadd "Entrenamiento de fuerza" )choice: rescskill1-get 
	4 recruitee .strength!
;
: rescskill2-get 1 rescskill-pick ;
: rescskill2 choice( groupadd "Entrenamiento de fitness" )choice: rescskill2-get 
	4 recruitee .fitness!
;
: rescskill3-get 4 rescskill-pick ;
: rescskill3 choice( groupadd "Entrenamiento de tiro" )choice: rescskill3-get 
	4 recruitee .shooting!
;
: rescskill4-get 3 rescskill-pick ;
: rescskill4 choice( groupadd "Entrenamiento mecanico" )choice: rescskill4-get 
	4 recruitee .mechanical!
;
: rescskill5-get 2 rescskill-pick ;
: rescskill5 choice( groupadd "Entrenamiento medico" )choice: rescskill5-get 
	4 recruitee .medical!
;
 
// Old way of doing it
0 var rescue-skill-deck
	deck-new
	card: rescskill1
	card: rescskill2
	card: rescskill3
	card: rescskill4
	card: rescskill5
constant RESCUE-SKILL
RESCUE-SKILL shuffles to rescue-skill-deck

: rescreward1-get 75 trunk-bullet+ ; 
: rescreward1 choice( "Alijo de 75 balas" )choice: rescreward1-get ;
: rescreward2-get 60 trunk-rifle-shell+ ;
: rescreward2 choice( "Alijo de 60 municiones para rifle" )choice: rescreward2-get ;
: rescreward3-get 45 trunk-shotgun-shell+ ;
: rescreward3 choice( "45 cartuchos de escopeta" )choice: rescreward3-get ;
: rescreward4-get 15 trunk-food+ ;
: rescreward4 choice( "Alijo de 15 alimentos" )choice: rescreward4-get ;
: rescreward5-get 80 trunk-gas+ ;
: rescreward5 choice( "Tanque de 80 de gasolina" )choice: rescreward5-get ;
: rescreward6-get 10 trunk-medical+ ;
: rescreward6 choice( "Alijo de 10 medicos" )choice: rescreward6-get ;
: rescreward7-get morale2+all goodfx ;
: rescreward8-get 5 groupheal goodfx ; // trader is 7
: rescreward7 // Two for the price of one
	choice( "Charla de animo moral" )choice: rescreward7-get
	choice( groupadd "Primeros auxilios" )choice: rescreward8-get 
;
 
0 var rescue-reward-deck
	deck-new
	card: rescreward1
	card: rescreward2
	card: rescreward3
	card: rescreward4
	card: rescreward5
	card: rescreward6
	card: rescreward7
	// Training added to resc reward deck
	card: rescskill1
	card: rescskill2
	card: rescskill3
	card: rescskill4
	card: rescskill5
constant RESCUE-REWARD
RESCUE-REWARD shuffles to rescue-reward-deck

: rescue-reward-draw rescue-reward-deck drawcard ;
: rescue-reward-shuffle
	rescue-reward-deck depths 0= if RESCUE-REWARD shuffles to rescue-reward-deck then
	rescue-reward-draw
;
: rescue-skill-shuffle
	rescue-skill-deck depths 0= if RESCUE-SKILL shuffles to rescue-skill-deck then
	rescue-skill-deck drawcard
;

: rescue-trainitem
	// rescue-skill-shuffle
	rescue-reward-shuffle
	rescue-reward-draw
;


: recruit-choice
	choice( "Reclutar " .. rname .. " a equipo" )choice: recruit-yes
;

: rescue-reward
	gstats{ ugnome-extraZP 0 > if 1 zombop+! then }
	recruit-choice
	rescue-trainitem
;

: judgerecruit
	most-paranoid if choosechar
		50 %chance if 
			($ cname .. " se mide " .. rhis .. " skills..." $) +crtext
			recruitee dup .medicalREV dup .mechanicREV dup .shootingREV dup .strengthREV dup .fitnessREV
		else
			($ cname .. " jueces " .. rhis .. " character..." $) +crtext
			recruitee dup .attitudeREV dup .temperREV dup .witsREV dup .loyaltyREV
		then goodfx witsattREV
	then
;

cyoa: rescuedme
	// rescue choosechar cicon
	// Maybe need to do a check here to see if the rescue team was left alive
	rescue to recruitee ricon
	0 rescue .party!
	recruitee .alive? if
		"Rescate Exitoso!" cyoa-title!
		($ rname .. " alcanza a " .. thegroup .. "  en un lugar seguro.		" .. rHe .. "  ofrece una recompensa en agradecimiento por salvar " .. rhim .. "!" $) cyoa-text!
		rescue-reward
		judgerecruit
	else
		"'Si bueno nadie es perfecto" cyoa-title!
		($ Thegroup .. "  se siente bastante culpable por " .. rname .. "  ser comido.		Asi es la vida, supongo." $) cyoa-text!
		morale--all
	then
;

cyoa: rescueddog
	// rescue choosechar cicon
	rescue to recruitee ricon
	0 rescue .party!
	recruitee .alive? if
		"Rescate Exitoso!" cyoa-title!
		gstats{ ugnome-extraZP 0 > if 1 zombop+! then }
		($ rname .. " alcanza a " .. thegroup .. "  en un lugar seguro.		" .. rHe .. "  parece que " .. rhe .. "  quiere quedarse!" $) cyoa-text!
		recruit-choice
		choice( "Establecer " .. rname .. "  libre" )choice: morale2+all
		judgerecruit
	else
		"Fallo en la adopcion del perro" cyoa-title!
		($ Thegroup .. "  se siente culpable por " .. rname .. "  ser comido. Un perro podria haber sido realmente util." $) cyoa-text!
		morale2-all
	then
;

cyoa-choice: chicken-dinner
	"El pollo es devuelto a sus componentes." text!
	"Cenizas a las cenizas, pepitas a las pepitas" pbold +crtext
	"El hermoso ciclo de la vida continua." +crtext
	3 4 rnd trunk-food+ goodfx
;

cyoa-choice: chicken-free
	"El pollo deja un huevo! Esto es un signo de buena suerte! Probablemente." text!
	morale++all goodfx
	1 weapondef{ WEAPON_EGG } trunk.weapon+
;

root{ defer freechicken-cheevo }

cyoa: rescuedchicken
	rescue choosechar cicon 0 rescue .party!
	rescue .alive? if
		"Pollo rescatado!" cyoa-title!
		gstats{ ugnome-extraZP 0 > if 1 zombop+! then }
		($ Thegroup .. " ' Pudo sacar un pollo de esa granja! El pollo no seria un buen recluta. Podria ser liberado o sacrificado para comer. No tiene mucha carne, pero podria ser bueno para algunas nuggets de pollo." $) cyoa-text!
		choice( "Liberar pollo" )choice: chicken-free
		choice( "Convertir pollo en nuggets" )choice: chicken-dinner
		freechicken-cheevo
	else
		"Fallo en el rescate del pollo" cyoa-title!
		($ Thegroup .. "  se siente vagamente culpable por el pollo que se come.		Fue un desperdicio de un buen pollo." $) cyoa-text!
		morale--all badfx
	then
; 

0 value gothic-ma
0 value gothic-pa

cyoa-choice: farmrescue-1
	($ Thegroup .. "  obtiene algo de sabiduria casera sobre la reparacion de montones de chatarra oxidados de todas las formas y tamanos." $) text! picon
	' .mechanicREV foreach-team-char ' .mechanic++ foreach-team-char goodfx ;
cyoa-choice: farmrescue-2
	($ Thegroup .. "  aprende algunos viejos remedios curativos para agricultores. Hierbas medicinales locales, unguentos, tecnicas de cirugia casera." $) text! picon
	' .medicalREV foreach-team-char ' .medical++ foreach-team-char goodfx ;
cyoa-choice: farmrescue-3 
	($ Thegroup .. " ' recibe una hoz afilada como regalo. Es ligera, no se rompe y puede hacer dano." $) text! picon 1 WEAPON_SICKLE trunk.weapon+ goodfx ;
cyoa-choice: farmrescue-4 
	($ Thegroup .. "  te lleva a un alijo de frutas encurtidas, verduras y carne extrana! La comida en tarro es un buen cambio de la comida enlatada." $) text! picon 10 trunk-food+ morale++all goodfx ;

cyoa: farmrescue
	"Rescate de la granja gotica" cyoa-title! picon
	($ Thegroup .. "  escapa de la granja!" $) text!
	gothic-ma 0= not if
		0 gothic-ma .party!
		gothic-ma .alive? if
			($ "Mama" pbold .. "  escapa de la granja, gracias a ti!" $) +crtext
		else
			($ "Mama" pbold .. "  no escapo contigo." $) +crtext
			morale--all
		then
	then
	gothic-pa 0= not if
		0 gothic-pa .party!
		gothic-pa .alive? if
			($ "Papa" pbold .. "  sobrevive gracias a tu ayuda!" $) +crtext
		else
			($ "Papa" pbold .. " ' no lo logro." $) +crtext
			morale--all
		then
	then
	gothic-ma 0= gothic-pa 0= and if "Desafortunadamente, ninguno de los agricultores lo hizo." +crtext badfx morale--all then;
	gothic-pa 0= not gothic-ma 0= not and if
		gothic-pa .alive? gothic-ma .alive? and if
			"Agradecidos de estar vivos, te ofrecen un regalo:" +crtext
			choice( "10 rations" )choice: farmrescue-4
			choice( "Consejos de Pa para arreglar cosas" )choice: farmrescue-1
			choice( "Remedios Caseros de Ma" )choice: farmrescue-2
			choice( "Una Guadana Robusta" )choice: farmrescue-3
		then
	then
;

: drunkchat-up
	rescue choosechar cicon
	($ "Vale, estoy despierto!" pbold .. "
	" .. cHe .. "  dice " .. che .. "''ll seguirte por un rato. Tambien que deberias encontrarte con " .. chim .. "  afuera del pueblo, si ambos lo logran." $) +crtext
		1 rescue .party-familiar 0 nearest .unseen!
		rescue .wakeup
		mission{ ' rescuedme to post-mission-cyoa }
		clear-cyoa
;

cyoa-choice: drunkchat-med
	rescue choosechar
	($ pname .. "  hace un diagnostico cuidadoso, y luego comienza a patear " .. cname .. "  en la grupa." $) cyoa-text!
	drunkchat-up
;

cyoa-choice: drunkchat-food
	($ pname .. "  comienza a aplastar algo de comida en " .. cname .. "''s boca mientras " .. che .. "  duerme." $) cyoa-text!
	-1 add-food
	drunkchat-up
;

cyoa-choice: drunkchat-poke
	($ pname .. "  pincha " .. cname .. "  para despertar " .. chim .. "  del sueno. Se necesitan cuatro horas consecutivas de pinchazos continuos." $) cyoa-text!
	drunkchat-up
	o'clock@ 4 + o'clock
	4 7 rnd nearest .tpos anchor! zombs
	// 6 10 rnd roomgen{ .zombspawn }
;

cyoa-choice: drunkchat-pet
	($ pname .. "  comienza a lamer " .. cname .. "''s cara como " .. che .. "  duerme." $) cyoa-text!
	drunkchat-up
;

cyoa: drunkchat
	// rescue to recruitee ricon
	rescue choosechar cicon
	dead? if npc-died then;
	"Se desmayo" cyoa-title!
	($ pname .. " " .. che .. "'s en necesidad de comida para igualar " .. chis .. " azucar en la sangre" $) cyoa-text!
	pchar notpet? if
		pchar .cmedical+ if
			pchar charchoice( " revive con habilidades medicas" )choice: drunkchat-med
		then
		LOOT_FOOD pchar .lootamt@ 0 > if
			choice( "Se alimenta de " .. chim .. "  1 comida" )choice: drunkchat-food
		else
			"There might be some food in a nearby building..." +crtext
		then
	else
		pchar charchoice( "  regenera " .. chim .. "  a traves de lamer la cara" )choice: drunkchat-pet
	then
	choice( "Salir " .. chim .. "  solo" )choice
	LOOT_FOOD pchar .lootamt@ 1 < if
		choice( "Pierde " .. chim .. "  durante 4 horas" )choice: drunkchat-poke
	then
;

cyoa-choice: rescueme-2
	($ cname .. "  comienza a seguirte " .. pname .. "!
	" .. cHe .. " puede ser util, si puedes sacarte de aqui vivos O una distraccion manejable, si no lo puedes " .. chim .. " salir de aqui vivo o una distraccion util, si no puedes." $) cyoa-text!
	rescueget mission{ ' rescuedme to post-mission-cyoa }
;

cyoa-choice: rescueme-d
	($ cname .. "  comienza a seguirte " .. pname .. "!
	" .. cHe .. " puede ser util, si puedes sacarte de aqui vivos O una distraccion manejable, si no lo puedes " .. chim .. " salir de aqui vivo o una distraccion util, si no puedes." $) cyoa-text!
	rescueget mission{ ' rescueddog to post-mission-cyoa }
;

cyoa: rescueme
	rescue choosechar cicon
	dead? if npc-died then;
	"Todos necesitan ayuda a veces" cyoa-title!
	($ cname .. " dice eso " .. che .. " realmente quiere salir de este lugar.	" .. cHe .. " pregunta si " .. che .. " puede seguirte." $) cyoa-text!
		choice( "Deja que " .. chim .. " te siga"  )choice: rescueme-2
		choice( "Salir " .. chim .. " por ahora" )choice
;

cyoa: dogrescue
	rescue choosechar cicon
	dead? if npc-died then;
	
	cchar .bodytype@ 20 = if
	"El Ultra Wiener" cyoa-title!
		($ "'Nunca has visto un perro salchicha mas impresionante. Este debe haber sido un perro de exhibicion, con papeles. El perro tambien lleva una escopeta. Su collar dice:" .. " 'LIL' SAM BYOOL" rainbold $) text!
		cchar .shootingREV cchar .witsREV		
	else
		"'Que hace un perro aqui?" cyoa-title!
		($ "El perro parece inusualmente manso! La mayoria de los perros que aun estan vivos se han vuelto salvajes en este momento. Este perro parece ansioso por ver humanos." $) text!
	then
		choice( "Deja que el perro te siga"  )choice: rescueme-d
		choice( "Deja al perro solo por ahora" )choice
;

cyoa-bridge: feral-recruit
	cchar to recruitee goodfx clear-cyoa
	($ "El perro quiere seguir " .. pname .. "!" $) text!
	pchar .morale++
	0.25 cchar .ai.wander_chance! 0 nearest .unseen!
	recruit-yesno
;

cyoa: feralrescue
	interactee .character@ choosechar cicon
	dead? if npc-died then;
	"Paquete de Perros" cyoa-title!
	($ "El perro es salvaje! Ya no es capaz de confiar en los humanos. " $) cyoa-text!
		mission-team-stack ' notpet? stack-filter nip 1 < if
			choice( "Dejar al perro en paz" )choice
			pchar dog? if 
				choice( "Reclutar al perro" )choice: feral-recruit
				"Esto no es un problema, ya que no hay humanos alrededor."				
			else
				($ pname .. " ' no es un perro. El perro salvaje grune con sospecha." $)
			then +crtext
		then;
		pchar isdogpal? if
			($ "Por supuesto, " .. pname .. " is different! The dog starts to remember some long forgotten memories of when dogs and humans worked together... " $) +crtext
			choice( "Reclutar al perro" )choice: feral-recruit
		else
			"This dog's eyes glimmer with recognition for a brief moment..." +text
		then
		choice( "Dejar al perro en paz" )choice
;

: southofshared
	interactee .pos lt .pos! // Put it right on the CYOA target we are interacting with
	lt .pos interactee .rad.y@ + lt .rad.x@ 2 * + lt .pos.y! // Move it down by the interactee's rectangular y radius and ours (circular) x2 (no guessing)
	-16 lt .pos.z! // Start above ground
	-1 lt .vel.z! // With a little bounce up
;

: southofcyoa
	southofshared
	lt .sproink // AND NEW SPECIAL FX
;

: southofaltar
	southofshared
	lt .poofmescary
;

: safe1-get 20 add-shell shotgun southofcyoa ;
: safe1 choice( "Escopeta con 20 cartuchos" )choice: safe1-get ;
: safe2-get 36 add-bullet dolt southofcyoa ;
: safe2 choice( "Magnum y 36 balas" )choice: safe2-get ;
: safe3-get 75 add-bullet ;
: safe3 choice( "Caja de 75 balas" )choice: safe3-get ;
: safe4-get 80 add-gas ;
: safe4 choice( "Contenedor de 80 gasolina" )choice: safe4-get ;
: safe5-get 45 add-shell ;
: safe5 choice( "Caja de 45 cartuchos" )choice: safe5-get ;
: safe6-get 60 add-rifle ;
: safe6 choice( "Caja de 60 municion de rifle" )choice: safe6-get ;
: safe7-get 10 add-medical ;
: safe7 choice( "Paquete de 10 medicamentos" )choice: safe7-get ;
: safe8-get 15 add-food ;
: safe8 choice( "Almacen de 15 alimentos" )choice: safe8-get ;
: safe9-get uzi southofcyoa ;
: safe9 choice( "Subfusil Maquina" )choice: safe9-get ;
: safe10-get hunting southofcyoa ;
: safe10 choice( "Fusil de caza" )choice: safe10-get ;
: safe11-get 30 add-rifle rifle southofcyoa ;
: safe11 choice( "Fusil Cowboy y 30 municion" )choice: safe11-get ;
: safe12-geta knight southofcyoa ;
: safe12-getb ak47 southofcyoa ;
: safe12-getc autoshot southofcyoa ;
: safe12-getd slug southofcyoa ;
: safe12
	20 %chance if
		choice( "Espada Caballero" )choice: safe12-geta
	else
		25 %chance if
			choice( "Escopeta de perdigones rayados" )choice: safe12-getd
		else
			50 %chance if
				choice( "Escopeta Automatica" )choice: safe12-getc
			else
				choice( "Rifle de Asalto" )choice: safe12-getb
			then
		then
	then
 ;

0 var safe-reward
	deck-new
	card: safe1
	card: safe2
	card: safe3
	card: safe4
	card: safe5
	card: safe6
	card: safe7
	card: safe8
	card: safe9
	card: safe10
	card: safe11
	card: safe12
constant SAFE-REWARD
SAFE-REWARD shuffles to safe-reward

: safe-reward-get
	1 nearest .open!
	safe-reward depths 0= if SAFE-REWARD shuffles to safe-reward then
	4 for safe-reward drawcard next	
	clear-cyoa resetsafe
;
: combo? safecombo 0 > ;

cyoa: safe-crack
	"Caja Fuerte Abierta!" cyoa-title!
	($ "Toma un par de horas, pero " .. pname .. " 'se encarga de desbloquear la combinacion del seguro!	Ademas de algunos papeles inutiles, tenia:" $) cyoa-text!
	safe-reward-get
	o'clock@ 2 + o'clock 6 10 rnd nearest .tpos anchor! zombs 
;
cyoa: safe
	pchar humany? not if
	"Seguro contra mascotas" cyoa-title!
		($ pname .. " 'no posee el cerebro necesario para poder abrir un seguro. Solo otro ejemplo del mundo humano contra" .. cdogcat .. " prejuicio." $) cyoa-text!
	then;
	combo? if 
	"Seguro abierto!" cyoa-title!
		($ pname .. " desbloquea y abre el seguro!		Ademas de algunos papeles inutiles, tenia:" $) cyoa-text!
		safe-reward-get
	else
	"Seguro cerrado" cyoa-title!
	"Este seguro esta cerrado cerca, y demasiado solido para destruir.	Con suerte la combinacion o llave estara alrededor, algun lugar." cyoa-text!
		pchar .cmechanic+ if
			choice( "Dejalo estar por ahora" )choice
			pchar charchoice( " quebranta el seguro!" )choice: safe-crack
		then
	then 
	
;

cyoa: comboskel
	"Numero de combinacion" cyoa-title!
	($ "Este esqueleto esta agarrando un pequeno trozo de papel. El papel tiene una serie de numeros que parecen una combinacion de candado o caja fuerte!" .. pname .. "  lo recoge." $) cyoa-text! goodfx
	1 to safecombo clear-cyoa
;

cyoa: combokey
	"Llave de metal grande" cyoa-title!
	($ "Esta gran llave elegante parece que podria abrir una caja fuerte!" .. pname .. "  lo recoge." $) cyoa-text! goodfx
	1 to safecombo deleteme
;

cyoa: combonote
	"Numero de combinacion" cyoa-title!
	($ "Este trozo de papel tiene una serie de numeros que parecen una combinacion de candado o caja fuerte" .. pname .. "  lo recoge." $) cyoa-text! goodfx
	1 to safecombo deleteme
;

: lab1 
	cchar .hairtype@ 0 = if 
		($ cname .. " le crece pelo nuevo! Bien!" $) +crtext morale3+ goodfx
		1 25 rnd cchar .hairtype!
	else
		($ cname .. "  pierde " .. chis .. "  pelo!" $) +crtext morale3- badfx
		0 cchar .hairtype!
	then
;
: lab2 
	cchar .cwits+ if
		($ cname .. "'s eyes and mind are a bit more blurry..." $) +crtext wits-- shooting-- badfx
		0.8 cchar .scale.head.x! 0.8 cchar .scale.head.y! 0.5 pchar .headoff.y!
	else
		($ cname .. " se siente mas perceptivo!" $) +crtext wits++ shooting++ goodfx
		1.2 cchar .scale.head.x! 1.2 cchar .scale.head.y! 0.5 pchar .headoff.y!
	then	
;
: lab3
	cchar .dexterity@ 0 > if
		($ cname .. "  se siente como " .. che .. " is slowing down..." $) +crtext 0 cchar .dexterity! badfx
		0.7 pchar .scale.body.y!
	else
		($ cname .. " se siente mas rapido!" $) +crtext dexterity++ goodfx
		1.3 pchar .scale.body.y!
	then	
;
: lab4
	($ cname .. " se siente como una persona nueva!" $) +crtext 6 cchar .morale! goodfx
		0 3 rnd cchar .bodytype!
		0 25 rnd cchar .hairtype!
		0 15 rnd cchar .torsotype!
		0 15 rnd cchar .legstype!	
		0 11 rnd cchar .headtype!
		cchar .female@ 0 = if 
			1
		else
			0
		then cchar .female!
;
: lab5
	cpet? if
		($ cname .. " se siente mas humano!" $) +crtext 1 cchar .loyalty! loyalty-- goodfx
		// 0 cchar .pet! cchar statmod{ .undog } 0 3 rnd cchar .bodytype!
		cchar .dogtosuperdog	
	else
		($ cname .. " se siente mas leal!" $) +crtext 5 cchar .loyalty! loyalty++ goodfx
		cchar .doggify cchar .dogtosuperdog	
	then loyaltyREV
;
: lab6
	50 %chance if
		($ cname .. " se siente como si el mundo se hubiera hecho mas grande!" $) +crtext badfx
		0.8 cchar .scale.head.x! 0.8 cchar .scale.head.y! 0.5 cchar .headoff.y!
		0.7 cchar .scale.body.y! 0.7 cchar .scale.body.x!
	else
		($ cname .. " se siente grande y en control!" $) +crtext vitality++ goodfx
		1.2 cchar .scale.head.x! 1.2 cchar .scale.head.y! 0.5 cchar .headoff.y!
		1.2 cchar .scale.body.y! 1.2 cchar .scale.body.x!
	then	
;
: lab7
	cchar .vitality@ 3 > if
		($ cname .. " se siente fragil!" $) +crtext 2 cchar .vitality!  2 cchar .health! badfx
		0.6 cchar .scale.body.x!
	else
		($ cname .. " se siente robusto!" $) +crtext vitality++ goodfx
		1.5 cchar .scale.body.x!
	then	
;
: lab8
	cchar .specialbody@ 73 = cchar .specialhead@ 73 = and if
		($ cname .. " turns into a dogdogdogtaur! It's unclear what this actually means..." $) +crtext
	then;
	cpet? if
		($ cname .. " se convierte en un hombre perro: un perro con el cuerpo de un humano!" $) +crtext goodfx
		0 cchar .pet! cchar statmod{ .undog } 0 3 rnd cchar .bodytype! 73 cchar .specialhead!
	then;
	cchar .specialbody@ 73 = cchar .specialhead@ 73 = or if
		($ cname .. " un perro con los brazos de un humano y la cabeza de un perro y las piernas de un perro!" $) +crtext goodfx 73 cchar .specialhead! cchar .dogtaur
	else
		($ cname .. "  se convierte en un centauro canino: un perro con el torso y los brazos de un humano!" $) +crtext goodfx
		cchar .dogtaur
	then	
;
: lab9
	cchar .specialtype@ 74 = if
		($ "' Una mosca entra en la maquina! No parece molestar " .. cname .. "..." $) +crtext
	then;
	cpet? if
		($ cname .. "  se convierte en una mosca canina! Que horror!" $) +crtext goodfx
		0 cchar .pet! cchar statmod{ .undog } 
		50 %chance if 73 cchar .specialhead! cchar .flybody else 73 cchar .specialbody! cchar .flyhead then
	then;
	($ " Una mosca entra en la maquina mientras se enciende " .. cname .. " gets spliced up with fly DNA! Bonus!!!" $) +crtext goodfx cchar
	cchar .specialbody@ 74 = cchar .specialhead@ 74 = or if
		.fly
	else
		rnd5050 if .flyhead else .flybody then
	then	
;


0 var lab-effect
	deck-new
	card: lab1
	card: lab2
	card: lab3
	card: lab4
	card: lab5
	card: lab6
	card: lab7
	card: lab8
	card: lab9
constant LAB-EFFECT
LAB-EFFECT shuffles to lab-effect

cyoa-choice: lab-go
	"Resultados del experimento" cyoa-title!
	($ cname .. "  es alcanzado por misteriosos rayos cientificos hasta que la maquina deja de funcionar " .. cHe .. "  se siente con energia!" $) text!
	strength++ fitness++ cchar .fullheal
	lab-effect depths 0= if LAB-EFFECT shuffles to lab-effect then
	drawcard clear-cyoa
;

cyoa: lab
	"Dispositivo misterioso" cyoa-title!
	($ "'Parece algo asi como una cosa de ciencia loca. No se sabe que podria hacer esto si se estropea." $) text!
		choice( "Dejalo en paz por ahora" )choice
		pchar charchoice( "  entra en la maquina!" )choice: lab-go
;

: altarpush lt .poofmescary ;

cyoa-choice: altar-1
	($ cname .. "  toca el altar y es instantaneamente atacado por energias extranas!" $) text!
	ouch2- bitefx 300 tint_trigger 0 light_mode!
	alive? if 
		($ cHe .. "  logra sobrevivir y se siente diferente que antes." $) +crtext goodfx
		strength++ fitness++ shooting++
	else
		($ "Para " .. chis .. "  sacrificio, el altar otorga un regalo! A lo lejos, un " .. "duende espeluznante" fbold .. "  se rie a carcajadas como un duende." $) +crtext
		33 %chance if ' .strength++ else 50 %chance if ' .fitness++ else ' .shooting++ then then foreach-team-char
		goblin-laugh
		0 6 rnd case
		0 of 30 sshot altarpush shotgun southofaltar endof
		1 of 50 sbullet altarpush dolt southofaltar endof
		2 of 10 for 1 LOOT_FOOD spawnscat altarpush next endof
		3 of 40 srifle altarpush rifle southofaltar endof
		4 of 60 sgas altarpush endof
		5 of 7 for 1 LOOT_MEDICAL spawnscat altarpush next endof
		6 of 
			70 %chance if 
				33 %chance if aluminum else 50 %chance if machete2 else fire then then
			else 
				60 %chance if hunting else 70 %chance if uzi else autoshot then then
			then southofaltar
		endof endcase
	then
;

cyoa: altar
	"Altar de sacrificio" cyoa-title!
	picon!
	($ cname .. "  esta cerca de un altar de piedra. Debe ser la fuente del sentimiento siniestro que proviene de todo este lugar. El altar parece " .. "extremadamente peligroso!" rbold .. " ' Es un poco extrano que alguien tenga esto en su casa. Probablemente no deberia ser molestado!" $) text!
	"Espanol" rbold +crtext
		choice( "Tiene " .. cname .. "  tocar altar" )choice: altar-1
		choice( "Dejar el altar solo por ahora" )choice
;

}

: randommag
	magazine 0 4 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health! scatter
;

: randombook
	book 5 9 rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health!
;

: randombookmag
	50 %chance if
	book 5 9
	else
		magazine 0 4
	then rnd cyoa{ to bookmagtype ' bookmag-get cyoa! } 50 health!
;

: altar altarstand cyoa{ ' altar cyoa! } ;
: laboratory labmachine cyoa{ ' lab cyoa! } ;
: metalsafe safe cyoa{ ' safe cyoa! } ;
: combokey goldkey cyoa{ ' combokey cyoa! } delete-covering ;
: comboskel combokey skelscat ;
// skelscat cyoa{ ' comboskel cyoa! } 12 health! ;
: combonote wrotenote scatter cyoa{ ' combonote cyoa! } delete-covering ;

: new-drunk-npc new-npc lt .character@ dup .liedown 1 lt .unseen! 
	0 over .ai.wander_chance! 0 over .ai.attack_chance! drop
	// lying on random side
	rndsign 90 * lt .angle!
	160 190 frnd lt .aim_angle! 
	npc@rescue
	lt cyoa{ ' drunkchat cyoa! }
	lt .character@ .notake
;

: new-rescue-npc new-npc@ rescue-weapon over .weapona! 1 lt .unseen! 0 lt .leaveok! npc@rescue cyoa{ ' rescueme cyoa! } drop ;
: new-feraldog map{ selected } . cr new-npc@ .doggify 1 lt .unseen! 0 lt .leaveok! npc@rescue lt cyoa{ ' feralrescue cyoa! } drop ;
: new-rescue-dog 
	map{ selected } . cr new-npc@ 
	1 %chance kspet-checker 1 < and if
		.sambyoolify
	else
		.doggify
	then
	1 lt .unseen! 0 lt .leaveok! 
	npc@rescue lt cyoa{ ' dogrescue cyoa! } drop 
;

// FIXME: Things like these NPC words should be somewhere else
: inside-npc new-npc 0 lt .leaveok! 1 lt .unseen! lt .character@ ;
: still-npc new-npc 0 lt .leaveok! 1 lt .unseen! 0 lt .character@ .ai.wander_chance! 185 rnd5050 + lt .aim_angle! lt .character@ ;
: nomove-npc new-npc lt .character@ args( ch )
	185 rnd5050 + lt .aim_angle!
	0 <- ch .ai.wander_chance!
	50 <- ch .ai.safety_leash!
;


// Mansion loot

: hauntedwareset hauntspoon .drift hauntfork .drift hauntknife .drift ;

0 value mansiontype

: mansionloot1
	mansiontype 0 > if // If a GHOST MANSION
		goldspear
	else // If a normal dark mansion
		goldknife
	then .drift
;
: mansionloot2 hauntedwareset 3 4 rnd sfood ;
// Used to be mansiontype 0 > if turretred else turretgreen then .drift
: mansionloot3 mansiontype 0 > if 6 else 3 then for pukeyball .drift next ;
: mansionloot4 mansiontype 0 > if dragonslayer else megaknight then .drift ;
: mansionloot5 cavalry .drift ;

0 stack
	' mansionloot1 shove
	' mansionloot2 shove
	' mansionloot3 shove
	' mansionloot4 shove
	' mansionloot5 shove
value mansionloot-picker-base
mansionloot-picker-base shuffles var mansionloot-picker

: mansionloot-pick mansionloot-picker depths 0= if mansionloot-picker-base shuffles to mansionloot-picker then pops nip execute ;

// Effective stats used to be here, was moved to charfilt.df due to weird dependencies

// =================
// Quick stat checks
// =================

: statroll 2 skillcheck? ;

: moraleroll cchar .morale statroll ;
: attituderoll cchar .attitude statroll ;
: temperroll cchar .composure statroll ;
: loyaltyroll cchar .loyalty statroll ;
: witsroll cchar .wits statroll ;

: charm-sum dup .attitude swap .wits + ;
: charmroll cchar charm-sum 2 / statroll ;

: strengthroll cchar .strength statroll ;
: medicalroll cchar .medical statroll ;
: mechanicroll cchar .mechanical statroll ;
: shootingroll cchar .shooting statroll ;
: fitnessroll cchar .fitness statroll ;

// checks for the choicechar to have over 4 or under 2 in a stat

: cmorale- cchar .morale low ;
: cattitude- cchar .attitude low ;
: ctemper- cchar .composure low ;
: cloyalty- cchar .loyalty low ;
: cwits- cchar .wits low ;

: cstrength- cchar .strength low ;
: cmedical- cchar .medical low ;
: cmechanic- cchar .mechanical low ;
: cshooting- cchar .shooting low ;
: cfitness- cchar .fitness low ;

: cmorale+ cchar .morale high ;
: cattitude+ cchar .attitude high ;
: ctemper+ cchar .composure high ;
: cloyalty+ cchar .loyalty high ;
: cwits+ cchar .wits high ;

: cstrength+ cchar .strength high ;
: cmedical+ cchar .medical high ;
: cmechanic+ cchar .mechanical high ;
: cshooting+ cchar .shooting high ;
: cfitness+ cchar .fitness high ;

: morale+roll cmorale+ moraleroll or ;
: attitude+roll cattitude+ attituderoll or ;
: temper+roll ctemper+ temperroll or ;
: loyalty+roll cloyalty+ loyaltyroll or ;
: wits+roll cwits+ witsroll or ;
: strength+roll cstrength+ strengthroll or ;
: medical+roll cmedical+ medicalroll or ;
: mechanic+roll cmechanic+ mechanicroll or ;
: shooting+roll cshooting+ shootingroll or ;
: fitness+roll cfitness+ fitnessroll or ;

: decruited
	+cr ($ cname .. "  ha dejado el equipo!" $) +text
;

: screcruit recruitee .specialtype! ;
: decruit-bad-silent TEAM_STATUS_BAD_LEFT cchar .decruit ;
: decruit-bad decruited decruit-bad-silent badfx ;
: decruit-bad-takeweapons ctakeweapons decruit-bad ;
: decruit-bad-silent-takeweapons ctakeweapons decruit-bad-silent ;
: decruit-good-silent ctakeweapons TEAM_STATUS_GOOD_LEFT cchar .decruit ;
: decruit-good decruited decruit-good-silent goodfx ;


// pain words moved upwards

: 0weapona
	cchar .weapona@ 0 > if
		($ cHe .. "  pierde " .. chis .. "  arma principal!" $) +crtext
		0 cchar .weapona!
	then
;
: 0weaponb
	cchar .weaponb@ 0 > if
		($ cHe .. "  pierde " .. chis .. "  arma secundaria!" $) +crtext
		0 cchar .weaponb!
	then
;
: 0weaponc
	cchar .weaponb@ 0 > if
		($ cHe .. "  pierde " .. chis .. "  arma terciaria!" $) +crtext
		0 cchar .weaponc!
	then
;
: 0weapons
	cchar .weaponc@ 0 > cchar .weaponb@ 0 > cchar .weapona@ 0 > or or if
		($ cHe .. "  pierde " .. chis .. "  armas!" $) +crtext
		0 cchar .weaponb! 0 cchar .weapona! 0 cchar .weaponc!
	then
;

: trunk.killer 
	trunk.count 0 > if
		($ trunk.count rbold .. "  ARMAS PERDIDAS!" rbold $) +crtext
		trunk.clear
	then
;

: morale--gourmandall ' .morale--gourmand foreach-team-char ;

: chowtime
	fullmeal if
		($ Thegroup .. "  come una comida decente." $)
		cyoa-text-append
		chowday
		goodfx
		' .cmorale-recover foreach-team-char
	then;
	halfmeal if
		($ Thegroup .. "  tiene poca comida y come menos." $)
		cyoa-text-append
		chowday
		badfx
		' .morale-- foreach-team-char morale--gourmandall
	then;
	trunk-food 0 = if
		($ Thegroup .. "  descubre que no queda nada de comida. La desesperacion se apodera de el." $)
		cyoa-text-append
		team-count 1 > if
			' .morale3- foreach-team-char
		else
			soloer .morale2-
		then morale--gourmandall badfx
	then;
	($ Thegroup .. " ' no tenia suficiente comida para comer. La duda se hace mas fuerte." $)
		cyoa-text-append
	chowday
	badfx
	' .morale2- foreach-team-char morale--gourmandall
;

// Sleeping

: tired statmod{ .tired } ;
: untired statmod{ .untired } ;

: nosleep ' tired foreach-team-char ;
: sleep	' untired foreach-team-char ;
: sleep-except cchar tired cchar ' untired foreach-team-char-except ;

: 50nosleep 50 %chance if tired then ;
: 5050nosleep ' 50nosleep foreach-team-char ;

: tiredcamp chowtime nosleep ;
: fullcamp chowtime sleep ;

// Words for trading
: charm-text choosechar 
	($ "El comerciante se enamora de " .. cname .. "''s encanto y da extra!" $) 
	+crtext +cr witsREV attitudeREV cicon
;

: charm-gas	most-charming if charm-text 40 trunk-gas+ then ;
: charmhalf-gas	most-charming if charm-text 20 trunk-gas+ then ;

: charm-bullet most-charming if charm-text 30 trunk-bullet+ then ;
: charmhalf-bullet most-charming if charm-text 15 trunk-bullet+ then ;

: charm-shotgun-shell most-charming if charm-text 20 trunk-shotgun-shell+ then ;
: charmhalf-shotgun-shell most-charming if charm-text 10 trunk-shotgun-shell+ then ;

: charm-rifle-shell	most-charming if charm-text 25 trunk-rifle-shell+ then ;
: charmhalf-rifle-shell	most-charming if charm-text 12 trunk-rifle-shell+ then ;

: charm-medical	most-charming if charm-text 5 trunk-medical+ then ;
: charmhalf-medical	most-charming if charm-text 2 trunk-medical+ then ;

: charm-allammo	most-charming if charm-text 20 trunk-bullet+ 10 trunk-shotgun-shell+ 15 trunk-rifle-shell+ then ;
: charmhalf-allammo	most-charming if charm-text 10 trunk-bullet+ 5 trunk-shotgun-shell+ 7 trunk-rifle-shell+ then ;

: food-all-to-gas trunk-food 10 * trunk-gas+ eat-all ;
: food-15-to-gas 150 trunk-gas+ -15 trunk-food+ charm-gas ;
: food-10-to-gas 100 trunk-gas+ -10 trunk-food+ charmhalf-gas ;
: food-5-to-gas 50 trunk-gas+ -5 trunk-food+ ;

: food-all-to-bullet trunk-food 8 * trunk-bullet+ eat-all ;
: food-15-to-bullet 120 trunk-bullet+ -15 trunk-food+ charm-bullet ;
: food-10-to-bullet 80 trunk-bullet+ -10 trunk-food+ charmhalf-bullet ;
: food-5-to-bullet 40 trunk-bullet+ -5 trunk-food+ ;

: food-all-to-shotgun-shell trunk-food 5 * trunk-shotgun-shell+ eat-all ;
: food-15-to-shotgun-shell 75 trunk-shotgun-shell+ -15 trunk-food+ charm-shotgun-shell ;
: food-10-to-shotgun-shell 50 trunk-shotgun-shell+ -10 trunk-food+ charmhalf-shotgun-shell ;
: food-5-to-shotgun-shell 25 trunk-shotgun-shell+ -5 trunk-food+ ;

: food-all-to-rifle-shell trunk-food 6 * trunk-rifle-shell+ eat-all ;
: food-15-to-rifle-shell 90 trunk-rifle-shell+ -15 trunk-food+ charm-rifle-shell ;
: food-10-to-rifle-shell 60 trunk-rifle-shell+ -10 trunk-food+ charmhalf-rifle-shell ;
: food-5-to-rifle-shell 30 trunk-rifle-shell+ -5 trunk-food+ ;

: food-all-to-medical trunk-food 1 * trunk-medical+ eat-all ;
: food-15-to-medical 15 trunk-medical+ -15 trunk-food+ charm-medical ;
: food-10-to-medical 10 trunk-medical+ -10 trunk-food+ charmhalf-medical ;
: food-5-to-medical 5 trunk-medical+ -5 trunk-food+ ;

: food-all-to-allammo 
	trunk-food 5 * trunk-bullet+ 
	trunk-food 4 * trunk-rifle-shell+ 
	trunk-food 3 * trunk-shotgun-shell+ 
	eat-all 
;
: food-15-to-allammo 75 trunk-bullet+ 45 trunk-shotgun-shell+ 60 trunk-rifle-shell+ -15 trunk-food+ charm-allammo ;
: food-10-to-allammo 50 trunk-bullet+ 30 trunk-shotgun-shell+ 40 trunk-rifle-shell+ -10 trunk-food+ charmhalf-allammo ;
: food-5-to-allammo 25 trunk-bullet+ 15 trunk-shotgun-shell+ 20 trunk-rifle-shell+ -5 trunk-food+ ;

: rob-guiltloyal
	dup .cloyalty+ if
		dup .loyaltyREV
		dup .morale2-
	then;
	dup .loyaltyREV
	dup .morale--
;
: rob-guilt?
	dup .cloyalty- if
		dup .loyaltyREV
		dup .morale++
	then;
	rob-guiltloyal
;
: rob-guilt2?
	dup .cloyalty- if dup .loyaltyREV then;
	rob-guiltloyal
;
: rob-guilt	' rob-guilt? foreach-team-char ;
: rob-guilt2 ' rob-guilt2? foreach-team-char ;

: rob-fail?
	70 %chance if .ouch-- else .morale2- then
;
: rob-fail ouch-- ' rob-fail? foreach-team-char ;
: rob-megafail?
	dup .ouch-- dup .morale3-
;
: rob-megafail ' rob-megafail? foreach-team-char ouch-- ;

// Words for dealing with bandits

: ezbandit? road{ easybandit-mode } 0 > ;
: ezbandit2/ ezbandit? if 2 / then ;

: anyammo? trunk-bullet 1 > trunk-rifle-shell 1 > trunk-shotgun-shell 1 > or or ;

: ishaggler? statmod{ .haggler? } ;
: numhaggler team-stack ' ishaggler? stack-filter ;
: haggler? numhaggler 0 > ;

: bandit-toll team-count 
	ezbandit? if // EZ mode
		haggler? gstats{ perk-haggler } 2 >= and if drop
			gstats{ perk-haggler } 3 >= if
			2
			else
			3
			then * 1
		else drop // No haggler
			3 * 3
		then
	else // Deadlier and beyond
		haggler? gstats{ perk-haggler } 2 >= and if drop
			gstats{ perk-haggler } 3 >= if
			3			
			else
			4
			then * 2
		else drop // No haggler
			4 * 4 
		then
	then +
;
// 5 * 5 + ;
: bandit-toll2 bandit-toll 1.3 / >int ;

: bandit-toll? trunk-food bandit-toll 1 - > ;
: bandit-toll-pay bandit-toll -1 * trunk-food+ badfx ;
: bandit-toll-pay2 bandit-toll2 -1 * trunk-food+ badfx ;
: bandit-ammo
	trunk-bullet 0 > if trunk-bullet ezbandit2/ -1 * trunk-bullet+ then
	trunk-rifle-shell 0 > if trunk-rifle-shell ezbandit2/ -1 * trunk-rifle-shell+ then
	trunk-shotgun-shell 0 > if trunk-shotgun-shell ezbandit2/ -1 * trunk-shotgun-shell+ then
;
: bandit-med
	trunk-medical 0 > if trunk-medical ezbandit2/ -1 * trunk-medical+ then
;
: bandit-gas
	trunk-gas 0 > if trunk-gas -1 * trunk-gas+ then
;
: bandit-gastank
	trunk-gas 100 > if trunk-gas 100 - -1 * >int trunk-gas+ then
;
: bandit-ammomed bandit-ammo bandit-med ;
: bandit-food trunk-food 0 > if eat-all then ;
: bandit-ammofood bandit-food bandit-ammo ;
: bandit-most bandit-ammofood bandit-med ;
: bandit-mostgas bandit-most bandit-gastank ;
: bandit-all bandit-most bandit-gas ;

: bandit-ammo2
	trunk-bullet 1 > if trunk-bullet 2 / ezbandit2/ -1 * >int trunk-bullet+ then
	trunk-rifle-shell 1 > if trunk-rifle-shell 2 / ezbandit2/ -1 * >int trunk-rifle-shell+ then
	trunk-shotgun-shell 1 > if trunk-shotgun-shell 2 / ezbandit2/ -1 * >int trunk-shotgun-shell+ then
;
: bandit-med2
	trunk-medical 1 > if trunk-medical 2 / ezbandit2/ -1 * >int trunk-medical+ then
;
: bandit-ammomed2
	bandit-ammo2 bandit-med2
;
: bandit-food2
	trunk-food 1 > if trunk-food 2 / ezbandit2/ -1 * >int trunk-food+ then
;
: bandit-supplies2
	bandit-ammomed2 bandit-food2 bandit-gastank
;

: vname victim .colourname ;
: bname bully .colourname ;
: bully-st bully if bully .specialtype@ then; 0 ;
: camper-st sc-camper if sc-camper .specialtype@ then; 0 ;


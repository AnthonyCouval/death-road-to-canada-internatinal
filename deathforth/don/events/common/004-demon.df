cyoa{ road{ 

(*
------------------------------------------------------------------------------------------------------------------------
	004 - "O Nao Invocado" - Don Thacker
	One of the group becomes posessed by the demon HEKTATRIUS. He wants PIZZA and SMOKES.
	That's cool, right? I mean he sounds pretty rad.
------------------------------------------------------------------------------------------------------------------------
*)

// The group kicks the character TO THE CURB
cyoa-choice: don-common-demon-offload
	victim to cchar
	($ notcname .. "  chuta " .. vname .. " pela porta do passageiro. Como " .. vhe .. " cai " .. vhe .. " explode em um enxame de pequenos morcegos pretos." $) cyoa-text!
	victim .kill
	morale--all
;

: don-common-demonify
	victim .morale-- 
	0 victim .tint.skin.g!
	0 victim .tint.skin.b!
	victim .strength++ victim .fitness++
	victim statmod{ .fireproof }
	victim ispet? if 
		0 victim .pet! victim statmod{ .undog } 0 3 rnd victim .bodytype! 73 victim .specialhead!
		WEAPON_FIERCE victim .weapon_default!
	else
		victim .weapons-to-trunk
		WEAPON_DEVILFORK victim .weapona!
		1 victim .weapon_nodropa!
	then
;

cyoa-choice: don-common-demon-pizza-refuse
// Victim was necessary to carry the character selected at the start all the way to here. Otherwise the intermediary cyoa: would have cleared it. Could have also gotten around this using the vortex-003 example of not using cyoa: and using cyoa-start instead, but it would have been really clumsy and prone to breaking.
	($ "UGH, BEM, OBRIGADO DE QUALQUER MANEIRA, EU ACHO.	" rbold .. "O demonio libera " .. vname .. ", que e mudado pela experiencia." $) cyoa-text!
	don-common-demonify sfx{ creepy }
;

: don-common-demon-pizzaboost dup .strength++ dup .fitness++ dup .morale-- ;

cyoa-choice: don-common-demon-pizza2
	($ "THANKS FOR THE SOOOUUUUUUUUL.
	" rbold .. "Todos os sobreviventes sao infundidos com poder demoniaco." $) cyoa-text!
	cchar .kill
	cchar ' don-common-demon-pizzaboost foreach-team-char-except
;

cyoa-bridge: don-common-demon-pizza2-4
	someicon
	"Quem deve ser sacrificado a HEKATRIUS?" cyoa-text!
	' don-common-demon-pizza2 cyoa-team-choice
;

cyoa: don-common-demon-pizza

image-demon
solo if
	soloer choosechar
	($ "MUITO OBRIGADO, DINGUS.	" rbold .. "O demonio ardilha com poderes espetaculares depois desaparece " .. cname .. " explodem com poderes misteriosos e desaparecem!" $) cyoa-text!
	cchar to victim
	don-common-demonify sfx{ creepy }
else
	($ "Essas batatas eram sabor pizza. BOM SUFICIENTE." .. " HEKATRIUS" rbold .. " pergunta por uma alma para lava la.	" .. "QUAL ALMA SERA SACRIFICADA?" rbold $) cyoa-text!
	team-count 3 > if
		' don-common-demon-pizza2 2 #cyoa-team-choice
		choice( "Escolha outra pessoa" )choice: don-common-demon-pizza2-4
	else
		' don-common-demon-pizza2 3 #cyoa-team-choice
	then
	choice( "Nao sacrificar ninguem." )choice: don-common-demon-pizza-refuse	
then
	-1 trunk-food+
;	


cyoa-choice: don-common-demon-facepunch
	
	($ "MALEDIZA VOCE HUMANO! MALEDIZA VOCE!	" pbold .. "HEKATRIUS" rbold .. " desaparece. " .. cname .. " se sente muito bem sobre isso." $) cyoa-text!
		($ "Que maravilhosa soco!" $) +crtext
		morale2+ strengthREV strength++ temperREV goodfx

;	

cyoa-choice: don-common-demon-freakout
	// the group freaks out, causing the demon to freak out - you all freak out together
	// the demon leaves and you have essentially lost a person for nothing
	// maybe don't freak out next time
	
	($ Thegroup .. "  surta!" rbold $) text!
	notsolo if
		($ "HEKATRIUS" rbold .. "  surta por um tempo! Ele entao se transforma de volta em " .. vname .. ", que entao explode!" $) +crtext
		victim .kill
	else
		($ "HEKATRIUS" rbold .. " ' surta por um tempo, entao desaparece em uma nuvem de fumaca! E muito traumatizante!" $) +crtext
		morale--
	then badfx
;	
		
	
: don-common-demon-angrypunch 
	most-angrystrong if
		choice( "Soque o rosto dele" )choice: don-common-demon-facepunch
	then
;	

: don-common-demon-choiceblock
		trunk-food 0 > if
			choice( "Ofereca a ele um saco de batatas fritas   1 comida" )choice: don-common-demon-pizza	
		then
		choice( "Surte" )choice: don-common-demon-freakout
;

: don-common-demon-transmutetext
	($ cname .. "'os olhos de " .. che .. "  comeca a convulsionar! " .. cHe .. "  treme e grita em linguas arcanas." $) +crtext
	($ "Em " .. vname .. "'o lugar de " .. "O DEMONIO HEKATRIUS" rbold .. ". Ele pergunta se voce tem pizza. Ele realmente gostaria de uma fatia quente." $) +crtext
	
	don-common-demon-choiceblock
;	
	
// Enter HEKTATRIUS
cyoa: don-common-demon
	"O Nao Invocado" cyoa-title!
	($ Thegroup .. "  percebe um cheiro ruim vindo das tabuas do assoalho. Enxofre. Carneiro podre. Doce de milho." $) cyoa-text! basic-tone 
	($ "Um flash de luz e o som de um " .. "mil baleias explodindo" pbold .. "  preenche o carro." $) +crtext
	
	solo if // alone
		image-demon
		soloer choosechar
		($ "A fumaca se dissipa para revelar " .. "O DEMONIO HEKATRIUS" rbold .. "." $) +crtext
		($ "Ele pergunta se voce tem pizza. Ele poderia comer uma fatia quente." $) +crtext	
		don-common-demon-angrypunch 
		don-common-demon-choiceblock
		
	else // not alone
		someicon
		cchar to victim 
		// added cchar to victim here so it's really easy to keep track of who turned into a demon. This lets me use victim and vname up above, instead of having to constantly keep track of cname.
			don-common-demon-transmutetext
	then
	

	
;

} }

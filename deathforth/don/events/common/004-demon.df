cyoa{ road{ 

(*
------------------------------------------------------------------------------------------------------------------------
	004 - "Linvocato" - Don Thacker
	One of the group becomes posessed by the demon HEKTATRIUS. He wants PIZZA and SMOKES.
	That's cool, right? I mean he sounds pretty rad.
------------------------------------------------------------------------------------------------------------------------
*)

// The group kicks the character TO THE CURB
cyoa-choice: don-common-demon-offload
	victim to cchar
	($ notcname .. "  prende a calci " .. vname .. "  fuori dal portellone lato passeggero. Come " .. vhe .. "  cade, " .. vhe .. "  esplode in uno sciame di minuscoli pipistrelli neri." $) cyoa-text!
	victim .kill
	morale--all
;

: don-common-demonify
	victim .morale-- 
	0 victim .tint.skin.g!
	0 victim .tint.skin.b!
	victim .strength++ victim .fitness++
	victim statmod{ .fireproof }
	victim ispet? if 
		0 victim .pet! victim statmod{ .undog } 0 3 rnd victim .bodytype! 73 victim .specialhead!
		WEAPON_FIERCE victim .weapon_default!
	else
		victim .weapons-to-trunk
		WEAPON_DEVILFORK victim .weapona!
		1 victim .weapon_nodropa!
	then
;

cyoa-choice: don-common-demon-pizza-refuse
// Victim was necessary to carry the character selected at the start all the way to here. Otherwise the intermediary cyoa: would have cleared it. Could have also gotten around this using the vortex-003 example of not using cyoa: and using cyoa-start instead, but it would have been really clumsy and prone to breaking.
	($ "UGH, BEH, GRAZIE LO STESSO PENSO.	" rbold .. "Il demone libera " .. vname .. ", che viene cambiato dallesperienza." $) cyoa-text!
	don-common-demonify sfx{ creepy }
;

: don-common-demon-pizzaboost dup .strength++ dup .fitness++ dup .morale-- ;

cyoa-choice: don-common-demon-pizza2
	($ "THANKS FOR THE SOOOUUUUUUUUL.
	" rbold .. "Tutti i sopravvissuti sono infusi di potere demoniaco." $) cyoa-text!
	cchar .kill
	cchar ' don-common-demon-pizzaboost foreach-team-char-except
;

cyoa-bridge: don-common-demon-pizza2-4
	someicon
	"Chi dovrebbe essere sacrificato a HEKATRIUS?" cyoa-text!
	' don-common-demon-pizza2 cyoa-team-choice
;

cyoa: don-common-demon-pizza

image-demon
solo if
	soloer choosechar
	($ "GRAZIE TANTE, IDIOTA.	" rbold .. "Il demone fa esplodere " .. cname .. "  con poteri spettrali, poi svanisce!" $) cyoa-text!
	cchar to victim
	don-common-demonify sfx{ creepy }
else
	($ "Le patatine avevano il sapore di pizza. ABBBASTANZA BUONE." .. "  HEKATRIUS" rbold .. "  chiede unanima per mandarlo giu.	" .. "CHI VERRA SACRIFICATO?" rbold $) cyoa-text!
	team-count 3 > if
		' don-common-demon-pizza2 2 #cyoa-team-choice
		choice( "Scegli qualcun altro" )choice: don-common-demon-pizza2-4
	else
		' don-common-demon-pizza2 3 #cyoa-team-choice
	then
	choice( "Eh, non sacrificare nessuno." )choice: don-common-demon-pizza-refuse	
then
	-1 trunk-food+
;	


cyoa-choice: don-common-demon-facepunch
	
	($ "MALEDETTO UMANO! MALEDETTO TE!	" pbold .. "HEKATRIUS" rbold .. "  svanisce. " .. cname .. "  si sente piuttosto bene riguardo a questo." $) cyoa-text!
		($ "Che pugno miracoloso!" $) +crtext
		morale2+ strengthREV strength++ temperREV goodfx

;	

cyoa-choice: don-common-demon-freakout
	// the group freaks out, causing the demon to freak out - you all freak out together
	// the demon leaves and you have essentially lost a person for nothing
	// maybe don't freak out next time
	
	($ Thegroup .. "  impazzisce!" rbold $) text!
	notsolo if
		($ "HEKATRIUS" rbold .. "  impazzisce per un po! Quindi si trasforma di nuovo in " .. vname .. ", che poi esplode!" $) +crtext
		victim .kill
	else
		($ "HEKATRIUS" rbold .. " ' impazzisce per un po', poi svanisce in una nuvola di fumo! E molto traumatico!" $) +crtext
		morale--
	then badfx
;	
		
	
: don-common-demon-angrypunch 
	most-angrystrong if
		choice( "Pugno la sua faccia" )choice: don-common-demon-facepunch
	then
;	

: don-common-demon-choiceblock
		trunk-food 0 > if
			choice( "Offrigli un sacchetto di patatine   1 cibo" )choice: don-common-demon-pizza	
		then
		choice( "Impazzire" )choice: don-common-demon-freakout
;

: don-common-demon-transmutetext
	($ cname .. "' gli occhi si ribaltano e " .. che .. "  inizia a convulsione! " .. cHe .. " scuote e urla in lingue arcane." $) +crtext
	($ "In " .. vname .. "'s place now sits " .. "THE DEMON HEKATRIUS" rbold .. ". Ti chiede se hai la pizza. Potrebbe davvero usare una fetta calda." $) +crtext
	
	don-common-demon-choiceblock
;	
	
// Enter HEKTATRIUS
cyoa: don-common-demon
	"Linvocato" cyoa-title!
	($ Thegroup .. "  nota un cattivo odore proveniente dal pavimento. Zolfo. Montone in putrefazione. Dolcetto o scherzetto." $) cyoa-text! basic-tone 
	($ "Un lampo di luce e il suono di a " .. "mille balene che esplodono" pbold .. "  riempie la macchina." $) +crtext
	
	solo if // alone
		image-demon
		soloer choosechar
		($ "Il fumo si dirada per rivelare " .. "THE DEMON HEKATRIUS" rbold .. "." $) +crtext
		($ "Ti chiede se hai la pizza. Potrebbe usare una fetta calda." $) +crtext	
		don-common-demon-angrypunch 
		don-common-demon-choiceblock
		
	else // not alone
		someicon
		cchar to victim 
		// added cchar to victim here so it's really easy to keep track of who turned into a demon. This lets me use victim and vname up above, instead of having to constantly keep track of cname.
			don-common-demon-transmutetext
	then
	

	
;

} }

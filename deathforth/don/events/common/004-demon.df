cyoa{ road{

(*
------------------------------------------------------------------------------------------------------------------------
	004 - "L'Invocation" - Don Thacker
	One of the group becomes posessed by the demon HEKTATRIUS. He wants PIZZA and SMOKES.
	That's cool, right? I mean he sounds pretty rad.
------------------------------------------------------------------------------------------------------------------------
*)

// The group kicks the character TO THE CURB
cyoa-choice: don-common-demon-offload
	victim to cchar
	($ notcname .. " expulse " .. vname .. " par la porte passager.
	Alors qu'" .. vhe .. " tombe, " .. vhe .. " explose en un essaim de petites chauve-souris noires." $) cyoa-text!
	victim .kill
	morale--all
;

: don-common-demonify
	victim .morale--
	0 victim .tint.skin.g!
	0 victim .tint.skin.b!
	victim .strength++ victim .fitness++
	victim statmod{ .fireproof }
	victim ispet? if
		0 victim .pet! victim statmod{ .undog } 0 3 rnd victim .bodytype! 73 victim .specialhead!
		WEAPON_FIERCE victim .weapon_default!
	else
		victim .weapons-to-trunk
		WEAPON_DEVILFORK victim .weapona!
		1 victim .weapon_nodropa!
	then
;


cyoa-choice: don-common-demon-pizza-refuse
// Victim was necessary to carry the character selected at the start all the way to here. Otherwise the intermediary cyoa: would have cleared it. Could have also gotten around this using the vortex-003 example of not using cyoa: and using cyoa-start instead, but it would have been really clumsy and prone to breaking.
	($ "OUCH, ET BIEN, MERCI QUAND MEME, JE SUPPOSE.
	" rbold .. "Le demon libere " .. vname .. ", qui est definitivement change par cette experience" $) cyoa-text!
	don-common-demonify sfx{ creepy }
;

: don-common-demon-pizzaboost dup .strength++ dup .fitness++ dup .morale-- ;

cyoa-choice: don-common-demon-pizza2
	($ "MERCI POUR L'AMEEEEEEEEEEEEEEEEEEEEEEEEE.
	" rbold .. "Tous les survivants sont impregnes d'un pouvoir demoniaque." $) cyoa-text!
	cchar .kill
	cchar ' don-common-demon-pizzaboost foreach-team-char-except
;

cyoa-bridge: don-common-demon-pizza2-4
	someicon
	"Qui devrait etre sacrifie a HEKATRIUS ?" cyoa-text!
	' don-common-demon-pizza2 cyoa-team-choice
;

cyoa: don-common-demon-pizza

image-demon
solo if
	soloer choosechar
	($ "MERCI BEAUCOUP, IDIOT.
	" rbold .. "Le demon fait exploser " .. cname .. " avec ses pouvoirs terrifiants, puis disparait!" $) cyoa-text!
	cchar to victim
	don-common-demonify sfx{ creepy }
else
	($ "Les chips etaient a la saveur pizza. C'EST DEJA PAS MAL." .. " HEKATRIUS" rbold .. " demande une ame pour accompagner ca.
	" .. "QUI SERA SACRIFIE?" rbold $) cyoa-text!
	team-count 3 > if
		' don-common-demon-pizza2 2 #cyoa-team-choice
		choice( "Choisissez quelqu'un d'autre" )choice: don-common-demon-pizza2-4
	else
		' don-common-demon-pizza2 3 #cyoa-team-choice
	then
	choice( "Euh, ne sacrifiez personne." )choice: don-common-demon-pizza-refuse
then
	-1 trunk-food+
;


cyoa-choice: don-common-demon-facepunch

	($ "MAUDITS SOIENT LES HUMAINS ! MAUDIT SOIS-TU !
	" pbold .. "HEKATRIUS" rbold .. " disparait. " .. cname .. " se sent plutot bien maintenant." $) cyoa-text!
		($ "Quel coup de poing miraculeux !" $) +crtext
		morale2+ strengthREV strength++ temperREV goodfx

;

cyoa-choice: don-common-demon-freakout
	// the group freaks out, causing the demon to freak out - you all freak out together
	// the demon leaves and you have essentially lost a person for nothing
	// maybe don't freak out next time

	($ Thegroup .. "  panique !" rbold $) text!
	notsolo if
		($ "HEKATRIUS" rbold .. " panique pendant un moment ! Puis se transforme en " .. vname .. ", qui explose ensuite !" $) +crtext
		victim .kill
	else
		($ "HEKATRIUS" rbold .. " panique pendant un moment, puis disparait dans un nuage de fumee ! C'est tres traumatisant !" $) +crtext
		morale--
	then badfx
;


: don-common-demon-angrypunch
	most-angrystrong if
		choice( "Frappez-le au visage" )choice: don-common-demon-facepunch
	then
;

: don-common-demon-choiceblock
		trunk-food 0 > if
			choice( "Offrez lui un paquet de chips - 1 nourriture" )choice: don-common-demon-pizza
		then
		choice( "Foutez le camp" )choice: don-common-demon-freakout
;

: don-common-demon-transmutetext
	($ cname .. "se met a convulser et ses yeux se revulsent !  " .. cHe .. " tremble et crie en langues anciennes." $) +crtext
	($ "La place de " .. vname .. " est maintenant occupee par " .. "LE DEMON HEKATRIUS" rbold .. ". Il vous demande si vous avez de la pizza. Il aurait bien besoin d'une part chaude." $) +crtext

	don-common-demon-choiceblock
;

// Enter HEKTATRIUS
cyoa: don-common-demon
	"L'invocation" cyoa-title!
	($ Thegroup .. "  remarque une odeur nauseabonde provenant du plancher. Du soufre. Du mouton pourri. Du mais souffle." $) cyoa-text! basic-tone
	($ "Un eclair et le son d'un " .. "millier de baleines qui explosent" pbold .. "  envahissent la voiture." $) +crtext

	solo if // alone
		image-demon
		soloer choosechar
		($ "La fumee se dissipe pour reveler " .. "LE DEMON HEKATRIUS" rbold .. "." $) +crtext
		($ "Il demande si vous avez de la pizza. Il aimerait bien en manger une part bien chaude." $) +crtext
		don-common-demon-angrypunch
		don-common-demon-choiceblock

	else // not alone
		someicon
		cchar to victim
		// added cchar to victim here so it's really easy to keep track of who turned into a demon. This lets me use victim and vname up above, instead of having to constantly keep track of cname.
			don-common-demon-transmutetext
	then



;

} }

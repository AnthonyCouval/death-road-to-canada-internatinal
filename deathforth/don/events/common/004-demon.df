cyoa{ road{ 

(*
------------------------------------------------------------------------------------------------------------------------
	004 - "Los no convocados" - Don Thacker
	One of the group becomes posessed by the demon HEKTATRIUS. He wants PIZZA and SMOKES.
	That's cool, right? I mean he sounds pretty rad.
------------------------------------------------------------------------------------------------------------------------
*)

// The group kicks the character TO THE CURB
cyoa-choice: don-common-demon-offload
	victim to cchar
	($ notcname .. "  patea " .. vname .. " 	Como " .. vhe .. " cae " .. vhe .. " se desintegra en una manada de murcielagos negros pequenos." $) cyoa-text!
	victim .kill
	morale--all
;

: don-common-demonify
	victim .morale-- 
	0 victim .tint.skin.g!
	0 victim .tint.skin.b!
	victim .strength++ victim .fitness++
	victim statmod{ .fireproof }
	victim ispet? if 
		0 victim .pet! victim statmod{ .undog } 0 3 rnd victim .bodytype! 73 victim .specialhead!
		WEAPON_FIERCE victim .weapon_default!
	else
		victim .weapons-to-trunk
		WEAPON_DEVILFORK victim .weapona!
		1 victim .weapon_nodropa!
	then
;

cyoa-choice: don-common-demon-pizza-refuse
// Victim was necessary to carry the character selected at the start all the way to here. Otherwise the intermediary cyoa: would have cleared it. Could have also gotten around this using the vortex-003 example of not using cyoa: and using cyoa-start instead, but it would have been really clumsy and prone to breaking.
	($ "UGH, BIEN, GRACIAS POR EL INTENTO.	" rbold .. "El demonio libera a " .. vname .. ", que se transforma por la experiencia." $) cyoa-text!
	don-common-demonify sfx{ creepy }
;

: don-common-demon-pizzaboost dup .strength++ dup .fitness++ dup .morale-- ;

cyoa-choice: don-common-demon-pizza2
	($ "THANKS FOR THE SOOOUUUUUUUUL.
	" rbold .. "Todos los supervivientes estan infundidos con poder demoniaco." $) cyoa-text!
	cchar .kill
	cchar ' don-common-demon-pizzaboost foreach-team-char-except
;

cyoa-bridge: don-common-demon-pizza2-4
	someicon
	"Quien debe ser sacrificado a HEKATRIUS?" cyoa-text!
	' don-common-demon-pizza2 cyoa-team-choice
;

cyoa: don-common-demon-pizza

image-demon
solo if
	soloer choosechar
	($ "GRACIAS POR LA AYUDA, DINGUS.	" rbold .. "El demonio destruye a " .. cname .. "  con poderes misteriosos, luego desaparece!" $) cyoa-text!
	cchar to victim
	don-common-demonify sfx{ creepy }
else
	($ "Las papas eran de sabor a pizza. SUFICIENTE BUENO." .. "  HEKATRIUS" rbold .. "  pide un alma para lavarla.	" .. "QUIEN SERA SACRIFICADO?" rbold $) cyoa-text!
	team-count 3 > if
		' don-common-demon-pizza2 2 #cyoa-team-choice
		choice( "Escoge a alguien mas" )choice: don-common-demon-pizza2-4
	else
		' don-common-demon-pizza2 3 #cyoa-team-choice
	then
	choice( "Uh, no sacrifiques a nadie." )choice: don-common-demon-pizza-refuse	
then
	-1 trunk-food+
;	


cyoa-choice: don-common-demon-facepunch
	
	($ "MALDITO HUMANO! MALDITO!	" pbold .. "HEKATRIUS" rbold .. "  desaparece. " .. cname .. "  se siente bastante bien por esto." $) cyoa-text!
		($ "Que milagroso punetazo!" $) +crtext
		morale2+ strengthREV strength++ temperREV goodfx

;	

cyoa-choice: don-common-demon-freakout
	// the group freaks out, causing the demon to freak out - you all freak out together
	// the demon leaves and you have essentially lost a person for nothing
	// maybe don't freak out next time
	
	($ Thegroup .. "  se asusta!" rbold $) text!
	notsolo if
		($ "HEKATRIUS" rbold .. "  se asusta por un rato! Luego se transforma de nuevo en " .. vname .. ", quien luego explota!" $) +crtext
		victim .kill
	else
		($ "HEKATRIUS" rbold .. " ' se asusta por un rato, luego se desvanece en una nube de humo! Es muy traumatizante!" $) +crtext
		morale--
	then badfx
;	
		
	
: don-common-demon-angrypunch 
	most-angrystrong if
		choice( "Golpearle la cara" )choice: don-common-demon-facepunch
	then
;	

: don-common-demon-choiceblock
		trunk-food 0 > if
			choice( "Ofrecele una bolsa de patatas fritas   1 comida" )choice: don-common-demon-pizza	
		then
		choice( "Asustarse" )choice: don-common-demon-freakout
;

: don-common-demon-transmutetext
	($ cname .. "'' da vueltas los ojos hacia atras y " .. che .. "  empieza a convulsionar! " .. cHe .. "  tiembla y grita en lenguas arcanas." $) +crtext
	($ "En " .. vname .. "'' ahora esta sentado " .. "EL DEMONIO HEKATRIUS" rbold .. ". Pregunta si tienes pizza. Realmente le vendria bien una porcion caliente." $) +crtext
	
	don-common-demon-choiceblock
;	
	
// Enter HEKTATRIUS
cyoa: don-common-demon
	"Los no convocados" cyoa-title!
	($ Thegroup .. "  nota un olor nauseabundo proveniente de las tablas del suelo. Azufre. Cordero podrido. Caramelo de maiz." $) cyoa-text! basic-tone 
	($ "Un destello de luz y el sonido de un " .. "mil ballenas explotando" pbold .. "  llena el coche." $) +crtext
	
	solo if // alone
		image-demon
		soloer choosechar
		($ "El humo se aclara para revelar " .. "EL DEMONIO HEKATRIUS" rbold .. "." $) +crtext
		($ "Pide que tienes pizza. Podria usar un pedazo caliente." $) +crtext	
		don-common-demon-angrypunch 
		don-common-demon-choiceblock
		
	else // not alone
		someicon
		cchar to victim 
		// added cchar to victim here so it's really easy to keep track of who turned into a demon. This lets me use victim and vname up above, instead of having to constantly keep track of cname.
			don-common-demon-transmutetext
	then
	

	
;

} }

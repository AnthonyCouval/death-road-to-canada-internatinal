cyoa{ road{ 

(*
------------------------------------------------------------------------------------------------------------------------
	003 - "O Vortice Tetrografico" - Don Thacker
	A small geometrically impossible vortex opens in front of the car.
	Is it a portal? A gravity well? A dimensional rift? A pizza?
------------------------------------------------------------------------------------------------------------------------
*)

// TODO: Use actual counts for body parts
: char-reroll args( char )
	0 3 rnd <- char .bodytype!
	0 25 rnd <- char .hairtype!
	0 15 rnd <- char .torsotype!
	0 15 rnd <- char .legstype!	
	0 11 rnd <- char .headtype!
	0 1 rnd <- char .female!
	0 6 rnd <- char .wits!
	0 6 rnd <- char .composure!
	0 6 rnd <- char .attitude!
	2 6 rnd <- char .morale!
	0 6 rnd <- char .loyalty!
	2 6 rnd <- char .fitness!
	2 6 rnd <- char .mechanical!
	2 6 rnd <- char .medical!
	2 6 rnd <- char .shooting!
	2 6 rnd <- char .strength!

;

: revname args( char )
	<- char .name@ $reverse <- char .name!
;

: don-vortex-extra // If your CALCULATOR is 5 or 6 wits, get bonus stats for the person you throw in
strengthREV fitnessREV shootingREV
33 %chance if
	 strength++
else
	50 %chance if
		 fitness++
	else
		 shooting++
	then 
then

	50 %chance if
		vitality++
	else
		dexterity++
	then 

;

: don-common-vortex-namechange // Just a way to save retyping things in bonus and toss
	cchar revname
	($ cHe .. " agora e " .. cname .. "!" $) +crtext
	cchar char-reroll
;

cyoa-choice: don-common-vortex-drivethrough
	($ cname .. " acelera e CONTINUA DIRIGINDO!" $) cyoa-text! car-plowthrough
	' char-reroll foreach-team-char
	' revname foreach-team-char
	($ Thegroup .. " volta para a estrada um momento depois, como se nada tivesse acontecido. Ainda assim " .. gthey .. " sentir" .. g1s .. " different..." pbold $) +crtext goblin-laugh
;

cyoa-choice: don-common-vortex-bonus
	($ "'AGORA VOCE FEZ" .. cname .. " ressurge, sentindo-se novo e incrivel!" $) cyoa-text!
	don-common-vortex-namechange don-vortex-extra goodfx
;

cyoa-choice: don-common-vortex-toss
	($ "'AGORA VOCE FEZ" .. cname .. " ressurge, mudado." $) cyoa-text!
	don-common-vortex-namechange goblin-laugh
;

cyoa-bridge: don-common-vortex-bonus4
	someicon
	"Quem deve ser OTIMAMENTE jogado no vortice?" cyoa-text!
	' don-common-vortex-bonus cyoa-team-choice
;

cyoa-bridge: don-common-vortex-toss4
	someicon
	"Quem deve ser jogado no vortice?" cyoa-text!
	' don-common-vortex-toss cyoa-team-choice
;
	
cyoa-bridge: don-common-vortex-calculate
	($ "DE ACORDO COM " .. cname .. "'OS CALCULOS de voce podem jogar alguem aqui e isso os mudaria, provavelmente nem mesmo de forma fatal!" $) cyoa-text!
	cwits+ if
	($ cHe .. " analisa alguns numeros extras e descobre a maneira ideal de jogar alguem!" $) +crtext goodfx
	then
	basic-tone
	cwits+ if
		team-count 3 > if
			' don-common-vortex-bonus 2 #cyoa-team-choice
			choice( "Escolha alguem outro" )choice: don-common-vortex-bonus4
		else
			' don-common-vortex-bonus 3 #cyoa-team-choice
		then
	else
		team-count 3 > if
			' don-common-vortex-toss 2 #cyoa-team-choice
			choice( "Escolha alguem outro" )choice: don-common-vortex-toss4
		else
			' don-common-vortex-toss 3 #cyoa-team-choice
		then
	then
	choice( "'Nao mexa com aquele portal" )choice		
;
	
cyoa-choice: don-common-vortex-fight
	cpet? if
		($ cname .. "  tenta morder o portal e depois desliza para dentro dele." .. cHe .. "  reaparece momentos depois, com uma nova inteligencia estranha." $)
	else
		($ cname .. "  da um balanco e depois cai no portal com o mesmo movimento suave." .. cHe .. " ' reaparece momentos depois, agora muito mais peludo. Pelo menos, provavelmente " .. chim .. "?" $) 
	then cyoa-text! cchar revname attitudeREV 
	cpet? if
		cchar .dogtosuperdog	
	else
		cchar .doggify
		10 %chance if
			($ cname .. "  mantem " .. chis .. "  inteligencia humana!" $) +crtext
			cchar .dogtosuperdog
		then
	then goodfx
	

;	

// A vortex opens in front of the car
cyoa: don-common-vortex
	image-vortex
	"O Vortice Tetrografico" cyoa-title!
	someone choosechar
	($ Thegroup .. "  percebe um " .. " vortice geometricamente impossivel" fbold ..  "  se formando na frente do carro em movimento. Ele paira alguns metros na frente do parachoque em alta velocidade, zumbindo ameacadoramente." $) cyoa-text! basic-tone
	choice( "Passe por ele" )choice: don-common-vortex-drivethrough
	smartest charchoice( ": CALCULAR seus VETORES" )choice: don-common-vortex-calculate
	choice( "'Nao mexa com isso!" )choice
	angriest-pass if
		angriest charchoice( ": Lute contra isso!" )choice: don-common-vortex-fight
	then
	;

} }

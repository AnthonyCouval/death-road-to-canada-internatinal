cyoa{ road{ 

(*
------------------------------------------------------------------------------------------------------------------------
	015 - "Tres Tumulos" - Don Thacker
	The group passes through an abandoned graveyard emptied but for three undesturbed graves. 
	There's only time enough to search one.
------------------------------------------------------------------------------------------------------------------------
*)

	cyoa-choice: don-common-graves-digup1
		image-three-graves
		($ Thegroup .. " encontra uma colecao de cartas de amor de um soldado para sua amada. Elas sao realmente tocantes!" $)  cyoa-text!
		($ "'Ha tambem uma caixa cheia de arenque enlatado e uma caixa de balas velhas embrulhadas em um laco dourado." $) +crtext
			10 trunk-food+ goodfx
			morale2+all
	;
	
	cyoa-choice: don-common-graves-digup2
		someone choosechar
		image-three-graves
		($ Thegroup .. " cava e cava e nao encontra nada de interessante. Que perda de tempo." $) cyoa-text!
		solo if
		($ "No entanto, " .. cname .. " encontra um grande pacote cheio de " .. "suprimentos medicos" pbold .. " em um anexo proximo!" $) +crtext
			9 trunk-medical+ goodfx
		else
		($ "Enquanto todo mundo estava cavando, " .. cname .. " encontrou uma lata de gasolina sob algumas folhas, ao lado de uma " .. "pilha de caveiras" rbold .. "." $) +crtext
			50 70 rnd trunk-gas+ goodfx
		then
		morale++all
	;
	
	cyoa-choice: don-common-graves-digup3
		image-three-graves
		($ Thegroup .. " desenterra um caixao de carvalho com alcas de latao e uma placa com o sentimento " $) cyoa-text!	
		($ "'NaO HA DESTINO SENaO AQUELE QUE VOCE FAZ " rbold $) +text
		($ "'gravado nele. O caixao e pesado mas abre facilmente. Esta cheio de " .. "ARMAS DE Fogo" pbold .. ". Uma tonelada de " .. "ARMAS DE Fogo" pbold .. "." $) +text goodfx
		80 %chance if 
			3 WEAPON_PISTOL trunk.weapon+
			($ "TEVE 3 PISTOLES, " rbold $) +crtext
		else
			2 WEAPON_DOLT trunk.weapon+
			($ "TEVE 2 REVOLVERS, " rbold $) +crtext
		then
		80 %chance if
			2 WEAPON_SHOTGUN trunk.weapon+
			($ "2 FUSoES DE CACADA" rbold $) +text
		else
			1 WEAPON_AUTOSHOT trunk.weapon+
			($ "UM FUSaO AUTOMATICO DE CACADA" fbold $) +text
		then
		($ ", " rbold $) +text
		80 %chance if
			2 WEAPON_HUNTING trunk.weapon+
			($ "E TINHA 2 FUSoES DE CAca!" rbold $) +text
		else
			80 %chance if
				1 WEAPON_AK47 trunk.weapon+
				($ "E TINHA UMA RIFLE DE ASSALTO!" fbold $) +text
			else
				1 WEAPON_MINIGUN trunk.weapon+
				($ "AND A MINIGUN!!!" fbold $) +text
			then
		then		
	;
				
	cyoa-choice: don-common-graves-screwthat
		image-three-graves
		($ Thegroup .. " pensa que escavar tumbas durante a " .. "apocalipse de zumbis" rbold .. " 'nao e a melhor ideia e escolhe em vez disso " ..  "SE MOVER EM PRA CA" pbold .. "." $) cyoa-text!
	;

	cyoa: don-common-graves
		image-three-graves
		"Tres Tumulos" cyoa-title! 
		($ Thegroup .. " passa por um cemiterio. A maioria dos tumulos havia sido escavados, a maioria do interior para fora. " $) cyoa-text! basic-tone
	 	($ "Existem tres sepulturas, em uma fileira, intocadas com os nomes " .. "--GRADE" pbold .. ", " .. "NICHOLAS" pbold .. "  e " .. "LUI--" pbold .. ". " $) +crtext
	 	// ($ Thegroup .. "  so tem tempo para desenterrar uma sepultura." $) +text

		choice( "Desenterrar --GRID" )choice: don-common-graves-digup1
		choice( "Desenterrar NICHOLAS" )choice: don-common-graves-digup2
		choice( "Desenterrar LUI--" )choice: don-common-graves-digup3
		choice( "Deixeos em paz!" )choice: don-common-graves-screwthat
	;
} }

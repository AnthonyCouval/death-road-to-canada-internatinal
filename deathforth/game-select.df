uses cyoa.df

cyoa{
	
	: make-it-snappy 1 cyoa-fade! 1 cyoa-clickspeed! ;

	: load-slotinfo
		3 array to slotinfo
		0 to slotcount
		3 for
			3 i - #slot-info-name $sread if
				slotinfo 3 i - rot poke drop
				' slotcount ++
			then
		next
	;

	: start-the-actual-game-here
		"NUEVA GUARDAR" slot-info-name $swrite
		"($ calavera .. calavera .. calavera .. calavera $) imprime" slot-data-name $swrite
		gogo
		MAIN_EVENT_NEW main-post-event
	;

	: continue-the-saved-game-here
		gogo
		MAIN_EVENT_DO_LOAD main-post-event
	;

	cyoa-choice: new-gamestart
		choicenum saveslot!
		to-gamestart-setup
	;

	cyoa-choice: cont-saveslot
		choicenum saveslot!
		cyoa-start
		"- CONTINUAR JUEGO -" cyoa-title!
		"THE HORROR CONTINUES..." cyoa-text!
		choice( "AIQUE" )choice: continue-the-saved-game-here
	;

	defer select-new

	cyoa-choice: delete-slot make-it-snappy
		skull cyoa-title!
		"BORRADO" cyoa-text!
		delete-the-game
		sfx{ bite } 10 0.75 rumble-gui
		choice( "OK" )choice: select-new
	;

// TODO: SLOT HEAD
	cyoa-choice: new-delete-game
		choicenum saveslot!
		cyoa-start make-it-snappy
		"- ELIMINAR JUEGO -" cyoa-title!
		($ "ELIMINAR antigua partida guardada" .. slotinfo saveslot peek nip .. "'?" $) cyoa-text!
		choice( "NO" )choice: select-new thumbdownicon
		choice( "SI" )choice: delete-slot thumbupicon
	;

	: slotchoices-new 0 args( slot )
		load-slotinfo
		3 for
			3 i - -> slot
			choice( "vacio" )choice: new-gamestart circleicon
			<- slot slot? if
				slotinfo <- slot peek cyoa-choice-text! drop
				icons16_id ICON_UP + cyoa-choice-iconid!
				ballicon
				' new-delete-game cyoa-choice-handler!
			then
			<- slot cyoa-choice-num!
		next
	;

	: slotchoices-cont 0 args( slot )
		load-slotinfo
		3 for
			3 i - -> slot
			<- slot slot? if
				choice( "..." )choice: cont-saveslot circleicon
				slotinfo <- slot peek cyoa-choice-text! drop
				<- slot cyoa-choice-num!
				ballicon
			then
		next
	;

	defer select-start // forward reference

	cyoa: select-new-game make-it-snappy
		"- NUEVA PARTIDA -" cyoa-title!
		"Elige espacio de juego:" cyoa-text!
		slotchoices-new
		choice( "ATRAS" )choice: select-start backicon cyoa-choice-esc-default
	;

	last to select-new

	cyoa: select-cont-game make-it-snappy
		"- CONTINUAR JUEGO -" cyoa-title!
		"Elige espacio de juego:" cyoa-text!
		slotchoices-cont
		choice( "ATRAS" )choice: select-start backicon cyoa-choice-esc-default
	;

	cyoa-choice: select-create-char
		to-new-char-maker
	;

	: do-char-maker
		select-start // Have to re-call this CYOA state for C-side char maker to return to
		to-new-char-maker
	;

	cyoa: select-cyoa make-it-snappy
		load-slotinfo

		// Make sure we can delete the game after zombotown or tutorial. Shouldn't really be done like this...
		filesave{ ' delete-game } to delete-the-game

		"- SELECCIONAR -" cyoa-title!
		"Elige opcion de juego:" cyoa-text!
		slotcount if
			choice( "Continuar Juego" )choice: select-cont-game
		then
		choice( "Nuevo juego" )choice: select-new-game

		// TODO: unlock?
		choice( "Personajes personalizados" )choice: do-char-maker
		// choice( "Personajes personalizados" )choice: select-create-char

		choice( "ATRAS" )choice backicon cyoa-choice-esc-default
	;

	// TEMP: FOR QUICK GAME START
(*
	cyoa: quick-start-game
		"- NUEVA PARTIDA -" cyoa-title!
		"THE HORROR BEGINS..." cyoa-text!
		choice( "OH, DANG!" )choice: start-the-actual-game-here
	;
*)
	: select-boot
		gstats{ load }
		gamedef{ modedefs }

		gstats{ tutorial-done } if select-cyoa then;

		' noop to delete-the-game

		0 saveslot! MAIN_EVENT_NEW main-post-event "events/tutorial.df" $load-region to-mission
	;

	last to select-start

	last to main-event-do-select
}


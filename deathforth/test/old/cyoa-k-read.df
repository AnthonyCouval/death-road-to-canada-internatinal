uses base.df
uses charfilt.df
uses charstats.df
uses road.df

cyoa{ road{

// Magazines: Popular After the Apocalypse

cyoa-choice: magazine1-butts
	"Enttauschung" cyoa-title!
	"" cyoa-text!
	solo not if ($ "Diese seltsame Lesart stort " .. theothers .. "  der Gruppe. Unbeirrt, " $) +text then
	($ cname .. "  lehnt sich zuruck und liest Giant Butts Monthly. Es stellt sich heraus dass es sich um eine trockene medizinische Fachzeitschrift mit einem irrefuhrenden Namen handelt." $) +text 2 400 tone
		attitude-- medical++ cchar morale--all-except
;

cyoa-choice: magazine1-guns
	"American Feuerwaffen" cyoa-title!
	($ "Das Magazin ist voller Waffenbilder, Waffeninformationen, Waffentipps und seltsame hyperpatriotische Rants. " .. cname .. "  absorbiert alle diese Dinge." $) cyoa-text! 1 400 tone
		temper-- shooting++
;

cyoa-choice: magazine1-mech
	"Umschalt  & Ready2Go" cyoa-title!
	($ "Dies war DAS Auto Magazin vor der Gesellschaftsexplosion. Die aufschlureichen Artikel haben ruhigende Wirkung auf " .. cname .. "." $) cyoa-text! 2 300 tone
		temper++ mechanic++
;

cyoa-choice: magazine1-cats
	" katzenvideobilder" cyoa-title!
	"'Das heit, was der Titel sagt Nur Katzenvideobilder. Es war sinnlos in besseren Zeiten, aber nachdem das Internet abgesturzt ist, hat es groe Popularitat erlangt." cyoa-text! goodfx
		morale++ attitude++
;

cyoa: magazine1-cyoa
	"Zeitschriften: Beliebt nach der Apokalypse" cyoa-title!
	choosehuman cicon
	($ Thegroup .. " versucht einen Lebensmittelladen zu plundern, aber er wurde absolut leergeraumt " .. cname-He .. " " .. "	Wahle ein Magazin aus:" $) cyoa-text! basic-tone
		choice( "American Feuerwaffen" )choice: magazine1-guns
		choice( "Umschalt  & Ready2Go" )choice: magazine1-mech
		choice( " katzenvideobilder" )choice: magazine1-cats
		choice( "GIANT BUTTS MONTHLY" )choice: magazine1-butts
;

// END Magazines: Popular after the Apocalpyse

// Dentist Office

: magazine2-loot morale++ 2 LOOT_FOOD trunk.loot+ 2 3 rnd LOOT_MEDICAL trunk.loot+ ;

cyoa-choice: magazine2-glu
	"Erbauliche Anekdoten" cyoa-title!
	($ cname .. "  erkennt, dass diese Zeitschrift schon immer kitschig war, geniet aber immer noch die optimistischen Geschichten. Eine Geschichte handelt von einer Katze, die ihren Besitzer vor dem Ersticken an einem Buffet rettet. Inspirierend." $) cyoa-text! 2 300 tone attitude++ magazine2-loot
;	
cyoa-choice: magazine2-sci
	"Fragwurdige Wissenschaft" cyoa-title!
	($ cname .. " ' ist pessimistisch gegenuber der Vorhersage der Zeitschrift, dass in ein paar Jahren fliegende Hovercars im Einsatz sein werden, jetzt, da die Zivilisation mehr oder weniger zerstort ist. Es war trotzdem ein ziemlich unterhaltsames Magazin." $) cyoa-text! 1 300 tone attitude-- magazine2-loot
;
cyoa-choice: magazine2-celeb
	"Promi News" cyoa-title!
	($ "Diese Zeitschrift war immer unglaublich gemein, moglicherweise noch mehr, da die meisten Leute darin jetzt aufgefressen sind " .. cname .. "  ist ein etwas schlechterer Mensch nur weil er sie liest." $) cyoa-text! 2 300 tone loyalty-- wits-- morale2+ magazine2-loot
;	
cyoa: magazine2-cyoa
	"Zahnarztpraxis" cyoa-title!
	choosehuman cicon
	($ Thegroup .. "  plundert eine Zahnarztpraxis und findet einige ubrig gebliebene medizinische Hilfsmittel und viele technisch essbare Tuben Zahnpasta " .. cname-He .. "  geht in den Warteraum und schnappt sich ein Magazin aus der sparlichen Auswahl." .. "	Wahle ein Magazin aus:" $) cyoa-text! basic-tone
		choice( "Schnulze fur die sensible Seele" )choice: magazine2-glu
		choice( "Fragwurdige Wissenschaft" )choice: magazine2-sci
		choice( "CELEBZ Nachrichten" )choice: magazine2-celeb
;

// END Dentist Office

// Prepper Magazine - Person Pick CYOA if you have people, Instant Result if you don't.

: mag3roll 30 %chance if .medical++ else 50 %chance if .shooting++ else .mechanic++ then then ;
: mag3share cyoa-good-dialog "Jeder reicht die Zeitschrift herum und liest, welche Artikel sie ansprechen. Wie immer bekommt irgendwann jemand Erdnussbutterflecken auf das Magazin, bis zu dem Punkt, an dem es unlesbar wird." cyoa-text! 
	' mag3roll foreach-team-char someone 100 LOOT_GAS trunk.loot+ ; 

: mag3solo shooting++ medical++ mechanic++ 100 LOOT_GAS trunk.loot+ ;
: mag3result cyoa-good-dialog
	($ "Dies war eine hypothetische, damals aktuelle Sonderausgabe zur Zombie Apokalypse " .. cname .. " studiert die Schusswaffentipps, den Artikel uber Notfallmedizin und ungewohnliche, aber effektive allgemeine Reparaturtricks." $) cyoa-text! cicon mag3solo
;

cyoa-bridge: mag3share-pick 
	someicon
	"Wer sollte das Prepared Preppers Magazine lesen?" cyoa-text!
	' mag3result cyoa-team-choice
;

: magazine3-cyoa
	cyoa-start "Prepared Preppers Magazine" cyoa-title! 
	0 to cyoaicon cyoaicon
	($ Thegroup .. " 'zapft Benzin aus einigen verlassenen Fahrzeugen ab und entdeckt dann eine Zeitschrift in einem der Autos. Es ist eine Ausgabe von Prepared Preppers, die Sonderausgabe, in neuwertigem Zustand " $) cyoa-text!
	solo if 
		soloer choosechar ($ cHe .. " studiert seine jetzt berechtigte Weisheit." $) +text 
		mag3solo goodfx
	then;
	+cr "Wer darf es lesen?" +text basic-tone 
	team-count 3 > if
		choice( "Wahle jemanden aus der es liest" )choice: mag3share-pick 
	else
		' mag3result 3 #cyoa-team-choice
	then
	choice( "Mit allen teilen" )choice: mag3share 
; 

// END Prepper Magazine

// Burnt Library

cyoa-choice: books-are-for-nerds 
	"BUCHER SIND FUR NERDS" cyoa-title!
	cpet? if
		($ cname .. " springt auf, schnappt sich das Buch in " .. chis .. " kiefer und " .. "ZERFETZT ES IN STUCKE" rbold .. " deutsch sein " .. che .. " schuttelt das Buch hin und her. Nimm DAS, Nerd." $) 
	else
		($ cname .. " greift das Buch und " .. "reit es in zwei Halften" rbold .. " wahrend er viel grunzt " .. "'JA DAS DENKE ICH UBER BUCHER." pbold $) 
	then cyoa-text! goodfx
	morale2+ temper-- strength++ attitude++ cchar morale--all-except
;
: book1-helproll 50 %chance if .attitude++ else .temper++ then ;
: book1-mechroll 50 %chance if .mechanic++ then ;
cyoa-choice: book1-improve
	"Verandere Dein Leben durch Positivitat" cyoa-title!
	($ Thegroup .. " lernt viel uber die Kraft positiv zu bleiben!" $) cyoa-text! 1 300 tone
		attitude++ temper++ morale++ cchar ' book1-helproll foreach-team-char-except
;
cyoa-choice: book1-mechanic
	"Enzyklopadie des Bastelns" cyoa-title!
	"'Dieses Buch enthalt Informationen zur Reparatur oder Modifikation vieler Alltagsgegenstande. Es ist wahrscheinlich auch schwer genug jemanden damit zu toten. Bonus!" cyoa-text! 2 300 tone
		mechanic++ cchar ' book1-mechroll foreach-team-char-except
;
cyoa-choice: book1-morale
	"'Dr. Dinks Reimzeit" cyoa-title!
	($ "Dieses Buch ist voller bunter Bilder und eignet sich hervorragend fur Kinder jeden Alters und " .. thegroup .. " freut sich sehr, etwas Neues zum Lesen zu haben." $) cyoa-text! goodfx morale2+ cchar morale++all-except
;
: book1-swole 
	// cchar angrystrong-revealed? not if then;	cchar angrystrong? if
		most-angrystrong if
			charchoice( ": BUCH WUT" )choice: books-are-for-nerds
	then
;
cyoa: book1-cyoa
	"Verbrannte Bibliothek" cyoa-title!
	choosehuman cicon
	($ Thegroup .. " begegnet einer Bibliothek in einer Stadt die grotenteils abgebrannt zu sein scheint " .. cname-He .. " findet ein einzelnes Buch das nicht wirklich durch das Feuer beschadigt wurde. Was war das Buch?" $) cyoa-text! basic-tone
		book1-swole
		choice( "Gigantisches Reparaturhandbuch" )choice: book1-mechanic
		choice( "Selbsthilfebuch uber Positivitat" )choice: book1-improve
		choice( "'Reimendes Kinderbuch" )choice: book1-morale
;

// END Burnt Library

} }



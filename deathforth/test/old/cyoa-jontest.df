uses base.df
uses charfilt.df
uses charstats.df
uses road.df

cyoa{ road{

// Dentist

// Slightly changed the structure of all below to the new stuff. There's cyoa-choice: to start a choice instead of just doing : , and cyoa: for starting off a cyoa. Both do a lot of little shortcut things, like you no longer need to necessarily use cicon more than once, and you no longer need to use cyoa-start.

// Note that words you make to check things or add optional text to the main text of a new window do not use cyoa-choice. Use one cyoa-choice per "janela".

// Also there's words for pronouns that change based on the correct grammar of the situation. che - picks the chosen character and then uses "ele" or "ela", depending. 
// Search for "xhis" in cyoa.df for a list


: cardJ4-mcheck 
	medicalroll if
		($ cname .. "  realmente sabia o que estavam fazendo. Usando as ferramentas e suprimentos medicos disponiveis, " .. che .. "  consegue arrancar o dente de forma limpa e com relativamente pouca dor. " $) cyoa-text! goodfx
		medical++ 
	then;
	
		($ cname .. "  acabou tendo muitos problemas. A extracao do dente foi confusa, desajeitada e dolorosa. Para piorar, " .. che .. "  puxou o dente errado e teve que fazer de novo.  " $) cyoa-text! bitefx ouch--
		
	// Added an extra line below for dying from tooth extraction.

	dead? if
		+cr ($ "Depois de puxar o segundo dente " .. cname .. "  foi ouvido exclamando " .. "SIM FERRE SE ESSE BARULHO" pbold .. "  e entao " .. che .. "  morreu espontaneamente." $) +text
	then
	
;

cyoa-choice: cardJ4-dentist cardJ4-mcheck medicalREV ;

// Expanded this to show that you can split up the text. cyoa-text! for main bodies of text at the top of the window, +text to add new lines below the main text, +cr to create a line break for spacing. Also did this in the above medical check, to a lesser extent.

: cardJ4-mecheck 
	mechanicroll if
		 ($ "Usando a vantagem adicional, " .. che .. "  bate a porta do carro, puxando o dente para fora de forma limpa." $) +text goodfx
		mechanic++ 
	then;
	
		($ "Despreparado para a forca repentina de bater a porta, " .. che .. "  e puxado em direcao ao carro e bate " .. chis .. "  cabeca na porta. " $) +text bitefx ouch--

// Death flavor text added.
		
	dead? if
		+cr ($ "Este cenario exato era na verdade uma forma muito comum de morrer antes do apocalipse zumbi. Esses acidentes foram mantidos fora das noticias devido a " .. "GRANDES CORPORACOES DE AUTOMOVEIS" rbold .. "." $) +text
	then
;

cyoa-choice: cardJ4-mechanical
	($ cname .. ", usando antigos programas de desenho animado como inspiracao, decide amarrar " .. chis .. "  dente na porta do carro." $) cyoa-text! 
		+cr cardJ4-mecheck mechanicREV
;

: cardJ4-scheck 
	cpet? if
		($ cname .. "  tenta se livrar do dente quebrado mordendo uma pedra. Alguns dentes caem e a dor e excruciante. Mas pelo menos o dente ruim tambem caiu." $) cyoa-text! badfx
		nonfatal if
			ouch--
		then morale3- attitude--
	then;
	strengthroll if
		($ cname .. "  pega um alicate e vai para a cidade, puxando o dente com toda a forca possivel. Usando nada alem de vontade para lutar contra a dor, " .. che .. "  tira o dente rapidamente e com surpreendentemente pouco trabalho." $) cyoa-text! goodfx
		strength++ 
	then;
		($ cname .. " ' pega um alicate e vai para a cidade. A extracao nao vai bem. Depois de muita dor e sangue o dente finalmente sai." $) cyoa-text! bitefx ouch2-

// Added extra commentary from dying from plier surgery.		
		
		dead? if
			+cr ($ "De alguma forma " .. cname .. "  morre com isso." $) +text
		then
		
;

cyoa-choice: cardJ4-force cardJ4-scheck strengthREV ;

cyoa: cardJ4-test
	"Ma Hooth" cyoa-title!
	choosehuman cicon
	($ cname .. "  esta tendo uma dor de dente quase incapacitante. Sem acesso as ferramentas de precisao necessarias a unica opcao e arrancar o dente " .. cHe .. "  pondera as opcoes atuais." $) cyoa-text! basic-tone
	
		choice( "Forca pra fora!" )choice: cardJ4-force
		cnotpet? if
			choice( "Eu costumava ser dentista!" )choice: cardJ4-dentist
			choice( "Eu vi isso uma vez em um desenho animado!" )choice: cardJ4-mechanical
		then
;

// END of Dentist

// Radio Repair

// Not many changes here except cleaning up the words. cyoa-text-cr replaced with +cr, cyoa-text-append replaced with +text, cyoa: added and the words it makes obsolete taken out.

// someone .colourname was also replaced with "algumnome" and soloer .colourname was replaced with "solonome", search for either in cyoa.df

: cardJ2-jerk ($ cname .. "  Remove o CD player e o joga pela janela." $) +text
	temperREV morale++
;
: cardJ2-pumped ($ cname .. "  Da um soco no CD player, parando a musica." $) +text
	strengthREV	strength++ morale++
;
cyoa: cardJ2-test ($ someone .colourname .. "  fica frustrado quando o CD player do carro fica preso, tocando a mesma musica repetidamente." $) cyoa-text! +cr 1 cyoa-image-icon! 4 500 tone 

	strongest-pass
	if strongest choosechar cardJ2-pumped then;

	most-ahole
	if choosechar cardJ2-jerk then;

	solo if ($ soloname .. " desliga o volume." $) +text then;

	"O grupo decide desligar por enquanto e tentar consertar mais tarde." +text
;

// END Radio Repair

// Shop Smart

// The usual cleanups from the above two, see if you can spot the difference.

 cyoa-choice: cardJ1-search ($ Thegroup .. "  encontra uma pequena quantidade de municao." $) cyoa-text! ammosearch1 goodfx ;

// Below is still a cyoa-choice because it's a result with its own window. cicon isn't necessary because it's built into cyoa-choice: if a character was picked before. If no character is picked by your button press, it usually shows cyoaicon.

cyoa-choice: cardJ1-picked
	"Arco iris da leitura" cyoa-title!
	
	($ cname .. " ' encontra e le algumas revistas antigas de armas. Isso de alguma forma os torna um atirador melhor, voce nao questiona isso. "  $) cyoa-text! goodfx

	shootingREV shooting++
;

// The old method involving 3 words to do this was replaced with a way more efficient thing... ' <cyoa-choice> cyoa-team-choice .. search for cyoa-team-choice in cyoa.df

cyoa-choice: cardJ1-one "Quem voce deve trazer?" cyoa-text! 
	' cardJ1-picked cyoa-team-choice 
;

: cardJ1-spray 40 %chance if .shooting++ else team-attacked then ;

// cyoa-bad-dialog and $cyoa-ok-dialog are pretty much obsolete now, just use cyoa-text! most of the time along with badfx, goodfx, bitefx, whatever else we add.

cyoa-choice: cardJ1-group "Alguem do grupo confunde um manequim com um zumbi e abre fogo. Durante a confusao todo o grupo abre fogo. Foi completamente caotico. No final nao havia mais manequins de pe. " cyoa-text! badfx
	
	// Note syntax: ' <single word, make shortcut if more> foreach-team-char
	
	' cardJ1-spray foreach-team-char 
	someone team-attacked 
;

cyoa: cardJ1-test
	"Compre de forma inteligente" cyoa-title!
	0 to cyoaicon cyoaicon
	"'Voce encontra uma loja de artigos esportivos. Parece ter sido saqueada mas ainda ha uma chance de que algo tenha passado despercebido." cyoa-text! basic-tone
		
	choice( "Procure municao rapidamente" )choice: cardJ1-search
	
// Changed choice( below to charchoice( , which shows a picture of their face and has the choosechar stuff after built in. Compare to your old version. Also, with charchoice showing their name is automatic, any text you put in the "" is added after.
	
	solo if
		soloer charchoice( "  busca solo" )choice: cardJ1-picked
	then;
	choice( "Leve todos" )choice: cardJ1-group
	choice( "Leve uma outra pessoa" )choice: cardJ1-one
;

// END Shop Smart


// Uncomment the below as you want, to overwrite the hotkeys from cyoa-example-7types

// ' cardJ1-test 1 hotkey!
// ' cardJ2-test 2 hotkey!
// ' cardJ7-test 3 hotkey!
' cardJ4-test 4 hotkey!
// ' goodfx 5 hotkey!
// ' bitefx 6 hotkey!
// ' goodfx 7 hotkey!
// ' card-deleteme 8 hotkey!
' goodfx 9 hotkey!

} }



uses base.df
uses charfilt.df
uses charstats.df
uses road.df

cyoa{ road{

// Dentist

// Slightly changed the structure of all below to the new stuff. There's cyoa-choice: to start a choice instead of just doing : , and cyoa: for starting off a cyoa. Both do a lot of little shortcut things, like you no longer need to necessarily use cicon more than once, and you no longer need to use cyoa-start.

// Note that words you make to check things or add optional text to the main text of a new window do not use cyoa-choice. Use one cyoa-choice per "finestra".

// Also there's words for pronouns that change based on the correct grammar of the situation. che - picks the chosen character and then uses "lui" or "lei", depending. 
// Search for "xhis" in cyoa.df for a list


: cardJ4-mcheck 
	medicalroll if
		($ cname .. "  sapevano davvero cosa stavano facendo. Utilizzando gli strumenti e le forniture mediche disponibili, " .. che .. "  riesce a estrarre il dente in modo pulito e con relativamente poco dolore. " $) cyoa-text! goodfx
		medical++ 
	then;
	
		($ cname .. "  ha finito per avere un sacco di problemi. Lestrazione del dente e stata disordinata, goffa e dolorosa. A peggiorare le cose, " .. che .. "  ha estratto il dente sbagliato e ha dovuto rifarlo.  " $) cyoa-text! bitefx ouch--
		
	// Added an extra line below for dying from tooth extraction.

	dead? if
		+cr ($ "Dopo aver estratto il secondo dente " .. cname .. " fu sentito esclamare " .. "SI VAI A FANCULO QUESTO RUMORE" pbold .. "  e poi " .. che .. "  e morto spontaneamente." $) +text
	then
	
;

cyoa-choice: cardJ4-dentist cardJ4-mcheck medicalREV ;

// Expanded this to show that you can split up the text. cyoa-text! for main bodies of text at the top of the window, +text to add new lines below the main text, +cr to create a line break for spacing. Also did this in the above medical check, to a lesser extent.

: cardJ4-mecheck 
	mechanicroll if
		 ($ "Usando la leva aggiunta, " .. che .. "  sbattere la portiera della macchina, estraendo nettamente il dente." $) +text goodfx
		mechanic++ 
	then;
	
		($ "Impreparato per la forza improvvisa di sbattere la porta, " .. che .. "  viene tirato verso la macchina e colpisce " .. chis .. "  testa contro la porta. " $) +text bitefx ouch--

// Death flavor text added.
		
	dead? if
		+cr ($ "Questo scenario esatto era in realta un modo piuttosto comune di morire prima dellapocalisse zombie. Questi incidenti sono stati tenuti fuori dalle notizie a causa di " .. "GRANDI SOCIETA AUTOMOBILISTICHE" rbold .. "." $) +text
	then
;

cyoa-choice: cardJ4-mechanical
	($ cname .. ", usando vecchi cartoni animati come ispirazione, decide di legare " .. chis .. "  dente alla portiera della macchina." $) cyoa-text! 
		+cr cardJ4-mecheck mechanicREV
;

: cardJ4-scheck 
	cpet? if
		($ cname .. "  cerca di sbarazzarsi del dente rotto masticando una roccia. Cadono alcuni denti e il dolore e straziante. Ma almeno anche il dente cattivo e caduto." $) cyoa-text! badfx
		nonfatal if
			ouch--
		then morale3- attitude--
	then;
	strengthroll if
		($ cname .. "  afferra un paio di pinze e va in citta, tirando il dente il piu forte possibile. Usando nientaltro che volonta per combattere il dolore " .. che .. " estrae il dente rapidamente e con sorprendentemente poca fatica." $) cyoa-text! goodfx
		strength++ 
	then;
		($ cname .. " " $) cyoa-text! bitefx ouch2-

// Added extra commentary from dying from plier surgery.		
		
		dead? if
			+cr ($ "In qualche modo " .. cname .. " muore per questo." $) +text
		then
		
;

cyoa-choice: cardJ4-force cardJ4-scheck strengthREV ;

cyoa: cardJ4-test
	"Ma Hooth" cyoa-title!
	choosehuman cicon
	($ cname .. "  sta avendo un dolore ai denti quasi invalidante. Senza accesso agli strumenti di precisione necessari lunica opzione e estrarre il dente " .. cHe .. "  valuta le opzioni correnti." $) cyoa-text! basic-tone
	
		choice( "Forzalo fuori!" )choice: cardJ4-force
		cnotpet? if
			choice( "Ero un dentista!" )choice: cardJ4-dentist
			choice( "Lho visto una volta in un cartone animato!" )choice: cardJ4-mechanical
		then
;

// END of Dentist

// Radio Repair

// Not many changes here except cleaning up the words. cyoa-text-cr replaced with +cr, cyoa-text-append replaced with +text, cyoa: added and the words it makes obsolete taken out.

// someone .colourname was also replaced with "somename" and soloer .colourname was replaced with "soloname", search for either in cyoa.df

: cardJ2-jerk ($ cname .. "  Rimuove il lettore CD e lo getta fuori dal finestrino." $) +text
	temperREV morale++
;
: cardJ2-pumped ($ cname .. "  Prende a pugni il lettore CD, fermando la musica." $) +text
	strengthREV	strength++ morale++
;
cyoa: cardJ2-test ($ someone .colourname .. "  si innervosisce quando il lettore CD in macchina si inceppa, riproducendo sempre la stessa canzone." $) cyoa-text! +cr 1 cyoa-image-icon! 4 500 tone 

	strongest-pass
	if strongest choosechar cardJ2-pumped then;

	most-ahole
	if choosechar cardJ2-jerk then;

	solo if ($ soloname .. "  abbassa il volume." $) +text then;

	"Il gruppo decide di spegnerlo per ora e poi provare a sistemarlo piu tardi." +text
;

// END Radio Repair

// Shop Smart

// The usual cleanups from the above two, see if you can spot the difference.

 cyoa-choice: cardJ1-search ($ Thegroup .. " trova una piccola quantita di munizioni." $) cyoa-text! ammosearch1 goodfx ;

// Below is still a cyoa-choice because it's a result with its own window. cicon isn't necessary because it's built into cyoa-choice: if a character was picked before. If no character is picked by your button press, it usually shows cyoaicon.

cyoa-choice: cardJ1-picked
	"Arcobaleno di Lettura" cyoa-title!
	
	($ cname .. " ' trova e legge vecchie riviste sulle armi. Questo in qualche modo li rende un tiratore migliore, non lo metti in dubbio. "  $) cyoa-text! goodfx

	shootingREV shooting++
;

// The old method involving 3 words to do this was replaced with a way more efficient thing... ' <cyoa-choice> cyoa-team-choice .. search for cyoa-team-choice in cyoa.df

cyoa-choice: cardJ1-one "Chi dovresti portare?" cyoa-text! 
	' cardJ1-picked cyoa-team-choice 
;

: cardJ1-spray 40 %chance if .shooting++ else team-attacked then ;

// cyoa-bad-dialog and $cyoa-ok-dialog are pretty much obsolete now, just use cyoa-text! most of the time along with badfx, goodfx, bitefx, whatever else we add.

cyoa-choice: cardJ1-group "Qualcuno del gruppo scambia un manichino per uno zombi e apre il fuoco. Durante la confusione lintero gruppo apre il fuoco. Era completamente caotico. Alla fine non cerano piu manichini in piedi. " cyoa-text! badfx
	
	// Note syntax: ' <single word, make shortcut if more> foreach-team-char
	
	' cardJ1-spray foreach-team-char 
	someone team-attacked 
;

cyoa: cardJ1-test
	"Acquista in modo intelligente" cyoa-title!
	0 to cyoaicon cyoaicon
	"'Trovi un negozio di articoli sportivi. Sembra che sia gia stato saccheggiato ma c'e ancora la possibilita che qualcosa sia stato perso." cyoa-text! basic-tone
		
	choice( "Cerca rapidamente le munizioni" )choice: cardJ1-search
	
// Changed choice( below to charchoice( , which shows a picture of their face and has the choosechar stuff after built in. Compare to your old version. Also, with charchoice showing their name is automatic, any text you put in the "" is added after.
	
	solo if
		soloer charchoice( "  saccheggio da solo" )choice: cardJ1-picked
	then;
	choice( "Prendi tutti" )choice: cardJ1-group
	choice( "Prendi unaltra persona" )choice: cardJ1-one
;

// END Shop Smart


// Uncomment the below as you want, to overwrite the hotkeys from cyoa-example-7types

// ' cardJ1-test 1 hotkey!
// ' cardJ2-test 2 hotkey!
// ' cardJ7-test 3 hotkey!
' cardJ4-test 4 hotkey!
// ' goodfx 5 hotkey!
// ' bitefx 6 hotkey!
// ' goodfx 7 hotkey!
// ' card-deleteme 8 hotkey!
' goodfx 9 hotkey!

} }



uses base.df
uses charfilt.df
uses charstats.df
uses road.df

cyoa{ road{

// Dentist

// Slightly changed the structure of all below to the new stuff. There's cyoa-choice: to start a choice instead of just doing : , and cyoa: for starting off a cyoa. Both do a lot of little shortcut things, like you no longer need to necessarily use cicon more than once, and you no longer need to use cyoa-start.

// Note that words you make to check things or add optional text to the main text of a new window do not use cyoa-choice. Use one cyoa-choice per "janela".

// Also there's words for pronouns that change based on the correct grammar of the situation. che - picks the chosen character and then uses "ele" or "ela", depending. 
// Search for "xhis" in cyoa.df for a list


: cardJ4-mcheck 
	medicalroll if
		($ cname .. " realmente sabiamos o que estamos fazendo. Usando as ferramentas e suprimentos medicos disponiveis, " .. che .. " consegue tirar a dente limpamente e com relativamente pouca dor. " $) cyoa-text! goodfx
		medical++ 
	then;
	
		($ cname .. " acabou tendo muita dificuldade. A extracao de dente foi suja, desajeitada e dolorosa. Para piorar, " .. che .. " tirou a dente errada e teve que fazer novamente. " $) cyoa-text! bitefx ouch--
		
	// Added an extra line below for dying from tooth extraction.

	dead? if
		+cr ($ "Depois de tirar a segunda dente " .. cname .. " foi ouvido gritar " .. "ESSA MERDA QUEBRA" pbold .. " e depois " .. che .. " morreu repentinamente." $) +text
	then
	
;

cyoa-choice: cardJ4-dentist cardJ4-mcheck medicalREV ;

// Expanded this to show that you can split up the text. cyoa-text! for main bodies of text at the top of the window, +text to add new lines below the main text, +cr to create a line break for spacing. Also did this in the above medical check, to a lesser extent.

: cardJ4-mecheck 
	mechanicroll if
		 ($ "Usando a forca adicionada, " .. che .. " fecha a porta do carro, tirando limpamente o dente." $) +text goodfx
		mechanic++ 
	then;
	
		($ "Despreparado para a forca subita de bater a porta " .. che .. "  e puxado em direcao ao carro e bate " .. chis .. " " $) +text bitefx ouch--

// Death flavor text added.
		
	dead? if
		+cr ($ "Este cenario exato era na verdade uma maneira muito comum de morrer antes do apocalipse zumbi. Esses acidentes foram mantidos fora das noticias devido a " .. "BIG AUTO CORPORATIONS" rbold .. "." $) +text
	then
;

cyoa-choice: cardJ4-mechanical
	($ cname .. ", usando programas de desenho animado antigos como inspiracao, decide amarrar " .. chis .. " 'dente na porta do carro'" $) cyoa-text! 
		+cr cardJ4-mecheck mechanicREV
;

: cardJ4-scheck 
	cpet? if
		($ cname .. "  Tenta se livrar do dente podre mordendo uma pedra. Alguns dentes caem, e a dor e excruciante. Mas pelo menos o dente ruim caiu tambem." $) cyoa-text! badfx
		nonfatal if
			ouch--
		then morale3- attitude--
	then;
	strengthroll if
		($ cname .. "  pega um alicate e vai para a cidade, puxando o dente com toda a forca possivel. Usando nada alem de vontade para lutar contra a dor, " .. che .. "  tira o dente rapidamente e com surpreendentemente pouco problema." $) cyoa-text! goodfx
		strength++ 
	then;
		($ cname .. " ' pega um alicate e vai para a cidade. A extracao nao vai bem. Depois de muita dor e sangue o dente finalmente sai." $) cyoa-text! bitefx ouch2-

// Added extra commentary from dying from plier surgery.		
		
		dead? if
			+cr ($ "De alguma forma " .. cname .. "  morre com isso." $) +text
		then
		
;

cyoa-choice: cardJ4-force cardJ4-scheck strengthREV ;

cyoa: cardJ4-test
	"Ma Hooth" cyoa-title!
	choosehuman cicon
	($ cname .. " esta sofrendo dor quase desmobilizante na dente. Sem acesso as ferramentas precisas a unica opcao e tirar o dente. " .. cHe .. "  pondera as opcoes atuais." $) cyoa-text! basic-tone
	
		choice( "Forceo para fora!" )choice: cardJ4-force
		cnotpet? if
			choice( "Eu costumava ser dentista!" )choice: cardJ4-dentist
			choice( "Eu vi isso uma vez em um desenho animado!" )choice: cardJ4-mechanical
		then
;

// END of Dentist

// Radio Repair

// Not many changes here except cleaning up the words. cyoa-text-cr replaced with +cr, cyoa-text-append replaced with +text, cyoa: added and the words it makes obsolete taken out.

// someone .colourname was also replaced with "alguemchamada" and soloer .colourname was replaced with "algumasenha", search for either in cyoa.df

: cardJ2-jerk ($ cname .. " Retira o tocadiscos e o joga fora da janela." $) +text
	temperREV morale++
;
: cardJ2-pumped ($ cname .. " Acerta no tocadiscos, parando a musica." $) +text
	strengthREV	strength++ morale++
;
cyoa: cardJ2-test ($ someone .colourname .. " se enoja quando o tocadiscos do carro fica preso, tocando a mesma musica repetidamente." $) cyoa-text! +cr 1 cyoa-image-icon! 4 500 tone 

	strongest-pass
	if strongest choosechar cardJ2-pumped then;

	most-ahole
	if choosechar cardJ2-jerk then;

	solo if ($ soloname .. " desliga o volume." $) +text then;

	"O grupo decide desligalo por enquanto e tentar corrigir depois." +text
;

// END Radio Repair

// Shop Smart

// The usual cleanups from the above two, see if you can spot the difference.

 cyoa-choice: cardJ1-search ($ Thegroup .. "  encontra uma pequena quantidade de municao." $) cyoa-text! ammosearch1 goodfx ;

// Below is still a cyoa-choice because it's a result with its own window. cicon isn't necessary because it's built into cyoa-choice: if a character was picked before. If no character is picked by your button press, it usually shows cyoaicon.

cyoa-choice: cardJ1-picked
	"Leitura Chuva de Beleza" cyoa-title!
	
	($ cname .. " encontra e le algumas velhas revistas de armas. Isso o torna um tiro mais eficaz, nao questiona isso. "  $) cyoa-text! goodfx

	shootingREV shooting++
;

// The old method involving 3 words to do this was replaced with a way more efficient thing... ' <cyoa-choice> cyoa-team-choice .. search for cyoa-team-choice in cyoa.df

cyoa-choice: cardJ1-one "Quem voce deve trazer?" cyoa-text! 
	' cardJ1-picked cyoa-team-choice 
;

: cardJ1-spray 40 %chance if .shooting++ else team-attacked then ;

// cyoa-bad-dialog and $cyoa-ok-dialog are pretty much obsolete now, just use cyoa-text! most of the time along with badfx, goodfx, bitefx, whatever else we add.

cyoa-choice: cardJ1-group "0.0 " cyoa-text! badfx
	
	// Note syntax: ' <single word, make shortcut if more> foreach-team-char
	
	' cardJ1-spray foreach-team-char 
	someone team-attacked 
;

cyoa: cardJ1-test
	"Compre Inteligente" cyoa-title!
	0 to cyoaicon cyoaicon
	"Voce encontra uma loja de artigos esportivos. Parece que ja foi saqueada mas ainda ha uma chance de que algo tenha sido esquecido." cyoa-text! basic-tone
		
	choice( "'Rapidamente procurar por municao'" )choice: cardJ1-search
	
// Changed choice( below to charchoice( , which shows a picture of their face and has the choosechar stuff after built in. Compare to your old version. Also, with charchoice showing their name is automatic, any text you put in the "" is added after.
	
	solo if
		soloer charchoice( " coleta de solo" )choice: cardJ1-picked
	then;
	choice( "Pegar todo mundo" )choice: cardJ1-group
	choice( "'Pegue uma outra pessoa'" )choice: cardJ1-one
;

// END Shop Smart


// Uncomment the below as you want, to overwrite the hotkeys from cyoa-example-7types

// ' cardJ1-test 1 hotkey!
// ' cardJ2-test 2 hotkey!
// ' cardJ7-test 3 hotkey!
' cardJ4-test 4 hotkey!
// ' goodfx 5 hotkey!
// ' bitefx 6 hotkey!
// ' goodfx 7 hotkey!
// ' card-deleteme 8 hotkey!
' goodfx 9 hotkey!

} }



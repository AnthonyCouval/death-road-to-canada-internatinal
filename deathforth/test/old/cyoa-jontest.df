uses base.df
uses charfilt.df
uses charstats.df
uses road.df

cyoa{ road{

// Dentist

// Slightly changed the structure of all below to the new stuff. There's cyoa-choice: to start a choice instead of just doing : , and cyoa: for starting off a cyoa. Both do a lot of little shortcut things, like you no longer need to necessarily use cicon more than once, and you no longer need to use cyoa-start.

// Note that words you make to check things or add optional text to the main text of a new window do not use cyoa-choice. Use one cyoa-choice per "ventana".

// Also there's words for pronouns that change based on the correct grammar of the situation. che - picks the chosen character and then uses "el" or "ella", depending. 
// Search for "xhis" in cyoa.df for a list


: cardJ4-mcheck 
	medicalroll if
		($ cname .. "  realmente sabian lo que estaban haciendo. Utilizando las herramientas y suministros medicos a su disposicion, " .. che .. "  logra sacar la muela limpiamente y con poca dolor. " $) cyoa-text! goodfx
		medical++ 
	then;
	
		($ cname .. "  acabo teniendo muchos problemas. La extraccion de dientes fue desordenada, mala y dolorosa. Para empeorar, " .. che .. "  saco la muela equivocada y tuvo que hacerlo de nuevo.  " $) cyoa-text! bitefx ouch--
		
	// Added an extra line below for dying from tooth extraction.

	dead? if
		+cr ($ "Despues de sacar la segunda muela " .. cname .. "  se escucho gritar " .. "EH ESTA MAL DE OIDOS" pbold .. "  y luego " .. che .. "  murio de forma espontanea." $) +text
	then
	
;

cyoa-choice: cardJ4-dentist cardJ4-mcheck medicalREV ;

// Expanded this to show that you can split up the text. cyoa-text! for main bodies of text at the top of the window, +text to add new lines below the main text, +cr to create a line break for spacing. Also did this in the above medical check, to a lesser extent.

: cardJ4-mecheck 
	mechanicroll if
		 ($ "Usando la fuerza adicional, " .. che .. "  cierra la puerta del coche, sacando la muela limpiamente." $) +text goodfx
		mechanic++ 
	then;
	
		($ "No preparado para la fuerza repentina de golpear la puerta, " .. che .. "  es tirado hacia el auto y golpea " .. chis .. "  la cabeza en la puerta. " $) +text bitefx ouch--

// Death flavor text added.
		
	dead? if
		+cr ($ "Este escenario exacto fue en realidad una forma bastante comun de morir antes del apocalipsis zombie. Estos accidentes se mantuvieron fuera de las noticias debido a " .. "GRANDES CORPORACIONES AUTOMOTRICES" rbold .. "." $) +text
	then
;

cyoa-choice: cardJ4-mechanical
	($ cname .. ", utilizando viejos dibujos animados como inspiracion, decide atar " .. chis .. "  diente a la puerta del auto." $) cyoa-text! 
		+cr cardJ4-mecheck mechanicREV
;

: cardJ4-scheck 
	cpet? if
		($ cname .. "  intenta deshacerse del diente roto mordiendo una roca. Se le caen algunos dientes y el dolor es insoportable. Pero al menos tambien se le cayo el diente malo." $) cyoa-text! badfx
		nonfatal if
			ouch--
		then morale3- attitude--
	then;
	strengthroll if
		($ cname .. "  toma un par de alicates y se va a la ciudad, tirando del diente tan fuerte como puede. Usando nada mas que voluntad para luchar contra el dolor, " .. che .. "  saca el diente rapidamente y con sorprendentemente pocos problemas." $) cyoa-text! goodfx
		strength++ 
	then;
		($ cname .. " ' agarra un par de alicates y se va a la ciudad. La extraccion no va bien. Despues de mucho dolor y sangre el diente finalmente sale." $) cyoa-text! bitefx ouch2-

// Added extra commentary from dying from plier surgery.		
		
		dead? if
			+cr ($ "De alguna manera " .. cname .. "  muere por esto." $) +text
		then
		
;

cyoa-choice: cardJ4-force cardJ4-scheck strengthREV ;

cyoa: cardJ4-test
	"Ma Hooth" cyoa-title!
	choosehuman cicon
	($ cname .. "  esta teniendo un dolor de muelas casi paralizante. Sin acceso a las herramientas de precision necesarias la unica opcion es extraer el diente " .. cHe .. "  sopesa las opciones actuales." $) cyoa-text! basic-tone
	
		choice( "Fuera a la fuerza!" )choice: cardJ4-force
		cnotpet? if
			choice( "Yo solia ser dentista!" )choice: cardJ4-dentist
			choice( "Vi esto una vez en una caricatura!" )choice: cardJ4-mechanical
		then
;

// END of Dentist

// Radio Repair

// Not many changes here except cleaning up the words. cyoa-text-cr replaced with +cr, cyoa-text-append replaced with +text, cyoa: added and the words it makes obsolete taken out.

// someone .colourname was also replaced with "algun nombre" and soloer .colourname was replaced with "nombre del sol", search for either in cyoa.df

: cardJ2-jerk ($ cname .. "  Retira el reproductor de CD y lo arroja por la ventana." $) +text
	temperREV morale++
;
: cardJ2-pumped ($ cname .. "  Golpea el reproductor de CD y detiene la musica." $) +text
	strengthREV	strength++ morale++
;
cyoa: cardJ2-test ($ someone .colourname .. "  se frustra cuando el reproductor de CD del automovil se atasca y reproduce la misma cancion una y otra vez." $) cyoa-text! +cr 1 cyoa-image-icon! 4 500 tone 

	strongest-pass
	if strongest choosechar cardJ2-pumped then;

	most-ahole
	if choosechar cardJ2-jerk then;

	solo if ($ soloname .. "  apaga el volumen." $) +text then;

	"El grupo decide apagarlo por ahora y luego intentar arreglarlo." +text
;

// END Radio Repair

// Shop Smart

// The usual cleanups from the above two, see if you can spot the difference.

 cyoa-choice: cardJ1-search ($ Thegroup .. "  encuentra una pequena cantidad de municion." $) cyoa-text! ammosearch1 goodfx ;

// Below is still a cyoa-choice because it's a result with its own window. cicon isn't necessary because it's built into cyoa-choice: if a character was picked before. If no character is picked by your button press, it usually shows cyoaicon.

cyoa-choice: cardJ1-picked
	"Arco iris de lectura" cyoa-title!
	
	($ cname .. " ' encuentra y lee algunas revistas viejas de armas. Esto de alguna manera los convierte en mejores tiradores, no lo cuestionas. "  $) cyoa-text! goodfx

	shootingREV shooting++
;

// The old method involving 3 words to do this was replaced with a way more efficient thing... ' <cyoa-choice> cyoa-team-choice .. search for cyoa-team-choice in cyoa.df

cyoa-choice: cardJ1-one "A quien deberias llevar?" cyoa-text! 
	' cardJ1-picked cyoa-team-choice 
;

: cardJ1-spray 40 %chance if .shooting++ else team-attacked then ;

// cyoa-bad-dialog and $cyoa-ok-dialog are pretty much obsolete now, just use cyoa-text! most of the time along with badfx, goodfx, bitefx, whatever else we add.

cyoa-choice: cardJ1-group "Alguien del grupo confunde un maniqui con un zombi y abre fuego. Durante la confusion todo el grupo abre fuego. Fue completamente caotico. Al final no quedaron maniquies en pie. " cyoa-text! badfx
	
	// Note syntax: ' <single word, make shortcut if more> foreach-team-char
	
	' cardJ1-spray foreach-team-char 
	someone team-attacked 
;

cyoa: cardJ1-test
	"Compra inteligente" cyoa-title!
	0 to cyoaicon cyoaicon
	"'Encuentras una tienda de articulos deportivos. Parece haber sido saqueada pero aun existe la posibilidad de que se haya pasado algo por alto." cyoa-text! basic-tone
		
	choice( "Busca municion rapidamente" )choice: cardJ1-search
	
// Changed choice( below to charchoice( , which shows a picture of their face and has the choosechar stuff after built in. Compare to your old version. Also, with charchoice showing their name is automatic, any text you put in the "" is added after.
	
	solo if
		soloer charchoice( "  saqueo en solitario" )choice: cardJ1-picked
	then;
	choice( "Lleva a todos" )choice: cardJ1-group
	choice( "Lleva a otra persona" )choice: cardJ1-one
;

// END Shop Smart


// Uncomment the below as you want, to overwrite the hotkeys from cyoa-example-7types

// ' cardJ1-test 1 hotkey!
// ' cardJ2-test 2 hotkey!
// ' cardJ7-test 3 hotkey!
' cardJ4-test 4 hotkey!
// ' goodfx 5 hotkey!
// ' bitefx 6 hotkey!
// ' goodfx 7 hotkey!
// ' card-deleteme 8 hotkey!
' goodfx 9 hotkey!

} }



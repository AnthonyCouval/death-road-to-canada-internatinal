
// Called once for each character
: some-bullcrap
	blurbslot 0= if // Conversation starter, then everyone in party responds
		"Esto es una MIERDA." blurbsay
	else
		rnd5050 if
			blurbslot 2 < if ($ "Estoy de acuerdo, " .. 1st-char .name@ .. "." $) blurbsay
			then
		else
			STAT_MORALE me .stat--
		then
	then
;

: turn-back // Random first person, random single responder, stuff happens.
	blurbslot 0= if
		"Maybe we should turn back..." blurbsay
	else
		blurbslot 2 < if
			"Quizas deberias callarte!" blurbsay
			STAT_MORALE me .stat++
			STAT_MORALE 1st-char .stat--
		then
	then
;

: legend-bodybuild
	blurbslot 0= if
		"Una vez vi a un culturista lanzar un auto a unos zombis!" blurbsay
	else
		blurbslot 2 < if
			rnd5050 if
				($ "Eso no tiene sentido, " .. 1st-char .name@ .. "." $) blurbsay
				"'Eres un bicho raro!" blurbsay
				STAT_MORALE 1st-char .stat--
		else
			"Oh GENIAL!" blurbsay
			STAT_MORALE me .stat++
		then then
	then
;

: whinetopic 1 6 rnd case
	1 of "zombies" endof
	2 of "estar sucio" endof
	3 of "el clima" endof
	4 of "bichos" endof
	5 of "como todo el mundo es un idiota" endof
	6 of ($ me .name@ .. "'el padre de" $) endof
	endcase ;

: whiner // Picks two other people, lowers their morale
	blurbslot 0= if
		qmsg ($ me .name@ .. " se queja incesantemente de " .. whinetopic .. "." $) txt
	else
		blurbslot 3 < if
			STAT_MORALE me .stat--
		then
	then
;

: brooding
	blurbslot 0= if
		qmsg ($ me .name@ .. " broods about a tragic past....." $) txt
	else
		blurbslot 2 < if
			rnd5050 if
			qmsg ($ me .name@ .. " pone los ojos en blanco." $) txt then
		then
	then
;

: smalltopic 1 6 rnd case
	1 of "una vieja mascota" endof
	2 of "Canada" endof
	3 of "el clima" endof
	4 of "dinosaurios" endof
	5 of "el siguiente paso del plan" endof
	6 of "algo gracioso en el pasado" endof
	endcase ;

: smalltalk // small chance to raise morale for each member of group
	blurbslot 0= if
		qmsg ($ me .name@ .. " habla de " .. smalltopic .. "." $) txt
	else
		15 %chance if
			STAT_MORALE me .stat++
		then
	then
;

: civnoun 1 11 rnd case
	1 of "Twitter" endof
	2 of "comida rapida" endof
	3 of "email" endof
	4 of "selfies" endof
	5 of "noticias de celebridades" endof
	6 of "videos virales" endof
	7 of "streamers de videojuegos" endof
	8 of "el concepto de higiene" endof
	9 of "fanfiction crudamente ilustrado" endof
	10 of "tablones de imagenes" endof
	11 of "creepypasta" endof
	endcase ;

	: one-line1 "Todo estara bien si puedo llegar a Canada." ;
	: one-line2 "Los zombis no son problema. Bueno, tal vez un pequeno problema." ;
	: one-line3 "Rooaaaad triiiiip." ;
	: one-line4 "Todo huele muy diferente a como lo hacia antes de que el jabon se volviera escaso." ;
	: one-line5 "Hope I don't run into any more bandits..." ;
	: one-line6 ($ "Con la civilizacion desaparecida, sobre todo echo de menos " .. civnoun .. "." $) ;
	: one-line7 "If only we knew about throwing chairs when the zombies first showed up..." ;
	: one-line8 "I wonder if I could get strong enough to just throw the car at zombies..." ;
	: one-line9 "Ya llegamos?" ;
	: one-line10 "Let's hope the car doesn't break down..." ;

	deck-new
		card: one-line1
		card: one-line2
		card: one-line3
		card: one-line4
		card: one-line5
		card: one-line6
		card: one-line7
		card: one-line8
		card: one-line9
		card: one-line10
	constant ONE-LINERS
	ONE-LINERS var one-liners

: one-liner // One person says something, no reactions, pulled from a deck
	one-liners depths 0= if ONE-LINERS shuffles to one-liners then
	blurbslot 0= if
		one-liners drawcard blurbsay
	then
;

	deck-new
		card: some-bullcrap
		card: turn-back
		card: legend-bodybuild
		card: whiner
		card: brooding
		card: smalltalk 
		10 cards: one-liner

	constant BLURB-DECK
	BLURB-DECK var blurb-deck
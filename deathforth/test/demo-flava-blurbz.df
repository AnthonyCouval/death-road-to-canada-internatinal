
// Called once for each character
: some-bullcrap
	blurbslot 0= if // Conversation starter, then everyone in party responds
		"Ceci est de la bouille." blurbsay
	else
		rnd5050 if
			blurbslot 2 < if ($ "Je suis d'accord. " .. 1st-char .name@ .. "." $) blurbsay
			then
		else
			STAT_MORALE me .stat--
		then
	then
;

: turn-back // Random first person, random single responder, stuff happens.
	blurbslot 0= if
		"Maybe we should turn back..." blurbsay
	else
		blurbslot 2 < if
			"Peut-etre devrais-tu taire un peu !" blurbsay
			STAT_MORALE me .stat++
			STAT_MORALE 1st-char .stat--
		then
	then
;

: legend-bodybuild
	blurbslot 0= if
		"J'ai vu une fois un bodybuilder jeter une voiture sur certains zombies !" blurbsay
	else
		blurbslot 2 < if
			rnd5050 if
				($ "Ceci n'a aucun sens. " .. 1st-char .name@ .. "." $) blurbsay
				"Tu es un etrange !" blurbsay
				STAT_MORALE 1st-char .stat--
		else
			"Tres joli !" blurbsay
			STAT_MORALE me .stat++
		then then
	then
;

: whinetopic 1 6 rnd case
	1 of "zombies : creatures vivantes" endof
	2 of "s sales" endof
	3 of "le temps meteorologique." endof
	4 of "insectes de" endof
	5 of "est peur des poulets." endof
	6 of ($ me .name@ .. "pere de '" $) endof
	endcase ;

: whiner // Picks two other people, lowers their morale
	blurbslot 0= if
		qmsg ($ me .name@ .. " gemisse sans cesse a propos de ... " .. whinetopic .. "." $) txt
	else
		blurbslot 3 < if
			STAT_MORALE me .stat--
		then
	then
;

: brooding
	blurbslot 0= if
		qmsg ($ me .name@ .. " broods about a tragic past....." $) txt
	else
		blurbslot 2 < if
			rnd5050 if
			qmsg ($ me .name@ .. " roule des yeux avec beaucoup de gravite." $) txt then
		then
	then
;

: smalltopic 1 6 rnd case
	1 of "un animal d'accompagnement ancien." endof
	2 of "Le canada" endof
	3 of "le temps meteorologique." endof
	4 of "les dinosaures" endof
	5 of "le prochain pas dans le plan :" endof
	6 of "quelque chose amusant du passe." endof
	endcase ;

: smalltalk // small chance to raise morale for each member of group
	blurbslot 0= if
		qmsg ($ me .name@ .. " parle de " .. smalltopic .. "." $) txt
	else
		15 %chance if
			STAT_MORALE me .stat++
		then
	then
;

: civnoun 1 11 rnd case
	1 of "Twitter :" endof
	2 of "rapide service alimentaire" endof
	3 of "courriel" endof
	4 of "selfies sont des photos que l'" endof
	5 of "des informations sur la derniere actualite de celebrites." endof
	6 of "videos virales" endof
	7 of "streamers de jeux video" endof
	8 of "la proprete corporelle ou personnelle, ainsi que l'entretien des lieux et des objets," endof
	9 of "histoire crudement illustree : fanfiction" endof
	10 of "tableaux d'images heberges sur internet, t" endof
	11 of "un creepypasta est une histoire ou une image inqu" endof
	endcase ;

	: one-line1 "Tout ira bien si je peux atteindre le canada." ;
	: one-line2 "Des zombies ne sont pas une grosse chose. bien sur, peut-etre un petit probleme." ;
	: one-line3 "Rooaaaad triiiiip." ;
	: one-line4 "Tout ca sent beaucoup differemment qu'avant que le savon soit rare." ;
	: one-line5 "Hope I don't run into any more bandits..." ;
	: one-line6 ($ "Dans la ruine de la civilisation, ce que je manque le plus est principalement cela : " .. civnoun .. "." $) ;
	: one-line7 "If only we knew about throwing chairs when the zombies first showed up..." ;
	: one-line8 "I wonder if I could get strong enough to just throw the car at zombies..." ;
	: one-line9 "Somme-nous la deja ?" ;
	: one-line10 "Let's hope the car doesn't break down..." ;

	deck-new
		card: one-line1
		card: one-line2
		card: one-line3
		card: one-line4
		card: one-line5
		card: one-line6
		card: one-line7
		card: one-line8
		card: one-line9
		card: one-line10
	constant ONE-LINERS
	ONE-LINERS var one-liners

: one-liner // One person says something, no reactions, pulled from a deck
	one-liners depths 0= if ONE-LINERS shuffles to one-liners then
	blurbslot 0= if
		one-liners drawcard blurbsay
	then
;

	deck-new
		card: some-bullcrap
		card: turn-back
		card: legend-bodybuild
		card: whiner
		card: brooding
		card: smalltalk 
		10 cards: one-liner

	constant BLURB-DECK
	BLURB-DECK var blurb-deck
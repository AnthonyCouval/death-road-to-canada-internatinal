
// EXAMPLE 2 - KING ZOMBIE

(*
@ A CYOA that says "VOCE VE O REI ZUMBI". Provides these choices:
	+ BRIBE WITH 5 FOOD
		- Only shows up as a choice if you have five food. Removes 5 food from your inventory.

	+ CALL HIM A DINGUS
		+ Picks biggest ahole = largest aggression and lowest loyalty, with each point under 3 being a +
			+ Checks that the biggest ahole qualifies as ahole? or the CYOA option doesn't show up
			+ Doesn't matter if the person's aggression and loyalty is revealed or not
		+ KING ZOMBIE IS SHOCKED BY name's INSULT AND FLEES
			+ The chosen person's aggression and loyalty is revealed if it wasn't already

	+ [CHOICENAME] CHARMS HIM
		+ Picks whoever in the group has the highest combined revealed charm and wits for the CHOICENAME, shows it in the CYOA button before you pick it.
		+ If charm>4 wit>4, says KING ZOMBIE IS VERY CHARMED and give a morale
		+ Otherwise do a roll to see if you pass, charm + wits. CHOICENAME CHARMS KING ZOMBIE, CHOICENAME FAILS TO CHARM KING ZOMBIE.
			+ Success: Do nothing. Failure: Remove a morale from NAME.

	@ TELL KING ZOMBIE TO 'COOL IT'
			+ COOL IT takes an average of everyone's violence
			+ If under 3, KING ZOMBIE THINKS YOU HAVE A POINT everyone gains a morale
			+ If over or equal to 3, KING ZOMBIE FLIPS OUT and everyone loses a point of health
				+ NAME DIES or NAME AND NAME (AND NAME etc) DIES if their health goes to 0
	- FLIP OUT ABOUT IT
		- Picks person in your group with lowest morale.
			- If under 3, [NAME] FLIPS OUT AND KING ZOMBIE TELLS HIM TO COOL IT
			- If over 2, UH NO ONE WANTS TO FLIP OUT

	- Picks four out of 5 of the above choices, at random, except
		- The ahole calls him a dingus one always shows up if it's available
		- Food bribe always shows up if it's available
*)

road{

: choice( cyoa-add-choice ($ ;
: )choice $) cyoa-choice-text! ;

: )choice: postpone )choice (') postpone cyoa-choice-handler! ; immediate

: biggest-ahole ' aholecheck pick-team-char ( -- false | chara true ) // variable results!
	ahole? 0= if false then;
	pickchar true
;

: saddest ' sadcheck pick-team-char ;

: #ahole #character 0 swap 2dup .composure! .loyalty! ;

: #charmr #character STAT_CHARM over .reveal STAT_WITS swap .reveal ;


// INSULT IT
: kz-flee
	cyoa-good-dialog

	($ "O REI ZUMBI fica totalmente chocado com " ..
		choicecharname .. "'insulto e foge."
	$) cyoa-text!

	STAT_TEMPERAMENT choicechar .reveal
	STAT_LOYALTY choicechar .reveal
 ;

: try-dingus-choice

	biggest-ahole not if exit then

	// character on stack here

	choice(
		over .name@ .. " "
	)choice: kz-flee

	choosechar
;


// FEED IT

: foodbribe
	cyoa-good-dialog
		"KING ZOMBIE aceita a contragosto LANCHES, e com um olhar vago, ele se afasta." cyoa-text!
		-5 LOOT_FOOD trunk.loot+
;

: trunk-food LOOT_FOOD trunk.loot@ ;

: try-food-choice 
	trunk-food 5 < if exit then
	choice( "Suborno com LANCHES" )choice: foodbribe
;


// CHARM IT

: charmwits-sum dup .charm@ swap .wits@ + ;

: charmwits-revealed?
	STAT_CHARM over .knownstat@ not if drop false exit then
	STAT_WITS swap .knownstat@ not if drop false exit then
	true
;


: charmwits-check checkchar
	// Don't even match if charm info isn't revealed
	checkchar charmwits-revealed? if false exit then

	// Now compare the current pick and the check
	charmwits-sum pickchar charmwits-sum > if true else false then
;

: bigcharm? dup .wits@ 4 > swap .charm@ 4 > and ;

: charm-it
	choicechar bigcharm? if
		cyoa-good-dialog
		($ " O REI ZUMBI esta muito encantado em conhecer " .. choicechar .name@
			.. "ruborizando, ele se afasta." $) cyoa-text!
		STAT_MORALE choicechar .stat++
		exit
	then

	choicechar charmwits-sum 4 skillcheck?

	if
		cyoa-good-dialog
		($ choicechar .name@ .. " encantos REI ZUMBI, que entao simplesmente congela como uma estatua. Que efeito peculiar." $)
		cyoa-text!
	then;

	cyoa-bad-dialog
	($ choicechar .name@ .. " 'Fails to charm KING ZOMBIE. Feeling awkward after the exchange, KING ZOMBIE quickly disappears around a corner.'" $)
	cyoa-text!
	STAT_MORALE choicechar .stat-- 
;

: try-charm-choice 

	' charmwits-check pick-team-char

	// Make sure picked char has revealed stats
	charmwits-revealed? not if exit then

	choice( pickchar .name@ .. " tenta ENCANTALO" )choice: charm-it

	pickchar choosechar
;

: sum-temperaments ( sum char -- sum ) .temperament@ + ;

: cool-average 0 ' sum-temperaments foreach-team-char
	dup team-count / swap
	"TEMPERAMENTO TOTAL " type type " media " type dup type cr ;

: textclr$ 0x f0 + $chr ;

(*
: team-attacked
	dup hit'n'die? if
		($ ICON_SKULL textclr$ .. over .name@ .. "  e MORTO!" ..
			0 textclr$ $) blurbevent blurbskullicon
		sfx{ creepy 30 fx .delay! }
	else
		($ ICON_DOWN textclr$ .. over .name@ .. "  esta FERIDO!" ..
			0 textclr$ $) blurbevent ICON_DOWN blurbicon
	then
	blurbchar blurb-last-to-cyoa
;
*)

: kz-cool-it
	cool-average 3 > // GOOD temperament is higher
	if
		cyoa-good-dialog
		($ "REI ZUMBI pondera sobre sua sabedoria e sente que voce tem razao."
			.. " Iluminado, ele se afasta." $) cyoa-text!
		' .morale++ foreach-team-char
	then;

	cyoa-bite-dialog
	"REI ZUMBI fica furioso com sua hipocrisia e ENLOUQUECE. Voce e atacado!"
	cyoa-text!
	' team-attacked foreach-team-char
;


// COOL IT
: try-coolit-choice
	choice( "ESFRIE" )choice: kz-cool-it
;

: kz-flip-out
	saddest dup .morale@ 3 <
	if
		cyoa-good-dialog
		($ swap .name@ " starts flipping out about this whole situation... until KING ZOMBIE raises its decrepit hand and, nonchalantly, croaks 'COOL IT'." $) cyoa-text!
	then;

	cyoa-bad-dialog
	"Ninguem realmente quer pirar com isso. Todos ficam quietos."
	cyoa-text!
;

: try-flipout-choice
	choice( "ENLOUQUECA sobre isso" )choice: kz-flip-out
;

: collect-choices
	try-food-choice
	try-dingus-choice
	try-charm-choice
	try-coolit-choice
	try-flipout-choice
;

: kzombi
	cyoa-new
		"ENCONTRO" cyoa-title!
		 1 to cyoaicon cyoaicon // The image in the CYOA dialog

		"Voce ve REI ZUMBI." cyoa-text!

		collect-choices
;

// ' kzombi 3 hotkey!

}

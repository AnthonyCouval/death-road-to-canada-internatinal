

0 value preload-region

defer clear-streak

mission{

: end-mission
	mission-summary-cyoa
;

: walk-try-end-mission
	ZOMBOTOWN if end-mission exit then
	absentees dup not if 2drop end-mission exit then

	cyoa-start
		"OH NO!" cyoa-title!

		1 > if "  sono" else "  e" then
		"  non e qui, e sara lasciato indietro." $cat $cat
		cyoa-text!

	cyoa-add-choice
		"Resta e trovali." cyoa-choice-text!

	cyoa-add-choice
		"Vai via!" cyoa-choice-text!
		' end-mission cyoa-choice-handler!
;

1 value outside-top-leave
1 value outside-bottom-leave
1 value outside-right-leave
1 value outside-left-leave

: outside-edge-leave! ( set edge ) 0 ( for room 0, outside ) room_edge_leave! ;
: outside-edge-leave@ ( edge ) 0 room_edge_leave@ ;

: mission-top-leave! 		dup >bool EDGE_TOP 		outside-edge-leave! to outside-top-leave ;
: mission-bottom-leave!		dup >bool EDGE_BOTTOM	outside-edge-leave! to outside-bottom-leave ;
: mission-right-leave! 		dup >bool EDGE_RIGHT 	outside-edge-leave! to outside-right-leave ;
: mission-left-leave! 		dup >bool EDGE_LEFT 	outside-edge-leave! to outside-left-leave ;

: mission-top-leave? 		EDGE_TOP 	outside-edge-leave@ ;
: mission-bottom-leave?		EDGE_BOTTOM	outside-edge-leave@ ;
: mission-right-leave? 		EDGE_RIGHT 	outside-edge-leave@ ;
: mission-left-leave? 		EDGE_LEFT 	outside-edge-leave@ ;

: no-leaving
	0 mission-right-leave!
	0 mission-left-leave!
	0 mission-top-leave!
	0 mission-bottom-leave!
;

: all-leaving
	1 mission-bottom-leave!
	1 mission-top-leave!
	1 mission-left-leave!
	1 mission-right-leave!
;

all-leaving

: right-leaving
	no-leaving
	1 mission-right-leave!
;

// Return the "bandiera" so we can put a CYOA in there and use that instead if we want
: edge-leave-check map{
	mission-bottom-leave? if
		player .pos.y@ pixelh 16 - > if mission-bottom-leave? then;
	then

	mission-top-leave? if
		player .pos.y@ 16 < if mission-top-leave? then;
	then

	mission-left-leave? if
		player .pos.x@ 16 < if mission-left-leave? then;
	then

	mission-right-leave? if
		player .pos.x@ pixelw 16 - > if mission-right-leave? then;	
	then

	0
} ;

: walk-lose-car-end-mission
	walk-try-end-mission
	0 vehicle.which! // Put here so the car is gone by the time it goes to this screen
	road{ ' car-lost stack-road-action }
;

: cyoa-reach-edge edge-leave-check args( edge )
	<- edge not if exit then

	// execute the edge flag if it's a word reference
	<- edge wordref? if <- edge execute then;

	cyoa-start
	"ANDARE VIA" cyoa-title!
	ZOMBOTOWN if 
		"USCIRE DA ZOMBO TOWN" cyoa-title!
		"Vuoi tornare alla schermata del titolo?"
	else
		"Vuoi tornare a piedi sulla strada?"
		
		mv@ 0 > if
			cr$ .. cr$ .. RED .. "Abbandonerai la tua macchina " .. WHITE .. "se te ne " .. RAINBOW .. "vai via" .. WHITE .. "  senza di essa!" ..
		then
	
	then cyoa-text!
	
	cyoa-add-choice
		"No, resta qui." cyoa-choice-text!

	cyoa-add-choice
		"'Si, torniamo sulla strada." cyoa-choice-text!
		mv@ 0 = if
			' walk-try-end-mission cyoa-choice-handler!
		else
			' walk-lose-car-end-mission cyoa-choice-handler!
		then
;

last to main-event-mission-edge

: door-no-escape
	cyoa-start
	beep "porta senza via di fuga" print
	"NESSUNA VIA DI FUGA" cyoa-title!
	"'Non puoi andartene da questa posizione. Dovrai trovare un altro modo per uscire." cyoa-text!
;

: door-sieged
	cyoa-start
	beep "porta assediata" print
	"NESSUNA VIA DI FUGA" cyoa-title!
	"'Fuori e troppo pericoloso in questo momento. Non puoi ancora andartene da questa posizione." cyoa-text!
;


// Leave an internal location with no_outside
: cyoa-leave-location
	cyoa-start
	"ANDARE VIA" cyoa-title!
	"Vuoi lasciare questa posizione?" cyoa-text!

	cyoa-add-choice
		"No, resta qui." cyoa-choice-text!

	cyoa-add-choice
		"'Si, andiamo." cyoa-choice-text!
		' walk-try-end-mission cyoa-choice-handler!
;

last to main-event-mission-location-leave

: set-leader cyoa-chosen-charid #character "Impostazione del leader su " type dup . cr .make-leader ;

: cyoa-pick-leader
	cyoa-start
	"OH NO" cyoa-title!
	"Il tuo leader di missione e morto! Scegli un nuovo leader:" cyoa-text!
	' set-leader cyoa-mission-team-choice
;


: car-fail2
	// TODO: summary stuff


	
	(* THIS WAS REALLY SCREWY and broke if you were in a building in a city, presumably because driveout would happen but you weren't around to see the car drive out
	getcar thecar if
		vehicle.which@ if driveout beep "cyoa car fail2 at driveout" print then; // Car
	then *)
	
	main-event-mission-end // No car
;

: cyoa-car-fail
	cyoa-start
	"FALLITO!" cyoa-title!
	"cyoa car fail" print
	"La tua squadra di ricognizione e stata spazzata via! Meglio andarsene da qui!"
		cyoa-text!
	' car-fail2 cyoa-choice-handler!
;


: cyoa-pick-last
	cyoa-start

	dup character# cyoa-char-icon!

	"OH NO" cyoa-title!
	"Tutti gli altri sono morti! " over .name@ $cat " is all alone..." $cat cyoa-text!

	.make-leader
;


(*
: mission-killed-singleplayer
	// Don't switch unless it's the leader who died
	team-leader #character .dead? 0= if then;

	TEAM_STATUS_DIED team-leader #character .decruit // deadtally - Moved this to death handler, since some of the decruit is on the C side.

	// Only one char left
	mission-team-stack depths 1 = if
		0 leader_playerid!

		pops cyoa-pick-last exit else drop
	then;

	// More than one left
	mission-team-stack depths nip if cyoa-pick-leader then;

	// Only reserve team remains
	team-stack-living depths nip if cyoa-car-fail then;

	clear-streak delete-the-game game-over
;
*)


: mission-killed-multiplayer

//	"TEAM COUNT: " team-count .. msg

	// Is this even necessary?
	team-leader #character .dead? if
		TEAM_STATUS_DIED team-leader #character .decruit
	then

	// At least one char left
	team-mission-count-permanent if then;

	// Only reserve team remains
//	team-stack-living depths nip if cyoa-car-fail then;
	team-count-alive-permanent if cyoa-car-fail then;

	// GAME OVER
	clear-streak delete-the-game game-over
;


last to main-event-mission-killed

: quit-from-mission
	quit-to-title
	clear-streak delete-the-game
;

// : back-esc cyoa{ cyoa-back-esc } ; - Now in cyoa.df instead

// Quit from screens
: handle-quit cyoa{
	cyoa-active? if then;
//	4 500 tone
	cyoa-new- 
	"QUIT?" cyoa-title!

	game.location 0 < if
		"EXIT il gioco?" cyoa-text!
		choice( "NO" )choice back-esc
		choice( "SI" )choice: mad_quit
	then;
	
	"ABORT il gioco e torna alla schermata del titolo?" cyoa-text!
	choice( "NO" )choice back-esc
	road? if
		"Puoi riprendere il gioco in seguito con CONTINUA GIOCO!" +crtext
		choice( "SI" )choice: quit-to-title
	then;
	ZOMBOTOWN if
		"EXIT ZOMBO TOWN" cyoa-title!
		"Ritorna alla schermata del titolo?	Tutti i miglioramenti che hai acquistato sono gia salvati." cyoa-text!
		choice( "SI" )choice: quit-to-title
	then;
	mission? if 
		"AVVISO: Uscendo direttamente da una missione verra eliminato il salvataggio!" rbold +crtext
		choice( "ESCI ED ELIMINA IL SALVATAGGIO" )choice: quit-from-mission
	then;
	choice( "SICURO" )choice: quit-from-mission

} ;

last to main-event-do-quit


: handle-esc
	game.location 0 < if
		cyoa-active? if then;
		cyoa-new- 
		"QUIT?" cyoa-title!
		"EXIT il gioco?" cyoa-text!
		choice( "NO" )choice back-esc
		choice( "SI" )choice: mad_quit
	then;
;
last to main-event-do-esc

}

: quitmission mission{ ' to-road-all-team cyoa-choice-handler! } ;

// NO SOLO PET MISSIONS
: premission-check-test 1 premission_check!
	party-team-stack depths- 1 > if then;

	pchar .pet@ if
		pchar statmod{ .superdog? } if then;
		pchar .name@ "  e solo un " .. RED .. "animale stupido" .. WHITE .. " ' e non puo guidare una missione da sola. Triste!" ..
			$cyoa-ok-dialog
		0 premission_check!
		picon
	then;
;

last to main-event-premission-check

1 constant SECONDS_OFFMAP

// Event is called every 30 ticks when buddy is off the leader's map
: event-buddy-offmap args( self )

	// Don't do it if zombie is currently attacking
	<- self .grappled? if then;

	// Don't do it for an NPC
	<- self .buddy? not if then;

	// Jump to leader after 3 seconds offmap
	<- self .offscreen_counter@ 60 SECONDS_OFFMAP * >= if
		<- self .jump2leader
		"Sup." <- self .yellmsg
	then;

	// Make invincible for this 30 tick period
	30 <- self .invincible_counter!
;

last to main-event-mission-buddy-offmap

